!-----------------------------------------------------------------------------
! (c) Crown copyright 2020 Met Office. All rights reserved.
! The file LICENCE, distributed with this code, contains details of the terms
! under which the code may be used.
!-----------------------------------------------------------------------------
!> @brief Interface to JULES surf_couple_extra.
!>
module jules_extra_kernel_mod

  use argument_mod,            only : arg_type,                  &
                                      GH_FIELD, GH_REAL,         &
                                      GH_READ, GH_WRITE,         &
                                      GH_READWRITE,              &
                                      ANY_DISCONTINUOUS_SPACE_1, &
                                      ANY_DISCONTINUOUS_SPACE_2, &
                                      ANY_DISCONTINUOUS_SPACE_3, &
                                      ANY_DISCONTINUOUS_SPACE_4, &
                                      ANY_DISCONTINUOUS_SPACE_5, &
                                      CELL_COLUMN
  use constants_mod,           only : i_def, i_um, r_def, r_um
  use kernel_mod,              only : kernel_type
  use empty_data_mod,          only : empty_real_data

  implicit none

  private

  !-----------------------------------------------------------------------------
  ! Public types
  !-----------------------------------------------------------------------------
  !> Kernel metadata type.
  !>
  type, public, extends(kernel_type) :: jules_extra_kernel_type
    private
    type(arg_type) :: meta_args(58) = (/                                       &
         arg_type(GH_FIELD, GH_REAL, GH_READ,      ANY_DISCONTINUOUS_SPACE_1), & ! ls_rain
         arg_type(GH_FIELD, GH_REAL, GH_READ,      ANY_DISCONTINUOUS_SPACE_1), & ! conv_rain
         arg_type(GH_FIELD, GH_REAL, GH_READ,      ANY_DISCONTINUOUS_SPACE_1), & ! ls_snow
         arg_type(GH_FIELD, GH_REAL, GH_READ,      ANY_DISCONTINUOUS_SPACE_1), & ! conv_snow
         arg_type(GH_FIELD, GH_REAL, GH_READ,      ANY_DISCONTINUOUS_SPACE_1), & ! lsca_2d
         arg_type(GH_FIELD, GH_REAL, GH_READ,      ANY_DISCONTINUOUS_SPACE_1), & ! cca_2d
         arg_type(GH_FIELD, GH_REAL, GH_READ,      ANY_DISCONTINUOUS_SPACE_2), & ! tile_fraction
         arg_type(GH_FIELD, GH_REAL, GH_READ,      ANY_DISCONTINUOUS_SPACE_3), & ! leaf_area_index
         arg_type(GH_FIELD, GH_REAL, GH_READ,      ANY_DISCONTINUOUS_SPACE_3), & ! canopy_height
         arg_type(GH_FIELD, GH_REAL, GH_READ,      ANY_DISCONTINUOUS_SPACE_3), & ! snow_unload_rate
         arg_type(GH_FIELD, GH_REAL, GH_READ,      ANY_DISCONTINUOUS_SPACE_1), & ! soil_moist_wilt
         arg_type(GH_FIELD, GH_REAL, GH_READ,      ANY_DISCONTINUOUS_SPACE_1), & ! soil_moist_crit
         arg_type(GH_FIELD, GH_REAL, GH_READ,      ANY_DISCONTINUOUS_SPACE_1), & ! soil_moist_sat
         arg_type(GH_FIELD, GH_REAL, GH_READ,      ANY_DISCONTINUOUS_SPACE_1), & ! soil_cond_sat
         arg_type(GH_FIELD, GH_REAL, GH_READ,      ANY_DISCONTINUOUS_SPACE_1), & ! soil_thermal_cap
         arg_type(GH_FIELD, GH_REAL, GH_READ,      ANY_DISCONTINUOUS_SPACE_1), & ! soil_thermal_cond
         arg_type(GH_FIELD, GH_REAL, GH_READ,      ANY_DISCONTINUOUS_SPACE_1), & ! soil_suction_sat
         arg_type(GH_FIELD, GH_REAL, GH_READ,      ANY_DISCONTINUOUS_SPACE_1), & ! clapp_horn_b
         arg_type(GH_FIELD, GH_REAL, GH_READ,      ANY_DISCONTINUOUS_SPACE_1), & ! soil_carbon_content
         arg_type(GH_FIELD, GH_REAL, GH_READ,      ANY_DISCONTINUOUS_SPACE_1), & ! soil_roughness
         arg_type(GH_FIELD, GH_REAL, GH_READ,      ANY_DISCONTINUOUS_SPACE_1), & ! mean_topog_index
         arg_type(GH_FIELD, GH_REAL, GH_READ,      ANY_DISCONTINUOUS_SPACE_1), & ! a_sat_frac
         arg_type(GH_FIELD, GH_REAL, GH_READ,      ANY_DISCONTINUOUS_SPACE_1), & ! c_sat_frac
         arg_type(GH_FIELD, GH_REAL, GH_READ,      ANY_DISCONTINUOUS_SPACE_1), & ! a_wet_frac
         arg_type(GH_FIELD, GH_REAL, GH_READ,      ANY_DISCONTINUOUS_SPACE_1), & ! c_wet_frac
         arg_type(GH_FIELD, GH_REAL, GH_READ,      ANY_DISCONTINUOUS_SPACE_2), & ! tile_temperature
         arg_type(GH_FIELD, GH_REAL, GH_READ,      ANY_DISCONTINUOUS_SPACE_1), & ! net_prim_prod
         arg_type(GH_FIELD, GH_REAL, GH_READ,      ANY_DISCONTINUOUS_SPACE_2), & ! snow_sublimation
         arg_type(GH_FIELD, GH_REAL, GH_READ,      ANY_DISCONTINUOUS_SPACE_2), & ! surf_heat_flux
         arg_type(GH_FIELD, GH_REAL, GH_READ,      ANY_DISCONTINUOUS_SPACE_2), & ! canopy_evap
         arg_type(GH_FIELD, GH_REAL, GH_READ,      ANY_DISCONTINUOUS_SPACE_4), & ! water_extraction
         arg_type(GH_FIELD, GH_REAL, GH_READ,      ANY_DISCONTINUOUS_SPACE_1), & ! thermal_cond_wet_soil
         arg_type(GH_FIELD, GH_REAL, GH_READ,      ANY_DISCONTINUOUS_SPACE_1), & ! soil_respiration
         arg_type(GH_FIELD, GH_REAL, GH_READWRITE, ANY_DISCONTINUOUS_SPACE_4), & ! soil_temperature
         arg_type(GH_FIELD, GH_REAL, GH_READWRITE, ANY_DISCONTINUOUS_SPACE_4), & ! soil_moisture
         arg_type(GH_FIELD, GH_REAL, GH_READWRITE, ANY_DISCONTINUOUS_SPACE_4), & ! unfrozen_soil_moisture
         arg_type(GH_FIELD, GH_REAL, GH_READWRITE, ANY_DISCONTINUOUS_SPACE_4), & ! frozen_soil_moisture
         arg_type(GH_FIELD, GH_REAL, GH_READWRITE, ANY_DISCONTINUOUS_SPACE_2), & ! canopy_water
         arg_type(GH_FIELD, GH_REAL, GH_READWRITE, ANY_DISCONTINUOUS_SPACE_2), & ! tile_snow_mass
         arg_type(GH_FIELD, GH_REAL, GH_READWRITE, ANY_DISCONTINUOUS_SPACE_2), & ! tile_snow_rgrain
         arg_type(GH_FIELD, GH_REAL, GH_READWRITE, ANY_DISCONTINUOUS_SPACE_2), & ! n_snow_layers
         arg_type(GH_FIELD, GH_REAL, GH_READWRITE, ANY_DISCONTINUOUS_SPACE_2), & ! snow_depth
         arg_type(GH_FIELD, GH_REAL, GH_READWRITE, ANY_DISCONTINUOUS_SPACE_2), & ! snow_under_canopy
         arg_type(GH_FIELD, GH_REAL, GH_READWRITE, ANY_DISCONTINUOUS_SPACE_2), & ! snowpack_density
         arg_type(GH_FIELD, GH_REAL, GH_READWRITE, ANY_DISCONTINUOUS_SPACE_5), & ! snow_layer_thickness
         arg_type(GH_FIELD, GH_REAL, GH_READWRITE, ANY_DISCONTINUOUS_SPACE_5), & ! snow_layer_ice_mass
         arg_type(GH_FIELD, GH_REAL, GH_READWRITE, ANY_DISCONTINUOUS_SPACE_5), & ! snow_layer_liq_mass
         arg_type(GH_FIELD, GH_REAL, GH_READWRITE, ANY_DISCONTINUOUS_SPACE_5), & ! snow_layer_temp
         arg_type(GH_FIELD, GH_REAL, GH_READWRITE, ANY_DISCONTINUOUS_SPACE_5), & ! snow_layer_rgrain
         arg_type(GH_FIELD, GH_REAL, GH_READWRITE, ANY_DISCONTINUOUS_SPACE_2), & ! total_snowmelt
         arg_type(GH_FIELD, GH_REAL, GH_READWRITE, ANY_DISCONTINUOUS_SPACE_1), & ! soil_sat_frac
         arg_type(GH_FIELD, GH_REAL, GH_READWRITE, ANY_DISCONTINUOUS_SPACE_1), & ! water_table
         arg_type(GH_FIELD, GH_REAL, GH_READWRITE, ANY_DISCONTINUOUS_SPACE_1), & ! wetness_under_soil
         arg_type(GH_FIELD, GH_REAL, GH_WRITE,     ANY_DISCONTINUOUS_SPACE_1), & ! soil_moisture_content
         arg_type(GH_FIELD, GH_REAL, GH_WRITE,     ANY_DISCONTINUOUS_SPACE_1), & ! grid_snow_mass
         arg_type(GH_FIELD, GH_REAL, GH_WRITE,     ANY_DISCONTINUOUS_SPACE_2), & ! throughfall
         arg_type(GH_FIELD, GH_REAL, GH_WRITE,     ANY_DISCONTINUOUS_SPACE_1), & ! surface_runoff
         arg_type(GH_FIELD, GH_REAL, GH_WRITE,     ANY_DISCONTINUOUS_SPACE_1)  & ! sub_surface_runoff
        /)
    integer :: operates_on = CELL_COLUMN
  contains
    procedure, nopass :: jules_extra_code
  end type

  public :: jules_extra_code

contains

  !> @brief Interface to JULES surf_couple_extra
  !> @details JULES surf_couple_extra calculates the surface and soil fluxes
  !> and stores of Water (via the hydrol{ogy}, snow and river_control
  !> modules) and Carbon (via triffid and inferno/fire modules). Note: only the
  !> hydrol and snow components are implemented so far.

  !> @param[in]     nlayers                Number of layers
  !> @param[in]     ls_rain                Large-scale rainfall rate (kg m-2 s-1)
  !> @param[in]     conv_rain              Convective rainfall rate (kg m-2 s-1)
  !> @param[in]     ls_snow                Large-scale snowfall rate (kg m-2 s-1)
  !> @param[in]     conv_snow              Convective snowfall rate (kg m-2 s-1)
  !> @param[in]     lsca_2d                Large-scale cloud amout (2d)
  !> @param[in]     cca_2d                 Convective cloud amout (2d) with no anvil
  !> @param[in]     tile_fraction          Surface tile fractions
  !> @param[in]     leaf_area_index        Leaf Area Index
  !> @param[in]     canopy_height          Canopy height (m)
  !> @param[in]     snow_unload_rate       Unloading of snow from PFTs by wind
  !> @param[in]     soil_moist_wilt        Volumetric soil moist at wilting pt
  !> @param[in]     soil_moist_crit        Volumetric soil moist at critical pt
  !> @param[in]     soil_moist_sat         Volumetric soil moist at saturation
  !> @param[in]     soil_cond_sat          Saturated soil thermal conductivity (kg m-2 s-1)
  !> @param[in]     soil_thermal_cap       Soil thermal capacity (J m-3 K-1)
  !> @param[in]     soil_thermal_cond      Soil thermal conductivity (W m-1 K-1)
  !> @param[in]     soil_suction_sat       Saturated soil water suction (m)
  !> @param[in]     clapp_horn_b           Clapp and Hornberger b coefficient
  !> @param[in]     soil_carbon_content    Soil carbon content (kg m-2)
  !> @param[in]     soil_roughness         Bare soil surface roughness length (m)
  !> @param[in]     mean_topog_index       Mean topographic index
  !> @param[in]     a_sat_frac             a gridbox saturated fraction
  !> @param[in]     c_sat_frac             c gridbox saturated fraction
  !> @param[in]     a_wet_frac             a gridbox wet fraction
  !> @param[in]     c_wet_frac             c gridbox wet fraction
  !> @param[in]     tile_temperature       Surface tile temperatures (K)
  !> @param[in]     net_prim_prod          Net Primary Productivity (kg m-2 s-1)
  !> @param[in]     snow_sublimation       Sublimation of snow (kg m-2 s-1)
  !> @param[in]     surf_heat_flux         Surface heat flux (W m-2)
  !> @param[in]     canopy_evap            Canopy evaporation from land tiles (kg m-2 s-1)
  !> @param[in]     water_extraction       Extraction of water from each soil layer (kg m-2 s-1)
  !> @param[in]     thermal_cond_wet_soil  Thermal conductivity of soil (W m-1 K-1)
  !> @param[in]     soil_respiration       Soil respiration (kg m-2 s-1)
  !> @param[in,out] soil_temperature       Soil temperature (K)
  !> @param[in,out] soil_moisture          Soil moisture content (kg m-2)
  !> @param[in,out] unfrozen_soil_moisture Unfrozen soil moisture proportion
  !> @param[in,out] frozen_soil_moisture   Frozen soil moisture proportion
  !> @param[in,out] canopy_water           Canopy water on each tile (kg m-2)
  !> @param[in,out] tile_snow_mass         Snow mass on tiles (kg m-2)
  !> @param[in,out] tile_snow_rgrain       Snow grain radius on tiles (microns)
  !> @param[in,out] n_snow_layers          Number of snow layers on tiles
  !> @param[in,out] snow_depth             Snow depth on tiles (m)
  !> @param[in,out] snow_under_canopy      Amount of snow under canopy (kg m-2)
  !> @param[in,out] snowpack_density       Density of snow on ground (kg m-3)
  !> @param[in,out] snow_layer_thickness   Thickness of snow layers (m)
  !> @param[in,out] snow_layer_ice_mass    Mass of ice in snow layers (kg m-2)
  !> @param[in,out] snow_layer_liq_mass    Mass of liquid in snow layers (kg m-2)
  !> @param[in,out] snow_layer_temp        Temperature of snow layer (K)
  !> @param[in,out] snow_layer_rgrain      Grain radius of snow layer (microns)
  !> @param[in,out] total_snowmelt         Surface plus canopy snowmelt rate (kg m-2 s-1)
  !> @param[in,out] soil_sat_frac          Soil saturated fraction
  !> @param[in,out] water_table            Water table depth (m)
  !> @param[in,out] wetness_under_soil     Soil wetness below soil column
  !> @param[in,out] soil_moisture_content  Soil moisture content of soil column
  !> @param[in,out] grid_snow_mass         Gridbox total snow mass (canopy + under canopy)
  !> @param[in,out] throughfall            Throughfall from land tiles
  !> @param[in,out] surface_runoff         Runoff from surface
  !> @param[in,out] sub_surface_runoff     Runoff from sub-surface
  !> @param[in]     ndf_2d                 Total DOFs per cell for 2D fields
  !> @param[in]     undf_2d                Unique DOFs per cell for 2D fields
  !> @param[in]     map_2d                 DOFmap for cells for 2D fields
  !> @param[in]     ndf_tile               Total DOFs per cell for surface tiles
  !> @param[in]     undf_tile              Unique DOFs per cell for surface tiles
  !> @param[in]     map_tile               DOFmap for cells for surface tiles
  !> @param[in]     ndf_pft                Total DOFs per cell for plant types
  !> @param[in]     undf_pft               Unique DOFs per cell for plant types
  !> @param[in]     map_pft                DOFmap for cells for plant types
  !> @param[in]     ndf_soil               Total DOFs per cell for soil levels
  !> @param[in]     undf_soil              Unique DOFs per cell for soil levels
  !> @param[in]     map_soil               DOFmap for cells for soil levels
  !> @param[in]     ndf_snow               Total DOFs per cell for snow layers
  !> @param[in]     undf_snow              Unique DOFs per cell for snow layers
  !> @param[in]     map_snow               DOFmap for cells for snow layers
  subroutine jules_extra_code(             &
               nlayers,                    &
               ls_rain,                    &
               conv_rain,                  &
               ls_snow,                    &
               conv_snow,                  &
               lsca_2d,                    &
               cca_2d,                     &
               tile_fraction,              &
               leaf_area_index,            &
               canopy_height,              &
               snow_unload_rate,           &
               soil_moist_wilt,            &
               soil_moist_crit,            &
               soil_moist_sat,             &
               soil_cond_sat,              &
               soil_thermal_cap,           &
               soil_thermal_cond,          &
               soil_suction_sat,           &
               clapp_horn_b,               &
               soil_carbon_content,        &
               soil_roughness,             &
               mean_topog_index,           &
               a_sat_frac,                 &
               c_sat_frac,                 &
               a_wet_frac,                 &
               c_wet_frac,                 &
               tile_temperature,           &
               net_prim_prod,              &
               snow_sublimation,           &
               surf_heat_flux,             &
               canopy_evap,                &
               water_extraction,           &
               thermal_cond_wet_soil,      &
               soil_respiration,           &
               soil_temperature,           &
               soil_moisture,              &
               unfrozen_soil_moisture,     &
               frozen_soil_moisture,       &
               canopy_water,               &
               tile_snow_mass,             &
               tile_snow_rgrain,           &
               n_snow_layers,              &
               snow_depth,                 &
               snow_under_canopy,          &
               snowpack_density,           &
               snow_layer_thickness,       &
               snow_layer_ice_mass,        &
               snow_layer_liq_mass,        &
               snow_layer_temp,            &
               snow_layer_rgrain,          &
               total_snowmelt,             &
               soil_sat_frac,              &
               water_table,                &
               wetness_under_soil,         &
               soil_moisture_content,      &
               grid_snow_mass,             &
               throughfall,                &
               surface_runoff,             &
               sub_surface_runoff,         &
               ndf_2d,                     &
               undf_2d,                    &
               map_2d,                     &
               ndf_tile,                   &
               undf_tile,                  &
               map_tile,                   &
               ndf_pft,                    &
               undf_pft,                   &
               map_pft,                    &
               ndf_soil,                   &
               undf_soil,                  &
               map_soil,                   &
               ndf_snow,                   &
               undf_snow,                  &
               map_snow                    &
                            )

    !---------------------------------------
    ! LFRic modules
    !---------------------------------------
    use jules_control_init_mod, only: &
         nsurft => n_land_tile, n_land_tile
    use jules_physics_init_mod, only: decrease_sath_cond

    ! Module imports for surf_couple_extra JULESvn5.4
    use ancil_info, only: nsoilt, soil_pts, lice_pts
    use atm_step_local, only: dim_cs1
    use cderived_mod, only: delta_lambda, delta_phi
    use jules_surface_types_mod, only: npft, ntype
    use jules_snow_mod, only: nsmax
    use jules_fields_mod, only: crop_vars, psparms, toppdm, fire_vars, ainfo, &
                                trif_vars, soilecosse, aerotype, urban_param, &
                                progs, trifctltype, jules_vars,               &
                                fluxes,                                       &
                                lake_vars,                                    &
                                forcing
                                !rivers, &
                                !veg3_parm, &
                                !veg3_field, &
                                !chemvars
    use nlsizes_namelist_mod, only: row_length, rows, land_pts => land_field, &
                                    sm_levels, ntiles
    use UM_ParCore, only: nproc

    ! Spatially varying fields used from modules
    use trignometric_mod, only: cos_theta_latitude

    ! Jules related subroutines
    use sparm_mod, only             : sparm
    use tilepts_mod, only           : tilepts
    use surf_couple_extra_mod, only : surf_couple_extra
    use infiltration_rate_mod, only : infiltration_rate

    implicit none

    ! Arguments
    integer(kind=i_def), intent(in) :: nlayers
    integer(kind=i_def), intent(in) :: ndf_2d, undf_2d, ndf_tile, undf_tile,  &
                                       ndf_pft, undf_pft, ndf_soil,           &
                                       undf_soil, ndf_snow, undf_snow

    integer(kind=i_def), dimension(ndf_2d),    intent(in) :: map_2d
    integer(kind=i_def), dimension(ndf_tile),  intent(in) :: map_tile
    integer(kind=i_def), dimension(ndf_pft),   intent(in) :: map_pft
    integer(kind=i_def), dimension(ndf_soil),  intent(in) :: map_soil
    integer(kind=i_def), dimension(ndf_snow),  intent(in) :: map_snow

    real(kind=r_def), dimension(undf_2d), intent(in) :: ls_rain, conv_rain,   &
                                                        ls_snow, conv_snow,   &
                                                        lsca_2d, cca_2d

    real(kind=r_def), intent(in)    :: tile_fraction(undf_tile)
    real(kind=r_def), intent(in)    :: snow_sublimation(undf_tile)
    real(kind=r_def), intent(in)    :: surf_heat_flux(undf_tile)
    real(kind=r_def), intent(in)    :: canopy_evap(undf_tile)
    real(kind=r_def), intent(in)    :: tile_temperature(undf_tile)

    real(kind=r_def), intent(in)    :: leaf_area_index(undf_pft)
    real(kind=r_def), intent(in)    :: canopy_height(undf_pft)
    real(kind=r_def), intent(in)    :: snow_unload_rate(undf_pft)

    real(kind=r_def), intent(in)    :: soil_moist_wilt(undf_2d)
    real(kind=r_def), intent(in)    :: soil_moist_crit(undf_2d)
    real(kind=r_def), intent(in)    :: soil_moist_sat(undf_2d)
    real(kind=r_def), intent(in)    :: soil_cond_sat(undf_2d)
    real(kind=r_def), intent(in)    :: soil_thermal_cap(undf_2d)
    real(kind=r_def), intent(in)    :: soil_suction_sat(undf_2d)
    real(kind=r_def), intent(in)    :: clapp_horn_b(undf_2d)
    real(kind=r_def), intent(in)    :: water_extraction(undf_soil)

    real(kind=r_def), intent(in)    :: soil_thermal_cond(undf_2d)
    real(kind=r_def), intent(in)    :: soil_carbon_content(undf_2d)
    real(kind=r_def), intent(in)    :: soil_roughness(undf_2d)
    real(kind=r_def), intent(in)    :: mean_topog_index(undf_2d)
    real(kind=r_def), intent(in)    :: a_sat_frac(undf_2d)
    real(kind=r_def), intent(in)    :: c_sat_frac(undf_2d)
    real(kind=r_def), intent(in)    :: a_wet_frac(undf_2d)
    real(kind=r_def), intent(in)    :: c_wet_frac(undf_2d)
    real(kind=r_def), intent(in)    :: net_prim_prod(undf_2d)
    real(kind=r_def), intent(in)    :: thermal_cond_wet_soil(undf_2d)
    real(kind=r_def), intent(in)    :: soil_respiration(undf_2d)

    real(kind=r_def), intent(inout) :: canopy_water(undf_tile)
    real(kind=r_def), intent(inout) :: tile_snow_mass(undf_tile)
    real(kind=r_def), intent(inout) :: tile_snow_rgrain(undf_tile)
    real(kind=r_def), intent(inout) :: n_snow_layers(undf_tile)
    real(kind=r_def), intent(inout) :: snow_depth(undf_tile)
    real(kind=r_def), intent(inout) :: snow_under_canopy(undf_tile)
    real(kind=r_def), intent(inout) :: snowpack_density(undf_tile)
    real(kind=r_def), intent(inout) :: total_snowmelt(undf_tile)

    real(kind=r_def), intent(inout) :: snow_layer_thickness(undf_snow)
    real(kind=r_def), intent(inout) :: snow_layer_ice_mass(undf_snow)
    real(kind=r_def), intent(inout) :: snow_layer_liq_mass(undf_snow)
    real(kind=r_def), intent(inout) :: snow_layer_temp(undf_snow)
    real(kind=r_def), intent(inout) :: snow_layer_rgrain(undf_snow)

    real(kind=r_def), intent(inout) :: soil_temperature(undf_soil)
    real(kind=r_def), intent(inout) :: soil_moisture(undf_soil)
    real(kind=r_def), intent(inout) :: unfrozen_soil_moisture(undf_soil)
    real(kind=r_def), intent(inout) :: frozen_soil_moisture(undf_soil)

    real(kind=r_def), intent(inout) :: soil_sat_frac(undf_2d)
    real(kind=r_def), intent(inout) :: water_table(undf_2d)
    real(kind=r_def), intent(inout) :: wetness_under_soil(undf_2d)

    real(kind=r_def), pointer, intent(inout) :: soil_moisture_content(:)
    real(kind=r_def), pointer, intent(inout) :: grid_snow_mass(:)
    real(kind=r_def), pointer, intent(inout) :: throughfall(:)
    real(kind=r_def), pointer, intent(inout) :: surface_runoff(:)
    real(kind=r_def), pointer, intent(inout) :: sub_surface_runoff(:)

    ! Local variables for the kernel
    integer(kind=i_def) :: i, j, n, i_snow

!------------------------------------------------------------------------------
    ! JULES surf_couple_extra subroutine arguments declared using JULESvn5.4
    !       order and naming

    ! Integer parameters
    integer(i_um), parameter :: river_row_length = 1, river_rows = 1,         &
                                aocpl_row_length = 1, aocpl_p_rows = 1

    ! Integer indices (module intent=in)
    integer(i_um) :: a_step, g_p_field, g_r_field, global_row_length,         &
         global_rows, global_river_row_length, global_river_rows

    integer(i_um), dimension(nsurft) :: surft_pts

    ! Logical (module intent=in)
    logical :: smlt, stf_sub_surf_roff

    logical, dimension(row_length,rows) :: land_sea_mask

    ! Real variables (module intent=in)
    ! Driving data
    real(r_um), dimension(row_length, rows) :: ls_graup_ij, &
         u_1_ij, v_1_ij, cca_2d_ij, soil_clay_ij,                             &
         flash_rate_ancil, pop_den_ancil, flandg, rho_star

    ! State
    real(r_um), dimension(land_pts, ntiles) ::                                &
    ! Fluxes
         tile_frac, u_s_std_surft

    real(r_um), dimension(land_pts, nsoilt) :: fexp_soilt, gamtot_soilt,      &
         ti_mean_soilt, ti_sig_soilt, a_fsat_soilt, c_fsat_soilt,             &
         a_fwet_soilt, c_fwet_soilt

    real(r_um), dimension(land_pts) :: npp_gb, frac_agr_gb

    ! River routing
    real(r_um), dimension(aocpl_row_length) :: xpa
    real(r_um), dimension(0:aocpl_row_length) :: xua
    real(r_um), dimension(aocpl_row_length+1) :: xva
    real(r_um), dimension(aocpl_p_rows) :: ypa, yua
    real(r_um), dimension(0:aocpl_p_rows) :: yva
    real(r_um), dimension(river_row_length, river_rows) :: trivdir, trivseq
    real(r_um), dimension(row_length, rows) :: r_area, slope, flowobs1,       &
         r_inext, r_jnext, r_land


    ! Integers (module intent = in out)
    integer(i_um) :: a_steps_since_riv, asteps_since_triffid

    ! Real variables (module intent = in out)
    real(r_um), dimension(row_length, rows) :: substore, surfstore, flowin,   &
         bflowin, acc_lake_evap

    real(r_um), dimension(river_row_length, river_rows) :: twatstor

    real(r_um), dimension(land_pts, dim_cs1) :: resp_s_acc_gb_um,             &
         cs_pool_gb_um

    real(r_um), dimension(land_pts) :: dhf_surf_minus_soil,                   &
         ls_rainfrac_gb, tot_surf_runoff, tot_sub_runoff, inlandout_atm_gb

    real(r_um), dimension(land_pts, nsoilt) :: hcons_soilt, fsat_soilt,       &
         fwetl_soilt, zw_soilt, sthzw_soilt, cs_ch4_soilt

    real(r_um), dimension(land_pts, nsoilt, 1, dim_cs1) :: resp_s_soilt

    !-------------------------------------------------------------------

    ! Data from 2D fields
    forcing%ls_rain_ij(1,1)  = ls_rain(map_2d(1))   ! Large-scale rainfall rate
    forcing%con_rain_ij(1,1) = conv_rain(map_2d(1)) ! Convective rainfall rate
    forcing%ls_snow_ij(1,1)  = ls_snow(map_2d(1))   ! Large-scale snowfall rate
    forcing%con_snow_ij(1,1) = conv_snow(map_2d(1)) ! Convective snowfallfall rate
    ls_rainfrac_gb(1) = lsca_2d(map_2d(1))  ! Large-scale cloud amount
    cca_2d_ij(1,1)   = cca_2d(map_2d(1))    ! Convective cloud amount

    !----------------------------------------------------------------------
    ! Surface fields as needed by Jules
    !----------------------------------------------------------------------

    ! Ancillaries:
    ! Land tile fractions (frac_surft)
    flandg = 0.0_r_um
    do i = 1, n_land_tile
      flandg = flandg + real(tile_fraction(map_tile(1)+i-1), r_um)
      ainfo%frac_surft(1, i)     = real(tile_fraction(map_tile(1)+i-1), r_um)
      fluxes%ei_surft(1, i)       = real(snow_sublimation(map_tile(1)+i-1), r_um)
      fluxes%surf_htf_surft(1, i) = real(surf_heat_flux(map_tile(1)+i-1), r_um)
      fluxes%ecan_surft(1, i)     = real(canopy_evap(map_tile(1)+i-1), r_um)
    end do
    flandg = min(flandg, 1.0_r_um)

    ! Jules requires fractions with respect to the land area
    if (flandg(1, 1) > 0.0_r_um) then
      land_pts = 1
      ainfo%land_index = 1
      ainfo%frac_surft(1, 1:n_land_tile) = ainfo%frac_surft(1, 1:n_land_tile) / flandg(1, 1)
      land_sea_mask = .true.
    else
      land_pts = 0
      ainfo%land_index = 0
      land_sea_mask = .false.
    end if

    ! Logical flags controlling diagnostic calculations
    smlt = .false.
    stf_sub_surf_roff = .false.

    ! GPW fudge: set tile_frac = frac_surft
    tile_frac = ainfo%frac_surft

    ! Set type_pts and type_index
    call tilepts(land_pts, ainfo%frac_surft, surft_pts, ainfo%surft_index,ainfo%l_lice_point)

    ! Vegetation prognostics (for TRIFFID)
    do n = 1, npft
      ! Leaf area index
      progs%lai_pft(1, n) = real(leaf_area_index(map_pft(1)+n-1), r_um)
      ! Canopy height
      progs%canht_pft(1, n) = real(canopy_height(map_pft(1)+n-1), r_um)
      ! Unloading rate of snow from plant functional types
      jules_vars%unload_backgrnd_pft(1, n) = real(snow_unload_rate(map_pft(1)+n-1), r_um)
    end do

    ! Soil roughness
    psparms%z0m_soil_gb = real(soil_roughness(map_2d(1)), r_um)

    ! Get catch_snow_surft and catch_surft from call to sparm
    call sparm(land_pts, n_land_tile, surft_pts, ainfo%surft_index,                   &
               ainfo%frac_surft, progs%canht_pft, progs%lai_pft, psparms%z0m_soil_gb, &
               psparms%catch_snow_surft, psparms%catch_surft,                         &
               psparms%z0_surft, psparms%z0h_bare_surft, urban_param%ztm_gb)

    ! Soil carbon content (cs_pool_gb_um, )
    cs_pool_gb_um = real(soil_carbon_content(map_2d(1)), r_um)

    ! GPW fudge while TRIFFID not implemented: set cs_ch4_soilt = cs_pool_gb_um
    cs_ch4_soilt = cs_pool_gb_um

    ! Decrease in saturated conductivity of soil with depth
    fexp_soilt = decrease_sath_cond

    ! Mean grid box topographic index
    ti_mean_soilt = real(mean_topog_index(map_2d(1)), r_um)

    ! a saturated soil fraction
    a_fsat_soilt = real(a_sat_frac(map_2d(1)), r_um)

    ! c saturated soil fraction
    c_fsat_soilt = real(c_sat_frac(map_2d(1)), r_um)

    ! a soil wetness fraction
    a_fwet_soilt = real(a_wet_frac(map_2d(1)), r_um)

    ! c soil wetness fraction
    c_fwet_soilt = real(c_wet_frac(map_2d(1)), r_um)

    ! Net primary productivity diagnostic
    npp_gb = real(net_prim_prod(map_2d(1)), r_um)

    ! Prognostics:
    ! Land tile temperatures
    do i = 1, n_land_tile
      progs%tstar_surft(1, i) = real(tile_temperature(map_tile(1)+i-1), r_um)
    end do

    ! Soil ancillaries and prognostics
    psparms%satcon_soilt(1, 1, 0) = real(soil_cond_sat(map_2d(1)), r_um)
    do i = 1, sm_levels
    ! Volumetric soil moisture at wilting point (smvcwt_soilt)
      psparms%smvcwt_soilt(1, 1, i) = real(soil_moist_wilt(map_2d(1)), r_um)
    ! Volumetric soil moisture at critical point (smvccl_soilt)
      psparms%smvccl_soilt(1, 1, i) = real(soil_moist_crit(map_2d(1)), r_um)
    ! Volumetric soil moisture at saturation (smvcst_soilt)
      psparms%smvcst_soilt(1, 1, i) = real(soil_moist_sat(map_2d(1)), r_um)
    ! Saturated soil conductivity (satcon_soilt)
      psparms%satcon_soilt(1, 1, i) = real(soil_cond_sat(map_2d(1)), r_um)
    ! Soil thermal capacity (hcap_soilt)
      psparms%hcap_soilt(1, 1, i) = real(soil_thermal_cap(map_2d(1)), r_um)
    ! Saturated soil water suction (sathh_soilt)
      psparms%sathh_soilt(1, 1, i) = real(soil_suction_sat(map_2d(1)), r_um)
    ! Clapp and Hornberger b coefficient (bexp_soilt)
      psparms%bexp_soilt(1, 1, i) = real(clapp_horn_b(map_2d(1)), r_um)
    ! Soil temperature (t_soil_soilt)
      progs%t_soil_soilt(1, 1, i) = real(soil_temperature(map_soil(1)+i-1), r_um)
    ! Soil moisture content (kg m-2, soil_layer_moisture/smcl_soilt)
      progs%smcl_soilt(1, 1, i) = real(soil_moisture(map_soil(1)+i-1), r_um)
    ! Unfrozen soil moisture proportion (sthu_soilt)
      psparms%sthu_soilt(1, 1, i) = real(unfrozen_soil_moisture(map_soil(1)+i-1), r_um)
    ! Frozen soil moisture proportion (sthf_soilt)
      psparms%sthf_soilt(1, 1, i) = real(frozen_soil_moisture(map_soil(1)+i-1), r_um)
    ! Water extraction (ext_soilt [=ext in bl_kernel))
      fluxes%ext_soilt(1, 1, i) = real(water_extraction(map_soil(1)+i-1), r_um)
    end do

    ! Soil thermal conductivity (hcon_soilt, hcons_soilt)
    psparms%hcon_soilt = real(soil_thermal_cond(map_2d(1)), r_um)
    hcons_soilt = real(thermal_cond_wet_soil(map_2d(1)), r_um)

    ! Soil and land ice ancils dependant on smvcst_soilt
    ! (soil moisture saturation limit)
    if ( psparms%smvcst_soilt(1, 1, 1) > 0.0_r_um ) then
      soil_pts = 1
      ainfo%soil_index = 1
      ainfo%l_soil_point = .true.
      lice_pts = 0
      ainfo%lice_index = 0
      ainfo%l_lice_point = .false.
    else
      soil_pts = 0
      ainfo%soil_index = 0
      ainfo%l_soil_point = .false.
      lice_pts = 1
      ainfo%lice_index = 1
      ainfo%l_lice_point = .true.
    end if

    ! Calculate the infiltration rate
    call infiltration_rate(land_pts, ntiles, surft_pts, ainfo%surft_index, &
                           psparms%satcon_soilt, ainfo%frac_surft, psparms%infil_surft)

    ! Canopy water on each tile (canopy_surft)
    do i = 1, n_land_tile
      progs%canopy_surft(1, i) = real(canopy_water(map_tile(1)+i-1), r_um)
    end do

    ! Snow prognostics
    i_snow = 0
    do i = 1, n_land_tile
      ! Lying snow mass on land tiles
      progs%snow_surft(1, i) = real(tile_snow_mass(map_tile(1)+i-1), r_um)
      ! Snow grain size on tiles (microns)
      progs%rgrain_surft(1, i) = real(tile_snow_rgrain(map_tile(1)+i-1), r_um)
      ! Number of snow layers on tiles (nsnow_surft)
      progs%nsnow_surft(1, i) = int(n_snow_layers(map_tile(1)+i-1), i_um)
      ! Snow depth on tiles (snowdepth_surft)
      progs%snowdepth_surft(1, i) = real(snow_depth(map_tile(1)+i-1), r_um)
      ! Snow mass under canopy
      progs%snow_grnd_surft(1, i) = real(snow_under_canopy(map_tile(1)+i-1), r_um)
      ! Snowpack density (rho_snow_grnd_surft)
      progs%rho_snow_grnd_surft(1, i) = real(snowpack_density(map_tile(1)+i-1), r_um)
      do j = 1, nsmax
        ! Thickness of snow layers
        progs%ds_surft(1, i, j) = real(snow_layer_thickness(map_snow(1)+i_snow), r_um)
        ! Mass of ice in snow layers
        progs%sice_surft(1, i, j) = real(snow_layer_ice_mass(map_snow(1)+i_snow), r_um)
        ! Mass of liquid in snow layers
        progs%sliq_surft(1, i, j) = real(snow_layer_liq_mass(map_snow(1)+i_snow), r_um)
        ! Temperature of snow layers
        progs%tsnow_surft(1, i, j) = real(snow_layer_temp(map_snow(1)+i_snow), r_um)
        ! Grain size of snow layers
        progs%rgrainl_surft(1, i, j) = real(snow_layer_rgrain(map_snow(1)+i_snow), r_um)
        ! Counting from 0 so increment here
        i_snow = i_snow + 1
      end do
    end do

    ! Snow melt
    do i = 1, n_land_tile
      fluxes%melt_surft(1, i) = real(total_snowmelt(map_tile(1)+i-1), r_um)
    end do

    ! Soil saturated fraction
    fsat_soilt = real(soil_sat_frac(map_2d(1)), r_um)

    ! Water table depth
    zw_soilt = real(water_table(map_2d(1)), r_um)

    ! Soil wetness below soil column
    sthzw_soilt = real(wetness_under_soil(map_2d(1)), r_um)

    ! Soil respiration (resp_s_soilt [=resp_s_soilt in bl_kernel])
    resp_s_soilt = real(soil_respiration(map_2d(1)), r_um)

  !----------------------------------------------------------------------------
  ! Call to surf_couple_extra using JULESvn5.4 standalone variable names

    !fqw_surft is not set so will not pick up value calculated via bl_imp
    !However, this is only presently used by river routing so has no effect in
    !LFRic

    !sw_surft is not set in this kernel so will not pick up appropriate values
    !This will affect water resource, irrigation and lakes once coupled to LFRic

    call surf_couple_extra(                                                   &
    !Driving data and associated INTENT(IN)
    u_1_ij, v_1_ij,                                                           &

    !Misc INTENT(IN)
    a_step, smlt, tile_frac, hcons_soilt, rho_star,                           &

    !IN
    land_pts, row_length, rows, river_row_length, river_rows,                 &
    ls_graup_ij, cca_2d_ij, nsurft, surft_pts,                                &
    lice_pts, soil_pts, stf_sub_surf_roff,fexp_soilt,                         &
    gamtot_soilt, ti_mean_soilt, ti_sig_soilt, cs_ch4_soilt, flash_rate_ancil,&
    pop_den_ancil, a_fsat_soilt, c_fsat_soilt, a_fwet_soilt, c_fwet_soilt,    &
    ntype, delta_lambda, delta_phi,                                &
    cos_theta_latitude, aocpl_row_length, aocpl_p_rows, xpa, xua, xva, ypa,   &
    yua, yva, g_p_field, g_r_field, nproc, global_row_length, global_rows,    &
    global_river_row_length, global_river_rows, flandg, trivdir, trivseq,     &
    r_area, slope, flowobs1, r_inext, r_jnext, r_land, frac_agr_gb,           &
    soil_clay_ij, resp_s_soilt, npp_gb,  u_s_std_surft,                       &

    !IN OUT
    a_steps_since_riv,                                                        &
    fsat_soilt, fwetl_soilt, zw_soilt, sthzw_soilt,                           &
    ls_rainfrac_gb, substore, surfstore, flowin, bflowin,                     &
    tot_surf_runoff, tot_sub_runoff, acc_lake_evap, twatstor,                 &
    asteps_since_triffid, resp_s_acc_gb_um,  cs_pool_gb_um,                   &
    inlandout_atm_gb,                                                         &

    !OUT- mostly for SCM diagnostics output below
    dhf_surf_minus_soil,                                                      &

    ! IN
    land_sea_mask,                                                            &

    ! JULES TYPES containing field data
    crop_vars, psparms, toppdm, fire_vars, ainfo, trif_vars, soilecosse,      &
    urban_param, progs, trifctltype, jules_vars,                              &
    fluxes, &
    lake_vars, &
    forcing &
    !rivers, &
    !veg3_parm, &
    !veg3_field, &
    !chemvars
    )

  !---------------------------------------------------------------------------
  ! Return the updated prognostic values to jules_prognostics.

    i_snow = 0
    do i = 1, n_land_tile
      ! Canopy water on each tile (canopy_surft)
      canopy_water(map_tile(1)+i-1) = real(progs%canopy_surft(1, i), r_def)
      ! Lying snow mass on land tiles
      tile_snow_mass(map_tile(1)+i-1) = real(progs%snow_surft(1, i), r_def)
      ! Number of snow layers on tiles (nsnow_surft)
      n_snow_layers(map_tile(1)+i-1) = real(progs%nsnow_surft(1, i), r_def)
      ! Snow depth on tiles (snowdepth_surft)
      snow_depth(map_tile(1)+i-1) = real(progs%snowdepth_surft(1, i), r_def)
      ! Snow grain size on tiles (microns)
      tile_snow_rgrain(map_tile(1)+i-1) = real(progs%rgrain_surft(1, i), r_def)
      ! Snow mass under canopy
      snow_under_canopy(map_tile(1)+i-1) = real(progs%snow_grnd_surft(1, i), r_def)
      ! Snowpack density (rho_snow_grnd_surft)
      snowpack_density(map_tile(1)+i-1) = real(progs%rho_snow_grnd_surft(1, i), r_def)
      ! Total snowmelt
      total_snowmelt(map_tile(1)+i-1) = real(fluxes%melt_surft(1, i), r_def)
      do j = 1, nsmax
        ! Thickness of snow layers
        snow_layer_thickness(map_snow(1)+i_snow) = real(progs%ds_surft(1, i, j), r_def)
        ! Mass of ice in snow layers
        snow_layer_ice_mass(map_snow(1)+i_snow) = real(progs%sice_surft(1, i, j), r_def)
        ! Mass of liquid in snow layers
        snow_layer_liq_mass(map_snow(1)+i_snow) = real(progs%sliq_surft(1, i, j), r_def)
        ! Temperature of snow layers
        snow_layer_temp(map_snow(1)+i_snow) = real(progs%tsnow_surft(1, i, j), r_def)
        ! Grain size of snow layers
        snow_layer_rgrain(map_snow(1)+i_snow) = real(progs%rgrainl_surft(1, i, j), r_def)
        ! Counting from 0 so increment here
        i_snow = i_snow + 1
      end do
    end do

    do i = 1, sm_levels
      ! Soil temperature (t_soil_soilt)
      soil_temperature(map_soil(1)+i-1) = real(progs%t_soil_soilt(1, 1, i), r_def)
      ! Soil moisture content (kg m-2, soil_layer_moisture)
      soil_moisture(map_soil(1)+i-1) = real(progs%smcl_soilt(1, 1, i), r_def)
      ! Unfrozen soil moisture proportion (sthu_soilt)
      unfrozen_soil_moisture(map_soil(1)+i-1) = real(psparms%sthu_soilt(1, 1, i), r_def)
      ! Frozen soil moisture proportion (sthf_soilt)
      frozen_soil_moisture(map_soil(1)+i-1) = real(psparms%sthf_soilt(1, 1, i), r_def)
    end do

    ! Soil saturated fraction
    soil_sat_frac(map_2d(1)) = real(fsat_soilt(1,1), r_def)

    ! Water table depth
    water_table(map_2d(1)) = real(zw_soilt(1,1), r_def)

    ! Wetness below soil column
    wetness_under_soil(map_2d(1)) = real(sthzw_soilt(1,1), r_def)

    if (.not. associated(soil_moisture_content, empty_real_data) ) then
      if (land_sea_mask(1,1)) then
        soil_moisture_content(map_2d(1)) = progs%smc_soilt(1,1)
      else
        soil_moisture_content(map_2d(1)) = 0.0_r_def
      end if
    end if

    if (.not. associated(grid_snow_mass, empty_real_data) ) then
      grid_snow_mass(map_2d(1)) = progs%snow_mass_ij(1,1)
    end if

    if (.not. associated(throughfall, empty_real_data) ) then
      do i = 1, n_land_tile
        throughfall(map_tile(1)+i-1) = fluxes%tot_tfall_surft(1,i)
      end do
    end if

    if (.not. associated(surface_runoff, empty_real_data) ) then
      surface_runoff(map_2d(1)) = fluxes%surf_roff_gb(1)
    end if

    if (.not. associated(sub_surface_runoff, empty_real_data) ) then
      sub_surface_runoff(map_2d(1)) = fluxes%sub_surf_roff_gb(1)
    end if

    ! Reset land_pts to 1 before exit
    ! This is required because prior to calling the kernel, it is unknown
    ! whether this is a land point or not. Local variables are dimensioned
    ! using this as their size, hence it needs to be at least 1 to allow
    ! for the possibility this is a land point. However it needs to be
    ! set correctly in the kernel for use in Jules. Therefore it needs setting
    ! back to 1 so that variables are dimensioned correctly on the next
    ! kernel call.
    land_pts = 1

  end subroutine jules_extra_code

end module jules_extra_kernel_mod
