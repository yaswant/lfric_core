!-----------------------------------------------------------------------------
! (c) Crown copyright 2018 Met Office. All rights reserved.
! The file LICENCE, distributed with this code, contains details of the terms
! under which the code may be used.
!-----------------------------------------------------------------------------
! @brief Interface to Jules for SW surface tile radiative properties

module sw_rad_tile_kernel_mod

use argument_mod,      only : arg_type,                  &
                              GH_FIELD, GH_SCALAR,       &
                              GH_REAL, GH_INTEGER,       &
                              GH_READ, GH_WRITE,         &
                              ANY_DISCONTINUOUS_SPACE_1, &
                              ANY_DISCONTINUOUS_SPACE_2, &
                              ANY_DISCONTINUOUS_SPACE_3, &
                              ANY_DISCONTINUOUS_SPACE_4, &
                              ANY_DISCONTINUOUS_SPACE_5, &
                              ANY_DISCONTINUOUS_SPACE_6, &
                              CELL_COLUMN
use fs_continuity_mod, only:  W3, WTheta
use constants_mod,     only : r_def, i_def, r_um, i_um
use kernel_mod,        only : kernel_type

implicit none

private

public :: sw_rad_tile_kernel_type
public :: sw_rad_tile_code

!------------------------------------------------------------------------------
! Public types
!------------------------------------------------------------------------------
! The type declaration for the kernel.
! Contains the metadata needed by the PSy layer.
type, extends(kernel_type) :: sw_rad_tile_kernel_type
  private
  type(arg_type) :: meta_args(25) = (/                                &
    arg_type(GH_FIELD, GH_REAL, GH_WRITE, ANY_DISCONTINUOUS_SPACE_1), & ! tile_sw_direct_albedo
    arg_type(GH_FIELD, GH_REAL, GH_WRITE, ANY_DISCONTINUOUS_SPACE_1), & ! tile_sw_diffuse_albedo
    arg_type(GH_FIELD, GH_REAL, GH_READ,  ANY_DISCONTINUOUS_SPACE_2), & ! tile_fraction
    arg_type(GH_FIELD, GH_REAL, GH_READ,  ANY_DISCONTINUOUS_SPACE_3), & ! leaf_area_index
    arg_type(GH_FIELD, GH_REAL, GH_READ,  ANY_DISCONTINUOUS_SPACE_3), & ! canopy_height
    arg_type(GH_FIELD, GH_REAL, GH_READ,  ANY_DISCONTINUOUS_SPACE_4), & ! sd_orog
    arg_type(GH_FIELD, GH_REAL, GH_READ,  ANY_DISCONTINUOUS_SPACE_4), & ! soil_albedo
    arg_type(GH_FIELD, GH_REAL, GH_READ,  ANY_DISCONTINUOUS_SPACE_4), & ! soil_roughness
    arg_type(GH_FIELD, GH_REAL, GH_READ,  ANY_DISCONTINUOUS_SPACE_4), & ! albedo_obs_vis
    arg_type(GH_FIELD, GH_REAL, GH_READ,  ANY_DISCONTINUOUS_SPACE_4), & ! albedo_obs_nir
    arg_type(GH_FIELD, GH_REAL, GH_WRITE, ANY_DISCONTINUOUS_SPACE_5), & ! albedo_obs_scaling
    arg_type(GH_FIELD, GH_REAL, GH_READ,  ANY_DISCONTINUOUS_SPACE_2), & ! tile_temperature
    arg_type(GH_FIELD, GH_REAL, GH_READ,  ANY_DISCONTINUOUS_SPACE_2), & ! tile_snow_mass
    arg_type(GH_FIELD, GH_REAL, GH_READ,  ANY_DISCONTINUOUS_SPACE_2), & ! tile_snow_rgrain
    arg_type(GH_FIELD, GH_REAL, GH_READ,  ANY_DISCONTINUOUS_SPACE_2), & ! snow_depth
    arg_type(GH_FIELD, GH_REAL, GH_READ,  ANY_DISCONTINUOUS_SPACE_2), & ! snowpack_density
    arg_type(GH_FIELD, GH_REAL, GH_READ,  ANY_DISCONTINUOUS_SPACE_4), & ! snow_soot
    arg_type(GH_FIELD, GH_REAL, GH_READ,  ANY_DISCONTINUOUS_SPACE_4), & ! chloro_sea
    arg_type(GH_FIELD, GH_REAL, GH_READ,  ANY_DISCONTINUOUS_SPACE_6), & ! sea_ice_thickness
    arg_type(GH_FIELD, GH_REAL, GH_READ,  W3),                        & ! u_in_w3
    arg_type(GH_FIELD, GH_REAL, GH_READ,  W3),                        & ! v_in_w3
    arg_type(GH_FIELD, GH_REAL, GH_READ,  WTHETA),                    & ! dz_wth
    arg_type(GH_FIELD, GH_REAL, GH_READ,  ANY_DISCONTINUOUS_SPACE_4), & ! z0msea
    arg_type(GH_FIELD, GH_REAL, GH_READ,  ANY_DISCONTINUOUS_SPACE_4), & ! cos_zenith_angle_rts
    arg_type(GH_SCALAR, GH_INTEGER, GH_READ)                          & ! n_band
    /)
  integer :: operates_on = CELL_COLUMN
contains
  procedure, nopass :: sw_rad_tile_code
end type

!------------------------------------------------------------------------------
! Contained functions/subroutines
!------------------------------------------------------------------------------
contains

!> @param[in]     nlayers                Number of layers
!> @param[in,out] tile_sw_direct_albedo  SW direct tile albedos
!> @param[in,out] tile_sw_diffuse_albedo SW diffuse tile albedos
!> @param[in]     tile_fraction          Surface tile fractions
!> @param[in]     leaf_area_index        Leaf Area Index
!> @param[in]     canopy_height          Canopy height
!> @param[in]     sd_orog                Standard deviation of orography
!> @param[in]     soil_albedo            Snow-free soil albedo
!> @param[in]     soil_roughness         Bare soil surface roughness length
!> @param[in]     albedo_obs_vis         Observed snow-free visible albedo
!> @param[in]     albedo_obs_nir         Observed snow-free near-IR albedo
!> @param[in,out] albedo_obs_scaling     Scaling factor to adjust albedos by
!> @param[in]     tile_temperature       Surface tile temperatures
!> @param[in]     tile_snow_mass         Snow mass on tiles (kg/m2)
!> @param[in]     tile_snow_rgrain       Snow grain size on tiles (microns)
!> @param[in]     snow_depth             Snow depth on tiles (m)
!> @param[in]     snowpack_density       Density of snow on ground (kg m-3)
!> @param[in]     snow_soot              Snow soot content (kg/kg)
!> @param[in]     chloro_sea             Chlorophyll content of the sea
!> @param[in]     sea_ice_thickness      Sea ice thickness (m)
!> @param[in]     u_in_w3                'Zonal' wind in density space
!> @param[in]     v_in_w3                'Meridional' wind in density space
!> @param[in]     dz_wth                 Delta z at wtheta levels
!> @param[in]     z0msea                 Roughness length of sea
!> @param[in]     cos_zenith_angle_rts   Cosine of the stellar zenith angle
!> @param[in]     n_band                 Number of spectral bands
!> @param[in]     ndf_sw_tile            DOFs per cell for tiles and sw bands
!> @param[in]     undf_sw_tile           Total DOFs for tiles and sw bands
!> @param[in]     map_sw_tile            Dofmap for cell at the base of the column
!> @param[in]     ndf_tile               Number of DOFs per cell for tiles
!> @param[in]     undf_tile              Number of total DOFs for tiles
!> @param[in]     map_tile               Dofmap for cell at the base of the column
!> @param[in]     ndf_pft                Number of DOFs per cell for PFTs
!> @param[in]     undf_pft               Number of total DOFs for PFTs
!> @param[in]     map_pft                Dofmap for cell at the base of the column
!> @param[in]     ndf_2d                 Number of DOFs per cell for 2D fields
!> @param[in]     undf_2d                Number of total DOFs for 2D fields
!> @param[in]     map_2d                 Dofmap for cell at the base of the column
!> @param[in]     ndf_scal               Number of DOFs per cell for albedo scaling
!> @param[in]     undf_scal              Number of total DOFs for albedo scaling
!> @param[in]     map_scal               Dofmap for cell at the base of the column
!> @param[in]     ndf_sice               Number of DOFs per cell for sea ice tiles
!> @param[in]     undf_sice              Number of total DOFs for sea ice tiles
!> @param[in]     map_sice               Dofmap for cell at the base of the column
!> @param[in]     ndf_w3                 Number of DOFs per cell for density space
!> @param[in]     undf_w3                Number of unique DOFs for density space
!> @param[in]     map_w3                 Dofmap for cell at the base of the column
!> @param[in]     ndf_wth                Number of DOFs per cell for theta space
!> @param[in]     undf_wth               Number of unique DOFs for theta space
!> @param[in]     map_wth                Dofmap for cell at the base of the column
subroutine sw_rad_tile_code(nlayers,                                &
                            tile_sw_direct_albedo,                  &
                            tile_sw_diffuse_albedo,                 &
                            tile_fraction,                          &
                            leaf_area_index,                        &
                            canopy_height,                          &
                            sd_orog,                                &
                            soil_albedo,                            &
                            soil_roughness,                         &
                            albedo_obs_vis,                         &
                            albedo_obs_nir,                         &
                            albedo_obs_scaling,                     &
                            tile_temperature,                       &
                            tile_snow_mass,                         &
                            tile_snow_rgrain,                       &
                            snow_depth,                             &
                            snowpack_density,                       &
                            snow_soot,                              &
                            chloro_sea,                             &
                            sea_ice_thickness,                      &
                            u_in_w3,                                &
                            v_in_w3,                                &
                            dz_wth,                                 &
                            z0msea,                                 &
                            cos_zenith_angle_rts,                   &
                            n_band,                                 &
                            ndf_sw_tile, undf_sw_tile, map_sw_tile, &
                            ndf_tile, undf_tile, map_tile,          &
                            ndf_pft, undf_pft, map_pft,             &
                            ndf_2d, undf_2d, map_2d,                &
                            ndf_scal, undf_scal, map_scal,          &
                            ndf_sice, undf_sice, map_sice,          &
                            ndf_w3, undf_w3, map_w3,                &
                            ndf_wth, undf_wth, map_wth)

  use socrates_init_mod,        only: wavelength_short, wavelength_long,      &
                                      weight_blue
  use atm_step_local,           only: dim_cs1
  use atm_fields_bounds_mod,    only: pdims_s, pdims
  use jules_control_init_mod,   only: n_surf_tile, n_land_tile, n_sea_tile,   &
                                      n_sea_ice_tile, first_sea_tile,         &
                                      first_sea_ice_tile
  use surface_config_mod,       only: albedo_obs
  use nlsizes_namelist_mod,     only: row_length, rows, land_field, ntiles,   &
                                  sm_levels, bl_levels
  use jules_surface_types_mod,  only: ntype, npft, ice, nnpft
  use jules_sea_seaice_mod,     only: nice, nice_use
  use ancil_info,               only: sea_pts, sice_pts_ncat, rad_nband,      &
                                      dim_cslayer, nsurft, nsoilt, nmasst
  use theta_field_sizes,        only: t_i_length, t_j_length,                 &
                                      u_i_length,u_j_length,                  &
                                      v_i_length,v_j_length
  use jules_vegetation_mod,     only: l_triffid, l_phenol, l_use_pft_psi,     &
                                      can_rad_mod, l_acclim
  use jules_soil_mod,           only: ns_deep, l_bedrock
  use jules_soil_biogeochem_mod, only: dim_ch4layer, soil_bgc_model,          &
                                        soil_model_ecosse, l_layeredc
  use jules_radiation_mod,      only: l_albedo_obs
  use jules_snow_mod,           only: nsmax, cansnowtile
  use jules_deposition_mod,     only: l_deposition
  use jules_surface_mod,        only: l_urban2t, l_flake_model
  use jules_urban_mod,          only: l_moruses
  use veg3_parm_mod,            only: l_veg3

  use prognostics,              only: progs_data_type, progs_type,            &
                                      prognostics_alloc, prognostics_assoc,   &
                                      prognostics_nullify, prognostics_dealloc
  use jules_vars_mod,           only: jules_vars_type, jules_vars_data_type,  &
                                      jules_vars_alloc, jules_vars_assoc,     &
                                      jules_vars_nullify, jules_vars_dealloc
  use p_s_parms,                only: psparms_type, psparms_data_type,        &
                                      psparms_alloc, psparms_assoc,           &
                                      psparms_nullify, psparms_dealloc
  use ancil_info,               only: ainfo_data_type, ainfo_type,            &
                                      ancil_info_alloc, ancil_info_assoc,     &
                                      ancil_info_nullify, ancil_info_dealloc
  use coastal,                  only: coastal_type, coastal_data_type,        &
                                      coastal_assoc, coastal_alloc,           &
                                      coastal_nullify, coastal_dealloc
  use urban_param_mod,          only: urban_param_data_type, urban_param_type, &
                                      urban_param_alloc, urban_param_assoc,   &
                                      urban_param_nullify, urban_param_dealloc
  use lake_mod,                 only: lake_data_type, lake_type,              &
                                      lake_assoc, lake_alloc, &
                                      lake_nullify, lake_dealloc
  use ancil_info,               only: ainfo_type, ainfo_data_type,            &
                                      ancil_info_assoc, ancil_info_alloc,     &
                                      ancil_info_dealloc, ancil_info_nullify
  use fluxes_mod,               only: fluxes_type, fluxes_data_type,          &
                                      fluxes_alloc, fluxes_assoc,             &
                                      fluxes_nullify, fluxes_dealloc
  use cable_fields_mod,         only: progs_cbl_vars
  
  use tilepts_mod, only: tilepts
  use sparm_mod, only: sparm
  use surf_couple_radiation_mod, only: surf_couple_radiation

  implicit none

  ! Arguments
  integer(i_def), intent(in) :: nlayers, n_band
  integer(i_def), intent(in) :: ndf_sw_tile, undf_sw_tile
  integer(i_def), intent(in) :: map_sw_tile(ndf_sw_tile)
  integer(i_def), intent(in) :: ndf_tile, undf_tile
  integer(i_def), intent(in) :: map_tile(ndf_tile)
  integer(i_def), intent(in) :: ndf_pft, undf_pft
  integer(i_def), intent(in) :: map_pft(ndf_pft)
  integer(i_def), intent(in) :: ndf_2d, undf_2d
  integer(i_def), intent(in) :: map_2d(ndf_2d)
  integer(i_def), intent(in) :: ndf_scal, undf_scal
  integer(i_def), intent(in) :: map_scal(ndf_scal)
  integer(i_def), intent(in) :: ndf_sice, undf_sice
  integer(i_def), intent(in) :: map_sice(ndf_sice)
  integer(i_def), intent(in) :: ndf_w3, undf_w3
  integer(i_def), intent(in) :: map_w3(ndf_w3)
  integer(i_def), intent(in) :: ndf_wth, undf_wth
  integer(i_def), intent(in) :: map_wth(ndf_wth)

  real(r_def), intent(inout) :: tile_sw_direct_albedo(undf_sw_tile)
  real(r_def), intent(inout) :: tile_sw_diffuse_albedo(undf_sw_tile)

  real(r_def), intent(in) :: tile_fraction(undf_tile)
  real(r_def), intent(in) :: tile_temperature(undf_tile)
  real(r_def), intent(in) :: tile_snow_mass(undf_tile)
  real(r_def), intent(in) :: tile_snow_rgrain(undf_tile)
  real(r_def), intent(in) :: snow_depth(undf_tile)
  real(r_def), intent(in) :: snowpack_density(undf_tile)

  real(r_def), intent(in) :: leaf_area_index(undf_pft)
  real(r_def), intent(in) :: canopy_height(undf_pft)

  real(r_def), intent(in)    :: sd_orog(undf_2d)
  real(r_def), intent(in)    :: soil_albedo(undf_2d)
  real(r_def), intent(in)    :: soil_roughness(undf_2d)
  real(r_def), intent(in)    :: albedo_obs_vis(undf_2d)
  real(r_def), intent(in)    :: albedo_obs_nir(undf_2d)
  real(r_def), intent(inout) :: albedo_obs_scaling(undf_scal)
  real(r_def), intent(in)    :: snow_soot(undf_2d)
  real(r_def), intent(in)    :: chloro_sea(undf_2d)
  real(r_def), intent(in)    :: z0msea(undf_2d)
  real(r_def), intent(in)    :: cos_zenith_angle_rts(undf_2d)

  real(r_def), intent(in) :: sea_ice_thickness(undf_sice)

  real(r_def), intent(in) :: u_in_w3(undf_w3)
  real(r_def), intent(in) :: v_in_w3(undf_w3)
  real(r_def), intent(in) :: dz_wth(undf_wth)

  ! Local variables for the kernel
  integer(i_def) :: i, i_tile, i_sice, i_band, n
  integer(i_def) :: df_rtile

  ! Inputs to surf_couple_radiation
  real(r_um), dimension(row_length, rows) :: &
    ws_10m_sea, chloro, flandg
  real(r_um), dimension(row_length, rows, nice_use) :: &
    ice_fract_cat
  real(r_um), dimension(land_field) :: &
    z0m_soil
  real(r_um), dimension(land_field, ntiles) :: &
    snow_surft, z0h_bare_tile, catch_snow_tile, catch_tile
  integer, dimension(ntype) :: &
    type_pts

  ! Outputs from surf_couple_radiation
  real(r_um), dimension(row_length, rows, 4) :: &
    sea_ice_albedo
  real(r_um), dimension(row_length, rows, ntiles, 2) :: &
    albobs_sc
  real(r_um), dimension(row_length, rows, 2, n_band) :: &
    open_sea_albedo

  !-----------------------------------------------------------------------
  ! JULES Types
  !-----------------------------------------------------------------------
  type(progs_type) :: progs
  type(progs_data_type) :: progs_data
  type(jules_vars_type) :: jules_vars
  type(jules_vars_data_type) :: jules_vars_data
  type(psparms_type) :: psparms
  type(psparms_data_type) :: psparms_data
  type(ainfo_type) :: ainfo
  type(ainfo_data_type) :: ainfo_data
  type(urban_param_type) :: urban_param
  type(urban_param_data_type) :: urban_param_data
  type(coastal_type) :: coast
  type(coastal_data_type) :: coastal_data
  type(lake_type) :: lake_vars
  type(lake_data_type) :: lake_data
  type(fluxes_type) :: fluxes
  type(fluxes_data_type) :: fluxes_data

  !-----------------------------------------------------------------------
  ! Initialisation of JULES data and pointer types
  !-----------------------------------------------------------------------
  call prognostics_alloc(land_field, t_i_length, t_j_length,                  &
                      nsurft, npft, nsoilt, sm_levels, ns_deep, nsmax,        &
                      dim_cslayer, dim_cs1, dim_ch4layer,                     &
                      nice, nice_use, soil_bgc_model, soil_model_ecosse,      &
                      l_layeredc, l_triffid, l_phenol, l_bedrock, l_veg3,     &
                      nmasst, nnpft, l_acclim, progs_data)
  call prognostics_assoc(progs,progs_data)

  call jules_vars_alloc(land_field,ntype,nsurft,rad_nband,nsoilt,sm_levels,   &
                t_i_length, t_j_length, npft, bl_levels, pdims_s, pdims,      &
                l_albedo_obs, cansnowtile, l_deposition,                      &
                jules_vars_data)
  call jules_vars_assoc(jules_vars,jules_vars_data)

  if (can_rad_mod == 6) then
    jules_vars%diff_frac = 0.4_r_um
  else
    jules_vars%diff_frac = 0.0_r_um
  end if

  call psparms_alloc(land_field,t_i_length,t_j_length,                        &
                   nsoilt,sm_levels,dim_cslayer,nsurft,npft,                  &
                   soil_bgc_model,soil_model_ecosse,l_use_pft_psi,            &
                   psparms_data)
  call psparms_assoc(psparms, psparms_data)

  call ancil_info_alloc(land_field,t_i_length,t_j_length,                     &
                      nice,nsoilt,ntype,                                      &
                      ainfo_data)
  call ancil_info_assoc(ainfo, ainfo_data)

  call urban_param_alloc(land_field, l_urban2t, l_moruses, urban_param_data)
  call urban_param_assoc(urban_param, urban_param_data)

  call coastal_alloc(land_field,t_i_length,t_j_length,                        &
                   u_i_length,u_j_length,                                     &
                   v_i_length,v_j_length,                                     &
                   nice_use,nice,coastal_data)
  call coastal_assoc(coast, coastal_data)

  call lake_alloc(land_field, l_flake_model, lake_data)
  call lake_assoc(lake_vars, lake_data)

  call fluxes_alloc(land_field, t_i_length, t_j_length,                       &
                      nsurft, npft, nsoilt, sm_levels,                        &
                      nice, nice_use,                                         &
                      fluxes_data)
  call fluxes_assoc(fluxes, fluxes_data)
  ! ---------------------------------------------------------------------------
  ! SW tile albedos
  ! ---------------------------------------------------------------------------

  ! Land tile fractions
  flandg = 0.0_r_um
  do i = 1, n_land_tile
    flandg = flandg + real(tile_fraction(map_tile(1)+i-1), r_um)
    ainfo%frac_surft(1, i) = real(tile_fraction(map_tile(1)+i-1), r_um)
  end do

  ! Sea-ice fraction
  i_sice = 0
  ainfo%ice_fract_ij = 0.0_r_um
  do i = first_sea_ice_tile, first_sea_ice_tile + n_sea_ice_tile - 1
    i_sice = i_sice + 1
    ainfo%ice_fract_ij = ainfo%ice_fract_ij + real(tile_fraction(map_tile(1)+i-1), r_um)
    ice_fract_cat(1, 1, i_sice) = real(tile_fraction(map_tile(1)+i-1), r_um)
  end do

  ! Because Jules tests on flandg < 1, we need to ensure this is exactly
  ! 1 when no sea or sea-ice is present
  if ( tile_fraction(map_tile(1)+first_sea_tile-1) == 0.0_r_def .and. &
       ainfo%ice_fract_ij(1,1) == 0.0_r_um) then
    flandg(1,1) = 1.0_r_um
  end if

  ! Jules requires fractions with respect to the land area
  if (flandg(1, 1) > 0.0_r_um) then
    land_field = 1
    ainfo%land_index = 1
    ainfo%frac_surft(1, 1:n_land_tile) = ainfo%frac_surft(1, 1:n_land_tile) / &
         flandg(1, 1)
  else
    land_field = 0
    ainfo%land_index = 0
  end if

  if (tile_fraction(map_tile(1)+ice-1) > 0.0_r_def) then
    ainfo%l_lice_point = .true.
  else
    ainfo%l_lice_point = .false.
  end if

  ! Set type_pts and type_index
  call tilepts(land_field, ainfo%frac_surft, type_pts, ainfo%surft_index, &
       ainfo%l_lice_point)

  ! Land tile temperatures
  do i = 1, n_land_tile
    progs%tstar_surft(1, i) = real(tile_temperature(map_tile(1)+i-1), r_um)
  end do

  ! Sea temperature
  fluxes%tstar_ij = real(tile_temperature(map_tile(1)+first_sea_tile-1), r_um)

  ! Sea-ice temperatures
  i_sice = 0
  do i = first_sea_ice_tile, first_sea_ice_tile + n_sea_ice_tile - 1
    i_sice = i_sice + 1
    coast%tstar_sice_sicat(1, 1, i_sice) = real(tile_temperature(map_tile(1)+i-1), r_um)
  end do

  ! Jules requires sea-ice fractions with respect to the sea area
  if (ainfo%ice_fract_ij(1, 1) > 0.0_r_um) then
    ainfo%ice_fract_ij(1, 1) = ainfo%ice_fract_ij(1, 1) / (1.0_r_um - flandg(1, 1))
    ice_fract_cat(1, 1, 1:n_sea_ice_tile) &
         = ice_fract_cat(1, 1, 1:n_sea_ice_tile) / (1.0_r_um - flandg(1, 1))
  end if

  ! Combined sea and sea-ice index
  if (flandg(1, 1) < 1.0_r_um) then
    ainfo%ssi_index = 1
  else
    ainfo%ssi_index = 0
  end if

  ! Individual sea and sea-ice indices
  ! first set defaults
  sea_pts = 0
  ainfo%sea_index = 0
  ! Then adjust based on state
  if (ainfo%ssi_index(1) > 0) then
    if (ainfo%ice_fract_ij(1, 1) < 1.0_r_um) then
      sea_pts = 1
      ainfo%sea_index = 1
    end if
  end if

  ! Multi-category sea-ice index
  do n = 1, nice_use
    if (ainfo%ssi_index(1) > 0 .and. ice_fract_cat(1, 1, n) > 0.0_r_um) then
      sice_pts_ncat(n) = 1
      ainfo%sice_index_ncat(1, n) = 1
      ainfo%sice_frac_ncat(1, n) = ice_fract_cat(1, 1, n)
    else
      sice_pts_ncat(n) = 0
      ainfo%sice_index_ncat(1, n) = 0
      ainfo%sice_frac_ncat(1, n) = 0.0_r_um
    end if
  end do

  do n = 1, n_sea_ice_tile
    ! Sea-ice thickness
    progs%di_ncat_sicat(1,1,n) = real(sea_ice_thickness(map_sice(1)+n-1),r_um)
  end do

  do n = 1, npft
    ! Leaf area index
    progs%lai_pft(1, n) = real(leaf_area_index(map_pft(1)+n-1), r_um)
    ! Canopy height
    progs%canht_pft(1, n) = real(canopy_height(map_pft(1)+n-1), r_um)
  end do

  ! Roughness length (z0_surft)
  z0m_soil = real(soil_roughness(map_2d(1)), r_um)
  call sparm(land_field, n_land_tile, type_pts, ainfo%surft_index, &
       ainfo%frac_surft, progs%canht_pft, progs%lai_pft, z0m_soil, &
       catch_snow_tile, catch_tile, psparms%z0_surft, z0h_bare_tile, &
       urban_param%ztm_gb)

  ! Snow-free soil albedo
  psparms%albsoil_soilt = real(soil_albedo(map_2d(1)), r_um)

  ! Cosine of the solar zenith angle
  psparms%cosz_ij = real(cos_zenith_angle_rts(map_2d(1)), r_um)

  ! Standard deviation of orography - note that the variables names here
  ! appear to mismatch; this mirrors what is done in the UM; it's possible
  ! that the variable is misnamed in JULES
  jules_vars%ho2r2_orog_gb = real(sd_orog(map_2d(1)), r_um)

  ! 10m wind speed over the sea
  ws_10m_sea = sqrt(u_in_w3(map_w3(1))**2 + v_in_w3(map_w3(1))**2) &
       * log(10.0_r_def / z0msea(map_2d(1))) &
       / log((dz_wth(map_wth(1))) / z0msea(map_2d(1)))

  ! Chlorophyll content of the sea
  chloro = real(chloro_sea(map_2d(1)), r_um)

  ! Observed albedo
  psparms%albobs_vis_gb = real(albedo_obs_vis(map_2d(1)), r_um)
  psparms%albobs_nir_gb = real(albedo_obs_nir(map_2d(1)), r_um)

  ! Lying snow mass on land tiles
  do i = 1, n_land_tile
    snow_surft(1, i) = real(tile_snow_mass(map_tile(1)+i-1), r_um)
    progs%rgrain_surft(1, i) = real(tile_snow_rgrain(map_tile(1)+i-1), r_um)
    progs%snowdepth_surft(1, i) = real(snow_depth(map_tile(1)+i-1), r_um)
    progs%rho_snow_grnd_surft(1, i) = real(snowpack_density(map_tile(1)+i-1), r_um)
  end do

  ! Lying snow mass on sea ice categories
  i_sice = 0
  do i = first_sea_ice_tile, first_sea_ice_tile + n_sea_ice_tile - 1
    i_sice = i_sice + 1
    progs%snow_mass_sea_sicat(1, 1, i_sice) = real(tile_snow_mass(map_tile(1)+i-1), r_um)
  end do

  ! Snow soot content
  progs%soot_ij = real(snow_soot(map_2d(1)), r_um)

  call surf_couple_radiation( &
    ! Misc INTENT(IN)
    ws_10m_sea, chloro, &
    n_band, n_band, wavelength_short, wavelength_long, &
    ! Misc INTENT(OUT)
    sea_ice_albedo, &
    ! (ancil_info mod)
    ntiles, land_field, type_pts, row_length, rows, &
    ! (coastal mod)
    flandg, &
    ! (prognostics mod)
    snow_surft, &
    ! UM-only args: INTENT(OUT)
    albobs_sc, open_sea_albedo, &
    ! JULES types
    psparms, ainfo, urban_param, progs, coast, jules_vars, &
    fluxes, lake_vars, &
    progs_cbl_vars  &
    )

  df_rtile = 0
  do i_band = 1, n_band
    ! Land tile albedos
    df_rtile = n_surf_tile*(i_band-1)
    do i_tile = 1, n_land_tile
      df_rtile = df_rtile + 1
      if (tile_fraction(map_tile(1)+i_tile-1) > 0.0_r_def) then
        tile_sw_direct_albedo(map_sw_tile(1)+df_rtile-1) &
             = weight_blue(i_band) &
             * real(fluxes%alb_surft(1, i_tile, 1), r_def) & ! visible direct albedo
             + (1.0_r_def - weight_blue(i_band)) &
             * real(fluxes%alb_surft(1, i_tile, 3), r_def)   ! near-ir direct albedo
        tile_sw_diffuse_albedo(map_sw_tile(1)+df_rtile-1) &
             = weight_blue(i_band) &
             * real(fluxes%alb_surft(1, i_tile, 2), r_def) & ! visible diffuse albedo
             + (1.0_r_def - weight_blue(i_band)) &
             * real(fluxes%alb_surft(1, i_tile, 4), r_def)   ! near-ir diffuse albedo
      else
        tile_sw_direct_albedo(map_sw_tile(1)+df_rtile-1) = 0.0_r_def
        tile_sw_diffuse_albedo(map_sw_tile(1)+df_rtile-1) = 0.0_r_def
      end if
    end do

    ! Sea tile albedos
    df_rtile = first_sea_tile-1 + n_surf_tile*(i_band-1)
    do i_tile = first_sea_tile, first_sea_tile + n_sea_tile - 1
      df_rtile = df_rtile + 1
      if (tile_fraction(map_tile(1)+i_tile-1) > 0.0_r_def) then
        tile_sw_direct_albedo(map_sw_tile(1)+df_rtile-1) &
             = real(open_sea_albedo(1, 1, 1, i_band), r_def)
        tile_sw_diffuse_albedo(map_sw_tile(1)+df_rtile-1) &
             = real(open_sea_albedo(1, 1, 2, i_band), r_def)
      else
        tile_sw_direct_albedo(map_sw_tile(1)+df_rtile-1) = 0.0_r_def
        tile_sw_diffuse_albedo(map_sw_tile(1)+df_rtile-1) = 0.0_r_def
      end if
    end do

    ! Sea-ice tile albedos
    df_rtile = first_sea_ice_tile-1 + n_surf_tile*(i_band-1)
    do i_tile = first_sea_ice_tile, first_sea_ice_tile + n_sea_ice_tile - 1
      df_rtile = df_rtile + 1
      if (tile_fraction(map_tile(1)+i_tile-1) > 0.0_r_def) then
        tile_sw_direct_albedo(map_sw_tile(1)+df_rtile-1) &
             = weight_blue(i_band) &
             * real(sea_ice_albedo(1, 1, 1), r_def) &
             + (1.0_r_def - weight_blue(i_band)) &
             * real(sea_ice_albedo(1, 1, 3), r_def)
        tile_sw_diffuse_albedo(map_sw_tile(1)+df_rtile-1) &
             = weight_blue(i_band) &
             * real(sea_ice_albedo(1, 1, 2), r_def) &
             + (1.0_r_def - weight_blue(i_band)) &
             * real(sea_ice_albedo(1, 1, 4), r_def)
      else
        tile_sw_direct_albedo(map_sw_tile(1)+df_rtile-1) = 0.0_r_def
        tile_sw_diffuse_albedo(map_sw_tile(1)+df_rtile-1) = 0.0_r_def
      end if
    end do
  end do

  ! Scaling factors needed for use in surface exchange code
  if (albedo_obs .and. flandg(1, 1) > 0.0_r_um) then
    df_rtile = 0
    do i_band = 1, rad_nband
      do i_tile = 1, n_land_tile
        albedo_obs_scaling(map_scal(1)+df_rtile) = &
             jules_vars%albobs_scaling_surft(1,i_tile,i_band)
        ! Counting from 0 so increment index here
        df_rtile = df_rtile + 1
      end do
    end do
  end if

  ! set this back to 1 before exit
  land_field = 1

  call ancil_info_nullify(ainfo)
  call ancil_info_dealloc(ainfo_data)

  call lake_nullify(lake_vars)
  call lake_dealloc(lake_data)

  call coastal_nullify(coast)
  call coastal_dealloc(coastal_data)

  call urban_param_nullify(urban_param)
  call urban_param_dealloc(urban_param_data)

  call psparms_nullify(psparms)
  call psparms_dealloc(psparms_data)

  call jules_vars_dealloc(jules_vars_data)
  call jules_vars_nullify(jules_vars)

  call prognostics_nullify(progs)
  call prognostics_dealloc(progs_data)

  call fluxes_nullify(fluxes)
  call fluxes_dealloc(fluxes_data)

end subroutine sw_rad_tile_code

end module sw_rad_tile_kernel_mod
