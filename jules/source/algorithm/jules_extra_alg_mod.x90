!-------------------------------------------------------------------------------
! (c) Crown copyright 2020 Met Office. All rights reserved.
! The file LICENCE, distributed with this code, contains details of the terms
! under which the code may be used.
!-------------------------------------------------------------------------------
!> @brief Interface to the JULES surf_couple_extra scheme

module jules_extra_alg_mod

  use constants_mod,                 only: i_def
  use field_mod,                     only: field_type
  use field_collection_mod,          only: field_collection_type
  use function_space_mod,            only: function_space_type
  use function_space_collection_mod, only: function_space_collection
  use fs_continuity_mod,             only: W3
  use lfric_xios_write_mod,          only: write_field_single_face
  use field_parent_mod,              only: write_interface
  use high_to_multi_kernel_mod,      only: high_to_multi_kernel_type
  use jules_control_init_mod,        only: n_land_tile
  use nlsizes_namelist_mod,          only: sm_levels
  use jules_physics_init_mod,        only: snow_lev_tile

  use io_config_mod, only: subroutine_timers
  use timer_mod,     only: timer

  ! xios output
  use io_config_mod, only: write_diag, use_xios_io

  implicit none

  private
  public jules_extra_alg

contains

  !> @brief Run the JULES surf_couple_extra scheme
  !> @details JULES surf_couple_extra calculates the surface and soil fluxes
  !> and stores of Water (via the hydrol{ogy}, snow and river_control
  !> modules) and Carbon (via triffid and inferno/fire modules). Note: only the
  !> hydrol and snow components are implemented so far.
  !>
  !> @param[in]     microphysics_fields Fields for mphys scheme
  !> @param[in]     convection_fields   Fields for convection scheme
  !> @param[in,out] surface_fields      Fields for surface scheme
  !> @param[in,out] soil_fields         Fields for soil hydrology scheme
  !> @param[in,out] snow_fields         Fields for snow scheme
  !> @param[in]     snow_sublimation    Sublimation of snow (kg m-2 s-1)
  !> @param[in]     surf_heat_flux      Net surface heat flux (W m-2)
  !> @param[in]     canopy_evap         Canopy evaporation from land tiles (kg m-2 s-1)
  !> @param[in]     water_extraction    Extraction of water from each soil layer (kg m-2 s-1)
  !> @param[in,out] total_snowmelt      Surface plus canopy snowmelt rate (kg m-2 s-1)
  subroutine jules_extra_alg(microphysics_fields, convection_fields,      &
                             surface_fields, soil_fields, snow_fields,    &
                             snow_sublimation, surf_heat_flux,            &
                             canopy_evap, water_extraction, total_snowmelt)

    use jules_extra_kernel_mod, only: jules_extra_kernel_type

    implicit none

    type( field_collection_type ), intent(in)    :: microphysics_fields
    type( field_collection_type ), intent(in)    :: convection_fields
    type( field_collection_type ), intent(inout) :: surface_fields
    type( field_collection_type ), intent(inout) :: soil_fields
    type( field_collection_type ), intent(inout) :: snow_fields

    type( field_type ), intent (in) :: snow_sublimation
    type( field_type ), intent (in) :: surf_heat_flux
    type( field_type ), intent (in) :: canopy_evap
    type( field_type ), intent (in) :: water_extraction
    type( field_type ), intent (inout) :: total_snowmelt

    ! Temporary fields: unpacked fields from collections
    type( field_type ), pointer :: ls_rain                => null()
    type( field_type ), pointer :: ls_snow                => null()
    type( field_type ), pointer :: conv_rain              => null()
    type( field_type ), pointer :: conv_snow              => null()

    type( field_type ), pointer :: tile_fraction          => null()
    type( field_type ), pointer :: leaf_area_index        => null()
    type( field_type ), pointer :: canopy_height          => null()
    type( field_type ), pointer :: tile_temperature       => null()
    type( field_type ), pointer :: net_prim_prod          => null()
    type( field_type ), pointer :: canopy_water           => null()

    type( field_type ), pointer :: soil_moist_wilt        => null()
    type( field_type ), pointer :: soil_moist_crit        => null()
    type( field_type ), pointer :: soil_moist_sat         => null()
    type( field_type ), pointer :: soil_cond_sat          => null()
    type( field_type ), pointer :: soil_thermal_cap       => null()
    type( field_type ), pointer :: soil_thermal_cond      => null()
    type( field_type ), pointer :: soil_suction_sat       => null()
    type( field_type ), pointer :: clapp_horn_b           => null()
    type( field_type ), pointer :: soil_carbon_content    => null()
    type( field_type ), pointer :: mean_topog_index       => null()
    type( field_type ), pointer :: a_sat_frac             => null()
    type( field_type ), pointer :: c_sat_frac             => null()
    type( field_type ), pointer :: a_wet_frac             => null()
    type( field_type ), pointer :: c_wet_frac             => null()
    type( field_type ), pointer :: soil_respiration       => null()
    type( field_type ), pointer :: thermal_cond_wet_soil  => null()
    type( field_type ), pointer :: soil_temperature       => null()
    type( field_type ), pointer :: soil_moisture          => null()
    type( field_type ), pointer :: unfrozen_soil_moisture => null()
    type( field_type ), pointer :: frozen_soil_moisture   => null()
    type( field_type ), pointer :: soil_sat_frac          => null()
    type( field_type ), pointer :: water_table            => null()
    type( field_type ), pointer :: wetness_under_soil     => null()

    type( field_type ), pointer :: tile_snow_mass         => null()
    type( field_type ), pointer :: tile_snow_rgrain       => null()
    type( field_type ), pointer :: n_snow_layers          => null()
    type( field_type ), pointer :: snow_depth             => null()
    type( field_type ), pointer :: snow_under_canopy      => null()
    type( field_type ), pointer :: snowpack_density       => null()
    type( field_type ), pointer :: snow_layer_thickness   => null()
    type( field_type ), pointer :: snow_layer_ice_mass    => null()
    type( field_type ), pointer :: snow_layer_liq_mass    => null()
    type( field_type ), pointer :: snow_layer_temp        => null()
    type( field_type ), pointer :: snow_layer_rgrain      => null()
    type( field_type ), pointer :: snow_unload_rate       => null()

    type( field_type ) :: tmp_diag
    type(function_space_type), pointer  :: tmp_space => null()
    procedure(write_interface), pointer :: write_diag_behaviour => null()

    if ( subroutine_timers ) call timer('jules_extra_alg')

    ! Unpack precipitation fields
    ls_rain                => microphysics_fields%get_field('ls_rain')
    ls_snow                => microphysics_fields%get_field('ls_snow')
    conv_rain              => convection_fields%get_field('conv_rain')
    conv_snow              => convection_fields%get_field('conv_snow')

    ! Surface fields
    tile_fraction          => surface_fields%get_field('tile_fraction')
    leaf_area_index        => surface_fields%get_field('leaf_area_index')
    canopy_height          => surface_fields%get_field('canopy_height')
    tile_temperature       => surface_fields%get_field('tile_temperature')
    net_prim_prod          => surface_fields%get_field('net_prim_prod')
    canopy_water           => surface_fields%get_field('canopy_water')

    ! Soil fields
    soil_moist_wilt        => soil_fields%get_field('soil_moist_wilt')
    soil_moist_crit        => soil_fields%get_field('soil_moist_crit')
    soil_moist_sat         => soil_fields%get_field('soil_moist_sat')
    soil_cond_sat          => soil_fields%get_field('soil_cond_sat')
    soil_thermal_cap       => soil_fields%get_field('soil_thermal_cap')
    soil_thermal_cond      => soil_fields%get_field('soil_thermal_cond')
    soil_suction_sat       => soil_fields%get_field('soil_suction_sat')
    clapp_horn_b           => soil_fields%get_field('clapp_horn_b')
    soil_carbon_content    => soil_fields%get_field('soil_carbon_content')
    mean_topog_index       => soil_fields%get_field('mean_topog_index')
    a_sat_frac             => soil_fields%get_field('a_sat_frac')
    c_sat_frac             => soil_fields%get_field('c_sat_frac')
    a_wet_frac             => soil_fields%get_field('a_wet_frac')
    c_wet_frac             => soil_fields%get_field('c_wet_frac')
    soil_respiration       => soil_fields%get_field('soil_respiration')
    thermal_cond_wet_soil  => soil_fields%get_field('thermal_cond_wet_soil')
    soil_temperature       => soil_fields%get_field('soil_temperature')
    soil_moisture          => soil_fields%get_field('soil_moisture')
    unfrozen_soil_moisture => soil_fields%get_field('unfrozen_soil_moisture')
    frozen_soil_moisture   => soil_fields%get_field('frozen_soil_moisture')
    soil_sat_frac          => soil_fields%get_field('soil_sat_frac')
    water_table            => soil_fields%get_field('water_table')
    wetness_under_soil     => soil_fields%get_field('wetness_under_soil')

    ! Snow fields
    tile_snow_mass         => snow_fields%get_field('tile_snow_mass')
    tile_snow_rgrain       => snow_fields%get_field('tile_snow_rgrain')
    n_snow_layers          => snow_fields%get_field('n_snow_layers')
    snow_depth             => snow_fields%get_field('snow_depth')
    snow_under_canopy      => snow_fields%get_field('snow_under_canopy')
    snowpack_density       => snow_fields%get_field('snowpack_density')
    snow_layer_thickness   => snow_fields%get_field('snow_layer_thickness')
    snow_layer_ice_mass    => snow_fields%get_field('snow_layer_ice_mass')
    snow_layer_liq_mass    => snow_fields%get_field('snow_layer_liq_mass')
    snow_layer_temp        => snow_fields%get_field('snow_layer_temp')
    snow_layer_rgrain      => snow_fields%get_field('snow_layer_rgrain')
    snow_unload_rate       => snow_fields%get_field('snow_unload_rate')

    call invoke(jules_extra_kernel_type( ls_rain, conv_rain, ls_snow,          &
                           conv_snow, tile_fraction, leaf_area_index,          &
                           canopy_height, snow_unload_rate,                    &
                           soil_moist_wilt, soil_moist_crit,                   &
                           soil_moist_sat, soil_cond_sat, soil_thermal_cap,    &
                           soil_thermal_cond, soil_suction_sat, clapp_horn_b,  &
                           soil_carbon_content,                                &
                           mean_topog_index, a_sat_frac, c_sat_frac,           &
                           a_wet_frac, c_wet_frac, tile_temperature,           &
                           net_prim_prod, snow_sublimation, surf_heat_flux,    &
                           canopy_evap, water_extraction,                      &
                           thermal_cond_wet_soil, soil_respiration,            &
                           soil_temperature, soil_moisture,                    &
                           unfrozen_soil_moisture, frozen_soil_moisture,       &
                           canopy_water, tile_snow_mass, tile_snow_rgrain,     &
                           n_snow_layers, snow_depth, snow_under_canopy,       &
                           snowpack_density, snow_layer_thickness,             &
                           snow_layer_ice_mass, snow_layer_liq_mass,           &
                           snow_layer_temp, snow_layer_rgrain, total_snowmelt, &
                           soil_sat_frac, water_table,                         &
                           wetness_under_soil) )

    if (use_xios_io .and. write_diag) then

      ! Make a 2D multi-data field for temporary diagnostic output
      ! (this can be retired when the fields themselves are multi-data)
      tmp_space => function_space_collection%get_fs( ls_rain%get_mesh_id(), 0, &
                                                     W3, sm_levels )
      call tmp_diag%initialise(tmp_space)
      write_diag_behaviour => write_field_single_face
      call tmp_diag%set_write_behaviour(write_diag_behaviour)

      ! Soil diagnostics
      call invoke( high_to_multi_kernel_type(tmp_diag, soil_moisture, &
                                             sm_levels) )
      call tmp_diag%write_field('soil_moisture')
      call invoke( high_to_multi_kernel_type(tmp_diag, unfrozen_soil_moisture, &
                                             sm_levels) )
      call tmp_diag%write_field('unfrozen_soil_moisture')
      call invoke( high_to_multi_kernel_type(tmp_diag, soil_temperature, &
                                             sm_levels) )
      call tmp_diag%write_field('soil_temperature')

      call soil_respiration%write_field('soil_respiration')
      call water_table%write_field('water_table')

      ! Remake differet size 2D multi-data field for temporary diagnostic output
      tmp_space => function_space_collection%get_fs( ls_rain%get_mesh_id(), 0, &
                                                     W3, n_land_tile )
      call tmp_diag%field_final()
      call tmp_diag%initialise(tmp_space)
      call tmp_diag%set_write_behaviour(write_diag_behaviour)

      ! Snow diagnostics
      call invoke( high_to_multi_kernel_type(tmp_diag, total_snowmelt, &
                                             n_land_tile) )
      call tmp_diag%write_field('total_snowmelt')
      call invoke( high_to_multi_kernel_type(tmp_diag, snow_sublimation, &
                                             n_land_tile) )
      call tmp_diag%write_field('snow_sublimation')
      call invoke( high_to_multi_kernel_type(tmp_diag, snow_depth, &
                                             n_land_tile) )
      call tmp_diag%write_field('snow_depth')
      call invoke( high_to_multi_kernel_type(tmp_diag, tile_snow_rgrain, &
                                             n_land_tile) )
      call tmp_diag%write_field('tile_snow_rgrain')
      call invoke( high_to_multi_kernel_type(tmp_diag, snowpack_density, &
                                             n_land_tile) )
      call tmp_diag%write_field('snowpack_density')

      ! Remake differet size 2D multi-data field for temporary diagnostic output
      tmp_space => function_space_collection%get_fs( ls_rain%get_mesh_id(), 0, &
                                                     W3, snow_lev_tile )
      call tmp_diag%field_final()
      call tmp_diag%initialise(tmp_space)
      call tmp_diag%set_write_behaviour(write_diag_behaviour)

      call invoke( high_to_multi_kernel_type(tmp_diag, snow_layer_temp, &
                                             snow_lev_tile) )
      call tmp_diag%write_field('snow_layer_temp')
      call invoke( high_to_multi_kernel_type(tmp_diag, snow_layer_thickness, &
                                             snow_lev_tile) )
      call tmp_diag%write_field('snow_layer_thickness')
      call invoke( high_to_multi_kernel_type(tmp_diag, snow_layer_ice_mass, &
                                             snow_lev_tile) )
      call tmp_diag%write_field('snow_layer_ice_mass')
      call invoke( high_to_multi_kernel_type(tmp_diag, snow_layer_liq_mass, &
                                             snow_lev_tile) )
      call tmp_diag%write_field('snow_layer_liq_mass')
      call invoke( high_to_multi_kernel_type(tmp_diag, snow_layer_rgrain, &
                                             snow_lev_tile) )
      call tmp_diag%write_field('snow_layer_rgrain')

    end if

    if ( subroutine_timers ) call timer('jules_extra_alg')

  end subroutine jules_extra_alg

end module jules_extra_alg_mod
