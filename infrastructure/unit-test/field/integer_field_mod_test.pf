!-----------------------------------------------------------------------------
! Copyright (c) 2017,  Met Office, on behalf of HMSO and Queen's Printer
! For further details please refer to the file LICENCE.original which you
! should have received as part of this distribution.
!-----------------------------------------------------------------------------

!> Test the integer field representation
!>
!-------------------------------------------------------------------------------
module integer_field_mod_test

  use constants_mod,                  only : i_def, r_def, str_def
  use halo_comms_mod,                 only : initialise_halo_comms, &
                                             finalise_halo_comms
  use halo_routing_collection_mod,    only : halo_routing_collection_type, &
                                             halo_routing_collection
  use halo_comms_mod,                 only : halo_routing_type
  use function_space_collection_mod,  only : function_space_collection_type, &
                                             function_space_collection
  use lfric_mpi_mod,                  only : global_mpi, &
                                             lfric_comm_type
  use local_mesh_mod,                 only : local_mesh_type
  use mesh_collection_mod,            only : mesh_collection_type, &
                                             mesh_collection
  use mesh_mod,                       only : mesh_type, PLANE_BI_PERIODIC
  use function_space_mod,             only : function_space_type
  use fs_continuity_mod,              only : W2
  use pfunit

  implicit none

  private
  public :: test_integer_field, test_integer_field_proxy
  !
  ! pFUnit depends on symbol spill
  !
  public :: MPITestCase, MPITestparameter


  @TestCase
  type, extends(MPITestCase), public :: integer_field_test_type
    private
    type(function_space_type), pointer :: w2_fs => null()
    type(local_mesh_type)    :: unit_test_local_mesh
    type(mesh_type), pointer :: mesh => null()
  contains
    procedure setUp
    procedure tearDown
    procedure get_local_mesh_ptr
    procedure test_integer_field
    procedure test_integer_field_proxy
  end type integer_field_test_type

  integer(i_def), parameter :: element_order_h = 0
  integer(i_def), parameter :: element_order_v = 0

contains

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  subroutine setUp( this )

    implicit none

    class(integer_field_test_type), intent(inout) :: this

    type(local_mesh_type), pointer :: unit_test_local_mesh_ptr
    type(mesh_type) :: unit_test_mesh
    integer(i_def)  :: mesh_id
    type(lfric_comm_type) :: lfric_comm

    call lfric_comm%set_comm_mpi_val(this%getMpiCommunicator())

    !Store the MPI communicator for later use
    call global_mpi%initialise(lfric_comm)

    ! Initialise halo functionality
    call initialise_halo_comms(lfric_comm)

    ! Create top level mesh collection
    mesh_collection = mesh_collection_type()
    ! Create top level function space collection
    function_space_collection = function_space_collection_type()
    ! Create top level halo_routing collection
    halo_routing_collection = halo_routing_collection_type()

    call this%unit_test_local_mesh%initialise()
    unit_test_local_mesh_ptr => this%get_local_mesh_ptr()

    ! Dummy mesh mod has 9 cells, 3 layers and is uniform in vertical
    unit_test_mesh = mesh_type( PLANE_BI_PERIODIC, unit_test_local_mesh_ptr )
    mesh_id   =  mesh_collection%add_new_mesh( unit_test_mesh )
    this%mesh => mesh_collection%get_mesh( mesh_id )

    ! Make a function space to be used to create fields in the tests
    this%w2_fs => function_space_collection%get_fs( this%mesh, &
                                                    element_order_h,            &
                                                    element_order_v, W2 )

  end subroutine setUp

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  subroutine tearDown( this )

    implicit none

    class(integer_field_test_type), intent(inout) :: this

    call function_space_collection%clear()
    call mesh_collection%clear()
    call halo_routing_collection%clear()

    ! Finalise halo functionality
    call finalise_halo_comms()
    ! Clear the stored MPI communicator
    call global_mpi%finalise()

  end subroutine tearDown

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

  function get_local_mesh_ptr(this) result(unit_test_local_mesh_ptr)
    implicit none
    class(integer_field_test_type), intent(inout), target :: this
    type(local_mesh_type), pointer :: unit_test_local_mesh_ptr
    unit_test_local_mesh_ptr => this%unit_test_local_mesh
  end function get_local_mesh_ptr

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

  @Test( npes=[1] )
  subroutine test_integer_field( this )

    use integer_field_mod,     only: integer_field_type, &
                                     integer_field_proxy_type

    implicit none

    class(integer_field_test_type), intent(inout) :: this

    type(integer_field_type)          :: f1
    type(integer_field_type)          :: f2
    type(integer_field_proxy_type)    :: f1_p
    type(integer_field_proxy_type)    :: f2_p

    character(str_def) :: name

    type(function_space_type), pointer :: fs => null()
    type(mesh_type),           pointer :: mesh_out => null()

    integer(i_def) :: iface, iedge, lid, test_integer
    integer(i_def) :: order_h, order_v

    ! Make a field with a function space
    call f1%initialise( vector_space = this%w2_fs, name='f1', halo_depth=3 )

    ! Get  and check the the name of the field
    name = f1%get_name()
    @assertEqual( 'f1', trim(name) )

    ! get the proxy
    f1_p = f1%get_proxy()

    ! Check the size of the data array is the same as undf from the fs
    @assertEqual( size( f1_p%data ) , f1_p%vspace%get_undf() )
    ! Check the the function space the field lives on is the one it was
    ! created with
    @assertEqual( f1%which_function_space(), W2 )

    ! Check the get function space returns W2 by comparing undf
    fs => f1%get_function_space()
    @assertEqual( fs%get_undf(), this%w2_fs%get_undf() )

    ! Check the order the field thinks it is
    order_h=f1%get_element_order_h()
    @assertEqual( element_order_h, order_h )
    order_v=f1%get_element_order_v()
    @assertEqual( element_order_v, order_v )

    ! Check what the field thinks the mesh is
    mesh_out => f1%get_mesh()
    @assertTrue( associated(this%mesh, mesh_out) )

    ! Copy field operator tests follow...
    call f1%copy_field_serial(f2)
    f2_p = f2%get_proxy()

    ! Check that contained vector_space pointers point to same address
    @assertTrue(associated(f1_p%vspace, f2_p%vspace), "Assigned field_type vspace pointers differ.")

    nullify(mesh_out)

  end subroutine test_integer_field

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  @Test( npes=[1] )
  subroutine test_integer_field_proxy( this )

    use integer_field_mod,             only: integer_field_type, &
                                             integer_field_proxy_type

    implicit none

    class(integer_field_test_type), intent(inout) :: this

    type(integer_field_type)         :: f1
    type(integer_field_type)         :: f2
    type(integer_field_type)         :: f3
    type(integer_field_proxy_type)   :: f1_p
    type(integer_field_proxy_type)   :: f2_p
    type(integer_field_proxy_type)   :: f3_p
    integer(i_def)                   :: i
    integer(i_def)                   :: halo_depth
    type(halo_routing_type), pointer :: halo_routing
    integer(i_def)                   :: order_h, order_v
    integer(i_def)                   :: test_sum, test_min, test_max
    logical                          :: dirty

    ! Make a field with a function space
    call f1%initialise( vector_space = this%w2_fs, name='f1', halo_depth=3 )

    ! get the proxy
    f1_p = f1%get_proxy()

    !Initialise field data
    do i=1,f1_p%vspace%get_undf()
      f1_p%data(i)=int(i, i_def)
    end do

    ! Make a second field, identical to the first
    call f2%initialise( vector_space = this%w2_fs, halo_depth=3 )
    f2_p = f2%get_proxy()
    do i=1,f2_p%vspace%get_undf()
      f2_p%data(i)=int(i, i_def)
    end do

    ! Perform a halo swap on the second field
    call f2_p%halo_exchange(1)

    ! For serial case, the halo swapped field should be the same as the original
    @assertEqual( f1_p%data  , f2_p%data )

    ! Test the global reductions
    test_sum=f1_p%get_sum()
    @assertEqual( 4095  , test_sum )

    test_min=f1_p%get_min()
    @assertEqual( 1  , test_min )

    test_max=f1_p%get_max()
    @assertEqual( 90  , test_max )

    halo_depth=f1_p%get_field_proxy_halo_depth()
    @assertEqual ( 3, halo_depth )

    halo_routing=>f1_p%get_halo_routing()
    order_h = halo_routing%get_element_order_h()
    @assertEqual( element_order_h, order_h )
    order_v = halo_routing%get_element_order_v()
    @assertEqual( element_order_v, order_v )

    call f1_p%set_dirty()
    dirty=f1_p%is_dirty(1)
    @assertEqual ( .true., dirty )

    call f1_p%set_clean(1)
    dirty=f1_p%is_dirty(1)
    @assertEqual ( .false., dirty )

  end subroutine test_integer_field_proxy

end module integer_field_mod_test
