!-----------------------------------------------------------------------------
! (C) Crown copyright 2023 Met Office. All rights reserved.
! The file LICENCE, distributed with this code, contains details of the terms
! under which the code may be used.
!-----------------------------------------------------------------------------
module reverse_model_clock_mod_test

  use constants_mod,           only : i_timestep, r_def, r_second
  use reverse_model_clock_mod, only : reverse_model_clock_type
  use pFUnit_mod,              only : anyExceptions,         &
                                      assertEqual,           &
                                      assertFalse,           &
                                      assertTrue,            &
                                      AbstractTestParameter, &
                                      ParameterizedTestCase, &
                                      SourceLocation

  implicit none

  private
  public :: get_parameters, test_constructor, test_everything


  ! Each test case consists of inputs:
  !   First step, Last step, Step length
  ! And expected outputs:
  !   First step, Last step, Clock ticks executed
  !
  @testParameter
  type, public, extends(AbstractTestParameter) :: parameter_type
    integer(i_timestep) :: inject_first
    integer(i_timestep) :: inject_last
    real(r_second)      :: inject_delta_t
    integer :: expect_first
    integer :: expect_last
    integer :: expect_ticks
  contains
    procedure :: toString
  end type parameter_type


  @TestCase(testParameters={get_parameters()}, constructor=test_constructor)
  type, public, extends(ParameterizedTestCase) :: reverse_clock_test_type
    private
    integer(i_timestep) :: inject_first
    integer(i_timestep) :: inject_last
    real(r_second)      :: inject_delta_t
    integer :: expect_first
    integer :: expect_last
    integer :: expect_ticks
  contains
    procedure test_everything
  end type reverse_clock_test_type


contains

  ! Tests a scenario described by the associated parameters.
  !
  ! The "inject_" parameters are used to initialise a clock object. The object
  ! is then stepped over the range described by the parameters and the result
  ! is checked against the "expect_*" parameters.
  !
  @test
  subroutine test_everything( this )

    implicit none

    class(reverse_clock_test_type), intent(inout) :: this

    type(reverse_model_clock_type) :: unit_under_test
    integer                        :: tally

    unit_under_test = reverse_model_clock_type( this%inject_first,   &
                                                this%inject_last,    &
                                                this%inject_delta_t )

    ! Ensure the clock is configured and ready to go on the first step.
    !
    @assertEqual( this%expect_first, unit_under_test%get_first_step() )
    @assertEqual( this%expect_last, unit_under_test%get_last_step() )
    @assertEqual( this%expect_first, unit_under_test%get_step() )
    @assertEqual( this%inject_delta_t, unit_under_test%get_seconds_per_step() )

    ! Run the clock until it thinks it's done.
    !
    ! At each step make sure the clock is in the correct states. Ensure the
    ! spinup fraction is correct.
    !
    tally = 0
    do while (unit_under_test%tick())
      @assertTrue( unit_under_test%is_running() )
      tally = tally + 1
    end do ! clock%is_running()

    ! Ensure the correct number of ticks occured.
    !
    @assertEqual( this%expect_ticks, tally )

  end subroutine test_everything


  ! Constructs the test object from a parameter object.
  !
  function test_constructor( clock_test_parameter ) result(new_test)
    implicit none
    type(parameter_type), intent(in) :: clock_test_parameter
    type(reverse_clock_test_type) :: new_test
    new_test%inject_first = clock_test_parameter%inject_first
    new_test%inject_last = clock_test_parameter%inject_last
    new_test%inject_delta_t = clock_test_parameter%inject_delta_t
    new_test%expect_first = clock_test_parameter%expect_first
    new_test%expect_last = clock_test_parameter%expect_last
    new_test%expect_ticks = clock_test_parameter%expect_ticks

  end function test_constructor


  ! Sets the array of test parameters. Each parameter group (object) is passed
  ! to the test in turn allowing a single test function to test multiple
  ! scenarios. See the comment prior to each object definition for details on
  ! the scenario it represents.
  !
  function get_parameters() result(parameters)
    implicit none
    type(parameter_type) :: parameters(2)
    parameters = (/                                        &
  ! Initial run
                   parameter_type(5, 1, -12.7, 5, 1, 5),   &
  ! Single step
                   parameter_type(3, 3, -7.9, 3, 3, 1)/)
  end function get_parameters

  ! Format a nice human readable description of the current parameter set.
  !
  function toString( this ) result(string)
    implicit none
    class(parameter_type), intent(in) :: this
    character(:), allocatable :: string
    character(20) :: buffer
    write( buffer,                                        &
           '(I0,", ",I0,", ",F5.2)') this%inject_first,   &
                                     this%inject_last,    &
                                     this%inject_delta_t
    string = trim(buffer)
  end function toString


end module reverse_model_clock_mod_test
