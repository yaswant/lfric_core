!-----------------------------------------------------------------------------
! Copyright (c) 2017,  Met Office, on behalf of HMSO and Queen's Printer
! For further details please refer to the file LICENCE.original which you
! should have received as part of this distribution.
!-----------------------------------------------------------------------------

!> Test the function space collection object
!>
module function_space_collection_mod_test

  use pfunit
  use constants_mod,                 only : i_def, r_def
  use function_space_collection_mod, only : function_space_collection_type, &
                                            function_space_collection
  use lfric_mpi_mod,                 only : global_mpi, &
                                            lfric_comm_type
  use local_mesh_mod,                only : local_mesh_type
  use mesh_collection_mod,           only : mesh_collection_type, &
                                            mesh_collection
  use mesh_mod,                      only : mesh_type
  use fs_continuity_mod,             only : W0, W2, W3
  use function_space_mod,            only : function_space_type

  implicit none

  private
  public :: test_all
  !
  ! pFUnit requirest symbol spillage
  !
  public :: MPITestCase, MPITestParameter

  @TestCase
  type, extends(MPITestCase), public :: function_space_collection_test_type
    private
    type(function_space_type), pointer :: w0_fs => null()
    type(function_space_type), pointer :: w2_fs => null()
    type(function_space_type), pointer :: w3_fs => null()
    type(function_space_type), pointer :: temp_w0_fs => null()
    type(local_mesh_type)              :: unit_test_local_mesh
    type(mesh_type), pointer           :: mesh => null()

  contains
    procedure setUp
    procedure tearDown
    procedure get_local_mesh_ptr
  end type function_space_collection_test_type

  integer(i_def), parameter :: element_order_h = 0
  integer(i_def), parameter :: element_order_v = 0

contains

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  subroutine setUp( this )

    use mesh_mod, only : mesh_type, PLANE_BI_PERIODIC

    implicit none

    class(function_space_collection_test_type), intent(inout) :: this

    type(local_mesh_type), pointer :: unit_test_local_mesh_ptr
    type(mesh_type) :: unit_test_mesh
    integer(i_def)  :: mesh_id
    type(lfric_comm_type) :: lfric_comm

    call lfric_comm%set_comm_mpi_val(this%getMpiCommunicator())

    ! Store the MPI communicator for later use
    call global_mpi%initialise(lfric_comm)

    ! Create top level mesh collection
    mesh_collection = mesh_collection_type()
    ! Create top level function space collection
    function_space_collection = function_space_collection_type()

    call this%unit_test_local_mesh%initialise()
    unit_test_local_mesh_ptr => this%get_local_mesh_ptr()

    call this%unit_test_local_mesh%initialise()
    unit_test_local_mesh_ptr => this%get_local_mesh_ptr()

    ! create the mesh
    ! Dummy mesh mod has 9 cells, 3 layers and is uniform in vertical
    unit_test_mesh = mesh_type( PLANE_BI_PERIODIC, unit_test_local_mesh_ptr )
    mesh_id   =  mesh_collection%add_new_mesh( unit_test_mesh )
    this%mesh => mesh_collection%get_mesh( mesh_id )

  end subroutine setUp

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  subroutine tearDown( this )

    implicit none

    class(function_space_collection_test_type), intent(inout) :: this

    call function_space_collection%clear()
    call mesh_collection%clear()

    ! Clear the stored MPI communicator
    call global_mpi%finalise()

  end subroutine tearDown

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

  function get_local_mesh_ptr(this) result(unit_test_local_mesh_ptr)
    implicit none
    class(function_space_collection_test_type), intent(inout), target :: this
    type(local_mesh_type), pointer :: unit_test_local_mesh_ptr
    unit_test_local_mesh_ptr => this%unit_test_local_mesh
  end function get_local_mesh_ptr

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

  @test(npes=[1])
  subroutine test_all( this )


    implicit none

    class(function_space_collection_test_type), intent(inout) :: this

    integer(i_def) :: answer = 3

    ! Create 3 function spaces
    this%w0_fs => function_space_collection%get_fs( this%mesh,       &
                                                    element_order_h, &
                                                    element_order_v, &
                                                    W0 )
    this%w2_fs => function_space_collection%get_fs( this%mesh,       &
                                                    element_order_h, &
                                                    element_order_v, &
                                                    W2 )
    this%w3_fs => function_space_collection%get_fs( this%mesh,       &
                                                    element_order_h, &
                                                    element_order_v, &
                                                    W3,              &
                                                    ndata=3 )

    ! Check we have 3 function spaces in the collection

    @assertEqual(answer, function_space_collection%get_fs_collection_size())

    ! Try to get a function space already in the list.
    ! The number of function spaces in the collection should not increase

    this%temp_w0_fs => function_space_collection%get_fs( this%mesh,       &
                                                         element_order_h, &
                                                         element_order_v, &
                                                         W0 )

    @assertEqual(answer, function_space_collection%get_fs_collection_size())

  end subroutine test_all

end module function_space_collection_mod_test
