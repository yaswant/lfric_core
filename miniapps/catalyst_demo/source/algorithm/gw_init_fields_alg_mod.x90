!-----------------------------------------------------------------------------
! Copyright (c) 2017,  Met Office, on behalf of HMSO and Queen's Printer
! For further details please refer to the file LICENCE.original which you
! should have received as part of this distribution.
!-----------------------------------------------------------------------------

!>@brief Initialisation of prognostic fields for the gravity wave equations
module gw_init_fields_alg_mod

  use log_mod,                           only: log_event,         &
                                               log_scratch_space, &
                                               LOG_LEVEL_INFO,    &
                                               LOG_LEVEL_TRACE,   &
                                               LOG_LEVEL_ERROR
  use constants_mod,                     only: r_def, i_def

  use iterative_solver_mod,              only: solver_algorithm
  use runtime_constants_mod,             only: get_coordinates

  ! Configuration options
  use finite_element_config_mod,         only: element_order
  use io_mod,                            only: ts_fname
  use io_config_mod,                     only: checkpoint_read, &
                                               checkpoint_stem_name
  use time_config_mod,                   only: timestep_start

  ! PsyKAl PSYClone kernels
  use initial_u_kernel_mod,              only: initial_u_kernel_type
  use compute_mass_matrix_kernel_w2_mod, only: compute_mass_matrix_kernel_w2_type
  use initial_buoyancy_kernel_mod,       only: initial_buoyancy_kernel_type

  ! Derived Types
  use function_space_collection_mod,     only: function_space_collection
  use function_space_mod,                only: function_space_type
  use field_mod,                         only: field_type
  use quadrature_xyoz_mod,               only: quadrature_xyoz_type
  use quadrature_rule_gaussian_mod,      only: quadrature_rule_gaussian_type

  ! Handles
  use fs_continuity_mod,                 only: W0, W2, W3, Wtheta

  implicit none

  private
  public :: gw_init_fields_alg

contains
  !> @details An algorithm for initialising prognostic fields for
  !>          all solvers.
  !> @param[inout] wind  The 3D wind field
  !> @param[inout] pressure The pressure
  !> @param[inout] buoyancy The buoyancy
  subroutine gw_init_fields_alg( wind, pressure, buoyancy)

    implicit none

    ! Prognostic fields
    type( field_type ),   intent(inout)   :: wind, pressure, buoyancy

    type( quadrature_xyoz_type )          :: qr
    type( quadrature_rule_gaussian_type ) :: quadrature_rule
    type( field_type )                    :: r_u

    ! Coordinate fields
    type( field_type ), pointer :: chi(:) => null()

    real(kind=r_def), parameter        :: initial_time = 0.0_r_def

    chi => get_coordinates()

    qr = quadrature_xyoz_type(element_order+3, quadrature_rule)

    !=== Initialise global prognostic fields ==================================!

    if (.not.checkpoint_read) then           ! No check point to start from

      call log_event( 'gravity_wave: Initialising prognostic fields', LOG_LEVEL_INFO )

      call invoke( initial_buoyancy_kernel_type(buoyancy, chi) )

      ! Initialise U and p according to formulation
      call r_u%initialise( vector_space = wind%get_function_space() )
      call invoke( name = 'Initialise U and p',     &
                   setval_c( wind,     0.0_r_def ), &
                   setval_c( r_u,      0.0_r_def ), &
                   setval_c( pressure, 0.0_r_def ), &
                   initial_u_kernel_type( r_u, chi, initial_time, qr ) )
      call mass_matrix_solver_alg(wind, r_u)

      call log_event( 'gravity_wave: Initialised prognostic fields', LOG_LEVEL_INFO )

    else                                   ! Recorded check point to start from


      call log_event('Reading checkpoint file to restart pressure',LOG_LEVEL_INFO)
      call pressure%read_checkpoint("restart_pressure", trim(ts_fname(checkpoint_stem_name,"", &
                                                     "pressure", (timestep_start - 1),"")))
      call log_event('Reading checkpoint file to restart wind',LOG_LEVEL_INFO)
      call wind%read_checkpoint("restart_wind", trim(ts_fname(checkpoint_stem_name,"", &
                                             "wind", (timestep_start - 1),"")))
      call log_event('Reading checkpoint file to restart buoyancy',LOG_LEVEL_INFO)
      call buoyancy%read_checkpoint("restart_buoyancy", trim(ts_fname(checkpoint_stem_name,"", &
                                                     "buoyancy", (timestep_start - 1),"")))

      call log_event( 'Gravity wave: Read prognostic fields from checkpoint files', LOG_LEVEL_INFO )

    end if

    !==========================================================================!

  end subroutine gw_init_fields_alg

end module gw_init_fields_alg_mod
