!-----------------------------------------------------------------------------
! Copyright (c) 2017,  Met Office, on behalf of HMSO and Queen's Printer
! For further details please refer to the file LICENCE.original which you
! should have received as part of this distribution.
!-----------------------------------------------------------------------------

!> Test the qergence operator computation
!>
module compute_q_operator_kernel_mod_test

    use constants_mod,                 only : i_def, r_def
    use function_space_collection_mod, only : function_space_collection_type, &
                                              function_space_collection
    use mesh_collection_mod,           only : mesh_collection_type, &
                                              mesh_collection
    use field_mod,                     only : field_type,field_proxy_type
    use fs_continuity_mod,             only : W0, W2, Wtheta
    use function_space_mod,            only : function_space_type
    use operator_mod,                  only : operator_type, operator_proxy_type
    use pFUnit_Mod
    use quadrature_xyoz_mod,           only: quadrature_xyoz_type, &
                                             quadrature_xyoz_proxy_type
    use quadrature_rule_gaussian_mod,  only: quadrature_rule_gaussian_type
    use yaxt,                          only: xt_initialize, xt_finalize
    use mpi_mod,                       only: store_comm, clear_comm

  implicit none

  private
  public :: test_all

  @TestCase
  type, extends(MPITestCase), public :: compute_q_operator_test_type
    private
    type( quadrature_xyoz_type )          :: qr
    type(quadrature_xyoz_proxy_type) :: qr_proxy
    type(function_space_type), pointer :: w0_fs => null()
    type(function_space_type), pointer :: w2_fs => null()
    type(function_space_type), pointer :: wt_fs => null()
    type(field_type)                   :: chi(3)
    type(operator_type)                :: q
    real(r_def), allocatable           :: basis_w2(:,:,:,:), &
                                          basis_wt(:,:,:,:)
    integer(i_def)                     :: mesh_id
  contains
    procedure setUp
    procedure tearDown
    procedure test_all
  end type compute_q_operator_test_type

  integer(i_def), parameter :: element_order = 0

contains

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  subroutine setUp( this )

    use finite_element_config_mod, only: cellshape_quadrilateral
    use feign_config_mod,          only: feign_finite_element_config
    use mesh_mod,                  only: PLANE_BI_PERIODIC

    implicit none

    class(compute_q_operator_test_type), intent(inout) :: this

    type(operator_proxy_type) :: q_p
    type( quadrature_rule_gaussian_type ) :: quadrature_rule

    integer :: nlayers, dim_w2, dim_wt
    integer :: ndf_w2, ndf_wt
    integer :: nqp_h, nqp_v

    integer :: i

    ! Initialise YAXT
    call xt_initialize(this%getMpiCommunicator())
    !Store the MPI communicator for later use
    call store_comm(this%getMpiCommunicator())

    call feign_finite_element_config( cellshape_quadrilateral, &
                                      0, 1, .true., .false., .false.)

    ! Create top level mesh collection
    mesh_collection = mesh_collection_type()
    ! Create top level function space collection
    function_space_collection = function_space_collection_type()

    this%qr = quadrature_xyoz_type(3, quadrature_rule)

    this%qr_proxy = this%qr%get_quadrature_proxy()
    nqp_h=this%qr_proxy%np_xy
    nqp_v=this%qr_proxy%np_z

    ! Dummy mesh mod has 9 cells, 3 layers and is uniform in vertical
    this%mesh_id = mesh_collection%add_unit_test_mesh( PLANE_BI_PERIODIC )

    this%w0_fs => function_space_collection%get_fs( this%mesh_id, &
                                                    element_order, &
                                                    W0 )
    this%w2_fs => function_space_collection%get_fs( this%mesh_id, &
                                                    element_order, &
                                                    W2 )
    this%wt_fs => function_space_collection%get_fs( this%mesh_id, &
                                                    element_order, &
                                                    Wtheta )

    this%q = operator_type(this%w2_fs,this%wt_fs)

    do i = 1,3
      call this%chi(i)%initialise( vector_space = this%w0_fs )
    end do

    q_p = this%q%get_proxy()

    ndf_w2  = q_p%fs_to%get_ndf()
    dim_w2 = q_p%fs_to%get_dim_space( )
    allocate(this%basis_w2(dim_w2 ,ndf_w2, nqp_h, nqp_v) )

    ndf_wt = q_p%fs_from%get_ndf()
    dim_wt = q_p%fs_from%get_dim_space( )
    allocate(this%basis_wt(dim_wt, ndf_wt, nqp_h, nqp_v) )

  end subroutine setUp

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  subroutine tearDown( this )

    implicit none

    class(compute_q_operator_test_type), intent(inout) :: this

    deallocate( this%basis_wt, this%basis_w2)

    call function_space_collection%clear()

    ! Finalise YAXT
    call xt_finalize()
    ! Clear the stored MPI communicator
    call clear_comm()

  end subroutine tearDown

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  @Test( npes=[1] )
  subroutine test_all( this )

    use compute_q_operator_kernel_mod, only : compute_q_operator_code, &
                                              compute_q_operator_init
    use mesh_mod,                      only : mesh_type

    implicit none

    class(compute_q_operator_test_type), intent(inout) :: this

    type(operator_proxy_type) :: q_p
    type(field_proxy_type )   :: chi_p(3)

    type(mesh_type),pointer   :: mesh => null()

    real(kind=r_def), parameter :: tol = 1.0e-6_r_def
    real(kind=r_def), parameter :: dx = 6000.0_r_def, &
                                   dy = 1000.0_r_def

    integer :: i, j, k, cell
    integer(i_def) :: err

    real(r_def) :: dz, dz_test(3)

    integer :: nlayers
    integer :: ndf_chi, undf_chi, ndf_w2, ndf_wt
    integer :: nqp_h, nqp_v

    integer, pointer                 :: map_chi(:)  => null()

    real(kind=r_def), pointer :: xp(:,:) => null()
    real(kind=r_def), pointer :: zp(:)   => null()
    real(kind=r_def), pointer :: wh(:), wv(:) => null()
    real(kind=r_def), dimension(6,2) :: answer

    mesh => mesh_collection%get_mesh( this%mesh_id )
    call mesh%get_dz(dz_test)
    dz = dz_test(1)

    ! Make a coordinate field with a function space
    do i = 1,3
      chi_p(i) = this%chi(i)%get_proxy()
    end do
    ! Compute coordinates
    cell = 1
    do j = 1,3
      do i = 1,3
        map_chi => chi_p(1)%vspace%get_cell_dofmap( cell )
        do k = 0,3
          chi_p(1)%data(map_chi(1) + k) = real(i-1)*dx
          chi_p(2)%data(map_chi(1) + k) = real(j-1)*dy
          chi_p(3)%data(map_chi(1) + k) = real(k)  *dz
        end do
        cell = cell + 1
      end do
    end do

    ! Test the operator kernel
    q_p = this%q%get_proxy()

    nqp_h=this%qr_proxy%np_xy
    nqp_v=this%qr_proxy%np_z
    xp=>this%qr_proxy%points_xy
    zp=>this%qr_proxy%points_z
    wh=>this%qr_proxy%weights_xy
    wv=>this%qr_proxy%weights_z

    nlayers = q_p%fs_from%get_nlayers()
    ndf_wt  = q_p%fs_from%get_ndf()
    call q_p%fs_from%compute_basis_function(                     &
         this%basis_wt, ndf_wt, nqp_h, nqp_v, xp, zp )

    ndf_w2 = q_p%fs_to%get_ndf()
    call q_p%fs_to%compute_basis_function(                            &
         this%basis_w2, ndf_w2, nqp_h, nqp_v, xp, zp )

    call compute_q_operator_init(dz_test, nlayers)
    cell = 1
    q_p%local_stencil(:,:,:) = 0.0_r_def
    call compute_q_operator_code( cell,                              &
                                  nlayers,                           &
                                  q_p%ncell_3d,                      &
                                  q_p%local_stencil,                 &
                                  ndf_w2,                            &
                                  this%basis_w2,                     &
                                  ndf_wt,                            &
                                  this%basis_wt,                     &
                                  nqp_h,                             &
                                  nqp_v,                             &
                                  wh,                                &
                                  wv                                 &
                                  )

    call q_operator(answer)
    @assertEqual(answer, q_p%local_stencil(:, :, 1 )/dz, tol)

  end subroutine test_all

  subroutine q_operator( q )
    use constants_mod, only: r_def
    implicit none

    real(kind=r_def), dimension(2,6), intent(inout) :: q

  q = real(reshape( [ &
   0, 0, 0, 0, 2, 1, &
   0, 0, 0, 0, 1, 2 &
 ], shape(q) ),r_def)/6.0_r_def

    return
  end subroutine q_operator

end module compute_q_operator_kernel_mod_test
