!-----------------------------------------------------------------------------
! (c) Crown copyright 2022 Met Office. All rights reserved.
! The file LICENCE, distributed with this code, contains details of the terms
! under which the code may be used.
!-----------------------------------------------------------------------------

!> Test the initial_tracer_field_sample_kernel
module initial_tracer_field_sample_kernel_mod_test

  use constants_mod,                       only : r_def, i_def
  use get_unit_test_m3x3_q3x3x3_sizes_mod, only : get_w0_m3x3_q3x3x3_size,   &
                                                  get_w3_m3x3_q3x3x3_size

  use get_unit_test_m3x3_dofmap_mod,       only : get_w0_m3x3_dofmap,        &
                                                  get_w3_m3x3_dofmap

  use get_unit_test_q3x3x3_basis_mod,      only : get_w0_q3x3x3_basis
  use get_unit_test_3x3x3_chi_mod,         only : get_w0_3x3x3_field

  use pFUnit_Mod

  implicit none

  private
  public:: test_all

  @TestCase
  type, extends(TestCase), public  :: initial_tracer_field_sample_kernel_test_type
    private
  contains
    procedure setUp
    procedure tearDown
    procedure test_all
  end type initial_tracer_field_sample_kernel_test_type

contains

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  subroutine setUp( this )

    use base_mesh_config_mod,       only : geometry_planar, &
                                           topology_fully_periodic
    use chi_transform_mod,          only : init_chi_transforms
    use feign_config_mod,           only : feign_base_mesh_config,      &
                                           feign_finite_element_config, &
                                           feign_idealised_config,      &
                                           feign_initial_tracer_field_config
    use finite_element_config_mod,  only : cellshape_quadrilateral, &
                                           coord_system_xyz
    use idealised_config_mod,       only : test_slotted_cylinder

    implicit none

    class(initial_tracer_field_sample_kernel_test_type), intent(inout) :: this

    integer(kind=i_def) :: nlayers

    call feign_base_mesh_config( file_prefix='foo',                &
                                 prime_mesh_name='unit_test',      &
                                 geometry=geometry_planar,         &
                                 offline_partitioning=.false.,     &
                                 topology=topology_fully_periodic, &
                                 fplane=.false., f_lat_deg=45.0_r_def )

    call feign_finite_element_config(               &
                 cellshape=cellshape_quadrilateral, &
                 coord_order=0_i_def,               &
                 coord_system=coord_system_xyz,     &
                 element_order=0_i_def,             &
                 rehabilitate=.true.,               &
                 vorticity_in_w1=.false. )

    call feign_initial_tracer_field_config( field_background=0.1_r_def, &
                                            field_max=2.0_r_def,        &
                                            r1=0.0_r_def, x1=0.0_r_def, &
                                            y1=0.0_r_def, z1=0.0_r_def, &
                                            r2=0.0_r_def, x2=0.0_r_def, &
                                            y2=0.0_r_def, z2=0.0_r_def)

    call feign_idealised_config( test=test_slotted_cylinder )

    call init_chi_transforms()

  end subroutine setUp

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  subroutine tearDown( this )

    use chi_transform_mod, only: final_chi_transforms
    use configuration_mod, only: final_configuration

    implicit none

    class(initial_tracer_field_sample_kernel_test_type), intent(inout) :: this

    ! Finalise namelists
    call final_configuration()
    call final_chi_transforms()

  end subroutine tearDown

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  @Test
  subroutine test_all( this )

    use initial_tracer_field_sample_kernel_mod, only: initial_tracer_field_sample_kernel_code

    implicit none

    class(initial_tracer_field_sample_kernel_test_type), intent(inout) :: this

    real(kind=r_def), parameter      :: tol = 1.0e-14_r_def
    real(kind=r_def), parameter      :: dx = 1.0_r_def
    real(kind=r_def), parameter      :: dy = 1.0_r_def
    real(kind=r_def), parameter      :: dz = 1.0_r_def
    real(kind=r_def), parameter      :: answer1 = 0.2_r_def
    real(kind=r_def), parameter      :: answer2 = 1.0_r_def
    real(kind=r_def), parameter      :: domain_max_x = 2.0_r_def

    ! Fields
    real(kind=r_def), allocatable    :: chi_1(:), chi_2(:), chi_3(:)
    real(kind=r_def), allocatable    :: rho(:), panel_id(:)

    ! Basis functions
    real(kind=r_def),    allocatable :: basis_w0(:,:,:,:)

    ! DoF maps
    integer(kind=i_def), allocatable :: map_w0(:,:)
    integer(kind=i_def), allocatable :: map_w3(:,:)
    integer(kind=i_def), allocatable :: map_pid(:,:)

    ! Other integers
    integer(kind=i_def)              :: nlayers, ncells, dim_space, dim_space_diff
    integer(kind=i_def)              :: nqp_h, nqp_v
    integer(kind=i_def)              :: ndf_w3, undf_w3
    integer(kind=i_def)              :: ndf_pid, undf_pid
    integer(kind=i_def)              :: ndf_w0, undf_w0
    integer(kind=i_def)              :: i, j, k, cell, const_flag

    nlayers  = 3_i_def
    call get_w0_m3x3_q3x3x3_size( ndf_w0, undf_w0,           &
                                  ncells,                    &
                                  dim_space, dim_space_diff, &
                                  nqp_h, nqp_v,              &
                                  nlayers )
    call get_w3_m3x3_q3x3x3_size( ndf_w3, undf_w3,           &
                                  ncells,                    &
                                  dim_space, dim_space_diff, &
                                  nqp_h, nqp_v,              &
                                  nlayers )

    call get_w0_m3x3_dofmap(map_w0)
    call get_w3_m3x3_dofmap(map_w3)

    ! Get canned basis functions
    call get_w0_q3x3x3_basis(basis_w0)

    ndf_pid = 1_i_def
    undf_pid = ncells
    call get_w3_m3x3_dofmap(map_pid, 1_i_def)

    ! Compute coordinates
    allocate(chi_1(undf_w0))
    allocate(chi_2(undf_w0))
    allocate(chi_3(undf_w0))
    allocate(panel_id(undf_pid))
    cell = 1
    call get_w0_3x3x3_field(chi_1, chi_2, chi_3, dx, dy, dz, &
                            map_w0, nlayers)

    panel_id(:) = 1.0_r_def
    const_flag = 0_i_def

    ! Make up initial fields
    allocate( rho(undf_w3) )
    rho(:) = 0.0_r_def

    ! Test using idealised namelist
    call initial_tracer_field_sample_kernel_code( nlayers,                         &
                                                  rho,                             &
                                                  chi_1, chi_2, chi_3,             &
                                                  panel_id,                        &
                                                  domain_max_x,                    &
                                                  const_flag,                      &
                                                  ndf_w3, undf_w3, map_w3(:,cell), &
                                                  ndf_w0, undf_w0, map_w0,         &
                                                  basis_w0,                        &
                                                  ndf_pid, undf_pid, map_pid(:,cell) )

    ! Check if the values in the first column are now equal to the answer
    @assertEqual( answer1, rho(1), tol )
    @assertEqual( answer1, rho(2), tol )
    @assertEqual( answer1, rho(3), tol )

    ! Test using const_flag
    const_flag = 1_i_def
    call initial_tracer_field_sample_kernel_code( nlayers,                         &
                                                  rho,                             &
                                                  chi_1, chi_2, chi_3,             &
                                                  panel_id,                        &
                                                  domain_max_x,                    &
                                                  const_flag,                      &
                                                  ndf_w3, undf_w3, map_w3(:,cell), &
                                                  ndf_w0, undf_w0, map_w0,         &
                                                  basis_w0,                        &
                                                  ndf_pid, undf_pid, map_pid(:,cell) )

    ! Check if the values in the first column are now equal to the answer
    @assertEqual( answer2, rho(1), tol )
    @assertEqual( answer2, rho(2), tol )
    @assertEqual( answer2, rho(3), tol )



    deallocate( rho )
    deallocate( chi_1 )
    deallocate( chi_2 )
    deallocate( chi_3 )
    deallocate( panel_id )
    deallocate( map_w0 )
    deallocate( map_w3 )
    deallocate( map_pid )
    deallocate( basis_w0 )

  end subroutine test_all

end module initial_tracer_field_sample_kernel_mod_test
