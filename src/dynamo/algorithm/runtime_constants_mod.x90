!-------------------------------------------------------------------------------
! (c) The copyright relating to this work is owned jointly by the Crown,
! Met Office and NERC 2014.
! However, it has been created with the help of the GungHo Consortium,
! whose members are identified at https://puma.nerc.ac.uk/trac/GungHo/wiki
!-------------------------------------------------------------------------------
!
!> @brief A module providing various run time constants
!>
!> @details A module holding various objects that are created at
!>         setup and are not changed thereafter but are needed
!>         throughout the algorithm layers

module runtime_constants_mod

  use constants_mod,                     only: i_def, r_def  
  use log_mod,                           only: log_event, &
                                               LOG_LEVEL_INFO, &
                                               LOG_LEVEL_ERROR
  ! Configuration
  use finite_element_config_mod,         only: element_order, wtheta_on

  ! Derived Types
  use field_mod,                         only: field_type
  use function_space_mod,                only: function_space_type
  use function_space_collection_mod,     only: function_space_collection
  use fs_continuity_mod,                 only: W0, W1, W2, W3, Wtheta
  use mesh_mod,                          only: mesh_type
  use operator_mod,                      only: operator_type
  use quadrature_mod,                    only: quadrature_type, GAUSSIAN
  use evaluator_xyz_mod,                 only: evaluator_xyz_type

  ! PsyKAl-lite kernels
  use psykal_lite_mod,                   only: invoke_set_field_scalar,  &
                                               invoke_compute_geopotential_kernel_type

  ! PsyKAl PSYClone kernels
  use compute_mass_matrix_kernel_w0_mod,     only: compute_mass_matrix_kernel_w0_type
  use compute_mass_matrix_kernel_w1_mod,     only: compute_mass_matrix_kernel_w1_type
  use compute_mass_matrix_kernel_w2_mod,     only: compute_mass_matrix_kernel_w2_type
  use compute_mass_matrix_kernel_w3_mod,     only: compute_mass_matrix_kernel_w3_type
  use compute_mass_matrix_kernel_wtheta_mod, only: compute_mass_matrix_kernel_wtheta_type
  use compute_geopotential_kernel_mod,       only: compute_geopotential_kernel_type
  use mm_diagonal_kernel_mod,                only: mm_diagonal_kernel_type
  use invert_local_operator_kernel_mod,      only: invert_local_operator_kernel_type
  use compute_grad_operator_kernel_mod,      only: compute_grad_operator_kernel_type
  use compute_curl_operator_kernel_mod,      only: compute_curl_operator_kernel_type
  use compute_div_operator_kernel_mod,       only: compute_div_operator_kernel_type




  implicit none

  private

  ! variables private to this module that can only be accessed by public
  ! functions returning pointers to them 

  type(field_type), pointer       :: coordinates(:) => null()
  type(field_type), target        :: mm0_diag 
  type(field_type), target        :: mm1_diag
  type(field_type), target        :: mm2_diag
  type(field_type), target        :: mm3_diag
  type(field_type), target        :: mmtheta_diag
  type(operator_type), target     :: mm_w0
  type(operator_type), target     :: mm_w1
  type(operator_type), target     :: mm_w2
  type(operator_type), target     :: mm_w3
  type(operator_type), target     :: mm_w3_inv
  type(operator_type), target     :: mm_wtheta
  type(field_type), target        :: geopotential
  type(operator_type), target     :: grad, curl, div
  type(quadrature_type), target   :: qr


  ! Public functions to create and access the module contents

  public :: create_runtime_constants
  public :: get_coordinates
  public :: get_geopotential
  public :: get_mass_matrix    
  public :: get_mass_matrix_diagonal  
  public :: get_grad
  public :: get_curl
  public :: get_div
  public :: get_qr

contains
  !> Subroutine to create the runtime constants
  !> @param[in] mesh_id Mesh_id
  !> @param[inout] chi Coordinate field
  subroutine create_runtime_constants( mesh_id, chi)
    implicit none

    integer(i_def),           intent(in)            :: mesh_id
    type(field_type),         target, intent(inout) :: chi(3)
    type(evaluator_xyz_type)                        :: evaluator

    type(function_space_type), pointer :: w0_fs     => null()
    type(function_space_type), pointer :: w1_fs     => null()
    type(function_space_type), pointer :: w2_fs     => null()
    type(function_space_type), pointer :: w3_fs     => null()
    type(function_space_type), pointer :: wtheta_fs => null()

    call log_event( "Dynamo: creating runtime_constants", LOG_LEVEL_INFO )

    coordinates => chi(:)

    !=========== Create function spaces required for setup ===================!

    w0_fs     => function_space_collection%get_fs( mesh_id, element_order, W0 )
    w1_fs     => function_space_collection%get_fs( mesh_id, element_order, W1 )
    w2_fs     => function_space_collection%get_fs( mesh_id, element_order, W2 )
    w3_fs     => function_space_collection%get_fs( mesh_id, element_order, W3 )
    wtheta_fs => function_space_collection%get_fs( mesh_id, element_order, Wtheta )
    !================= Create quadrature object =====================!

    qr = quadrature_type(element_order+3, GAUSSIAN)


    !=================== Create geopotential field ============================!

    geopotential = field_type( vector_space = w0_fs )
    evaluator = evaluator_xyz_type( w0_fs%get_ndf( ), w0_fs%get_nodes( ) )
    call invoke_compute_geopotential_kernel_type( geopotential, chi, evaluator )

    !=================== Create mass matrix operators =========================!

    mm_w0 = operator_type( w0_fs, w0_fs )
    mm_w1 = operator_type( w1_fs, w1_fs )
    mm_w2 = operator_type( w2_fs, w2_fs )
    mm_w3 = operator_type( w3_fs, w3_fs )
    mm_w3_inv = operator_type( w3_fs, w3_fs )
    mm_wtheta = operator_type( wtheta_fs, wtheta_fs )

    call invoke( compute_mass_matrix_kernel_w0_type(mm_w0, chi, qr),         &
                 compute_mass_matrix_kernel_w1_type(mm_w1, chi, qr),         &
                 compute_mass_matrix_kernel_w2_type(mm_w2, chi, qr),         &
                 compute_mass_matrix_kernel_w3_type(mm_w3, chi, qr),         &
                 compute_mass_matrix_kernel_wtheta_type(mm_wtheta, chi, qr), &
                 invert_local_operator_kernel_type(mm_w3_inv, mm_w3) )

    !=================== Create grad, div, curl operators =====================!

    grad = operator_type( w1_fs, w0_fs )
    curl = operator_type( w2_fs, w1_fs )
    div  = operator_type( w3_fs, w2_fs )

    ! These are seperate invokes as chi is defined as any space and psyclone
    ! does not support multiple any_spaces in the same invoke
    call invoke( compute_grad_operator_kernel_type(grad, chi, qr) )
    call invoke( compute_curl_operator_kernel_type(curl, chi, qr) )
    call invoke( compute_div_operator_kernel_type (div,  chi, qr) )


    !================= Create mass matrix diagonal fields =====================!

    mm0_diag       = field_type( vector_space = w0_fs )
    mm1_diag       = field_type( vector_space = w1_fs )
    mm2_diag       = field_type( vector_space = w2_fs )
    mm3_diag       = field_type( vector_space = w3_fs )
    mmtheta_diag   = field_type( vector_space = wtheta_fs )

    call invoke_set_field_scalar(0.0_r_def,mm0_diag)
    call invoke_set_field_scalar(0.0_r_def,mm1_diag)
    call invoke_set_field_scalar(0.0_r_def,mm2_diag)
    call invoke_set_field_scalar(0.0_r_def,mmtheta_diag)

    call invoke( mm_diagonal_kernel_type(mm0_diag, mm_w0))
    call invoke( mm_diagonal_kernel_type(mm1_diag, mm_w1))
    call invoke( mm_diagonal_kernel_type(mm2_diag, mm_w2))
    call invoke( mm_diagonal_kernel_type(mmtheta_diag, mm_wtheta))

    ! W3 equations are solved pointwise and so the mass matrix is inverted
    ! exactly, therefore the diagonal, where used should be 1
    call invoke_set_field_scalar(1.0_r_def,mm3_diag)

    call log_event( "Dynamo: created runtime_constants", LOG_LEVEL_INFO )

  end subroutine create_runtime_constants


  !> Function to return a pointer to the coordinate array
  !> @return The coordinate field array
  function get_coordinates() result(coords)

    type(field_type), pointer :: coords(:)
    coords => coordinates

  end function get_coordinates

  !> Function to return a pointer to the geopotential field
  !> @return The geopotential field
  function get_geopotential() result(phi)

    type(field_type), pointer :: phi
    phi => geopotential

  end function get_geopotential

  !> Function to return a pointer to a mass matrix
  !> @param[in] i the index of the desired mass matrix field
  !> @return The mass matrix operator
  function get_mass_matrix(i) result(mm)

    integer, intent(in) :: i
    type(operator_type), pointer :: mm 

    select case (i)
      case (0)
        mm => mm_w0
      case (1)
        mm => mm_w1
      case (2)
        mm => mm_w2
      case (3)
        mm => mm_w3
      case (4)
        mm => mm_w3_inv
      case (5)
        mm => mm_wtheta
      case (6)
        if (wtheta_on)then
          mm => mm_wtheta
        else
          mm => mm_w0
        end if
      case default
        mm => null()
        call log_event( "Mass matrix operator does not exist", LOG_LEVEL_ERROR)
        stop
    end select

  end function get_mass_matrix

  !> Function to return a pointer to a mass matrix diagonal
  !> @param[in] i Index of the desired mass matrix diagonal field
  !> @return The mass matrix diagonal field
  function get_mass_matrix_diagonal(i) result(mmd)

    integer, intent(in) :: i
    type(field_type), pointer :: mmd  

    select case (i)
      case (0)
        mmd => mm0_diag
      case (1)
        mmd => mm1_diag
      case (2)
        mmd => mm2_diag
      case (3)
        mmd => mm3_diag
      case (4)
        mmd => mmtheta_diag
      case default
        mmd => null()
        call log_event( "Mass matrix diagonal does not exist", LOG_LEVEL_ERROR)
        stop
    end select

  end function get_mass_matrix_diagonal

  !> Function to return a pointer to the grad operator
  !> @return The grad operator
  function get_grad() result(grad_op)

    type(operator_type), pointer :: grad_op
    grad_op => grad

  end function get_grad

  !> Function to return a pointer to the curl operator
  !> @return The curl operator
  function get_curl() result(curl_op)

    type(operator_type), pointer :: curl_op
    curl_op => curl

  end function get_curl

  !> Function to return a pointer to the div operator
  !> @return The grad operator
  function get_div() result(div_op)

    type(operator_type), pointer :: div_op
    div_op => div

  end function get_div

  !> Function to return a pointer to the quadrature object
  !> @return The quadrature object
  function get_qr() result(qr_obj)

    type(quadrature_type), pointer :: qr_obj
    qr_obj => qr

  end function get_qr

end module runtime_constants_mod
