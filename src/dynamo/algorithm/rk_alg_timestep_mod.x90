!-------------------------------------------------------------------------------
! (c) The copyright relating to this work is owned jointly by the Crown,
! Met Office and NERC 2014.
! However, it has been created with the help of the GungHo Consortium,
! whose members are identified at https://puma.nerc.ac.uk/trac/GungHo/wiki
!-------------------------------------------------------------------------------

!>@brief A Runge-Kutta time-discretisation of the non-linear 3D equations,
!>       currently using 3-stage SSP.
!>@details An algorithm for timstepping the 3D nonlinear linear
!>         equations using a multistage explicit Runge-Kutta algortihm.
module rk_alg_timestep_mod

  use log_mod,       only: log_event,         &
                           log_scratch_space, &
                           LOG_LEVEL_INFO,    &
                           LOG_LEVEL_TRACE
  use constants_mod,       only: r_def

  ! Configuration and restart/checkpoint options
  use finite_element_config_mod, only : element_order
  use formulation_config_mod,    only : rotating
  use restart_control_mod,       only : restart_type
  use solver_config_mod,         only : method
  use timestepping_config_mod,   only : dt

  ! PsyKAl-lite kernels
  use psykal_lite_mod, only: invoke_axpy,             &
                             invoke_copy_field_data,  &
                             invoke_set_field_scalar, &
                             invoke_flux_rhs

  ! PsyKAl PSYClone kernels
  use compute_mass_matrix_kernel_w0_mod, only: compute_mass_matrix_kernel_w0_type
  use compute_mass_matrix_kernel_w1_mod, only: compute_mass_matrix_kernel_w1_type
  use compute_mass_matrix_kernel_w2_mod, only: compute_mass_matrix_kernel_w2_type
  use compute_geopotential_kernel_mod,   only: compute_geopotential_kernel_type
  use rtheta_kernel_mod,                 only: rtheta_kernel_type
  use enforce_bc_kernel_mod,             only: enforce_bc_kernel_type
  use rrho_kernel_mod,                   only: rrho_kernel_type
  use vorticity_rhs_kernel_mod,          only: vorticity_rhs_kernel_type
  use rotation_kernel_mod,               only: rotation_kernel_type
  use pressure_gradient_kernel_mod,      only: pressure_gradient_kernel_type
  use kinetic_energy_gradient_kernel_mod,only: kinetic_energy_gradient_kernel_type
  use geopotential_gradient_kernel_mod,  only: geopotential_gradient_kernel_type
  use vorticity_advection_kernel_mod,    only: vorticity_advection_kernel_type

  ! Derived Types
  use mesh_mod,            only: mesh_type
  use field_mod,           only: field_type
  use function_space_mod,  only: function_space_type
  use fs_continuity_mod,   only: W0, W1, W2
  use operator_mod,        only: operator_type

  ! Algorithms
  use solver_mod,          only: solver_algorithm
  use quadrature_mod, only: quadrature_type, GAUSSIAN
  use conservation_algorithm_mod, &
                           only: conservation_algorithm
  use output_alg_mod,      only: output_alg

  implicit none

  private
  public :: rk_alg_timestep

contains

  !> @brief A Runge-Kutta time-discretisation of the non-linear 3D equations,
  !>        currently using 3-stage SSP.
  !> @details An algorithm for timstepping the 3D nonlinear
  !>          equations using a Runge-Kutta algortihm. The
  !>          algorithm initialialises all fields, sets up temporary fields
  !>          and performs the timestepping and checks to see if it should
  !>          dump output.
  !>          The type of Runge-Kutta scheme can be simply changed by
  !>          modifying the (currently hardwired) Butcher tableau (ak array)
  !>          The current set up uses the 3rd-order three stage
  !>          Strong-Stability-Preserving (SSP3) method
  !> @param[in]    mesh Mesh object on which the model runs
  !> @param[inout] chi  The finite element form of the coordinates
  !> @param[inout] u The 3D wind field
  !> @param[inout] rho The density
  !> @param[inout] theta The potential temperature
  !> @param[inout] xi The vorticity field  
  !> @param[in] restart Checkpoint/restart type with timestepping information
  subroutine rk_alg_timestep( mesh, chi, u, rho, theta, xi, restart)

    implicit none

    ! Mesh
    type (mesh_type), intent(in) :: mesh 
    ! Coordinate fields
    type( field_type ), intent( inout ) :: chi(3)
    ! Prognostic fields
    type( field_type ), intent( inout ) :: u, rho, theta, xi
    ! Restart for timestep control and diagnostic output control 
    type( restart_type ), intent( in )  :: restart

    ! Temporary fields
    type( field_type ) :: u_n, rho_n, theta_n,       &
                          r_u, r_rho, r_theta,       &
                          u_inc, rho_inc, theta_inc, &
                          mass_flux, rhs_xi,         &
                          geopotential

    type( field_type ), allocatable :: rt_prediction(:),  &
                                       ru_prediction(:),  &
                                       rr_prediction(:)

    type(function_space_type) :: fs
    type(function_space_type), pointer :: theta_fs => null()
    type(function_space_type), pointer :: u_fs     => null()
    type(function_space_type), pointer :: rho_fs   => null()
    type(function_space_type), pointer :: xi_fs    => null()
    type(function_space_type), pointer :: w0_fs    => null()
    type(function_space_type), pointer :: w1_fs    => null()
    type(function_space_type), pointer :: w2_fs    => null()


    real(kind=r_def), allocatable :: ak(:,:)
    type( quadrature_type) :: qr 
    integer, parameter :: VECTOR_FIELD = 3, &
                          SCALAR_FIELD = 1

    ! Timestepping variables
    integer :: timestep, ts_init
    integer :: stage, num_rk_stage, st
    type(operator_type) :: mm_w2, mm_w1, mm_w0

    !=== Section 0: Create RK weights and objects =============================!
    ! SSP3 weights
    num_rk_stage = 3
    allocate ( ak (num_rk_stage,num_rk_stage) )
    ak(1,:) = (/ 1.0_r_def,  0.0_r_def,  0.0_r_def /)
    ak(2,:) = (/ 0.25_r_def, 0.25_r_def, 0.0_r_def /)
    ak(3,:) = (/ 1.0_r_def,  1.0_r_def,  4.0_r_def /)/6.0_r_def

    allocate ( rt_prediction(num_rk_stage), &
               ru_prediction(num_rk_stage), &
               rr_prediction(num_rk_stage) )

    !=== Section 1: Create field objects =====================================!
    ! Function spaces
    theta_fs => fs%get_instance( mesh, element_order, theta%which_function_space() )
    u_fs     => fs%get_instance( mesh, element_order, u%which_function_space()     )
    rho_fs   => fs%get_instance( mesh, element_order, rho%which_function_space()   )
    xi_fs    => fs%get_instance( mesh, element_order, xi%which_function_space()    )
    w0_fs    => fs%get_instance( mesh, element_order, W0 )
    w1_fs    => fs%get_instance( mesh, element_order, W1 )
    w2_fs    => fs%get_instance( mesh, element_order, W2 )

    qr = quadrature_type(element_order+3, GAUSSIAN)

    ! Local fields
    theta_n      = field_type( vector_space = theta_fs )
    u_n          = field_type( vector_space = u_fs     )
    rho_n        = field_type( vector_space = rho_fs   )
    r_theta      = field_type( vector_space = theta_fs )
    r_u          = field_type( vector_space = u_fs     )
    r_rho        = field_type( vector_space = rho_fs   )
    theta_inc    = field_type( vector_space = theta_fs )
    u_inc        = field_type( vector_space = u_fs     )
    rho_inc      = field_type( vector_space = rho_fs   )
    mass_flux    = field_type( vector_space = u_fs     )
    rhs_xi       = field_type( vector_space = xi_fs    )
    geopotential = field_type( vector_space = theta_fs )

    do stage = 1,num_rk_stage
      rt_prediction(stage) = field_type( vector_space = theta_fs )
      ru_prediction(stage) = field_type( vector_space = u_fs     )
      rr_prediction(stage) = field_type( vector_space = rho_fs   )
    end do

    ! Create operators needed for timestepping
    mm_w0 = operator_type(W0_fs, W0_fs)
    mm_w1 = operator_type(W1_fs, W1_fs)
    mm_w2 = operator_type(W2_fs, W2_fs)

    !=== Section 2: Initialise local algorithm fields ========================!
    call log_event( "Dynamo: Computing Nonlinear RK algorithm fields", &
                     LOG_LEVEL_INFO )
    ! Compute geopotential
    call invoke(compute_geopotential_kernel_type( geopotential, chi ) )
    ! Compute mass matrices
    call invoke( compute_mass_matrix_kernel_w0_type(mm_w0, chi, qr),           &
                 compute_mass_matrix_kernel_w1_type(mm_w1, chi, qr),           &
                 compute_mass_matrix_kernel_w2_type(mm_w2, chi, qr) )
    ! Initialise other required fields
    call invoke_set_field_scalar(0.0_r_def, mass_flux)
    call invoke_set_field_scalar(0.0_r_def, xi)

    !--- Intial output and output frequency -----------------------------------! 
    ! Output initial conditions
    call log_event( "Dynamo: Outputing initial fields", LOG_LEVEL_INFO )
    ts_init = max( (restart%ts_start() - 1), 0 ) ! 0 or t previous.
    call output_alg(ts_init, restart, theta, xi, u, rho, chi, mesh, mm_w0)
    call conservation_algorithm(ts_init, mesh, rho, u, theta, xi, &
                                geopotential, chi)

    !=== Section 3: Timestepping ==============================================!

    do timestep = restart%ts_start(),restart%ts_end()
      call log_event( "&
      /****************************************************************************\ ", &
                      LOG_LEVEL_TRACE )
      write( log_scratch_space, '(A,I0)' ) 'Start of timestep ', timestep
      call log_event( log_scratch_space, LOG_LEVEL_INFO )

      call invoke_copy_field_data(theta,theta_n)
      call invoke_copy_field_data(u,u_n)
      call invoke_copy_field_data(rho,rho_n)

      ! Runge-Kutta algorithm
      do stage = 1,num_rk_stage
        write( log_scratch_space, '(A,I0)' ) '  Start of stage ', stage
        call log_event( log_scratch_space, LOG_LEVEL_INFO )
        ! Compute advection terms
        ! mass flux ( F = rho * u )
        call invoke_set_field_scalar(0.0_r_def, r_u)
        call invoke_flux_rhs( r_u, u, rho, chi, qr ) 

        call solver_algorithm( mass_flux     &
                             , r_u           &
                             , mesh          &
                             , chi           &
                             , method &
                             , mm=mm_w2 )

        ! Compute rrho = div(F)
        call invoke( rrho_kernel_type  ( rr_prediction(stage), mass_flux, qr ) )
        ! Compute vorticity
        call invoke_set_field_scalar(0.0_r_def, rhs_xi)
        call invoke( vorticity_rhs_kernel_type( rhs_xi, u, chi, qr ) )
        call invoke( enforce_bc_kernel_type( rhs_xi ) )
        call solver_algorithm( xi, rhs_xi, mesh, chi, method, mm=mm_w1)

        ! Compute new rhs
        call invoke_set_field_scalar(0.0_r_def, rt_prediction(stage))
        call invoke( rtheta_kernel_type( rt_prediction(stage), theta, &
             mass_flux, rho, qr) )
        call invoke_set_field_scalar(0.0_r_def, ru_prediction(stage))
        if ( rotating ) &
          call invoke( rotation_kernel_type( ru_prediction(stage), u, chi, qr ) )
        call invoke( pressure_gradient_kernel_type       &
             ( ru_prediction(stage), rho, theta, qr ) )
        call invoke( kinetic_energy_gradient_kernel_type &
             ( ru_prediction(stage), u, chi, qr ) )
        call invoke( geopotential_gradient_kernel_type   &
             ( ru_prediction(stage), geopotential, qr ) )
        call invoke( vorticity_advection_kernel_type     &
             ( ru_prediction(stage), mass_flux, rho, xi, chi, qr ) )
        call invoke( enforce_bc_kernel_type( ru_prediction(stage) ) ) 

        call invoke_set_field_scalar(0.0_r_def, r_theta)
        call invoke_set_field_scalar(0.0_r_def, r_u)
        call invoke_set_field_scalar(0.0_r_def, r_rho)

        do st = 1, stage
          call invoke_axpy(ak(stage,st), rt_prediction(st),r_theta, r_theta)
          call invoke_axpy(ak(stage,st), ru_prediction(st),r_u, r_u)
          call invoke_axpy(ak(stage,st), rr_prediction(st),r_rho, r_rho)
        end do

        ! Invert mass matrices
        call solver_algorithm( theta_inc     &
                             , r_theta       &
                             , mesh          &
                             , chi           &
                             , method &
                             , mm=mm_w0 )
        call solver_algorithm( u_inc         &
                             , r_u           &
                             , mesh          &
                             , chi           &
                             , method &
                             , mm=mm_w2 )
        call solver_algorithm( rho_inc       &
                             , r_rho         &
                             , mesh          &
                             , chi           &
                             , method &
                             , qr=qr )

        ! Add increments
        call invoke_axpy(dt, theta_inc, theta_n, theta)
        call invoke_axpy(dt, u_inc, u_n, u)
        call invoke_axpy(dt, rho_inc, rho_n, rho)

        ! Diagnostics
        call theta_inc%log_minmax(LOG_LEVEL_TRACE, 'theta_inc');
        call u_inc%log_minmax(LOG_LEVEL_TRACE, 'u_inc');
        call rho_inc%log_minmax(LOG_LEVEL_TRACE, 'rho_inc');

      end do
       !---------End of timestep diagnostics ----------------------------------!
      if ( mod(timestep, (restart%ts_start()+restart%diag_freq() - 1) )  == 0 ) then
        call output_alg(timestep, restart, theta, xi, u, rho, chi, mesh, mm_w0)
      end if
      call conservation_algorithm(timestep, mesh, rho, u, theta, xi, &
                                  geopotential, chi)

      write( log_scratch_space, '(A,I0)' ) 'End of timestep ', timestep
      call log_event( log_scratch_space, LOG_LEVEL_TRACE )
      call log_event( &
      '\****************************************************************************/ ', &
                      LOG_LEVEL_TRACE )
    end do
    call log_event( "Dynamo: Finished timestep", LOG_LEVEL_INFO )
    !==========================================================================

  end subroutine rk_alg_timestep

end module rk_alg_timestep_mod
