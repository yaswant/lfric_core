module nodal_output_alg_mod

use base_mesh_config_mod,      only: geometry, &
                                     base_mesh_geometry_spherical
use constants_mod,             only: r_def, str_max_filename
use field_mod,                 only: field_type
use finite_element_config_mod, only: element_order
use fs_continuity_mod,         only: W0, W1, W2, W3
use function_space_mod,        only: function_space_type
use log_mod,                   only: log_event,         &
                                     log_scratch_space, &
                                     LOG_LEVEL_INFO
use mesh_mod,                  only: mesh_type
use multiplicity_kernel_mod,   only: multiplicity_kernel_type
use psykal_lite_mod,           only: invoke_set_field_scalar, &
                                     invoke_nodal_coordinates_kernel, &
                                     invoke_copy_field_data, &
                                     invoke_convert_hcurl_field, &
                                     invoke_convert_hdiv_field, &
                                     invoke_divide_field, &
                                     invoke_convert_cart2sphere_vector, &
                                     invoke_pointwise_convert_xyz2llr, &
                                     invoke_compute_dof_level_kernel, &
                                     invoke_write_fields

implicit none

contains

subroutine nodal_output_alg(field, chi, fname, mesh) 

    type(field_type),            intent(in) :: field, chi(3)   
    character(str_max_filename), intent(in) :: fname
    type(mesh_type),             intent(in) :: mesh

    ! Local variables
    type(field_type)             :: nodal_output(3), nodal_coordinates(3)
    type(field_type)             :: nodal_multiplicity, level
    integer, parameter           :: nodal_output_unit = 21
    type( function_space_type )  :: fs
    integer                      :: i, output_fs, output_dim

    !(1) Create output fields
    output_fs = field%which_function_space()
    do i = 1,3
      nodal_output(i)      = field_type( vector_space = fs%get_instance(mesh, element_order, output_fs) )
      nodal_coordinates(i) = field_type( vector_space = fs%get_instance(mesh, element_order, output_fs) )
      call invoke_set_field_scalar( 0.0_r_def, nodal_output(i) )
    end do
    nodal_multiplicity = field_type( vector_space = fs%get_instance(mesh, element_order, output_fs) )
    call invoke_set_field_scalar( 0.0_r_def, nodal_multiplicity )
    call invoke ( multiplicity_kernel_type( nodal_multiplicity ) )

    !(2) Convert field to physical nodal output & sample chi on nodal points
    ! If the field is a vector space we provide the 3 components as separate
    ! output fields
    call invoke_nodal_coordinates_kernel(nodal_coordinates, chi)
    select case ( output_fs )
      case ( W0 ) 
      ! for H1 spaces no conversion is required
        call  invoke_copy_field_data(field, nodal_output(1))
        output_dim = 1
      case ( W1 )
        call invoke_convert_hcurl_field(nodal_output, field, chi)
        output_dim = 3
      case ( W2 )
        call invoke_convert_hdiv_field(nodal_output, field, chi)
        output_dim = 3
      case ( W3 ) 
        ! for L2 spaces no conversion is currently required (due to
        ! rehabilitation)
        call  invoke_copy_field_data(field, nodal_output(1))
        output_dim = 1
      case default
        write( log_scratch_space, '(A)') &
        'nodal_output: no field conversion has been applied'
        call log_event( log_scratch_space, log_level_info)
        call  invoke_copy_field_data(field, nodal_output(1))
        output_dim = 1
    end select
    ! (3) If in spherical geometry convert the vector spaces to standard SI units
    ! (m/s) in orthogonal directions and convert the coordinate field to
    ! (longitude, latitude, radius)
    if ( geometry == base_mesh_geometry_spherical ) then
      if ( output_fs == W1 .or. output_fs == W2) then
        do i = 1,output_dim
          call invoke_divide_field( nodal_output(i), nodal_multiplicity, nodal_output(i) ) 
        end do
        call invoke_convert_cart2sphere_vector(nodal_output, nodal_coordinates)         
      end if
      call invoke_pointwise_convert_xyz2llr(nodal_coordinates) 
    end if

    !(4) Compute fractional level of every dof
    level = field_type( vector_space = fs%get_instance(mesh, element_order, output_fs) )
    call invoke_compute_dof_level_kernel(level)
  
    !(5) write info to file    
    write( log_scratch_space, '(A,A)' ) 'Writing nodal output: ',trim(fname)
    call log_event( log_scratch_space, LOG_LEVEL_INFO )
    call invoke_write_fields(nodal_coordinates, level, nodal_output, output_dim ,nodal_output_unit, fname)
end subroutine nodal_output_alg

end module nodal_output_alg_mod
