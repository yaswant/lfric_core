!-------------------------------------------------------------------------------
! (c) The copyright relating to this work is owned jointly by the Crown,
! Met Office and NERC 2014.
! However, it has been created with the help of the GungHo Consortium,
! whose members are identified at https://puma.nerc.ac.uk/trac/GungHo/wiki
!-------------------------------------------------------------------------------

!>@brief Initialisation of prognostic fields
module init_prognostic_fields_alg_mod

  use log_mod,                         only: log_event,         &
                                             log_scratch_space, &
                                             LOG_LEVEL_INFO,    &
                                             LOG_LEVEL_TRACE
  use constants_mod,                   only: r_def, i_def

  use solver_mod,                      only: solver_algorithm
  use solver_config_mod,               only: method

  ! Configuration and restart/checkpoint options
  use finite_element_config_mod,       only: element_order, wtheta_on
  use formulation_config_mod,          only: nonlinear, transport_only, use_moisture
  use transport_config_mod,            only: scheme, &
                                             operators, &
                                             transport_scheme_method_of_lines, &
                                             transport_operators_fv
  use restart_control_mod,             only: restart_type
  ! I/O tools
  use field_io_mod,                    only: read_state_netcdf

  ! PsyKAl-lite kernels
  use psykal_lite_mod,                 only: invoke_copy_field_data, &
                                             invoke_set_field_scalar, &
                                             invoke_initial_mr_kernel, &
                                             invoke_initial_theta_kernel

  ! PsyKAl PSYClone kernels
  use initial_rho_kernel_mod,          only: initial_rho_kernel_type
  use initial_u_kernel_mod,            only: initial_u_kernel_type
  use compute_mass_matrix_kernel_w2_mod, only: compute_mass_matrix_kernel_w2_type

  ! Derived Types
  use function_space_collection_mod,   only: function_space_collection
  use function_space_mod,              only: function_space_type
  use field_mod,                       only: field_type
  use quadrature_mod,                  only: quadrature_type, GAUSSIAN
  use operator_mod,                    only: operator_type
  use evaluator_xyz_mod,               only: evaluator_xyz_type

  ! Handles
  use fs_continuity_mod,               only: W0, W1, W2, W3, Wtheta
  use mr_indices_mod,                  only: nummr
  use map_rho_to_theta_mod,            only: map_rho_to_theta
  implicit none

  private
  public :: init_prognostic_fields_alg

contains
  !> @details An algorithm for initialising prognostic fields for 
  !>          all solvers.
  !> @param[in] mesh_id Id of Mesh object on which the model runs
  !> @param[inout] chi  The finite element form of the coordinates
  !> @param[inout] u  The 3D wind field
  !> @param[inout] rho The density
  !> @param[inout] theta The potential temperature
  !> @param[inout] xi The vorticity field 
  !> @param[in] restart Checkpoint/restart type with timestepping information
  subroutine init_prognostic_fields_alg( mesh_id, chi, u, rho, theta, rho_in_wth, mr, xi, restart)

    implicit none

    ! Mesh id
    integer(i_def),  intent(in)         :: mesh_id
    ! Coordinate fields
    type( field_type ), intent( inout ) :: chi(3)
    ! Prognostic fields
    type( field_type ), intent( inout ) :: u, rho, theta, xi, mr(nummr),rho_in_wth
    ! Control
    type( restart_type ), intent(in)    :: restart

    type( quadrature_type )             :: qr
    type( field_type ), allocatable     :: state(:)
    type( field_type )                  :: r_u
    type( operator_type )               :: mm_w2
    type(function_space_type), pointer  :: w2_fs, wtheta_fs   => null()

    integer                             :: n_fields
    type(evaluator_xyz_type)            :: evaluator

    integer :: i
    integer :: n_checkpoint  ! number of fields to checkpoint
    character(5) :: name

    qr = quadrature_type( element_order + 3, GAUSSIAN )

    !=== Initialise global prognostic fields ==================================!

    if (.not.restart%read_file()) then           ! No check point to start from

      call log_event( "Dynamo: Initialising prognostic fields", LOG_LEVEL_INFO )

      ! Initialise Xi
      call invoke_set_field_scalar(0.0_r_def, xi) 
      ! Initialise theta
      wtheta_fs => function_space_collection%get_fs( mesh_id, element_order, theta%which_function_space() )
      evaluator = evaluator_xyz_type( wtheta_fs%get_ndf( ), wtheta_fs%get_nodes( ))
      call invoke_initial_theta_kernel( theta, chi, evaluator )
      
      ! Initialise U and Rho according to formulation
      if (  nonlinear .OR. transport_only ) then
        r_u = field_type( vector_space =                                           &
                    function_space_collection%get_fs(mesh_id, element_order, W2) )
        call invoke_set_field_scalar(0.0_r_def, u)
        call invoke_set_field_scalar(0.0_r_def, r_u)
        call invoke( initial_u_kernel_type( r_u, chi, qr ) )
        w2_fs    => function_space_collection%get_fs(mesh_id, &
                                                     element_order, &
                                                     W2)

        mm_w2 = operator_type( w2_fs, w2_fs )
        call invoke( compute_mass_matrix_kernel_w2_type(mm_w2, chi, qr) )
        call solver_algorithm(u, r_u, mesh_id, chi, method, mm=mm_w2)

        call invoke( initial_rho_kernel_type ( rho, chi, qr ) )

      else
        call invoke_set_field_scalar(0.0_r_def, u)
        call invoke_set_field_scalar(0.0_r_def, rho)
      end if

      call log_event( "Dynamo: Mapping rho->rho_in_wth in same space as theta..", LOG_LEVEL_INFO )

      call map_rho_to_theta(rho, theta, rho_in_wth, chi)
      
      if (use_moisture)call invoke_initial_mr_kernel( theta, rho_in_wth, mr(:), chi, evaluator )

      call log_event( "Dynamo: Initialised prognostic fields", LOG_LEVEL_INFO )

    else                                   ! Recorded check point to start from

      n_checkpoint = 4
      if (use_moisture)n_checkpoint=n_checkpoint+nummr
      allocate(state(n_checkpoint))
      n_fields = 1
      write(log_scratch_space,'(A,A)') "Reading file:", &
            trim(restart%startfname("rho"))
      call log_event(log_scratch_space,LOG_LEVEL_INFO)

      state(1) = field_type(vector_space =                                     &
                        function_space_collection%get_fs(mesh_id,element_order, W3))

      call read_state_netcdf(n_fields, state(1), trim(restart%startfname("rho")))
      rho = state(1)

      write(log_scratch_space,'(A,A)') "Reading file:", &
            trim(restart%startfname("u"))
      call log_event(log_scratch_space,LOG_LEVEL_INFO)

      state(2) = field_type(vector_space =                                      &
                       function_space_collection%get_fs(mesh_id, element_order, W2))

      call read_state_netcdf(n_fields, state(2), trim(restart%startfname("u")))
      u = state(2)

      write(log_scratch_space,'(A,A)') "Reading file:", &
            trim(restart%startfname("theta"))
      call log_event(log_scratch_space,LOG_LEVEL_INFO)

      if (wtheta_on) then
        state(3) = field_type(vector_space =                                      &
                          function_space_collection%get_fs(mesh_id, element_order, Wtheta))
      else
        state(3) = field_type(vector_space =                                      &
                          function_space_collection%get_fs(mesh_id, element_order, W0))
      end if

      call read_state_netcdf(n_fields, state(3), &
           trim(restart%startfname("theta")))
      theta = state(3)

      write(log_scratch_space,'(A,A)') "Reading file:", &
            trim(restart%startfname("xi"))
      call log_event(log_scratch_space,LOG_LEVEL_INFO)

      state(4) = field_type(vector_space =                                      &
                       function_space_collection%get_fs(mesh_id, element_order, W1))

      call read_state_netcdf(n_fields, state(4), trim(restart%startfname("xi")))
      xi = state(4)

      if (use_moisture)then
        do i=1,nummr
          state(4+i) = field_type(vector_space =                                      &
             function_space_collection%get_fs(mesh_id, element_order, Wtheta))
          write(name, '(A,I2.2)') 'mr_', i
          call read_state_netcdf(n_fields, state(4+i), trim(restart%startfname(name)))
          mr(i) = state(4+i)
        end do
      end if

      deallocate(state)

      call log_event( "Dynamo: Mapping rho->rho_in_wth in same space as theta..", LOG_LEVEL_INFO )
      call map_rho_to_theta(rho, theta, rho_in_wth, chi)

      call log_event( "Dynamo: Read prognostic fields from files", LOG_LEVEL_INFO )
    end if

    !==========================================================================!

  end subroutine init_prognostic_fields_alg

end module init_prognostic_fields_alg_mod
