!-------------------------------------------------------------------------------
! (c) The copyright relating to this work is owned jointly by the Crown,
! Met Office and NERC 2014.
! However, it has been created with the help of the GungHo Consortium,
! whose members are identified at https://puma.nerc.ac.uk/trac/GungHo/wiki
!-------------------------------------------------------------------------------

!>@brief A two time-level iterative time-discretisation of the nonlinear 3D equations
module iter_timestep_alg_mod

  use log_mod,            only: log_event,         &
                                log_scratch_space, &
                                LOG_LEVEL_INFO,    &
                                LOG_LEVEL_TRACE

  ! PsyKAl lite kernels
  use psy,                               only: invoke_axpby,                   &
                                               invoke_plus_field_data,         &
                                               invoke_minus_field_data,        &
                                               invoke_copy_field_data,         &
                                               invoke_set_field_scalar
  ! PsyKAl PSYClone kernels
  use initial_theta_kernel_mod,          only: initial_theta_kernel_type
  use compute_mass_matrix_kernel_w0_mod, only: compute_mass_matrix_kernel_w0_type
  use compute_mass_matrix_kernel_w1_mod, only: compute_mass_matrix_kernel_w1_type
  use compute_mass_matrix_kernel_w2_mod, only: compute_mass_matrix_kernel_w2_type
  use compute_geopotential_kernel_mod,   only: compute_geopotential_kernel_type
  use initial_rho_kernel_mod,            only: initial_rho_kernel_type

  ! Derived Types
  use mesh_mod,                          only: mesh_type
  use field_mod,                         only: field_type
  use function_space_mod,                only: function_space_type, W0, W1, W2, W3
  use solver_mod,                        only: solver_algorithm
  use constants_mod,                     only: r_def, SOLVER_OPTION, DT, NT
  use quadrature_mod,                    only: quadrature_type, QR3
  use operator_mod,                      only: operator_type
  use runtime_constants_mod,             only: runtime_constants_type

  ! Algorithms
  use conservation_algorithm_mod,        only: conservation_algorithm
  use rhs_alg_mod,                       only: rhs_alg
  use advection_alg_mod,                 only: advection_alg
  use output_alg_mod,                    only: output_alg

  implicit none

  private
  public :: iter_timestep_alg

contains
  !> @details An algorithm for timstepping the 3D nonlinear equations
  !>         using a iterative process of the same form as endgame.
  !>         Initialialises all fields, sets up tempoary fields, performs the
  !>         timestepping and checks to see if it should dump output.
  !>         The algorithm splits all processes into one of three parts.
  !>         Old time level forcings computed once per timestep.
  !>         Advection terms computed in an outer loop using time-averaged
  !>         fields.
  !>         New time level forcings computed in an inner loop.
  !>         To match ENDGame 2 outer and 2 inner loops are used per timestep
  !>         by default. This means that there is one evaluation of old time
  !>         level terms, 2 evaluation of advective terms and 4 evaluations 
  !>         of new time level terms and increment updates per timestep
  !> @param[in]    mesh Mesh object on which the model runs
  !> @param[inout] chi  The finite element form of the coordinates
  !> @param[inout] u  The 3D wind field
  !> @param[inout] rho The density
  !> @param[inout] theta The potential temperature
  !> @param[inout] xi The vorticity field 
  subroutine iter_timestep_alg( mesh, chi, u, rho, theta, xi)

    implicit none

    type (mesh_type), intent(in) :: mesh 

    ! coordinate fields
    type( field_type ), intent( inout ) :: chi(3)
    ! prognostic fields    
    type( field_type ), intent( inout ) :: u, rho, theta, xi
    ! temporary fields    
    type( field_type ) :: r_u, r_rho, r_theta,       &
                          u_inc, theta_inc, &
                          geopotential

    type( field_type ) :: state_n(3), state_np1(3), state_adv(3)
    type( field_type ) :: rhs_n(3), rhs_np1(3), rhs_adv(3)
    
    type( runtime_constants_type ) :: runtime_constants

    integer :: theta_fs, u_fs, rho_fs

    type( function_space_type )      :: fs
    type( quadrature_type ), pointer :: qr => null()
    type( operator_type )            :: mm_w2, mm_w1, mm_w0

    ! output variables
    integer :: output_freq

    ! Iterative timestepping variables
    integer            :: n
    integer, parameter :: N_OUTER_ITER = 2
    integer, parameter :: N_INNER_ITER = 2
    integer            :: outer, inner
    real(kind=r_def), parameter :: ALPHA = 0.5_r_def
    real(kind=r_def), parameter :: BETA = (1.0_r_def - ALPHA)

    !=== Section 1: Create field objects =====================================!
    ! Local fields
    theta_fs  = theta%which_function_space()
    u_fs      = u%which_function_space()
    rho_fs    = rho%which_function_space()

    qr => qr%get_instance(QR3,9,3)

    ! Create W0 fields needed for timestepping
    r_theta      = field_type( vector_space = fs%get_instance(mesh, theta_fs) )
    theta_inc    = field_type( vector_space = fs%get_instance(mesh, theta_fs) )
    geopotential = field_type( vector_space = fs%get_instance(mesh, theta_fs) )
    rhs_n(2)   = field_type( vector_space = fs%get_instance(mesh, theta_fs) )
    rhs_np1(2) = field_type( vector_space = fs%get_instance(mesh, theta_fs) )
    rhs_adv(2) = field_type( vector_space = fs%get_instance(mesh, theta_fs) )
    state_adv(2) = field_type( vector_space = fs%get_instance(mesh, theta_fs) )

    ! Create W2 fields needed for timestepping
    r_u       = field_type( vector_space = fs%get_instance(mesh, u_fs) )
    u_inc     = field_type( vector_space = fs%get_instance(mesh, u_fs) )
    rhs_n(1)   = field_type( vector_space = fs%get_instance(mesh, u_fs) )
    rhs_np1(1) = field_type( vector_space = fs%get_instance(mesh, u_fs) )
    rhs_adv(1) = field_type( vector_space = fs%get_instance(mesh, u_fs) )
    state_adv(1) = field_type( vector_space = fs%get_instance(mesh, u_fs) )

    ! Create W3 fields needed for timestepping
    r_rho     = field_type( vector_space = fs%get_instance(mesh, rho_fs) )
    rhs_n(3)   = field_type( vector_space = fs%get_instance(mesh, rho_fs) )
    rhs_np1(3) = field_type( vector_space = fs%get_instance(mesh, rho_fs) )
    rhs_adv(3) = field_type( vector_space = fs%get_instance(mesh, rho_fs) )
    state_adv(3) = field_type( vector_space = fs%get_instance(mesh, rho_fs) )

    ! Create operators needed for timestepping
    mm_w0 = operator_type(fs%get_instance(mesh, W0),fs%get_instance(mesh, W0))
    mm_w1 = operator_type(fs%get_instance(mesh, W1),fs%get_instance(mesh, W1))
    mm_w2 = operator_type(fs%get_instance(mesh, W2),fs%get_instance(mesh, W2))

    !=== Section 2: Initialise Fields ========================================!
    call log_event( "Dynamo: computing initial fields", LOG_LEVEL_INFO )
    call invoke( compute_geopotential_kernel_type( geopotential, chi ) )
    call invoke( initial_theta_kernel_type( theta, chi ) )   
    call invoke( initial_rho_kernel_type( rho, chi, qr ) )
    call invoke_set_field_scalar(0.0_r_def, u) 
    call invoke_set_field_scalar(0.0_r_def, xi) 

    call invoke( compute_mass_matrix_kernel_w0_type(mm_w0, chi, qr), &
                 compute_mass_matrix_kernel_w1_type(mm_w1, chi, qr), &
                 compute_mass_matrix_kernel_w2_type(mm_w2, chi, qr) )

    ! Create fixed state container
    runtime_constants = runtime_constants_type(mesh, chi, geopotential, &
                                             mm_w0, mm_w1, mm_w2)

    !=== Intial output and output frequency ==================================! 
    call output_alg(0, theta, u, rho, chi, mesh, mm_w0)
    call conservation_algorithm(0, mesh, rho, u, theta, xi, &
                                geopotential, chi)
    output_freq = NT

    !=== Section 3: Timestepping =============================================!
    do n = 1,NT
      call log_event( "/****************************************************************************\ ", &
                      LOG_LEVEL_TRACE )
      write( log_scratch_space, '(A,I0)' ) 'Start of timestep ', n
      call log_event( log_scratch_space, LOG_LEVEL_INFO )
      ! Time level n fields
      state_n(1) = u
      state_n(2) = theta
      state_n(3) = rho
      call rhs_alg(rhs_n, BETA*DT, state_n, runtime_constants)
      !=== Section 3.1:  Outer (advection loop) ==============================!
      do outer = 1,N_OUTER_ITER
        ! Set advecting velocity & terms
        call invoke_axpby( ALPHA, u,     BETA, state_n(1), state_adv(1) ) 
        call invoke_axpby( ALPHA, theta, BETA, state_n(2), state_adv(2) ) 
        call invoke_axpby( ALPHA, rho,   BETA, state_n(3), state_adv(3) )
        call advection_alg(rhs_adv, state_adv, (/u,theta,rho/), state_n,  &
                                                  xi, runtime_constants)

        !=== Section 3.2: Inner (nonlinear, coriolis loop) ===================!
        do inner = 1,N_INNER_ITER

          ! Construct rhs terms
          state_np1(1) = u
          state_np1(2) = theta
          state_np1(3) = rho
          call rhs_alg(rhs_np1, -ALPHA*DT, state_np1, runtime_constants)

          ! Add on advective terms: rhs = rhs_n - rhs_np1 + rhs_adv
          call invoke_minus_field_data( rhs_n(1), rhs_np1(1),  r_u )
          call invoke_plus_field_data ( rhs_adv(1), r_u,  r_u )

          call invoke_minus_field_data( rhs_n(2), rhs_np1(2),  r_theta )
          call invoke_plus_field_data ( rhs_adv(2), r_theta,  r_theta )

          call invoke_minus_field_data( rhs_n(3), rhs_np1(3),  r_rho )
          call invoke_plus_field_data ( rhs_adv(3), r_rho,  r_rho )

          ! === Section 3.3: Solve for increments ============================!
          ! Solve for u
          call invoke_set_field_scalar( 0.0_r_def, u_inc )
          call solver_algorithm(u_inc, r_u, mesh, chi, SOLVER_OPTION, mm=mm_w2)
          call invoke_plus_field_data( u_inc, u, u ) 
          ! Solve for theta
          call invoke_set_field_scalar( 0.0_r_def, theta_inc )
          call solver_algorithm(theta_inc, r_theta, mesh, chi, SOLVER_OPTION, &
                                mm=mm_w0)
          call invoke_plus_field_data( theta_inc, theta, theta )
          ! Solve for rho
          call invoke_plus_field_data( r_rho, rho, rho ) 

          ! diagnostics
          write( log_scratch_space, '(A,3I4)' ) &
             'Increments from solve: ', n, outer, inner
          call log_event( log_scratch_space, LOG_LEVEL_INFO )
          call theta_inc%log_minmax(LOG_LEVEL_INFO, 'theta_inc ')
          call u_inc%log_minmax(LOG_LEVEL_INFO,     'u_inc     ')
          call r_rho%log_minmax(LOG_LEVEL_INFO,     'rho_inc   ')
          !=== End Section 3.3 ===============================================!         
        end do
        !=== End Section 3.2 =================================================!
      end do     
      !=== End Section 3.1 ===================================================!
      !=== Section 3.4: End of timestep diagnostics ==========================!
      if ( mod(n, output_freq) == 0 ) &
        call output_alg(n, theta, u, rho, chi, mesh, mm_w0)

      call conservation_algorithm(n, mesh, rho, u, theta, xi, &
                                  geopotential, chi)

      write( log_scratch_space, '(A,I0)' ) 'End of timestep ', n
      call log_event( log_scratch_space, LOG_LEVEL_TRACE )
      call log_event( '\****************************************************************************/ ', &
                      LOG_LEVEL_TRACE )
      !=== End of Section 3.4 ================================================!
    end do
    
    call log_event( "Dynamo: finished timestep", LOG_LEVEL_INFO )
    !=== End Section 3.0 ======================================================!
  end subroutine iter_timestep_alg

end module iter_timestep_alg_mod
