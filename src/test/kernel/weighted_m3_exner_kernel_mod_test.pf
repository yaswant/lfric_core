!-----------------------------------------------------------------------------
! (c) The copyright relating to this work is owned jointly by the Crown, 
! Met Office and NERC 2014. 
! However, it has been created with the help of the GungHo Consortium, 
! whose members are identified at https://puma.nerc.ac.uk/trac/GungHo/wiki
!-----------------------------------------------------------------------------

!> Test the W3 mass matrix computation
!>
module weighted_m3_exner_kernel_mod_test

  use constants_mod,                 only : i_def, r_def
  use function_space_collection_mod, only : function_space_collection_type, &
                                            function_space_collection
  use field_mod,                     only : field_type, field_proxy_type
  use fs_continuity_mod,             only : W0, W3
  use function_space_mod,            only : function_space_type
  use operator_mod,                  only : operator_type, &
                                            operator_proxy_type
  use pFUnit_Mod
  use quadrature_mod,                only : quadrature_type, GAUSSIAN
  use mesh_collection_mod,           only : mesh_collection_type, &
                                            mesh_collection
  implicit none

  private
  public :: test_all

  @TestCase
  type, extends(TestCase), public :: weighted_m3_exner_test_type
    private
    integer(i_def)                     :: mesh_id
    type(quadrature_type)              :: qr
    type(function_space_type), pointer :: w0_fs => null()
    type(function_space_type), pointer :: w3_fs => null()
    type(field_type)                   :: chi(3), theta, rho
    type(operator_type)                :: mm
    real(r_def ), allocatable          :: diff_basis(:,:,:,:), &
                                          basis(:,:,:,:),      &
                                          basis_w0(:,:,:,:)
  contains
    procedure setUp
    procedure tearDown
    procedure test_all
  end type weighted_m3_exner_test_type

  integer(i_def), parameter :: element_order = 0
  real(r_def), parameter :: gravity = 9.80665_r_def
  real(r_def), parameter :: radius  = 6371229.0_r_def
  real(r_def), parameter :: omega   = 7.292116E-5_r_def
  real(r_def), parameter :: p_zero  = 100000.0_r_def
  real(r_def), parameter :: rd      = 287.05_r_def
  real(r_def), parameter :: cp      = 1005.0_r_def
  real(r_def), parameter :: scaling = 1.0_r_def

contains

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  subroutine setUp( this )

    use finite_element_config_mod, only : finite_element_shape_quadrilateral
    use feign_config_mod,          only : feign_finite_element_config, &
                                          feign_planet_config
    use mesh_mod,                  only : PLANE_BI_PERIODIC
    use reference_element_mod,     only : reference_cube, reference_element

    implicit none

    class(weighted_m3_exner_test_type), intent(inout) :: this

    type(field_proxy_type)    :: chi_p(3)
    type(operator_proxy_type) :: mm_p
    type(field_proxy_type)    :: theta_p

    integer(i_def) :: dim_mm, diff_dim, dim_w0
    integer(i_def) :: nqp_h, nqp_v
    integer(i_def) :: ndf, ndf_chi, ndf_w0

    integer :: i

    call feign_finite_element_config( finite_element_shape_quadrilateral, &
                                      0, 1, .true., .false., .false., .false. )

    call feign_planet_config( gravity=gravity, radius=radius, omega=omega, &
                              rd=rd, cp=cp, p_zero=p_zero,                 &
                              scaling_factor=scaling )

    reference_element = finite_element_shape_quadrilateral
    call reference_cube()

    ! Create top level function space collection

    function_space_collection = function_space_collection_type()

    this%qr  = quadrature_type(3,GAUSSIAN)

    nqp_h=this%qr%get_nqp_h()
    nqp_v=this%qr%get_nqp_v()

    ! Dummy mesh mod has 9 cells, 3 layers and is uniform in vertical
    mesh_collection = mesh_collection_type()
    this%mesh_id = mesh_collection%add_unit_test_mesh( PLANE_BI_PERIODIC )

    this%w0_fs => function_space_collection%get_fs( this%mesh_id, element_order, W0 )
    this%w3_fs => function_space_collection%get_fs( this%mesh_id, element_order, W3 )

    do i = 1,3
      this%chi(i) = field_type( vector_space = this%w0_fs )
      chi_p(i)    = this%chi(i)%get_proxy()
    end do
    this%theta =  field_type( vector_space = this%w0_fs )
    this%rho   =  field_type( vector_space = this%w3_fs )


    diff_dim = chi_p(1)%vspace%get_dim_space_diff( )   
    ndf_chi  = chi_p(1)%vspace%get_ndf()
    allocate( this%diff_basis( diff_dim, ndf_chi, nqp_h, nqp_v ) )

    this%mm = operator_type( this%w3_fs, this%w3_fs )
    mm_p    = this%mm%get_proxy()

    ndf    = mm_p%fs_from%get_ndf()
    dim_mm = mm_p%fs_from%get_dim_space( )
    allocate( this%basis( dim_mm, ndf, nqp_h, nqp_v ) )

    theta_p = this%theta%get_proxy()
    dim_w0 = theta_p%vspace%get_dim_space( )   
    ndf_w0 = theta_p%vspace%get_ndf()
    allocate( this%basis_w0( dim_w0, ndf_w0, nqp_h, nqp_v ) )
  end subroutine setUp

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  subroutine tearDown( this )

    use reference_element_mod, only : deallocate_reference

    implicit none

    class(weighted_m3_exner_test_type), intent(inout) :: this

    deallocate( this%basis, this%diff_basis, this%basis_w0 )

    call function_space_collection%clear()
    call mesh_collection%clear()
    call deallocate_reference()

  end subroutine tearDown

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  @test
  subroutine test_all( this )

    use weighted_m3_exner_kernel_mod, only : weighted_m3_exner_code
    use calc_exner_pointwise_mod,     only : calc_exner_pointwise
    use mesh_mod,                     only : mesh_type 
    use planet_config_mod,            only : kappa
    implicit none

    class(weighted_m3_exner_test_type), intent(inout) :: this

    real(r_def), parameter :: tol = 1.0e-6_r_def
    real(r_def), parameter :: dx = 6000.0_r_def, &
                              dy = 1000.0_r_def

    type(field_proxy_type)   :: chi_p(3), theta_p, rho_p

    real(kind=r_def) :: answer

    integer :: i, j, k, cell
    integer(i_def) :: err

    real(r_def) :: dz, dz_test(3)

    ! Required for calling mass matrix kernel

    integer :: nlayers, ndf, undf, ndf_w0, undf_w0
    integer :: ndf_chi, undf_chi
    integer :: nqp_h, nqp_v

    integer, pointer        ::  map(:), map_chi(:), map_w0(:)  => null()
    type(operator_proxy_type) :: mm_p

    real(kind=r_def), pointer :: xp(:,:) => null()
    real(kind=r_def), pointer :: zp(:)   => null()
    real(kind=r_def), pointer :: wh(:), wv(:) => null()
    real(kind=r_def)          :: exner

    type(mesh_type), pointer  :: mesh => null()
    mesh => mesh_collection%get_mesh( this%mesh_id )
    call mesh%get_dz(dz_test)
    dz = dz_test(1)

    do i = 1,3
      chi_p(i) = this%chi(i)%get_proxy()
    end do
    ! Compute coordinates
    cell = 1
    do j = 1,3
      do i = 1,3
        map_chi => chi_p(1)%vspace%get_cell_dofmap( cell )
        do k = 0,3
          chi_p(1)%data(map_chi(1) + k) = real(i-1)*dx
          chi_p(2)%data(map_chi(1) + k) = real(j-1)*dy
          chi_p(3)%data(map_chi(1) + k) = real(k)  *dz
        end do
        cell = cell + 1
      end do
    end do

    ! Test mass matrix kernel   
    mm_p = this%mm%get_proxy()
    theta_p = this%theta%get_proxy()
    rho_p   = this%rho%get_proxy()

    nqp_h=this%qr%get_nqp_h()
    nqp_v=this%qr%get_nqp_v()
    zp=>this%qr%get_xqp_v()
    xp=>this%qr%get_xqp_h()
    wh=>this%qr%get_wqp_h()
    wv=>this%qr%get_wqp_v()

    ndf_chi      = chi_p(1)%vspace%get_ndf()
    undf_chi     = chi_p(1)%vspace%get_undf()

    ndf  = mm_p%fs_from%get_ndf()
    undf = rho_p%vspace%get_undf()
 
    ndf_w0      = theta_p%vspace%get_ndf()
    undf_w0     = theta_p%vspace%get_undf()
  
    call chi_p(1)%vspace%compute_diff_basis_function(                    &
         this%diff_basis,ndf_chi, nqp_h, nqp_v, xp, zp )

    call mm_p%fs_from%compute_basis_function(                            &
         this%basis, ndf, nqp_h, nqp_v, xp, zp )

    call theta_p%vspace%compute_basis_function(                    &
         this%basis_w0,ndf_w0, nqp_h, nqp_v, xp, zp )

    nlayers  = mm_p%fs_from%get_nlayers()
    cell = 5
    map_chi => chi_p(1)%vspace%get_cell_dofmap(cell)
    map_w0  => theta_p%vspace%get_cell_dofmap(cell)
    map     => rho_p%vspace%get_cell_dofmap(cell)
    rho_p%data(:)   = 1.0_r_def
    theta_p%data(:) = 300.0_r_def
    call weighted_m3_exner_code( cell,                              &
                                 nlayers,                           &
                                 mm_p%ncell_3d,                     &
                                 mm_p%local_stencil,                &
                                 rho_p%data,                        &
                                 theta_p%data,                      &
                                 chi_p(1)%data,                     &
                                 chi_p(2)%data,                     &
                                 chi_p(3)%data,                     &
                                 ndf,                               &
                                 undf,                              &
                                 map,                               &
                                 this%basis,                        &
                                 ndf_w0,                            &
                                 undf_w0,                           &
                                 map_w0,                            &
                                 this%basis_w0,                     &
                                 ndf_chi,                           &
                                 undf_chi,                          &
                                 map_chi,                           &
                                 this%diff_basis,                   &
                                 nqp_h,                             &
                                 nqp_v,                             &
                                 wh,                                &
                                 wv                                 &
                                 )
   
    exner = calc_exner_pointwise(rho_p%data(1), theta_p%data(1))    
    answer = ((1.0_r_def-kappa)/kappa) /exner
    @assertEqual(answer, mm_p%local_stencil(1, 1, 13 )/(dx*dy*dz), tol)
    
  end subroutine test_all

end module weighted_m3_exner_kernel_mod_test
