!-----------------------------------------------------------------------------
! (c) The copyright relating to this work is owned jointly by the Crown, 
! Met Office and NERC 2014. 
! However, it has been created with the help of the GungHo Consortium, 
! whose members are identified at https://puma.nerc.ac.uk/trac/GungHo/wiki
!-----------------------------------------------------------------------------

!> Test the W0 mass matrix computation
!>
module compute_mass_matrix_kernel_w0_mod_test

    use constants_mod,     only : i_def, r_def
    use field_mod,         only : field_type, field_proxy_type
    use fs_continuity_mod, only : W0
    use mesh_mod,          only : mesh_type
    use operator_mod,      only : operator_type, operator_proxy_type
    use pFUnit_Mod
    use quadrature_mod,    only : quadrature_type, GAUSSIAN

  implicit none

  private
  public :: test_all

  @TestCase
  type, extends(TestCase), public :: compute_mass_matrix_w0_test_type
    private
    type(field_type)         :: chi(3)
    type(operator_type)      :: mm
    type(mesh_type)          :: mesh
    real(r_def), allocatable :: diff_basis(:,:,:,:), &
                                basis(:,:,:,:)
  contains
    procedure setUp
    procedure tearDown
    procedure test_all
  end type compute_mass_matrix_w0_test_type

  integer(i_def), parameter :: element_order = 0

contains

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  subroutine setUp( this )

    use finite_element_config_mod, only : finite_element_shape_quadrilateral
    use function_space_mod,        only : function_space_type
    use mesh_mod,                  only : PLANE_BI_PERIODIC
    use reference_element_mod,     only : reference_cube, reference_element

    implicit none

    class(compute_mass_matrix_w0_test_type), intent(inout) :: this

    type(quadrature_type)     :: qr
    type(function_space_type) :: fs
    type(field_proxy_type)    :: chi_p(3)
    type(operator_proxy_type) :: mm_p

    type(function_space_type), pointer :: w0_fs

    integer(i_def) :: nqp_h, nqp_v
    integer(i_def) :: ndf
    integer(i_def) :: diff_dim, dim_mm

    integer :: i

    qr  = quadrature_type(3, GAUSSIAN)

    nqp_h=qr%get_nqp_h()
    nqp_v=qr%get_nqp_v()

    reference_element = finite_element_shape_quadrilateral
    call reference_cube()

    ! Dummy mesh mod has 9 cells, 3 layers and is uniform in vertical
    this%mesh = mesh_type(PLANE_BI_PERIODIC)

    w0_fs => fs%get_instance( this%mesh, element_order, W0 )

    do i = 1,3
      this%chi(i) = field_type( vector_space = w0_fs )
      chi_p(i) = this%chi(i)%get_proxy()
    end do

    ndf      = chi_p(1)%vspace%get_ndf()
    diff_dim = chi_p(1)%vspace%get_dim_space_diff( )
    allocate(this%diff_basis(diff_dim,ndf,nqp_h,nqp_v) )

    this%mm = operator_type(W0_fs,W0_fs)
    mm_p = this%mm%get_proxy()

    dim_mm = mm_p%fs_from%get_dim_space_diff( )
    allocate(this%basis(dim_mm,ndf,nqp_h,nqp_v) )

  end subroutine setUp

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  subroutine tearDown( this )

    use function_space_mod,    only : purge_function_spaces
    use reference_element_mod, only : deallocate_reference

    implicit none

    class(compute_mass_matrix_w0_test_type), intent(inout) :: this

    deallocate(this%basis, this%diff_basis)

    call purge_function_spaces()
    call deallocate_reference()

  end subroutine tearDown

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  @test
  subroutine test_all( this )

    use compute_mass_matrix_kernel_w0_mod, only : compute_mass_matrix_w0_code

    implicit none

    class(compute_mass_matrix_w0_test_type), intent(inout) :: this

    real(r_def), parameter :: tol = 1.0e-6_r_def
    real(r_def), parameter :: dx = 6000.0_r_def, &
                              dy = 1000.0_r_def

    type(field_proxy_type )   :: chi_p(3)

    type(quadrature_type)     :: qr

    real(kind=r_def), dimension(8,8) :: answer

    integer :: i, j, k, cell
    integer(i_def) :: err

    real(r_def) :: dz, dz_test(3)

    ! Required for calling mass matrix kernel
    integer :: nlayers, ndf, undf
    integer :: nqp_h, nqp_v

    integer, pointer        :: map(:)  => null()

    type( operator_proxy_type) :: mm_p

    real(kind=r_def), pointer :: xp(:,:) => null()
    real(kind=r_def), pointer :: zp(:)   => null()
    real(kind=r_def), pointer :: wh(:), wv(:) => null()

    qr  = quadrature_type(3, GAUSSIAN)

    call this%mesh%get_dz(dz_test)
    dz = dz_test(1)

    do i = 1,3
      chi_p(i) = this%chi(i)%get_proxy()
    end do
    ! Compute coordinates
    cell = 1
    do j = 1,3
      do i = 1,3
        map => chi_p(1)%vspace%get_cell_dofmap( cell )
        do k = 0,3
          chi_p(1)%data(map(1) + k) = real(i-1)*dx
          chi_p(2)%data(map(1) + k) = real(j-1)*dy
          chi_p(3)%data(map(1) + k) = real(k)  *dz
        end do
        cell = cell + 1
      end do
    end do

    ! Test mass matrix kernel
    mm_p = this%mm%get_proxy()

    nqp_h=qr%get_nqp_h()
    nqp_v=qr%get_nqp_v()
    zp=>qr%get_xqp_v()
    xp=>qr%get_xqp_h()
    wh=>qr%get_wqp_h()
    wv=>qr%get_wqp_v()

    nlayers  = mm_p%fs_from%get_nlayers()

    ndf      = chi_p(1)%vspace%get_ndf()
    undf     = chi_p(1)%vspace%get_undf()

    call chi_p(1)%vspace%compute_diff_basis_function(                    &
         this%diff_basis,ndf, nqp_h, nqp_v, xp, zp )

    call mm_p%fs_from%compute_basis_function(                            &
         this%basis, ndf, nqp_h, nqp_v, xp, zp )

    cell = 1
    map => chi_p(1)%vspace%get_cell_dofmap(cell)
    ! Create the data
    mm_p%local_stencil(:,:,:) = 0.0_r_def
    call compute_mass_matrix_w0_code( cell,                              &
                                      nlayers,                           &
                                      mm_p%ncell_3d,                     &
                                      mm_p%local_stencil,                &
                                      chi_p(1)%data,                     &
                                      chi_p(2)%data,                     &
                                      chi_p(3)%data,                     &
                                      ndf,                               &
                                      undf,                              &
                                      map,                               &
                                      this%basis,                        &
                                      this%diff_basis,                   &
                                      nqp_h,                             &
                                      nqp_v,                             &
                                      wh,                                &
                                      wv                                 &
                                      )

    call mass_matrix_w0(answer)
    ! Normalise answers by volume element
    answer = answer(:,:) / (dx*dy*dz)
    mm_p%local_stencil(:, :, 1 ) = mm_p%local_stencil(:, :, 1 )/(dx*dy*dz)
    @assertEqual(answer, mm_p%local_stencil(:, :, 1 ), tol)

  end subroutine test_all

  subroutine mass_matrix_w0( mm )
    use constants_mod, only: r_def 
    implicit none

    real(kind=r_def), dimension(8,8), intent(inout) :: mm
    mm = reshape( [ &
  0.4444444444444401E+09_r_def, &
  0.2222222222222204E+09_r_def, &
  0.1111111111111104E+09_r_def, &
  0.2222222222222204E+09_r_def, &
  0.2222222222222204E+09_r_def, &
  0.1111111111111103E+09_r_def, &
  0.5555555555555524E+08_r_def, &
  0.1111111111111103E+09_r_def, &
  0.2222222222222204E+09_r_def, &
  0.4444444444444401E+09_r_def, &
  0.2222222222222204E+09_r_def, &
  0.1111111111111104E+09_r_def, &
  0.1111111111111103E+09_r_def, &
  0.2222222222222203E+09_r_def, &
  0.1111111111111104E+09_r_def, &
  0.5555555555555524E+08_r_def, &
  0.1111111111111104E+09_r_def, &
  0.2222222222222204E+09_r_def, &
  0.4444444444444401E+09_r_def, &
  0.2222222222222204E+09_r_def, &
  0.5555555555555524E+08_r_def, &
  0.1111111111111104E+09_r_def, &
  0.2222222222222203E+09_r_def, &
  0.1111111111111104E+09_r_def, &
  0.2222222222222204E+09_r_def, &
  0.1111111111111104E+09_r_def, &
  0.2222222222222204E+09_r_def, &
  0.4444444444444401E+09_r_def, &
  0.1111111111111103E+09_r_def, &
  0.5555555555555524E+08_r_def, &
  0.1111111111111104E+09_r_def, &
  0.2222222222222204E+09_r_def, &
  0.2222222222222204E+09_r_def, &
  0.1111111111111103E+09_r_def, &
  0.5555555555555524E+08_r_def, &
  0.1111111111111103E+09_r_def, &
  0.4444444444444401E+09_r_def, &
  0.2222222222222203E+09_r_def, &
  0.1111111111111103E+09_r_def, &
  0.2222222222222203E+09_r_def, &
  0.1111111111111103E+09_r_def, &
  0.2222222222222203E+09_r_def, &
  0.1111111111111104E+09_r_def, &
  0.5555555555555524E+08_r_def, &
  0.2222222222222203E+09_r_def, &
  0.4444444444444399E+09_r_def, &
  0.2222222222222203E+09_r_def, &
  0.1111111111111103E+09_r_def, &
  0.5555555555555524E+08_r_def, &
  0.1111111111111104E+09_r_def, &
  0.2222222222222203E+09_r_def, &
  0.1111111111111104E+09_r_def, &
  0.1111111111111103E+09_r_def, &
  0.2222222222222203E+09_r_def, &
  0.4444444444444399E+09_r_def, &
  0.2222222222222203E+09_r_def, &
  0.1111111111111103E+09_r_def, &
  0.5555555555555524E+08_r_def, &
  0.1111111111111104E+09_r_def, &
  0.2222222222222204E+09_r_def, &
  0.2222222222222203E+09_r_def, &
  0.1111111111111103E+09_r_def, &
  0.2222222222222203E+09_r_def, &
  0.4444444444444400E+09_r_def  &
    ], shape(mm) )
    return
  end subroutine mass_matrix_w0

end module compute_mass_matrix_kernel_w0_mod_test
