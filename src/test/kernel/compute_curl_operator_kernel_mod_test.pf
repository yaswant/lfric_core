!-----------------------------------------------------------------------------
! (c) The copyright relating to this work is owned jointly by the Crown, 
! Met Office and NERC 2014. 
! However, it has been created with the help of the GungHo Consortium, 
! whose members are identified at https://puma.nerc.ac.uk/trac/GungHo/wiki
!-----------------------------------------------------------------------------

!> Test the curl operator computation
!>
module compute_curl_operator_kernel_mod_test

    use constants_mod,      only : i_def, r_def
    use field_mod,          only : field_type,field_proxy_type
    use fs_continuity_mod,  only : W0, W1, W2
    use function_space_mod, only : function_space_type
    use mesh_mod,           only : mesh_type
    use operator_mod,       only : operator_type, operator_proxy_type
    use pFUnit_Mod
    use quadrature_mod,     only : quadrature_type, GAUSSIAN

  implicit none

  private
  public :: test_all

  @TestCase
  type, extends(TestCase), public :: compute_curl_operator_test_type
    private
    type(mesh_type)          :: mesh
    type(field_type)         :: chi(3)
    type(operator_type)      :: curl
    real(r_def), allocatable :: diff_basis_w1(:,:,:,:), &
                                basis_w2(:,:,:,:), &
                                diff_basis_chi(:,:,:,:)
  contains
    procedure setUp
    procedure tearDown
    procedure test_all
  end type compute_curl_operator_test_type

  integer(i_def), parameter :: element_order = 0

contains

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  subroutine setUp( this )

    use finite_element_config_mod, only : finite_element_shape_quadrilateral
    use mesh_mod,                  only : PLANE_BI_PERIODIC
    use reference_element_mod,     only : reference_cube, reference_element

    implicit none

    class(compute_curl_operator_test_type), intent(inout) :: this

    type(operator_proxy_type) :: curl_p
    type(quadrature_type)     :: qr
    type(function_space_type) :: fs
    type(field_proxy_type)    :: chi_p(3)
    type(function_space_type), pointer :: w0_fs => null()
    type(function_space_type), pointer :: w1_fs => null()
    type(function_space_type), pointer :: w2_fs => null()

    integer :: nlayers, dim, diff_dim, dim_w2, diff_dim_w1
    integer :: ndf_chi, ndf_w2, ndf_w1
    integer :: nqp_h, nqp_v

    integer :: i

    reference_element = finite_element_shape_quadrilateral
    call reference_cube()

    qr  = quadrature_type(3, GAUSSIAN)

    nqp_h=qr%get_nqp_h()
    nqp_v=qr%get_nqp_v()

    ! Dummy mesh mod has 9 cells, 3 layers and is uniform in vertical
    this%mesh = mesh_type(PLANE_BI_PERIODIC)

    w0_fs => fs%get_instance( this%mesh, element_order, W0 )
    w1_fs => fs%get_instance( this%mesh, element_order, W1 )
    w2_fs => fs%get_instance( this%mesh, element_order, W2 )

    this%curl = operator_type(w2_fs,w1_fs)

    do i = 1,3
      this%chi(i) = field_type( vector_space = w0_fs )
      chi_p(i) = this%chi(i)%get_proxy()
    end do

    diff_dim = chi_p(1)%vspace%get_dim_space_diff( )
    ndf_chi  = chi_p(1)%vspace%get_ndf()
    allocate(this%diff_basis_chi(diff_dim,ndf_chi,nqp_h,nqp_v) )

    curl_p = this%curl%get_proxy()   
 
    ndf_w1  = curl_p%fs_from%get_ndf()
    diff_dim_w1 = curl_p%fs_from%get_dim_space_diff( )
    allocate(this%diff_basis_w1(diff_dim_w1 ,ndf_w1, nqp_h, nqp_v) )

    ndf_w2 = curl_p%fs_to%get_ndf()
    dim_w2 = curl_p%fs_to%get_dim_space( )
    allocate(this%basis_w2(dim_w2, ndf_w2, nqp_h, nqp_v) )

  end subroutine setUp

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  subroutine tearDown( this )

    use function_space_mod,    only : purge_function_spaces
    use reference_element_mod, only : deallocate_reference

    implicit none

    class(compute_curl_operator_test_type), intent(inout) :: this

    deallocate( this%basis_w2, this%diff_basis_w1, this%diff_basis_chi)

    call purge_function_spaces()
    call deallocate_reference()

  end subroutine tearDown

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  @test
  subroutine test_all( this )

    use compute_curl_operator_kernel_mod, &
                                    only : compute_curl_operator_code

    implicit none

    class(compute_curl_operator_test_type), intent(inout) :: this

    type(operator_proxy_type) :: curl_p
    type(field_proxy_type )   :: chi_p(3) 
    type(quadrature_type)     :: qr

    real(kind=r_def), parameter :: tol = 1.0e-6_r_def
    real(kind=r_def), parameter :: dx = 6000.0_r_def, &
                                   dy = 1000.0_r_def

    integer :: i, j, k, cell
    integer(i_def) :: err

    real(r_def) :: dz, dz_test(3)
     
    integer :: nlayers
    integer :: ndf_chi, undf_chi, ndf_w1, ndf_w2
    integer :: nqp_h, nqp_v

    integer, pointer :: map_chi(:)  => null()

    real(kind=r_def), pointer :: xp(:,:) => null()
    real(kind=r_def), pointer :: zp(:)   => null()
    real(kind=r_def), pointer :: wh(:), wv(:) => null()
    real(kind=r_def), dimension(6,12) :: answer

    qr  = quadrature_type(3, GAUSSIAN)

    call this%mesh%get_dz(dz_test)
    dz = dz_test(1)

    ! Make a coordinate field with a function space
    do i = 1,3
      chi_p(i) = this%chi(i)%get_proxy()
    end do
    ! Compute coordinates
    cell = 1
    do j = 1,3
      do i = 1,3
        map_chi => chi_p(1)%vspace%get_cell_dofmap( cell )
        do k = 0,3          
          chi_p(1)%data(map_chi(1) + k) = real(i-1)*dx
          chi_p(2)%data(map_chi(1) + k) = real(j-1)*dy
          chi_p(3)%data(map_chi(1) + k) = real(k)  *dz
        end do
        cell = cell + 1
      end do
    end do

    ! Test the operator kernel
    curl_p = this%curl%get_proxy()

    nqp_h=qr%get_nqp_h()
    nqp_v=qr%get_nqp_v()
    zp=>qr%get_xqp_v()
    xp=>qr%get_xqp_h()
    wh=>qr%get_wqp_h()
    wv=>qr%get_wqp_v()

    ndf_chi  = chi_p(1)%vspace%get_ndf()
    undf_chi = chi_p(1)%vspace%get_undf()
    call chi_p(1)%vspace%compute_diff_basis_function(                    &
         this%diff_basis_chi,ndf_chi, nqp_h, nqp_v, xp, zp )

    nlayers = curl_p%fs_from%get_nlayers()
    ndf_w1  = curl_p%fs_from%get_ndf()
    call curl_p%fs_from%compute_diff_basis_function(                     &
         this%diff_basis_w1, ndf_w1, nqp_h, nqp_v, xp, zp ) 

    ndf_w2 = curl_p%fs_to%get_ndf()
    call curl_p%fs_to%compute_basis_function(                            &
         this%basis_w2, ndf_w2, nqp_h, nqp_v, xp, zp ) 

    cell = 1
    map_chi => chi_p(1)%vspace%get_cell_dofmap(cell)
    curl_p%local_stencil(:,:,:) = 0.0_r_def
    call compute_curl_operator_code( cell,                             &
                                    nlayers,                           &
                                    curl_p%ncell_3d,                   &
                                    curl_p%local_stencil,              &
                                    chi_p(1)%data,                     &
                                    chi_p(2)%data,                     &
                                    chi_p(3)%data,                     &
                                    ndf_w2,                            &
                                    this%basis_w2,                     &
                                    ndf_w1,                            &
                                    this%diff_basis_w1,                &
                                    ndf_chi,                           &
                                    undf_chi,                          &
                                    map_chi,                           &
                                    this%diff_basis_chi,               &
                                    nqp_h,                             &
                                    nqp_v,                             &
                                    wh,                                &
                                    wv                                 &
                                    )
    
    call curl_operator(answer)
    ! Normalise by average edge length
    answer(:,:) = answer(:,:) * (dx + dy + dz ) / 3.0_r_def
    curl_p%local_stencil(:, :, 1 ) =  curl_p%local_stencil(:, :, 1 ) &
                                  * (dx + dy + dz ) / 3.0_r_def
    @assertEqual(answer, curl_p%local_stencil(:, :, 1 ), tol)

  end subroutine test_all

  subroutine curl_operator( curl ) 
    use constants_mod, only: r_def
    implicit none

    real(kind=r_def), dimension(6,12), intent(inout) :: curl
    real(kind=r_def), dimension(6,6) :: m2
    real(kind=r_def), dimension(6,12) :: d21

    m2 = reshape( [ &
  0.9999999999999905E-03_r_def, &
  0.0000000000000000E+00_r_def, &
  0.4999999999999963E-03_r_def, &
  0.0000000000000000E+00_r_def, &
  0.0000000000000000E+00_r_def, &
  0.0000000000000000E+00_r_def, &
  0.0000000000000000E+00_r_def, &
  0.2777777777777752E-04_r_def, &
  0.0000000000000000E+00_r_def, &
  0.1388888888888879E-04_r_def, &
  0.0000000000000000E+00_r_def, &
  0.0000000000000000E+00_r_def, &
  0.4999999999999963E-03_r_def, &
  0.0000000000000000E+00_r_def, &
  0.9999999999999907E-03_r_def, &
  0.0000000000000000E+00_r_def, &
  0.0000000000000000E+00_r_def, &
  0.0000000000000000E+00_r_def, &
  0.0000000000000000E+00_r_def, &
  0.1388888888888879E-04_r_def, &
  0.0000000000000000E+00_r_def, &
  0.2777777777777752E-04_r_def, &
  0.0000000000000000E+00_r_def, &
  0.0000000000000000E+00_r_def, &
  0.0000000000000000E+00_r_def, &
  0.0000000000000000E+00_r_def, &
  0.0000000000000000E+00_r_def, &
  0.0000000000000000E+00_r_def, &
  0.1111111111111101E-03_r_def, &
  0.5555555555555515E-04_r_def, &
  0.0000000000000000E+00_r_def, &
  0.0000000000000000E+00_r_def, &
  0.0000000000000000E+00_r_def, &
  0.0000000000000000E+00_r_def, &
  0.5555555555555515E-04_r_def, &
  0.1111111111111101E-03_r_def  &
    ], shape(m2) )

  d21 = real(reshape( [ & 
   1,  0,  0,  0, -1,  0, &
   0,  1,  0,  0,  1,  0, &
   0,  0,  1,  0,  1,  0, &
   0,  0,  0,  1, -1,  0, &
  -1, -1,  0,  0,  0,  0, &
   0,  1, -1,  0,  0,  0, &
   0,  0,  1,  1,  0,  0, &
   1,  0,  0, -1,  0,  0, &
  -1,  0,  0,  0,  0, -1, &
   0, -1,  0,  0,  0,  1, &
   0,  0, -1,  0,  0,  1, &
   0,  0,  0, -1,  0, -1  &
 ], shape(d21) ),r_def) 

  curl = matmul(m2,d21)

    return
  end subroutine curl_operator

end module compute_curl_operator_kernel_mod_test
