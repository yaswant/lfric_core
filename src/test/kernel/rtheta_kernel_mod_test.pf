!-----------------------------------------------------------------------------
! (c) The copyright relating to this work is owned jointly by the Crown, 
! Met Office and NERC 2014. 
! However, it has been created with the help of the GungHo Consortium, 
! whose members are identified at https://puma.nerc.ac.uk/trac/GungHo/wiki
!-----------------------------------------------------------------------------

!> Test the rtheta computation
!>
module rtheta_kernel_mod_test

  use constants_mod,      only : i_def, r_def
  use field_mod,          only : field_type,      &
                                 field_proxy_type
  use fs_continuity_mod,  only : W0, W2, W3
  use function_space_mod, only: function_space_type
  use mesh_mod,           only : mesh_type
  use pFUnit_Mod
  use quadrature_mod,     only : quadrature_type, GAUSSIAN

  implicit none

  private
  public :: test_all

  @TestCase
  type, extends(TestCase), public :: rtheta_test_type
    private
    type(mesh_type)           :: mesh
    type(field_type)          :: r_theta, f, rho
    real(r_def), allocatable  :: basis_w2(:,:,:,:), &
                                 basis_w0(:,:,:,:), &
                                 basis_w3(:,:,:,:), &
                                 diff_basis_w0(:,:,:,:)
    real(r_def), allocatable  :: answer(:)
  contains
    procedure setup
    procedure tearDown
    procedure test_all
  end type rtheta_test_type

  integer(i_def) :: element_order = 0

contains

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  subroutine setUp( this )

    use finite_element_config_mod, only : finite_element_shape_quadrilateral
    use mesh_mod,                  only : PLANE_BI_PERIODIC
    use reference_element_mod,     only: reference_cube, reference_element

    implicit none

    class(rtheta_test_type), intent(inout) :: this

    type(quadrature_type)     :: qr
    type(function_space_type) :: fs

    type(field_proxy_type) :: r_theta_proxy, f_proxy, rho_proxy

    type(function_space_type), pointer :: w0_fs => null()
    type(function_space_type), pointer :: w2_fs => null()
    type(function_space_type), pointer :: w3_fs => null()

    integer                 :: nqp_h, nqp_v
    integer                 :: ndf_w0, undf_w0, dim_w0, dim_diff_w0
    integer                 :: ndf_w2, undf_w2, dim_w2
    integer                 :: ndf_w3, undf_w3, dim_w3

    qr  = quadrature_type(3, GAUSSIAN)

    nqp_h=qr%get_nqp_h()
    nqp_v=qr%get_nqp_v()

    reference_element = finite_element_shape_quadrilateral
    call reference_cube()

    ! Dummy mesh mod has 9 cells, 3 layers and is uniform in vertical
    this%mesh = mesh_type( PLANE_BI_PERIODIC )

    w0_fs => fs%get_instance( this%mesh, element_order, W0 )
    w2_fs => fs%get_instance( this%mesh, element_order, W2 )
    w3_fs => fs%get_instance( this%mesh, element_order, W3 )

    this%f  = field_type( vector_space = w2_fs )
    f_proxy = this%f%get_proxy()
    ndf_w2  = f_proxy%vspace%get_ndf( )
    dim_w2  = f_proxy%vspace%get_dim_space( )
    allocate( this%basis_w2(dim_w2,ndf_w2,nqp_h,nqp_v) )

    this%r_theta  = field_type( vector_space = w0_fs )
    r_theta_proxy = this%r_theta%get_proxy()
    ndf_w0        = r_theta_proxy%vspace%get_ndf( )
    dim_w0        = r_theta_proxy%vspace%get_dim_space( )
    dim_diff_w0   = r_theta_proxy%vspace%get_dim_space_diff( )
    undf_w0       = r_theta_proxy%vspace%get_undf()
    allocate( this%basis_w0(dim_w0,ndf_w0,nqp_h,nqp_v) )
    allocate( this%diff_basis_w0(dim_diff_w0,ndf_w0,nqp_h,nqp_v) )
    allocate( this%answer(undf_w0) )

    this%rho  = field_type( vector_space = w3_fs )
    rho_proxy = this%rho%get_proxy()
    ndf_w3    = rho_proxy%vspace%get_ndf( )
    dim_w3    = rho_proxy%vspace%get_dim_space( )
    allocate( this%basis_w3(dim_w3,ndf_w3,nqp_h,nqp_v) )


  end subroutine setUp

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  subroutine tearDown( this )

    use function_space_mod,    only : purge_function_spaces
    use reference_element_mod, only : deallocate_reference

    implicit none

    class(rtheta_test_type), intent(inout) :: this

    deallocate( this%basis_w2,      &
                this%basis_w0,      &
                this%diff_basis_w0, &
                this%basis_w3 )
    deallocate( this%answer )

    call purge_function_spaces()
    call deallocate_reference()

  end subroutine tearDown

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  @test
  subroutine test_all( this )

    use rtheta_kernel_mod,          only : rtheta_code

    implicit none

    class(rtheta_test_type), intent(inout) :: this

    real(r_def), parameter :: tol = 1.0e-6_r_def

    type(quadrature_type)     :: qr
    type(function_space_type) :: fs

    type(function_space_type), pointer :: w0_fs => null()

    integer :: cell
    integer(i_def) :: err

    ! Required for calling kernel 
    type(field_type)  :: theta

    integer                 :: nlayers, nqp_h, nqp_v
    integer                 :: ndf_w0, undf_w0
    integer                 :: ndf_w2, undf_w2
    integer                 :: ndf_w3, undf_w3

    integer, pointer        :: map_w0(:) => null()
    integer, pointer        :: map_w2(:) => null()
    integer, pointer        :: map_w3(:) => null()

    type(field_proxy_type)        :: r_theta_proxy, theta_proxy, &
                                     f_proxy, rho_proxy

    real(kind=r_def), pointer :: xp(:,:) => null()
    real(kind=r_def), pointer :: zp(:)   => null()
    real(kind=r_def), pointer :: wh(:)   => null(), &
                                 wv(:)   => null()

    qr  = quadrature_type(3, GAUSSIAN)

    w0_fs => fs%get_instance( this%mesh, element_order, W0 )

    ! Test kernel
    theta   = field_type( vector_space = w0_fs )

    r_theta_proxy   = this%r_theta%get_proxy()
    theta_proxy     = theta%get_proxy()
    f_proxy         = this%f%get_proxy()
    rho_proxy       = this%rho%get_proxy()

    nlayers = r_theta_proxy%vspace%get_nlayers()

    nqp_h=qr%get_nqp_h()
    nqp_v=qr%get_nqp_v()
    zp=>qr%get_xqp_v()
    xp=>qr%get_xqp_h()
    wh=>qr%get_wqp_h()
    wv=>qr%get_wqp_v()

    ndf_w2      = f_proxy%vspace%get_ndf( )
    undf_w2     = f_proxy%vspace%get_undf()

    ndf_w0      = r_theta_proxy%vspace%get_ndf( )
    undf_w0     = r_theta_proxy%vspace%get_undf()

    ndf_w3      = rho_proxy%vspace%get_ndf( )
    undf_w3     = rho_proxy%vspace%get_undf()

    call f_proxy%vspace%compute_basis_function(                     &
        this%basis_w2, ndf_w2, nqp_h, nqp_v, xp, zp)

    call r_theta_proxy%vspace%compute_basis_function(this%basis_w0, ndf_w0,      &
         nqp_h, nqp_v, xp, zp)

    call r_theta_proxy%vspace%compute_diff_basis_function(                  &
         this%diff_basis_w0, ndf_w0, nqp_h, nqp_v, xp, zp)

    call rho_proxy%vspace%compute_basis_function(                     &
        this%basis_w3, ndf_w3, nqp_h, nqp_v, xp, zp)


    ! Create the data
    r_theta_proxy%data(:) = 0.0_r_def
    theta_proxy%data(:) = 300.0_r_def
    f_proxy%data(:) = 2.0_r_def
    rho_proxy%data(:) = 0.5_r_def


    do cell = 1, r_theta_proxy%vspace%get_ncell()
       map_w0 => r_theta_proxy%vspace%get_cell_dofmap( cell )
       map_w2 => f_proxy%vspace%get_cell_dofmap( cell )
       map_w3 => rho_proxy%vspace%get_cell_dofmap( cell )


       call rtheta_code( nlayers, &
                         r_theta_proxy%data, &
                         theta_proxy%data, &
                         f_proxy%data, &
                         rho_proxy%data, &
                         ndf_w0, undf_w0, &
                         map_w0, &
                         this%basis_w0, &
                         this%diff_basis_w0, &
                         ndf_w2, undf_w2, &
                         map_w2, &
                         this%basis_w2, &
                         ndf_w3, undf_w3, &
                         map_w3, &
                         this%basis_w3, &
                         nqp_h, nqp_v, wh, wv &
                         )
    end do

    ! Simple test, check that when theta=const, f/rho.grad(theta) = 0    
    this%answer(:) = 0.0_r_def
    @assertEqual( this%answer, r_theta_proxy%data(:), tol )

  end subroutine test_all

end module rtheta_kernel_mod_test
