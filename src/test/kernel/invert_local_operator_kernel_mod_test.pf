!-----------------------------------------------------------------------------
! (c) The copyright relating to this work is owned jointly by the Crown, 
! Met Office and NERC 2014. 
! However, it has been created with the help of the GungHo Consortium, 
! whose members are identified at https://puma.nerc.ac.uk/trac/GungHo/wiki
!-----------------------------------------------------------------------------

!> Test the operator inversion
!>
module invert_local_operator_kernel_mod_test

  use constants_mod,      only : i_def, r_def
  use field_mod,          only : field_type, field_proxy_type
  use fs_continuity_mod,  only : W3
  use function_space_mod, only : function_space_type
  use mesh_mod,           only : mesh_type
  use operator_mod,       only : operator_type, operator_proxy_type
  use pFUnit_Mod
  use quadrature_mod,     only : quadrature_type, GAUSSIAN

  implicit none

  private
  public :: test_all

  @TestCase
  type, extends(TestCase), public :: invert_local_operator_test_type
    private
    type(mesh_type)           :: mesh
    type(operator_type)       :: mm, mm_inv
  contains
    procedure setUp
    procedure tearDown
    procedure test_all
  end type invert_local_operator_test_type

  integer(i_def), parameter :: element_order = 0

contains

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  subroutine setUp( this )

    use finite_element_config_mod, only : finite_element_shape_quadrilateral
    use mesh_mod,                  only : PLANE_BI_PERIODIC
    use reference_element_mod,     only : reference_cube, reference_element

    implicit none

    class(invert_local_operator_test_type), intent(inout) :: this

    type(function_space_type) :: fs

    type(function_space_type), pointer :: w3_fs => null()

    reference_element = finite_element_shape_quadrilateral
    call reference_cube()

    ! Dummy mesh mod has 9 cells, 3 layers and is uniform in vertical
    this%mesh = mesh_type(PLANE_BI_PERIODIC)

    w3_fs => fs%get_instance( this%mesh, element_order, W3 )

    this%mm     = operator_type( w3_fs, w3_fs )
    this%mm_inv = operator_type( w3_fs, w3_fs )

  end subroutine setUp

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  subroutine tearDown( this )

    use function_space_mod,    only : purge_function_spaces
    use reference_element_mod, only : deallocate_reference

    implicit none

    class(invert_local_operator_test_type), intent(inout) :: this

    call purge_function_spaces()
    call deallocate_reference()

  end subroutine tearDown

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  @test
  subroutine test_all( this )

    use invert_local_operator_kernel_mod, only : invert_local_operator_code

    implicit none

    class(invert_local_operator_test_type), intent(inout) :: this

    real(r_def), parameter :: tol = 1.0e-6_r_def

    real(kind=r_def) :: answer

    integer :: cell
    integer(i_def) :: err

    real(r_def) :: dz_test(3)

    ! Required for calling operator inversion kernel
    integer :: nlayers, ndf
    type(operator_proxy_type) :: mm_p, mm_inv_p

    call this%mesh%get_dz(dz_test)

    mm_p = this%mm%get_proxy()
    nlayers  = mm_p%fs_from%get_nlayers()
    ndf  = mm_p%fs_from%get_ndf()
    mm_inv_p = this%mm_inv%get_proxy()

    cell = 1
    mm_p%local_stencil(:,:,:) = 5.0_r_def
    call invert_local_operator_code( cell,                              &
                                     nlayers,                           &
                                     mm_inv_p%ncell_3d,                 &
                                     mm_inv_p%local_stencil,            &
                                     mm_p%ncell_3d,                     &
                                     mm_p%local_stencil,                &
                                     ndf                                &
                                     )
 
    answer = 1.0_r_def/5.0_r_def
    @assertEqual(answer, mm_inv_p%local_stencil(1, 1, 1 ), tol)

  end subroutine test_all

end module invert_local_operator_kernel_mod_test
