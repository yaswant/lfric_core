!-----------------------------------------------------------------------------
! (c) The copyright relating to this work is owned jointly by the Crown, 
! Met Office and NERC 2014. 
! However, it has been created with the help of the GungHo Consortium, 
! whose members are identified at https://puma.nerc.ac.uk/trac/GungHo/wiki
!-----------------------------------------------------------------------------

!> Test the assembly of the term diag(D_h.M_{2,h,inv}.D_h^T) into a columnwise
!> operator. This term is required in the gravity wave test
!>
module columnwise_op_asm_diag_hmht_kernel_mod_test

  use constants_mod,                 only : i_def, r_def
  use function_space_collection_mod, only : function_space_collection_type, &
                                            function_space_collection
  use mesh_collection_mod,           only : mesh_collection_type, &
                                            mesh_collection
  use field_mod,                     only : field_type, field_proxy_type
  use fs_continuity_mod,             only : W2H, W3
  use function_space_mod,            only : function_space_type

  use operator_mod,                  only : operator_type,                 &
                                            operator_proxy_type,           &
                                            columnwise_operator_type,      &
                                            columnwise_operator_proxy_type
  use pFUnit_Mod

  implicit none

  private
  public :: test_all

  @TestCase
  type, extends(TestCase), public :: columnwise_op_asm_diag_hmht_test_type
    private
    type(function_space_type), pointer :: w2h_fs => null()
    type(function_space_type), pointer :: w3_fs => null()
    type(operator_type)                :: div
    type(field_type)                   :: M2h_lumped
    type(columnwise_operator_type)     :: columnwise_diag_hmht
    integer(i_def)                     :: mesh_id
  contains
    procedure setUp
    procedure tearDown
    procedure test_all
  end type columnwise_op_asm_diag_hmht_test_type

  integer(i_def), parameter :: element_order = 0

contains

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  subroutine setUp( this )
    use finite_element_config_mod, only : finite_element_shape_quadrilateral
    use feign_config_mod,          only : feign_finite_element_config
    use mesh_mod,                  only : PLANE_BI_PERIODIC
    use reference_element_mod,     only : reference_cube, reference_element
 
    implicit none

    class(columnwise_op_asm_diag_hmht_test_type), intent(inout) :: this

    integer :: i
    integer :: alpha, beta, gamma_m, gamma_p

    call feign_finite_element_config( finite_element_shape_quadrilateral, &
                                      0, 0, .true., .false., .false., .false. )

    reference_element = finite_element_shape_quadrilateral
    call reference_cube()
    
    ! Create top level mesh collection
    mesh_collection = mesh_collection_type()
    ! Create top level function space collection
    function_space_collection = function_space_collection_type()

    ! Dummy mesh mod has 9 cells, 3 layers and is uniform in vertical
    this%mesh_id = mesh_collection%add_unit_test_mesh( PLANE_BI_PERIODIC )


    this%w2h_fs => function_space_collection%get_fs( this%mesh_id, &
                                                     element_order, &
                                                     W2H )

    this%w3_fs => function_space_collection%get_fs( this%mesh_id, &
                                                    element_order, &
                                                    W3 )

    this%div = operator_type( this%w3_fs, this%w2h_fs )
    this%M2h_lumped = field_type( this%w2h_fs)

    alpha = 1
    beta = 1
    gamma_m = 0
    gamma_p = 0
    this%columnwise_diag_hmht = columnwise_operator_type( this%w3_fs, this%w3_fs, &
                                                          alpha, beta, gamma_m, gamma_p )

  end subroutine setUp

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  subroutine tearDown( this )

    use reference_element_mod, only : deallocate_reference

    implicit none

    class(columnwise_op_asm_diag_hmht_test_type), intent(inout) :: this

    call function_space_collection%clear()
    call deallocate_reference()

  end subroutine tearDown

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  @test
  subroutine test_all( this )

    use columnwise_op_asm_diag_hmht_kernel_mod, only : &
         columnwise_op_asm_diag_hmht_kernel_code

    implicit none

    class(columnwise_op_asm_diag_hmht_test_type), intent(inout) :: this

    real(r_def), parameter                          :: tol = 1.0e-6_r_def
    integer                                         :: i, j, k, cell
    integer                                         :: ndf_w3, ndf_w2h, undf_w2h
    integer                                         :: nlayers, ncell_2d
    type(operator_proxy_type)                       :: div_p
    type(field_proxy_type)                          :: M2h_lumped_p
    type(columnwise_operator_proxy_type)            :: columnwise_diag_hmht_p

    real(kind=r_def), allocatable, dimension(:,:,:) :: local_stencil
    real(kind=r_def), allocatable, dimension(:,:)   :: field_data
    real(kind=r_def),              dimension(3)     :: rho
    real(kind=r_def)                                :: sigma
    real(kind=r_def)                                :: answer

    rho(1) = 2.0_r_def
    rho(2) = 3.0_r_def
    rho(3) = 4.0_r_def
    sigma = 5.0_r_def
    ! Test mass matrix kernel
    div_p = this%div%get_proxy()
    columnwise_diag_hmht_p = this%columnwise_diag_hmht%get_proxy()
    M2h_lumped_p = this%M2h_lumped%get_proxy()

    nlayers  = columnwise_diag_hmht_p%fs_from%get_nlayers()
    ncell_2d = columnwise_diag_hmht_p%ncell_2d
    ndf_w3  = div_p%fs_to%get_ndf()
    ndf_w2h  = div_p%fs_from%get_ndf()
    undf_w2h  = div_p%fs_from%get_undf()

    allocate(local_stencil(ndf_w3,ndf_w2h,div_p%ncell_3d))
    allocate(field_data(undf_w2h,ncell_2d))

    cell = 1

    do i=1, div_p%ncell_3d
       local_stencil(:,:,i) = reshape( (/ rho(1), rho(2), rho(3) /), (/1, 3/) )
    end do

    field_data(:,cell) = sigma

    call columnwise_op_asm_diag_hmht_kernel_code(cell,                                     &
                                                 nlayers,                                  &
                                                 ncell_2d,                                 &
                                                 div_p%ncell_3d,                           &
                                                 local_stencil,                            &
                                                 field_data,                               &
                                                 columnwise_diag_hmht_p%columnwise_matrix, &
                                                 columnwise_diag_hmht_p%nrow,              &
                                                 columnwise_diag_hmht_p%ncol,              &
                                                 columnwise_diag_hmht_p%bandwidth,         &
                                                 columnwise_diag_hmht_p%alpha,             &
                                                 columnwise_diag_hmht_p%beta,              &
                                                 columnwise_diag_hmht_p%gamma_m,           &
                                                 columnwise_diag_hmht_p%gamma_p,           &
                                                 ndf_w3,                                   &
                                                 ndf_w2h,                                  &
                                                 undf_w2h,                                 &
                                                 M2h_lumped_p%vspace%get_cell_dofmap(cell),&
                                                 columnwise_diag_hmht_p%column_banded_dofmap_to)

    answer = ( rho(1)**2 + rho(2)**2 + rho(3)**2 )/sigma
    do i = 1, columnwise_diag_hmht_p%nrow
      ! All other rows should be ( 3/2 )
      @assertEqual(answer, columnwise_diag_hmht_p%columnwise_matrix(1,i,1), tol)
    end do

    deallocate(local_stencil)
    deallocate(field_data)

  end subroutine test_all

end module columnwise_op_asm_diag_hmht_kernel_mod_test
