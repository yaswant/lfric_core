!-----------------------------------------------------------------------------
! (c) The copyright relating to this work is owned jointly by the Crown, 
! Met Office and NERC 2014. 
! However, it has been created with the help of the GungHo Consortium, 
! whose members are identified at https://puma.nerc.ac.uk/trac/GungHo/wiki
!-----------------------------------------------------------------------------

module convert_hcurl_field_kernel_mod_test

  use constants_mod,              only : i_def, r_def
  use field_mod,                  only : field_type, field_proxy_type
  use fs_continuity_mod,          only : W0, W1
  use function_space_mod,         only : function_space_type
  use mesh_mod,                   only : mesh_type
  use pFUnit_Mod
  use quadrature_mod,             only : quadrature_type, GAUSSIAN

  implicit none

  private
  public :: test_all

  @TestCase
  type, extends(TestCase), public :: convert_hcurl_field_test_type
    private
    type(mesh_type)          :: mesh
    type(field_type)         :: chi(3)
    type(field_type)         :: comp_field
    real(r_def), allocatable :: diff_basis_chi(:,:,:), basis(:,:,:)
  contains
    procedure setUp
    procedure tearDown
    procedure test_all
  end type convert_hcurl_field_test_type

  integer(i_def), parameter :: element_order = 0

contains

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  subroutine setUp( this )

    use finite_element_config_mod, only : finite_element_shape_quadrilateral
    use mesh_mod,                  only : PLANE_BI_PERIODIC
    use reference_element_mod,     only : reference_cube, reference_element

    implicit none

    class(convert_hcurl_field_test_type), intent(inout) :: this

    type(function_space_type) :: fs
    type(field_proxy_type)    :: chi_p(3)
    type(field_proxy_type)    :: comp_p

    type(function_space_type), pointer :: w0_fs => null()
    type(function_space_type), pointer :: w1_fs => null()

    integer(i_def) :: ndf_chi, ndf
    integer(i_def) :: diff_dim_chi, dim_comp

    integer :: i

    reference_element = finite_element_shape_quadrilateral
    call reference_cube()

    this%mesh = mesh_type(PLANE_BI_PERIODIC)

    w0_fs => fs%get_instance( this%mesh, element_order, W0 )
    w1_fs => fs%get_instance( this%mesh, element_order, W1 )

    this%comp_field = field_type( vector_space = w1_fs )
    comp_p = this%comp_field%get_proxy()

    ndf      = comp_p%vspace%get_ndf( )
    comp_p   = this%comp_field%get_proxy()
    dim_comp = comp_p%vspace%get_dim_space( )
    allocate(this%basis(dim_comp, ndf, ndf) )

    do i = 1,3
      this%chi(i) = field_type( vector_space = w0_fs )
      chi_p(i)    = this%chi(i)%get_proxy()
    end do
    ndf_chi      = chi_p(1)%vspace%get_ndf( )
    diff_dim_chi = chi_p(1)%vspace%get_dim_space_diff( )
    allocate(this%diff_basis_chi(diff_dim_chi, ndf_chi, ndf))

  end subroutine setUp

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  subroutine tearDown( this )

    use function_space_mod,    only : purge_function_spaces
    use reference_element_mod, only : deallocate_reference

    implicit none

    class(convert_hcurl_field_test_type), intent(inout) :: this

    deallocate( this%diff_basis_chi, this%basis )

    call purge_function_spaces()
    call deallocate_reference()

  end subroutine tearDown

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  @test
  subroutine test_all( this )

    use convert_hcurl_field_kernel_mod, only: convert_hcurl_field_code

    implicit none

    class(convert_hcurl_field_test_type), intent(inout) :: this

    real(r_def), parameter :: tol = 1.0e-6_r_def
    real(r_def), parameter :: dx = 6000.0_r_def, &
                              dy = 1000.0_r_def, &
                              dz = 2000.0_r_def

    type(function_space_type) :: fs
    type( quadrature_type)    :: qr

    type(function_space_type), pointer :: w1_fs => null()

    real(r_def) :: answer

    integer :: i, j, k, cell

    ! Variables for testing kernel
    type(field_proxy_type) :: chi_p(3)

    type(field_type) :: phys_field(3)

    type(field_proxy_type) :: phys_p(3), comp_p

    integer                 :: nlayers
    integer                 :: ndf_chi, ndf
    integer                 :: undf_chi, undf
    integer, pointer        :: map_chi(:), map(:) => null()

    real(kind=r_def), pointer :: nodes(:,:) => null()

    ! Setup
    qr = quadrature_type(3, GAUSSIAN)

    ! Insert Psykal_lite unpacking code here
    do i = 1,3
      chi_p(i) = this%chi(i)%get_proxy()
    end do

    ! Compute coordinates
    cell = 1
    do j = 1,3
      do i = 1,3
        map_chi => chi_p(1)%vspace%get_cell_dofmap( cell )
        do k = 0,3
          chi_p(1)%data(map_chi(1) + k) = real(i-1)*dx
          chi_p(2)%data(map_chi(1) + k) = real(j-1)*dy
          chi_p(3)%data(map_chi(1) + k) = real(k)  *dz
        end do
        cell = cell + 1
      end do
    end do

    w1_fs => fs%get_instance( this%mesh, element_order, W1 )
    do i = 1,3
      phys_field(i) = field_type( vector_space = w1_fs )
      phys_p(i) = phys_field(i)%get_proxy()
    end do
    comp_p = this%comp_field%get_proxy()

    nlayers = comp_p%vspace%get_nlayers()

    ndf  = comp_p%vspace%get_ndf( )
    undf = comp_p%vspace%get_undf()

    ndf_chi  = chi_p(1)%vspace%get_ndf( )
    undf_chi = chi_p(1)%vspace%get_undf()

    nodes => comp_p%vspace%get_nodes( )
    call chi_p(1)%vspace%compute_nodal_diff_basis_function(this%diff_basis_chi, ndf_chi, ndf, nodes)
    call comp_p%vspace%compute_nodal_basis_function(this%basis, ndf, ndf, nodes)

    cell = 5
    map     => comp_p%vspace%get_cell_dofmap( cell )
    map_chi => chi_p(1)%vspace%get_cell_dofmap( cell )
    phys_p(1)%data(:) = 0.0_r_def
    phys_p(2)%data(:) = 0.0_r_def
    phys_p(3)%data(:) = 0.0_r_def
    comp_p%data(:) = 0.0_r_def
    comp_p%data(map(1)) = 1.0_r_def
    comp_p%data(map(2)) = 3.0_r_def
    comp_p%data(map(6)) = -6.0_r_def

    call convert_hcurl_field_code(nlayers, &
                                  phys_p(1)%data, &
                                  phys_p(2)%data, &
                                  phys_p(3)%data, &
                                  comp_p%data, &
                                  chi_p(1)%data, &
                                  chi_p(2)%data, &
                                  chi_p(3)%data, &
                                  ndf, undf, map, &
                                  ndf_chi, undf_chi, map_chi, &
                                  this%basis, &
                                  this%diff_basis_chi &
                                 )

    answer = 1.0_r_def / dy
    @assertEqual(answer, phys_p(2)%data(map(1)), tol)
    answer = 3.0_r_def / dx
    @assertEqual(answer, phys_p(1)%data(map(2)), tol)
    answer = -6.0_r_def / dz
    @assertEqual(answer, phys_p(3)%data(map(6)), tol)

  end subroutine test_all

end module convert_hcurl_field_kernel_mod_test
