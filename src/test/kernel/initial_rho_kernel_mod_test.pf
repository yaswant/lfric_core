!-----------------------------------------------------------------------------
! (c) The copyright relating to this work is owned jointly by the Crown, 
! Met Office and NERC 2014. 
! However, it has been created with the help of the GungHo Consortium, 
! whose members are identified at https://puma.nerc.ac.uk/trac/GungHo/wiki
!-----------------------------------------------------------------------------

!> Test the initial rho computation
!>
module initial_rho_kernel_mod_test

  use constants_mod,      only : i_def, r_def
  use field_mod,          only : field_type,      &
                                 field_proxy_type
  use function_space_mod, only : function_space_type, W0, W3
  use fs_continuity_mod,  only : W0, W3
  use mesh_mod,           only : mesh_type
  use pFUnit_Mod
  use quadrature_mod,     only : quadrature_type, GAUSSIAN

  implicit none 

  private
  public :: initial_rho_kernel_test_type, test_all

  @TestCase
  type, extends(TestCase) :: initial_rho_kernel_test_type
    private
    type(mesh_type)          :: mesh
    type(field_type)         :: chi(3)
    type(field_type)         :: rho
    real(r_def), allocatable :: basis_rho(:,:,:,:), &
                                basis_chi(:,:,:,:), &
                                diff_basis_chi(:,:,:,:)
  contains
    procedure setUp
    procedure tearDown
    procedure test_all
  end type initial_rho_kernel_test_type

  integer(i_def), parameter :: element_order = 0

  real(r_def), parameter :: gravity = 9.80665_r_def
  real(r_def), parameter :: radius  = 6371229.0_r_def
  real(r_def), parameter :: omega   = 8.0E-5_r_def
  real(r_def), parameter :: p_zero  = 100000.0_r_def
  real(r_def), parameter :: rd      = 287.05_r_def
  real(r_def), parameter :: cp      = 1005.0_r_def
  real(r_def), parameter :: scaling = 1.0_r_def

  real(r_def), parameter :: tracer_max        = 2.0_r_def
  real(r_def), parameter :: tracer_background = 0.1_r_def
  real(r_def), parameter :: y1                = 0.0_r_def
  real(r_def), parameter :: y2                = 0.0_r_def

  real(r_def), parameter :: bvf_square = 0.0001_r_def

contains

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  subroutine setUp( this )


    use base_mesh_config_mod,      only : base_mesh_geometry_planar, &
                                          base_mesh_partitioner_planar
    use finite_element_config_mod, only : finite_element_shape_quadrilateral
    use frig_config_mod,           only : frig_base_mesh_config,           &
                                          frig_idealised_config,           &
                                          frig_initial_density_config,     &
                                          frig_initial_temperature_config, &
                                          frig_planet_config
    use idealised_config_mod,      only : idealised_test_gravity_wave
    use mesh_mod,                  only : PLANE_BI_PERIODIC
    use reference_element_mod,     only : reference_cube, reference_element

    implicit none

    class(initial_rho_kernel_test_type), intent(inout) :: this

    type(quadrature_type)     :: qr
    type(function_space_type) :: fs
    type(field_proxy_type)    :: rho_p
    type(field_proxy_type)    :: chi_p(3)

    type(function_space_type), pointer :: w0_fs => null()
    type(function_space_type), pointer :: w3_fs => null()

    integer(i_def) :: ndf_rho, undf_rho, dim_rho, &
                      ndf_chi, undf_chi, dim_chi, dim_diff_chi, &
                      nqp_h, nqp_v

    integer :: i

    call frig_base_mesh_config( 'foo',                        &
                                base_mesh_geometry_planar,    &
                                base_mesh_partitioner_planar, &
                                0.0_r_def )
    call frig_idealised_config( idealised_test_gravity_wave )
    call frig_initial_density_config( tracer_max, tracer_background, &
                                      y1, y2 )
    call frig_initial_temperature_config( bvf_square )
    call frig_planet_config( radius, gravity, omega, rd, cp, p_zero, scaling )

    reference_element = finite_element_shape_quadrilateral
    call reference_cube()

    qr = quadrature_type( 3, GAUSSIAN )

    nqp_h=qr%get_nqp_h()
    nqp_v=qr%get_nqp_v()

    this%mesh = mesh_type( PLANE_BI_PERIODIC )

    w0_fs => fs%get_instance( this%mesh, element_order, W0 )
    w3_fs => fs%get_instance( this%mesh, element_order, W3 )

    this%rho = field_type( vector_space = w3_fs )
    rho_p    = this%rho%get_proxy()

    ndf_rho  = rho_p%vspace%get_ndf( )
    undf_rho = rho_p%vspace%get_undf( )
    dim_rho  = rho_p%vspace%get_dim_space( )

    do i = 1,3
      this%chi(i) = field_type( vector_space = w0_fs )
      chi_p(i)    = this%chi(i)%get_proxy()
    end do

    ndf_chi  = chi_p(1)%vspace%get_ndf( )
    undf_chi = chi_p(1)%vspace%get_undf( )
    dim_chi  = chi_p(1)%vspace%get_dim_space( )
    dim_diff_chi  = chi_p(1)%vspace%get_dim_space_diff( )

    allocate( this%basis_rho(dim_rho, ndf_rho, nqp_h, nqp_v), &
              this%basis_chi(dim_chi, ndf_chi, nqp_h, nqp_v), &
              this%diff_basis_chi(dim_diff_chi, ndf_chi, nqp_h, nqp_v) )

  end subroutine setUp

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  subroutine tearDown( this )

    use function_space_mod,    only : purge_function_spaces
    use reference_element_mod, only : deallocate_reference

    implicit none

    class(initial_rho_kernel_test_type), intent(inout) :: this

    deallocate( this%basis_rho, this%basis_chi )

    call purge_function_spaces()
    call deallocate_reference()

  end subroutine tearDown

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  @test
  subroutine test_all( this )

    use initial_rho_kernel_mod,     only : initial_rho_code

    implicit none

    class(initial_rho_kernel_test_type), intent(inout) :: this

    real(r_def), parameter :: tol = 1.0e-6_r_def
    real(r_def), parameter :: dx = 6000.0_r_def, &
                              dy = 2000.0_r_def

    ! Information for testing initial rho
    type(field_proxy_type)    :: rho_p
    type(field_proxy_type )   :: chi_p(3)

    type(mesh_type)           :: mesh
    type(quadrature_type)     :: qr

    integer          :: ndf_rho, undf_rho, &
                        ndf_chi, undf_chi, &
                        nqp_h, nqp_v


    integer, pointer :: map_rho(:) => null(), &
                        map_chi(:) => null()

    real(kind=r_def), pointer :: xp(:,:) => null()
    real(kind=r_def), pointer :: zp(:)   => null()
    real(kind=r_def), pointer :: wh(:), wv(:) => null()

    real(r_def) :: answer(3)

    integer :: i, j, k, cell
    integer(i_def) :: err

    real(r_def) :: dz, dz_test(3)

    qr = quadrature_type(3, GAUSSIAN)

    ! Dummy mesh mod has 9 cells, 3 layers and is uniform in vertical
    call this%mesh%get_dz(dz_test)
    dz = dz_test(1)

    do i = 1,3
      chi_p(i) = this%chi(i)%get_proxy()
    end do

    ! Compute coordinates
    cell = 1
    do j = 1,3
      do i = 1,3
        map_chi => chi_p(1)%vspace%get_cell_dofmap( cell )
        do k = 0,3
          chi_p(1)%data(map_chi(1) + k) = real(i-1)*dx
          chi_p(2)%data(map_chi(1) + k) = real(j-1)*dy
          chi_p(3)%data(map_chi(1) + k) = real(k)  *dz
        end do
        cell = cell + 1
      end do
    end do

    ! Test initial rho
    rho_p = this%rho%get_proxy()

    ndf_rho  = rho_p%vspace%get_ndf( )
    undf_rho = rho_p%vspace%get_undf( )

    ndf_chi  = chi_p(1)%vspace%get_ndf( )
    undf_chi = chi_p(1)%vspace%get_undf( )

    nqp_h=qr%get_nqp_h()
    nqp_v=qr%get_nqp_v()
    zp=>qr%get_xqp_v()
    xp=>qr%get_xqp_h()
    wh=>qr%get_wqp_h()
    wv=>qr%get_wqp_v()

    call rho_p%vspace%compute_basis_function( &
         this%basis_rho, ndf_rho, nqp_h, nqp_v, xp, zp)

    call chi_p(1)%vspace%compute_basis_function( &
         this%basis_chi, ndf_chi, nqp_h, nqp_v, xp, zp)

    call chi_p(1)%vspace%compute_diff_basis_function( &
         this%diff_basis_chi, ndf_chi, nqp_h, nqp_v, xp, zp)

    cell = 1
    map_rho => rho_p%vspace%get_cell_dofmap( cell )
    map_chi => chi_p(1)%vspace%get_cell_dofmap( cell )
    call initial_rho_code( rho_p%vspace%get_nlayers(), &
                           rho_p%data,                 &
                           chi_p(1)%data,              &
                           chi_p(2)%data,              &
                           chi_p(3)%data,              &
                           ndf_rho,                    &
                           undf_rho,                   &
                           map_rho,                    &
                           this%basis_rho,             &
                           ndf_chi,                    &
                           undf_chi,                   &
                           map_chi,                    &
                           this%basis_chi,             &
                           this%diff_basis_chi,        &
                           nqp_h,                      &
                           nqp_v,                      &
                           wh,                         &
                           wv )

    answer(1) = 1.059869_r_def
    answer(2) = 0.8758241_r_def
    answer(3) = 0.7175226_r_def
    do i = 1,3
      @assertEqual(answer(i), rho_p%data(i), tol)
    end do

  end subroutine test_all

end module initial_rho_kernel_mod_test
