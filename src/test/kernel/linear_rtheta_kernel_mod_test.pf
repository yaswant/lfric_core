!-----------------------------------------------------------------------------
! (c) The copyright relating to this work is owned jointly by the Crown, 
! Met Office and NERC 2014. 
! However, it has been created with the help of the GungHo Consortium, 
! whose members are identified at https://puma.nerc.ac.uk/trac/GungHo/wiki
!-----------------------------------------------------------------------------

!> Test the linear_rtheta computation
!>
module linear_rtheta_kernel_mod_test

  use constants_mod,      only : i_def, r_def
  use field_mod,          only : field_type,      &
                                 field_proxy_type
  use function_space_mod, only : function_space_type
  use fs_continuity_mod,  only : W0, W2
  use mesh_mod,           only : mesh_type
  use pFUnit_Mod
  use quadrature_mod,     only : quadrature_type, GAUSSIAN

  implicit none

  private
  public :: linear_rtheta_test_type, test_all

  @TestCase
  type, extends(TestCase) :: linear_rtheta_test_type
    private
    type(mesh_type)          :: mesh
    type(field_type)         :: r_theta, u
    real(r_def), allocatable :: answer(:)
    real(r_def), allocatable :: basis_w2(:,:,:,:), &
                                basis_w0(:,:,:,:), &
                                diff_basis_w0(:,:,:,:)
  contains
    procedure setUp
    procedure tearDown
    procedure test_all
  end type linear_rtheta_test_type

  integer(i_def), parameter :: element_order = 0

  real(r_def), parameter :: bvf_square = 0.0001_r_def

  real(r_def), parameter :: gravity = 10.0_r_def
  real(r_def), parameter :: radius  = 6000000_r_def
  real(r_def), parameter :: omega   = 8.0E-5_r_def
  real(r_def), parameter :: p_zero  = 100000.0_r_def
  real(r_def), parameter :: rd      = 300.0_r_def
  real(r_def), parameter :: cp      = 1000.0_r_def
  real(r_def), parameter :: scaling = 1.0_r_def

contains

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  subroutine setUp( this )

    use finite_element_config_mod, only : finite_element_shape_quadrilateral
    use frig_config_mod,           only : frig_initial_temperature_config, &
                                          frig_planet_config
    use mesh_mod,                  only : PLANE_BI_PERIODIC
    use reference_element_mod,     only : reference_cube, reference_element

    implicit none

    class(linear_rtheta_test_type), intent(inout) :: this

    type(quadrature_type)     :: qr
    type(function_space_type) :: fs
    type(field_proxy_type)    :: r_theta_proxy, u_proxy

    type(function_space_type), pointer :: w0_fs => null()
    type(function_space_type), pointer :: w2_fs => null()

    integer(i_def) :: nqp_h, nqp_v
    integer(i_def) :: ndf_w0, undf_w0, dim_w0, dim_diff_w0
    integer(i_def) :: ndf_w2, undf_w2, dim_w2

    call frig_initial_temperature_config( bvf_square )
    call frig_planet_config( radius, gravity, omega, rd, cp, p_zero, scaling )

    reference_element = finite_element_shape_quadrilateral
    call reference_cube()

    qr = quadrature_type( 3, GAUSSIAN )

    nqp_h=qr%get_nqp_h()
    nqp_v=qr%get_nqp_v()

    ! Dummy mesh mod has 9 cells, 3 layers and is uniform in vertical
    this%mesh = mesh_type( PLANE_BI_PERIODIC )

    w0_fs => fs%get_instance( this%mesh, element_order, W0 )
    w2_fs => fs%get_instance( this%mesh, element_order, W2 )

    this%u  = field_type( vector_space = w2_fs )
    u_proxy = this%u%get_proxy()

    ndf_w2      = u_proxy%vspace%get_ndf( )
    dim_w2      = u_proxy%vspace%get_dim_space( )
    undf_w2     = u_proxy%vspace%get_undf()
    allocate(this%basis_w2(dim_w2,ndf_w2,nqp_h,nqp_v))

    this%r_theta = field_type( vector_space = w0_fs )
    r_theta_proxy = this%r_theta%get_proxy()

    ndf_w0      = r_theta_proxy%vspace%get_ndf( )
    dim_w0      = r_theta_proxy%vspace%get_dim_space( )
    dim_diff_w0 = r_theta_proxy%vspace%get_dim_space_diff( )
    undf_w0     = r_theta_proxy%vspace%get_undf()
    allocate(this%basis_w0(dim_w0,ndf_w0,nqp_h,nqp_v))
    allocate(this%diff_basis_w0(dim_diff_w0,ndf_w0,nqp_h,nqp_v))
    allocate( this%answer(undf_w0) )

  end subroutine setUp

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  subroutine tearDown( this )

    use function_space_mod,    only : purge_function_spaces
    use reference_element_mod, only : deallocate_reference

    implicit none

    class(linear_rtheta_test_type), intent(inout) :: this

    deallocate( this%basis_w2, this%basis_w0, this%diff_basis_w0 )
    deallocate( this%answer )

    call deallocate_reference()
    call purge_function_spaces()

  end subroutine tearDown

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  @test
  subroutine test_all( this )

    use linear_rtheta_kernel_mod, only : linear_rtheta_code

    implicit none

    class(linear_rtheta_test_type), intent(inout) :: this

    real(r_def), parameter :: dx = 6000.0_r_def, &
                              dy = 1000.0_r_def
    real(r_def), parameter :: tol = 1.0e-6_r_def

    type(field_type) :: chi(3)
    type(field_proxy_type)   :: chi_p(3)

    type(function_space_type) :: fs
    type(mesh_type)           :: mesh
    type(quadrature_type)     :: qr

    type(function_space_type), pointer :: w0_fs => null()

    integer :: i, j, k, cell, err
    real(r_def) :: dz, dz_test(3)

    ! Required for calling kernel
    type(field_type) :: phi

    integer                 :: nlayers, nqp_h, nqp_v
    integer                 :: ndf_w0, undf_w0
    integer                 :: ndf_w2, undf_w2
    integer, pointer        :: map_w2(:), map_w0(:) => null()

    type(field_proxy_type)        :: r_theta_proxy, u_proxy, phi_proxy

    real(kind=r_def), pointer :: xp(:,:) => null()
    real(kind=r_def), pointer :: zp(:) => null()
    real(kind=r_def), pointer :: wh(:), wv(:) => null()

    qr  = quadrature_type(3, GAUSSIAN)

    call this%mesh%get_dz(dz_test)
    dz = dz_test(1)

    w0_fs => fs%get_instance( this%mesh, element_order, W0 )

    ! Make a coordinate field with a function space
    do i = 1,3
      chi(i) = field_type( vector_space = w0_fs )
      chi_p(i) = chi(i)%get_proxy()
    end do

    ! Compute coordinates
    cell = 1
    do j = 1,3
      do i = 1,3
        map_w0 => chi_p(1)%vspace%get_cell_dofmap( cell )
        do k = 0,3
          chi_p(1)%data(map_w0(1) + k) = real(i-1)*dx
          chi_p(2)%data(map_w0(1) + k) = real(j-1)*dy
          chi_p(3)%data(map_w0(1) + k) = real(k)  *dz
        end do
        cell = cell + 1
      end do
    end do

    ! Test kernel
    phi       = field_type( vector_space = w0_fs )
    phi_proxy = phi%get_proxy()

    u_proxy       = this%u%get_proxy()
    r_theta_proxy = this%r_theta%get_proxy()

    nlayers = r_theta_proxy%vspace%get_nlayers()

    nqp_h=qr%get_nqp_h()
    nqp_v=qr%get_nqp_v()
    zp=>qr%get_xqp_v()
    xp=>qr%get_xqp_h()
    wh=>qr%get_wqp_h()
    wv=>qr%get_wqp_v()

    ndf_w2      = u_proxy%vspace%get_ndf( )
    undf_w2     = u_proxy%vspace%get_undf()

    ndf_w0      = r_theta_proxy%vspace%get_ndf( )
    undf_w0     = r_theta_proxy%vspace%get_undf()

    call u_proxy%vspace%compute_basis_function( this%basis_w2, ndf_w2, nqp_h, &
                                                nqp_v, xp, zp )

    call r_theta_proxy%vspace%compute_basis_function( this%basis_w0, ndf_w0, &
                                                      nqp_h, nqp_v, xp, zp )

    call r_theta_proxy%vspace%compute_diff_basis_function( &
                            this%diff_basis_w0, ndf_w0, nqp_h, nqp_v, xp, zp )

    cell = 1
    map_w0 => r_theta_proxy%vspace%get_cell_dofmap( cell )
    map_w2 => u_proxy%vspace%get_cell_dofmap( cell )

    ! Create the data
    r_theta_proxy%data(:) = 0.0_r_def
    u_proxy%data(:) = 0.0_r_def
    phi_proxy%data(:) = gravity*chi_p(3)%data(:)

    call linear_rtheta_code( nlayers,             &
                             r_theta_proxy%data,  &
                             u_proxy%data,        &
                             phi_proxy%data,      &
                             chi_p(1)%data,       &
                             chi_p(2)%data,       &
                             chi_p(3)%data,       &
                             ndf_w0, undf_w0,     &
                             map_w0,              &
                             this%basis_w0,       &
                             this%diff_basis_w0,  &
                             ndf_w2, undf_w2,     &
                             map_w2,              &
                             this%basis_w2,       &
                             nqp_h, nqp_v, wh, wv &
                             )

    ! Simple test, check that when u=0 the rhs=0
    this%answer(:) = 0.0_r_def

    @assertEqual(this%answer, r_theta_proxy%data(:), tol)

  end subroutine test_all

end module linear_rtheta_kernel_mod_test
