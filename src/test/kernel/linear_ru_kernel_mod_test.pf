!-----------------------------------------------------------------------------
! (c) The copyright relating to this work is owned jointly by the Crown, 
! Met Office and NERC 2014. 
! However, it has been created with the help of the GungHo Consortium, 
! whose members are identified at https://puma.nerc.ac.uk/trac/GungHo/wiki
!-----------------------------------------------------------------------------

!> Test the ru computation
!>
module linear_ru_kernel_mod_test

  use constants_mod,      only : i_def, r_def
  use field_mod,          only : field_type,      &
                                 field_proxy_type
  use function_space_mod, only : function_space_type, W0, W2, W3
  use mesh_mod,           only : mesh_type
  use pFUnit_Mod
  use quadrature_mod,     only : quadrature_type, GAUSSIAN

  implicit none

  private
  public :: linear_ru_test_type, test_all

  @TestCase
  type, extends(TestCase) :: linear_ru_test_type
    private
    type(mesh_type)          :: mesh
    type(field_type)         :: r_u, rho, theta
    real(r_def), allocatable :: answer(:)
    real(r_def), allocatable :: basis_w3(:,:,:,:), &
                                basis_w2(:,:,:,:), &
                                basis_w0(:,:,:,:), &
                                diff_basis_w0(:,:,:,:), &
                                diff_basis_w2(:,:,:,:)
  contains
    procedure setUp
    procedure tearDown
    procedure test_all
  end type linear_ru_test_type

  integer(i_def), parameter :: element_order = 0

  real(r_def), parameter :: gravity = 10.0_r_def
  real(r_def), parameter :: radius  = 6000000_r_def
  real(r_def), parameter :: omega   = 8.0E-5_r_def
  real(r_def), parameter :: p_zero  = 100000.0_r_def
  real(r_def), parameter :: rd      = 300.0_r_def
  real(r_def), parameter :: cp      = 1000.0_r_def
  real(r_def), parameter :: scaling = 1.0_r_def

  real(r_def), parameter :: bvf_square = 0.0001_r_def

contains

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  subroutine setUp( this )

    use base_mesh_config_mod,      only : base_mesh_geometry_planar, &
                                          base_mesh_partitioner_planar
    use frig_config_mod,           only : frig_base_mesh_config,           &
                                          frig_idealised_config,           &
                                          frig_initial_temperature_config, &
                                          frig_planet_config
    use finite_element_config_mod, only : finite_element_shape_quadrilateral
    use idealised_config_mod,      only : idealised_test_cold_bubble
    use mesh_mod,                  only : PLANE_BI_PERIODIC
    use reference_element_mod,     only : reference_element, reference_cube

    implicit none

    class(linear_ru_test_type), intent(inout) :: this

    type(quadrature_type)     :: qr
    type(function_space_type) :: fs
    type(field_proxy_type)    :: r_u_proxy, rho_proxy, theta_proxy

    type(function_space_type), pointer :: w0_fs => null()
    type(function_space_type), pointer :: w2_fs => null()
    type(function_space_type), pointer :: w3_fs => null()

    integer(i_def) :: nqp_h, nqp_v
    integer(i_def) :: ndf_w0, ndf_w2, ndf_w3
    integer(i_def) :: undf_w0, undf_w2, undf_w3
    integer(i_def) :: dim_w0, diff_dim_w0, dim_w2, diff_dim_w2,dim_w3

    call frig_base_mesh_config( 'foo',                        &
                                base_mesh_geometry_planar,    &
                                base_mesh_partitioner_planar, &
                                0.0_r_def )
    call frig_idealised_config( idealised_test_cold_bubble )
    call frig_initial_temperature_config( bvf_square )
    call frig_planet_config( radius, gravity, omega, rd, cp, p_zero, scaling )

    reference_element = finite_element_shape_quadrilateral
    call reference_cube()

    qr = quadrature_type( 3, GAUSSIAN )

    nqp_h = qr%get_nqp_h()
    nqp_v = qr%get_nqp_v()

    ! Dummy mesh mod has 9 cells, 3 layers and is uniform in vertical
    this%mesh = mesh_type( PLANE_BI_PERIODIC )

    w0_fs => fs%get_instance( this%mesh, element_order, W0 )
    w2_fs => fs%get_instance( this%mesh, element_order, W2 )
    w3_fs => fs%get_instance( this%mesh, element_order, W3 )

    this%rho   = field_type( vector_space = w3_fs )
    rho_proxy = this%rho%get_proxy()

    ndf_w3  = rho_proxy%vspace%get_ndf( )
    dim_w3  = rho_proxy%vspace%get_dim_space( )
    undf_w3 = rho_proxy%vspace%get_undf()
    allocate( this%basis_w3(dim_w3,ndf_w3,nqp_h,nqp_v) )

    this%r_u   = field_type( vector_space = w2_fs )
    r_u_proxy = this%r_u%get_proxy()

    ndf_w2      = r_u_proxy%vspace%get_ndf( )
    dim_w2      = r_u_proxy%vspace%get_dim_space( )
    diff_dim_w2 = r_u_proxy%vspace%get_dim_space_diff( )
    undf_w2     = r_u_proxy%vspace%get_undf()
    allocate( this%basis_w2(dim_w2,ndf_w2,nqp_h,nqp_v) )
    allocate( this%diff_basis_w2(diff_dim_w2,ndf_w2,nqp_h,nqp_v) )
    allocate( this%answer(undf_w2) )

    this%theta = field_type( vector_space = w0_fs )
    theta_proxy = this%theta%get_proxy()

    ndf_w0      = theta_proxy%vspace%get_ndf( )
    dim_w0      = theta_proxy%vspace%get_dim_space( )
    diff_dim_w0 = theta_proxy%vspace%get_dim_space_diff( )
    undf_w0     = theta_proxy%vspace%get_undf()
    allocate( this%basis_w0(dim_w0,ndf_w0,nqp_h,nqp_v) )
    allocate( this%diff_basis_w0(diff_dim_w0,ndf_w0,nqp_h,nqp_v) )

  end subroutine setUp

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  subroutine tearDown( this )

    use function_space_mod,    only : purge_function_spaces
    use reference_element_mod, only : deallocate_reference

    implicit none

    class(linear_ru_test_type), intent(inout) :: this

    deallocate( this%basis_w3,                &
                this%basis_w2, this%diff_basis_w2, &
                this%basis_w0, this%diff_basis_w0 )
    deallocate( this%answer )

    call purge_function_spaces()
    call deallocate_reference()

  end subroutine tearDown

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  @test
  subroutine test_all( this )

    use function_space_mod,   only : function_space_type
    use fs_continuity_mod,    only : W0, W2, W3
    use linear_ru_kernel_mod, only : linear_ru_code

    implicit none

    class(linear_ru_test_type), intent(inout) :: this

    real(r_def), parameter :: dx = 6000.0_r_def, &
                              dy = 1000.0_r_def
    real(r_def), parameter :: tol = 1.0e-6_r_def

    type(field_type) :: chi(3)
    type(field_proxy_type)   :: chi_p(3)

    type(function_space_type) :: fs
    type(mesh_type)           :: mesh
    type(quadrature_type)     :: qr

    type(function_space_type), pointer :: w0_fs => null()
    type(function_space_type), pointer :: w2_fs => null()

    real(r_def) :: dz, dz_test(3)

    integer :: i, j, k, cell, err

    ! Required for calling kernel 
    type(field_type) :: u, phi

    integer                 :: nlayers, nqp_h, nqp_v
    integer                 :: ndf_w0, ndf_w2, ndf_w3
    integer                 :: undf_w0, undf_w2, undf_w3
    integer, pointer        :: map_w3(:), map_w2(:), map_w0(:) => null()
    integer, pointer        :: boundary_dofs(:,:) => null()

    type(field_proxy_type) :: r_u_proxy, u_proxy, rho_proxy, theta_proxy, phi_proxy

    real(kind=r_def), pointer :: xp(:,:) => null()
    real(kind=r_def), pointer :: zp(:) => null()
    real(kind=r_def), pointer :: wh(:), wv(:) => null()

    qr = quadrature_type(3, GAUSSIAN)

    call this%mesh%get_dz(dz_test)
    dz = dz_test(1)

    w0_fs => fs%get_instance( this%mesh, element_order, W0 )
    w2_fs => fs%get_instance( this%mesh, element_order, W2 )

    ! Make a coordinate field with a function space
    do i = 1,3
      chi(i)   = field_type( vector_space = w0_fs )
      chi_p(i) = chi(i)%get_proxy()
    end do

    ! Compute coordinates
    cell = 1
    do j = 1,3
      do i = 1,3
        map_w0 => chi_p(1)%vspace%get_cell_dofmap( cell )
        do k = 0,3
          chi_p(1)%data(map_w0(1) + k) = real(i-1)*dx
          chi_p(2)%data(map_w0(1) + k) = real(j-1)*dy
          chi_p(3)%data(map_w0(1) + k) = real(k)  *dz
        end do
        cell = cell + 1
      end do
    end do

    ! Test kernel
    u   = field_type( vector_space = w2_fs )
    phi = field_type( vector_space = w0_fs )

    r_u_proxy   = this%r_u%get_proxy()
    u_proxy     = u%get_proxy()
    rho_proxy   = this%rho%get_proxy()
    theta_proxy = this%theta%get_proxy()
    phi_proxy   = phi%get_proxy()

    boundary_dofs => r_u_proxy%vspace%get_boundary_dofs()

    nlayers = rho_proxy%vspace%get_nlayers()
    nqp_h=qr%get_nqp_h()
    nqp_v=qr%get_nqp_v()
    zp=>qr%get_xqp_v()
    xp=>qr%get_xqp_h()
    wh=>qr%get_wqp_h()
    wv=>qr%get_wqp_v()

    ndf_w3  = rho_proxy%vspace%get_ndf( )
    undf_w3 = rho_proxy%vspace%get_undf()

    ndf_w2      = r_u_proxy%vspace%get_ndf( )
    undf_w2     = r_u_proxy%vspace%get_undf()

    ndf_w0      = theta_proxy%vspace%get_ndf( )
    undf_w0     = theta_proxy%vspace%get_undf()

    call rho_proxy%vspace%compute_basis_function( this%basis_w3, ndf_w3, &
                                                  nqp_h, nqp_v, xp, zp)

    call r_u_proxy%vspace%compute_basis_function( this%basis_w2, ndf_w2, &
                                                  nqp_h, nqp_v, xp, zp )

    call r_u_proxy%vspace%compute_diff_basis_function( &
                            this%diff_basis_w2, ndf_w2, nqp_h, nqp_v, xp, zp )

    call theta_proxy%vspace%compute_basis_function( this%basis_w0, ndf_w0, &
                                                    nqp_h, nqp_v, xp, zp )

    call theta_proxy%vspace%compute_diff_basis_function( &
                            this%diff_basis_w0, ndf_w0, nqp_h, nqp_v, xp, zp )



    cell = 1

    map_w3 => rho_proxy%vspace%get_cell_dofmap( cell )
    map_w2 => r_u_proxy%vspace%get_cell_dofmap( cell )
    map_w0 => theta_proxy%vspace%get_cell_dofmap( cell )

    ! Create the data
    phi_proxy%data(:) = gravity*chi_p(3)%data(:)
    r_u_proxy%data(:) = 0.0_r_def
    u_proxy%data(:)   = 0.0_r_def
    theta_proxy%data(:) = 0.0_r_def
    rho_proxy%data(:) = 0.0_r_def
    call linear_ru_code( nlayers,                                   &
                         ndf_w2, undf_w2,                           &
                         map_w2, this%basis_w2, this%diff_basis_w2, &
                         boundary_dofs,                             &
                         r_u_proxy%data,                            &
                         u_proxy%data,                              &
                         ndf_w3, undf_w3,                           &
                         map_w3, this%basis_w3,                     &
                         rho_proxy%data,                            &
                         ndf_w0, undf_w0,                           &
                         map_w0, this%basis_w0, this%diff_basis_w0, &
                         theta_proxy%data,                          &
                         phi_proxy%data,                            &
                         chi_p(1)%data,                             &
                         chi_p(2)%data,                             &
                         chi_p(3)%data,                             &
                         nqp_h, nqp_v, wh, wv                       &
                         )

    ! Simple test, zero in zero out
    this%answer(:) = 0.0_r_def
    @assertEqual(this%answer(:), r_u_proxy%data(:), tol)

  end subroutine test_all

end module linear_ru_kernel_mod_test
