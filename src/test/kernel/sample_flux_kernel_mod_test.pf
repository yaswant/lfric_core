!-----------------------------------------------------------------------------
! (c) The copyright relating to this work is owned jointly by the Crown, 
! Met Office and NERC 2014. 
! However, it has been created with the help of the GungHo Consortium, 
! whose members are identified at https://puma.nerc.ac.uk/trac/GungHo/wiki
!-----------------------------------------------------------------------------

module sample_flux_kernel_mod_test

  use constants_mod,      only : i_def, r_def
  use field_mod,          only : field_type,      &
                                 field_proxy_type
  use fs_continuity_mod,  only : W2, W3
  use function_space_mod, only : function_space_type
  use mesh_mod,           only : mesh_type
  use pFUnit_Mod
  use quadrature_mod,     only : quadrature_type, GAUSSIAN

  implicit none

  private
  public :: test_all

  @TestCase
  type, extends(TestCase), public :: sample_flux_test_type
    private
    type(mesh_type)          :: mesh
    type(field_type)         :: rho, u
    real(r_def), allocatable :: basis_rho(:,:,:)
  contains
    procedure setUp
    procedure tearDown
    procedure test_all
  end type sample_flux_test_type

  integer(i_def), parameter :: element_order = 0

contains

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  subroutine setUp( this )

    use finite_element_config_mod, only : finite_element_shape_quadrilateral
    use mesh_mod,                  only : PLANE_BI_PERIODIC
    use reference_element_mod,     only : reference_cube, reference_element

    implicit none

    class(sample_flux_test_type), intent(inout) :: this

    type(function_space_type) :: fs
    type(field_proxy_type)    :: rho_p, u_p

    type(function_space_type), pointer :: w2_fs => null()
    type(function_space_type), pointer :: w3_fs => null()

    integer(i_def) :: ndf_rho, dim_rho, ndf_u

    reference_element = finite_element_shape_quadrilateral
    call reference_cube()

    ! Dummy mesh mod has 9 cells, 3 layers and is uniform in vertical
    this%mesh = mesh_type(PLANE_BI_PERIODIC)

    w2_fs => fs%get_instance( this%mesh, element_order, W2 )
    w3_fs => fs%get_instance( this%mesh, element_order, W3 )

    this%u = field_type( vector_space = w2_fs )
    u_p    = this%u%get_proxy()
    ndf_u  = u_p%vspace%get_ndf( )

    this%rho = field_type( vector_space = w3_fs )
    rho_p    = this%rho%get_proxy()
    ndf_rho  = rho_p%vspace%get_ndf( )
    dim_rho  = rho_p%vspace%get_dim_space( )

    allocate( this%basis_rho(dim_rho, ndf_rho, ndf_u) )

  end subroutine setUp

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  subroutine tearDown( this )

    use function_space_mod,    only : purge_function_spaces
    use reference_element_mod, only : deallocate_reference

    implicit none

    class(sample_flux_test_type), intent(inout) :: this

    deallocate( this%basis_rho )

    call purge_function_spaces()
    call deallocate_reference()

  end subroutine tearDown

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  @test
  subroutine test_all( this )

    use sample_flux_kernel_mod,     only : sample_flux_code

    implicit none

    class(sample_flux_test_type), intent(inout) :: this

    real(r_def), parameter :: tol = 1.0e-6_r_def

    type(quadrature_type)     :: qr
    type(function_space_type) :: fs
    type(function_space_type), pointer :: w2_fs => null()
    type(function_space_type), pointer :: w3_fs => null()

    real(r_def) :: answer

    integer :: i, j, k, cell
    integer(i_def) :: err

    ! Variables for testing sample_flux
    type(field_type) :: flux, m

    integer          :: ndf_rho, undf_rho, dim_rho, &
                        ndf_u, undf_u, dim_u

    integer, pointer :: map_rho(:) => null(), &
                        map_u(:) => null() 

    real(kind=r_def), pointer :: nodes(:,:) => null()
    type(field_proxy_type) :: flux_p, rho_p, u_p, m_p

    qr  = quadrature_type(3, GAUSSIAN)

    w2_fs => fs%get_instance( this%mesh, element_order, W2 )
    w3_fs => fs%get_instance( this%mesh, element_order, W3 )

    ! Test flux computation
    flux = field_type( vector_space = w2_fs )
    m    = field_type( vector_space = w2_fs )

    flux_p = flux%get_proxy()
    rho_p  = this%rho%get_proxy()
    u_p    = this%u%get_proxy()
    m_p    = m%get_proxy()

    ndf_u  = u_p%vspace%get_ndf( )
    undf_u = u_p%vspace%get_undf( )

    ndf_rho  = rho_p%vspace%get_ndf( )
    undf_rho = rho_p%vspace%get_undf( )

    nodes => flux_p%vspace%get_nodes( )
    call rho_p%vspace%compute_nodal_basis_function( this%basis_rho, &
                                                    ndf_rho,        &
                                                    ndf_u,          &
                                                    nodes )

    cell = 5
    map_u   => u_p%vspace%get_cell_dofmap( cell )
    map_rho => rho_p%vspace%get_cell_dofmap( cell )
    ! Create the data
    flux_p%data(:) = 0.0_r_def
    u_p%data(:) = 0.0_r_def
    rho_p%data(:) = 2.0_r_def
    m_p%data(:) = 2.0_r_def

    u_p%data(map_u(5)+0) = 0.0_r_def
    u_p%data(map_u(5)+1) = 1.0_r_def
    u_p%data(map_u(6)+1) = 1.0_r_def
    u_p%data(map_u(6)+2) = 0.0_r_def
    rho_p%data(map_rho(1)) = 3.0_r_def
    rho_p%data(map_rho(1)+1) = 4.0_r_def

    call sample_flux_code(flux_p%vspace%get_nlayers(), &
                          flux_p%data, & 
                          m_p%data, &
                          u_p%data, &
                          rho_p%data, &
                          ndf_u, & 
                          undf_u, &
                          map_u, &
                          ndf_rho, &
                          undf_rho, &
                          map_rho, &
                          this%basis_rho &
                          )

     answer = 0.0_r_def 
     @assertEqual(answer, flux_p%data(map_u(1) ), tol)
     answer = 0.0_r_def 
     @assertEqual(answer, flux_p%data(map_u(5) ), tol)
     answer = 3.5_r_def 
     @assertEqual(answer, flux_p%data(map_u(6) ), tol)
     answer = 3.0_r_def 
     @assertEqual(answer, flux_p%data(map_u(5)+ 2 ), tol)

  end subroutine test_all

end module sample_flux_kernel_mod_test
