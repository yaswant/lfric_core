!-----------------------------------------------------------------------------
! (c) The copyright relating to this work is owned jointly by the Crown, 
! Met Office and NERC 2014. 
! However, it has been created with the help of the GungHo Consortium, 
! whose members are identified at https://puma.nerc.ac.uk/trac/GungHo/wiki
!-----------------------------------------------------------------------------

!> Test the extraction of the inverse lumped diagonal from the
!> vertical velocity mass matrix
!>
module columnwise_op_asm_m2v_lumped_inv_kernel_mod_test

  use constants_mod,                 only : i_def, r_def
  use function_space_collection_mod, only : function_space_collection_type, &
                                            function_space_collection
  use mesh_collection_mod,           only : mesh_collection_type, &
                                            mesh_collection
  use field_mod,                     only : field_type, field_proxy_type
  use fs_continuity_mod,             only : W2V
  use function_space_mod,            only : function_space_type

  use operator_mod,                  only : operator_type,                 &
                                            operator_proxy_type,           &
                                            columnwise_operator_type,      &
                                            columnwise_operator_proxy_type
  use pFUnit_Mod

  implicit none

  private
  public :: test_all

  @TestCase
  type, extends(TestCase), public :: columnwise_op_asm_m2v_lumped_inv_test_type
    private
    type(function_space_type), pointer :: w2v_fs => null()
    type(operator_type)                :: mm
    type(columnwise_operator_type)     :: columnwise_mm
    integer(i_def)                     :: mesh_id
  contains
    procedure setUp
    procedure tearDown
    procedure test_all
  end type columnwise_op_asm_m2v_lumped_inv_test_type

  integer(i_def), parameter :: element_order = 0

contains

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  subroutine setUp( this )
    use finite_element_config_mod, only : finite_element_shape_quadrilateral
    use feign_config_mod,          only : feign_finite_element_config
    use mesh_mod,                  only : PLANE_BI_PERIODIC
    use reference_element_mod,     only : reference_cube, reference_element
 
    implicit none

    class(columnwise_op_asm_m2v_lumped_inv_test_type), intent(inout) :: this

    integer :: i
    integer :: alpha, beta, gamma_m, gamma_p

    call feign_finite_element_config( finite_element_shape_quadrilateral, &
                                      0, 0, .true., .false., .false., .false. )

    reference_element = finite_element_shape_quadrilateral
    call reference_cube()
    
    ! Create top level mesh collection
    mesh_collection = mesh_collection_type()
    ! Create top level function space collection
    function_space_collection = function_space_collection_type()

    ! Dummy mesh mod has 9 cells, 3 layers and is uniform in vertical
    this%mesh_id = mesh_collection%add_unit_test_mesh( PLANE_BI_PERIODIC )


    this%w2v_fs => function_space_collection%get_fs( this%mesh_id, &
                                                     element_order, &
                                                     W2V )

    this%mm = operator_type( this%w2v_fs, this%w2v_fs )

    alpha = 1
    beta = 1
    gamma_m = 0
    gamma_p = 0
    this%columnwise_mm = columnwise_operator_type( this%w2v_fs, this%w2v_fs, &
                                                   alpha, beta, gamma_m, gamma_p )

  end subroutine setUp

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  subroutine tearDown( this )

    use reference_element_mod, only : deallocate_reference

    implicit none

    class(columnwise_op_asm_m2v_lumped_inv_test_type), intent(inout) :: this

    call function_space_collection%clear()
    call deallocate_reference()

  end subroutine tearDown

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  @test
  subroutine test_all( this )

    use columnwise_op_asm_m2v_lumped_inv_kernel_mod, only : &
         columnwise_op_asm_m2v_lumped_inv_kernel_code

    implicit none

    class(columnwise_op_asm_m2v_lumped_inv_test_type), intent(inout) :: this

    real(r_def), parameter                          :: tol = 1.0e-6_r_def
    real(kind=r_def)                                :: answer
    integer                                         :: i, j, k, cell
    integer                                         :: nlayers, ndf
    type(operator_proxy_type)                       :: mm_p
    type(columnwise_operator_proxy_type)            :: columnwise_mm_p
    real(kind=r_def), allocatable, dimension(:,:,:) :: local_stencil

    ! Test mass matrix kernel
    mm_p = this%mm%get_proxy()
    columnwise_mm_p = this%columnwise_mm%get_proxy()

    nlayers  = columnwise_mm_p%fs_from%get_nlayers()
    ndf  = columnwise_mm_p%fs_from%get_ndf()

    allocate(local_stencil(ndf,ndf,mm_p%ncell_3d))

    cell = 1

    do i=1, mm_p%ncell_3d
       local_stencil(:,:,i) = reshape( (/ 1.0_r_def/3.0_r_def, 1.0_r_def/6.0_r_def,    &
                                          1.0_r_def/6.0_r_def, 1.0_r_def/3.0_r_def /), &
                                          (/2, 2/) )
    end do

    call columnwise_op_asm_m2v_lumped_inv_kernel_code(cell,                              &
                                                      nlayers,                           &
                                                      mm_p%ncell_3d,                     &
                                                      columnwise_mm_p%ncell_2d,          &
                                                      local_stencil,                     &
                                                      columnwise_mm_p%columnwise_matrix, &
                                                      columnwise_mm_p%nrow,              &
                                                      columnwise_mm_p%ncol,              &
                                                      columnwise_mm_p%bandwidth,         &
                                                      columnwise_mm_p%alpha,             &
                                                      columnwise_mm_p%beta,              &
                                                      columnwise_mm_p%gamma_m,           &
                                                      columnwise_mm_p%gamma_p,           &
                                                      ndf,                               &
                                                      ndf,                               &
                                                      columnwise_mm_p%column_banded_dofmap_to, &
                                                      columnwise_mm_p%column_banded_dofmap_from)
    do i = 1, columnwise_mm_p%nrow
      if ( (i == 1) .or. (i == columnwise_mm_p%nrow) ) then
        ! First and final row should be ( 3 )
        answer = 3.0_r_def
        @assertEqual(answer, columnwise_mm_p%columnwise_matrix(1,i,1), tol)
      else
        ! All other rows should be ( 3/2 )
        answer = (3.0_r_def/2.0_r_def)
        @assertEqual(answer, columnwise_mm_p%columnwise_matrix(1,i,1), tol)
      end if
    end do

    deallocate(local_stencil)

  end subroutine test_all

end module columnwise_op_asm_m2v_lumped_inv_kernel_mod_test
