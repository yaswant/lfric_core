!-------------------------------------------------------------------------------
! (c) The copyright relating to this work is owned jointly by the Crown, 
! Met Office and NERC 2014. 
! However, it has been created with the help of the GungHo Consortium, 
! whose members are identified at https://puma.nerc.ac.uk/trac/GungHo/wiki
!-------------------------------------------------------------------------------
!
!-------------------------------------------------------------------------------

module calc_exner_pointwise_mod_test

  use constants_mod, only: r_def
  use pFUnit_Mod

  implicit none

  private
  public :: calc_exner_pointwise_test_type, test_all

  @TestCase
  type, extends(TestCase) :: calc_exner_pointwise_test_type
    private
  contains
    procedure setUp
    procedure test_all
  end type calc_exner_pointwise_test_type

  real(r_def), parameter :: gravity = 10.0_r_def
  real(r_def), parameter :: radius  = 6000000_r_def
  real(r_def), parameter :: omega   = 8.0E-5_r_def
  real(r_def), parameter :: p_zero  = 100000.0_r_def
  real(r_def), parameter :: rd      = 300.0_r_def
  real(r_def), parameter :: cp      = 1000.0_r_def
  real(r_def), parameter :: scaling = 1.0_r_def

contains
  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  subroutine setUp( this )

    use frig_config_mod, only : frig_planet_config

    implicit none

    class(calc_exner_pointwise_test_type), intent(inout) :: this

    call frig_planet_config( radius, gravity, omega, rd, cp, p_zero, scaling )

  end subroutine setUp

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  @test
  subroutine test_all( this )

    use calc_exner_pointwise_mod, only: calc_exner_pointwise, &
                                        linear_calc_exner_pointwise

    implicit none

    class(calc_exner_pointwise_test_type), intent(inout) :: this

    real(r_def), parameter :: kappa = rd / cp

    real(kind=r_def) :: exner, exner_exact
    real(kind=r_def) :: rho, theta, exner_s, rho_s, theta_s

    rho     = 0.5_r_def
    theta   = 300.0_r_def

    exner_exact =  ( Rd/p_zero * rho * theta ) ** (  kappa / ( 1.0_r_def - kappa ) )
    exner = calc_exner_pointwise(rho, theta)

    @assertEqual(exner_exact, exner)

    rho_s   = 0.75_r_def
    theta_s = 310.0_r_def
    exner_s = 0.9_r_def

    exner_exact = kappa / ( 1.0_r_def - kappa ) * exner_s * ( rho/rho_s + theta/theta_s )
    exner = linear_calc_exner_pointwise(rho, theta, exner_s, rho_s, theta_s)

    @assertEqual(exner_exact, exner)

  end subroutine test_all

end module calc_exner_pointwise_mod_test
