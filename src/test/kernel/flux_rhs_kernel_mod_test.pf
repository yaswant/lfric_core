!-----------------------------------------------------------------------------
! (c) The copyright relating to this work is owned jointly by the Crown, 
! Met Office and NERC 2014. 
! However, it has been created with the help of the GungHo Consortium, 
! whose members are identified at https://puma.nerc.ac.uk/trac/GungHo/wiki
!-----------------------------------------------------------------------------

!> Test the flux rhs computation
!>
module flux_rhs_kernel_mod_test

  use constants_mod,      only : i_def, r_def
  use field_mod,          only : field_type,      &
                                field_proxy_type
  use fs_continuity_mod,  only : W0, W2, W3
  use function_space_mod, only : function_space_type
  use mesh_mod,           only : mesh_type
  use pFUnit_Mod
  use quadrature_mod,     only : quadrature_type, GAUSSIAN

  implicit none

  private
  public :: test_all

  @TestCase
  type, extends(TestCase), public :: flux_rhs_test_type
    private
      type(mesh_type)          :: mesh
      type(field_type)         :: chi(3)
      type(field_type)         :: f, rhs
      real(r_def), allocatable :: basis_u(:,:,:,:), &
                                  basis_f(:,:,:,:), &
                                  diff_basis_chi(:,:,:,:)
  contains
    procedure setUp
    procedure tearDown
    procedure test_all
  end type flux_rhs_test_type

  integer(i_def), parameter :: element_order = 0

contains

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  subroutine setUp( this )

    use finite_element_config_mod, only : finite_element_shape_quadrilateral
    use mesh_mod,                  only : PLANE_BI_PERIODIC
    use reference_element_mod,     only : reference_cube, reference_element

    implicit none

    class(flux_rhs_test_type), intent(inout) :: this

    type(quadrature_type)     :: qr
    type(function_space_type) :: fs
    type(field_proxy_type)    :: chi_p(3)
    type(field_proxy_type)    :: f_p, rhs_p

    type(function_space_type), pointer :: w0_fs => null()
    type(function_space_type), pointer :: w2_fs => null()
    type(function_space_type), pointer :: w3_fs => null()

    integer(i_def) :: nqp_h, nqp_v
    integer(i_def) :: ndf_f, dim_f, &
                      ndf_u, dim_u, &
                      ndf_chi, dim_diff_chi

    integer :: i

    reference_element = finite_element_shape_quadrilateral
    call reference_cube()

    qr  = quadrature_type(3, GAUSSIAN)

    nqp_h=qr%get_nqp_h()
    nqp_v=qr%get_nqp_v()

    ! Dummy mesh mod has 9 cells, 3 layers and is uniform in vertical
    this%mesh = mesh_type(PLANE_BI_PERIODIC)

    w0_fs => fs%get_instance( this%mesh, element_order, W0 )
    w2_fs => fs%get_instance( this%mesh, element_order, W2 )
    w3_fs => fs%get_instance( this%mesh, element_order, W3 )

    do i = 1,3
      this%chi(i) = field_type( vector_space = w0_fs )
      chi_p(i) = this%chi(i)%get_proxy()
    end do
    ndf_chi      = chi_p(1)%vspace%get_ndf( )
    dim_diff_chi = chi_p(1)%vspace%get_dim_space_diff( )

    this%rhs =  field_type( vector_space = w2_fs )
    rhs_p    = this%rhs%get_proxy()
    ndf_u    = rhs_p%vspace%get_ndf( )
    dim_u    = rhs_p%vspace%get_dim_space( )

    this%f =  field_type( vector_space = w3_fs )
    f_p    = this%f%get_proxy()
    ndf_f  = f_p%vspace%get_ndf( )
    dim_f  = f_p%vspace%get_dim_space( )

    allocate( this%basis_u(dim_u, ndf_u, nqp_h, nqp_v),           &
              this%basis_f(dim_f, ndf_f, nqp_h, nqp_v),           &
              this%diff_basis_chi(dim_diff_chi, ndf_chi, nqp_h, nqp_v) )

  end subroutine setUp

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  subroutine tearDown( this )

    use function_space_mod,    only : purge_function_spaces
    use reference_element_mod, only : deallocate_reference

    implicit none

    class(flux_rhs_test_type), intent(inout) :: this

    deallocate( this%basis_u, this%basis_f, this%diff_basis_chi )

    call purge_function_spaces()
    call deallocate_reference()

  end subroutine tearDown

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  @test
  subroutine test_all( this )

    use flux_rhs_kernel_mod,        only : flux_rhs_code

    implicit none

    class(flux_rhs_test_type), intent(inout) :: this

    real(r_def), parameter :: tol = 1.0e-6_r_def

    type(field_proxy_type ) :: chi_p(3)

    type(quadrature_type)     :: qr
    type(function_space_type) :: fs

    type(function_space_type), pointer :: w2_fs => null()

    real(kind=r_def) :: answer(3)

    integer :: i, j, k, cell
    integer(i_def) :: err

    ! Variables for testing flux_rhs
    type( field_type ) :: u

    integer          :: ndf_f, undf_f, &
                        ndf_u, undf_u, &
                        ndf_chi, undf_chi, &
                        nqp_h, nqp_v

    integer, pointer :: map_f(:) => null(), &
                        map_u(:) => null(), & 
                        map_chi(:) => null(), &
                        boundary_dofs(:,:) => null()

    real(kind=r_def), pointer :: xp(:,:) => null()
    real(kind=r_def), pointer :: zp(:)   => null()
    real(kind=r_def), pointer :: wh(:), wv(:) => null()

    type( field_proxy_type )        :: rhs_p, f_p, u_p


    qr  = quadrature_type(3, GAUSSIAN)

    w2_fs => fs%get_instance( this%mesh, element_order, W2 )

    ! Make a coordinate field with a function space
    do i = 1,3
      chi_p(i) = this%chi(i)%get_proxy()
    end do
    ! Compute coordinates
    cell = 1
    do j = 1,3
      do i = 1,3
        map_chi => chi_p(1)%vspace%get_cell_dofmap( cell )
        do k = 0,3
          chi_p(1)%data(map_chi(1) + k) = real(i-1)*4.0_r_def
          chi_p(2)%data(map_chi(1) + k) = real(j-1)*3.0_r_def
          chi_p(3)%data(map_chi(1) + k) = real(k)  *2.0_r_def
        end do
        cell = cell + 1
      end do
    end do

    ! Test flux rhs
    u   =  field_type( vector_space = w2_fs )

    rhs_p = this%rhs%get_proxy()
    f_p   = this%f%get_proxy()
    u_p   = u%get_proxy()

    boundary_dofs => rhs_p%vspace%get_boundary_dofs()

    ndf_u  = rhs_p%vspace%get_ndf( )
    undf_u = rhs_p%vspace%get_undf( )

    ndf_f  = f_p%vspace%get_ndf( )
    undf_f = f_p%vspace%get_undf( )

    ndf_chi  = chi_p(1)%vspace%get_ndf( )
    undf_chi = chi_p(1)%vspace%get_undf( )

    nqp_h=qr%get_nqp_h()
    nqp_v=qr%get_nqp_v()
    zp=>qr%get_xqp_v()
    xp=>qr%get_xqp_h()
    wh=>qr%get_wqp_h()
    wv=>qr%get_wqp_v()

    call rhs_p%vspace%compute_basis_function( &
         this%basis_u, ndf_u, nqp_h, nqp_v, xp, zp)
    call f_p%vspace%compute_basis_function( &
         this%basis_f, ndf_f, nqp_h, nqp_v, xp, zp)
    call chi_p(1)%vspace%compute_diff_basis_function( &
         this%diff_basis_chi, ndf_chi, nqp_h, nqp_v, xp, zp)

    cell = 5
    map_f   => f_p%vspace%get_cell_dofmap( cell )
    map_u   => rhs_p%vspace%get_cell_dofmap( cell )
    map_chi => chi_p(1)%vspace%get_cell_dofmap( cell )

    ! Create the data
    f_p%data(:) = 2.0_r_def
    u_p%data(:) = 0.0_r_def
    rhs_p%data(:) = 0.0_r_def

    u_p%data(map_u(5)+0) = 0.0_r_def
    u_p%data(map_u(5)+1) = 1.0_r_def
    u_p%data(map_u(6)+1) = 1.0_r_def
    u_p%data(map_u(6)+2) = 0.0_r_def


    call flux_rhs_code( rhs_p%vspace%get_nlayers(), &
                        ndf_u, &
                        undf_u, &
                        map_u, &
                        this%basis_u, &
                        boundary_dofs, &
                        rhs_p%data, &
                        u_p%data, &
                        ndf_f, &
                        undf_f, &
                        map_f, &
                        this%basis_f, &
                        f_p%data, &
                        ndf_chi, &
                        undf_chi, &
                        map_chi, &
                        this%diff_basis_chi, &
                        chi_p(1)%data, &
                        chi_p(2)%data, &
                        chi_p(3)%data, &
                        nqp_h, &
                        nqp_v, &
                        wh, &
                        wv &
                        )

    answer(1) = 0.0_r_def
    answer(2) = 5.0_r_def/6.0_r_def * (2.0_r_def/(4.0_r_def*3.0_r_def))*2.0_r_def
    answer(3) = 5.0_r_def/6.0_r_def * (2.0_r_def/(4.0_r_def*3.0_r_def))*2.0_r_def
    do k = 1,3
      @assertEqual(answer(k), rhs_p%data(map_u(5) + (k-1) ), tol)
    end do

  end subroutine test_all

end module flux_rhs_kernel_mod_test
