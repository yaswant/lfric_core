!-------------------------------------------------------------------------------
! (c) The copyright relating to this work is owned jointly by the Crown, 
! Met Office and NERC 2014. 
! However, it has been created with the help of the GungHo Consortium, 
! whose members are identified at https://puma.nerc.ac.uk/trac/GungHo/wiki
!-------------------------------------------------------------------------------
module gencube_mod_test

  use constants_mod,     only: i_def, r_def
  use gencube_mod,       only: gencube_ps_type
  use pFUnit_Mod

  implicit none

  private
  public :: test_all

  @TestCase
  type, extends(TestCase), public :: gencube_test_type
    private
    integer(i_def), allocatable :: faces_on_face(:,:)
    integer(i_def), allocatable :: nodes_on_face(:,:)
    integer(i_def), allocatable :: edges_on_face(:,:)
    integer(i_def), allocatable :: nodes_on_edge(:,:)
    real(r_def),    allocatable :: coords(:,:)
  contains
    procedure setUp
    procedure tearDown
    procedure test_all
  end type gencube_test_type

  integer(kind=i_def), parameter :: ndivs = 3
  integer(kind=i_def), parameter :: nsmooth = 1

contains

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  subroutine setUp( this )

    implicit none

    class(gencube_test_type), intent(inout) :: this

    allocate( this%faces_on_face(4, 6*ndivs*ndivs) )
    allocate( this%nodes_on_face(4, 6*ndivs*ndivs) )
    allocate( this%edges_on_face(4, 6*ndivs*ndivs) )
    allocate( this%nodes_on_edge(2, 2*6*ndivs*ndivs) )
    allocate( this%coords(2, 6*ndivs*ndivs+2) )

  end subroutine setUp

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  subroutine tearDown( this )

    implicit none

    class(gencube_test_type), intent(inout) :: this

    deallocate( this%faces_on_face )
    deallocate( this%nodes_on_face )
    deallocate( this%edges_on_face )
    deallocate( this%nodes_on_edge )
    deallocate( this%coords )

  end subroutine tearDown

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  @test
  subroutine test_all( this )

    implicit none

    class(gencube_test_type), intent(inout) :: this

    type(gencube_ps_type)                  :: csgen
    integer(kind=i_def)                    :: nodes, edges, faces
    integer(kind=i_def)                    :: nodes_per_face, edges_per_face
    integer(kind=i_def)                    :: nodes_per_edge, max_faces_per_node
    real(kind=r_def), parameter            :: eps = 1E-09_r_def

    csgen = gencube_ps_type(ndivs, nsmooth)
    ! Generate the mesh and connectivity
    call csgen%generate()
    ! Retrieve calculated dimensions
    call csgen%get_dimensions(nodes, edges, faces, nodes_per_face, &
                              edges_per_face, nodes_per_edge)

    ! Mesh has correct dimensions for arguments
    @assertEqual(nodes, 6*ndivs*ndivs+2, "Incorrect number of vertices for mesh dimensions")
    @assertEqual(edges, 2*6*ndivs*ndivs, "Incorrect number of edges for mesh dimensions")
    @assertEqual(faces, 6*ndivs*ndivs, "Incorrect number of faces for mesh dimensions")

    ! Mesh has correct element dimensions
    @assertEqual(edges_per_face, 4, "Number of edges per face does not correspond to quad mesh")
    @assertEqual(nodes_per_edge, 2, "Number of vertices per edge does not correspond to quad mesh")

    ! Retrieve mesh connectivity
    call csgen%get_connectivity(this%nodes_on_face, this%nodes_on_edge, &
                                this%edges_on_face, this%faces_on_face)

    ! Mesh has expected vertex values on certain faces
    @assertEqual(this%nodes_on_face(:,1), (/ 4,5,2,1 /), "Incorrect vertex sequence on faces.")
    @assertEqual(this%nodes_on_face(:,20), (/ 24,21,20,23 /), "Incorrect vertex sequence on faces.")

    ! Mesh connectivity is complete
    @assertTrue(minval(this%faces_on_face, 2) /= 0, "Incomplete faces_on_face connectivity.")
    @assertTrue(minval(this%nodes_on_face, 2) /= 0, "Incomplete nodes_on_face connectivity.")
    @assertTrue(minval(this%edges_on_face, 2) /= 0, "Incomplete edges_on_face connectivity.")
    @assertTrue(minval(this%nodes_on_edge, 2) /= 0, "Incomplete nodes_on_edge connectivity.")

    ! Mesh connectivity has expected values at certain points
    @assertEqual(this%faces_on_face(:,1), (/ 30,4,2,37 /), "Incorrect faces_on_face connectivity.")
    @assertEqual(this%faces_on_face(:,11), (/ 10,14,12,44 /), "Incorrect faces_on_face connectivity.")

    @assertEqual(this%edges_on_face(:,1), (/ 2,3,5,1 /), "Incorrect edges_on_face connectivity.")
    @assertEqual(this%edges_on_face(:,20), (/ 48,50,46,47 /), "Incorrect edges_on_face connectivity.")

    @assertEqual(this%nodes_on_edge(:,1), (/ 1,2 /), "Incorrect nodes_on_edge connectivity.")
    @assertEqual(this%nodes_on_edge(:,40), (/ 43,42 /), "Incorrect nodes_on_edge connectivity.")

    ! Retrieve mesh coordinates
    call csgen%get_coordinates(this%coords)

    ! Mesh coordinates have expected values at specified points
    @assertTrue(abs(this%coords(1,1) - 5.49778714537437_r_def) < eps, "Incorrect mesh coordinates.")
    @assertTrue(abs(this%coords(2,1) - 0.615479708713493_r_def) < eps, "Incorrect mesh coordinates.")
    @assertTrue(abs(this%coords(1,20) - 2.80515847438956_r_def) < eps, "Incorrect mesh coordinates.")
    @assertTrue(abs(this%coords(2,20) - 0.756566664789698_r_def) < eps, "Incorrect mesh coordinates.")

  end subroutine test_all

end module gencube_mod_test
