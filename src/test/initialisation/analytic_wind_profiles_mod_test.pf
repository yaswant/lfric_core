!-------------------------------------------------------------------------------
! (c) The copyright relating to this work is owned jointly by the Crown, 
! Met Office and NERC 2014. 
! However, it has been created with the help of the GungHo Consortium, 
! whose members are identified at https://puma.nerc.ac.uk/trac/GungHo/wiki
!-------------------------------------------------------------------------------
!
!-------------------------------------------------------------------------------

module analytic_wind_profiles_mod_test

  use constants_mod, only : r_def
  use pFUnit_Mod

  implicit none

  private
  public :: analytic_wind_profiles_test_type, test_all

  @TestCase
  type, extends(TestCase) :: analytic_wind_profiles_test_type
    private
  contains
    procedure setUp
    procedure test_all
  end type analytic_wind_profiles_test_type

  real(r_def), parameter :: gravity = 10.0_r_def
  real(r_def), parameter :: radius  = 6000000_r_def
  real(r_def), parameter :: omega   = 8.0E-5_r_def
  real(r_def), parameter :: p_zero  = 100000.0_r_def
  real(r_def), parameter :: rd      = 300.0_r_def
  real(r_def), parameter :: cp      = 1000.0_r_def
  real(r_def), parameter :: scaling = 1.0_r_def

contains

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  subroutine setUp( this )

    use feign_config_mod, only : feign_planet_config

    implicit none

    class(analytic_wind_profiles_test_type), intent(inout) :: this

    call feign_planet_config( gravity=gravity, radius=radius, omega=omega, &
                              rd=rd, cp=cp, p_zero=p_zero,                 &
                              scaling_factor=scaling )

  end subroutine setUp

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

  @test
  subroutine test_all( this )

    use analytic_wind_profiles_mod, only: analytic_wind
    use constants_mod,              only: PI
    use initial_wind_config_mod, &
                         only: initial_wind_profile_solid_body_rotation, &
                               initial_wind_profile_constant_shear_uv,   &
                               initial_wind_profile_vortex

    implicit none

    class(analytic_wind_profiles_test_type), intent(inout) :: this

    real(kind=r_def) :: u(3), v(3), llr(3), xyz(3)
    real(kind=r_def) :: u0, v0, a, h, s
    real(kind=r_def) :: tol

    llr = (/ PI, PI/4.0_r_def, radius + 10000.0_r_def /)
    xyz = (/ 1000.0_r_def, 75000.0_r_def, 5000.0_r_def /)
    u0 = 20.0_r_def
    a = 1.0_r_def/8.0_r_def

    u = analytic_wind(llr, initial_wind_profile_solid_body_rotation, 2, (/ u0, a /))
    s = 0.5_r_def*(llr(3)/radius + 1.0_r_def)
    v(1) = u0 * s * ( cos(llr(2))*cos(a*PI) &
                    + sin(llr(1))*sin(llr(2))*sin(a*PI) )
    v(2) = u0 * s * cos(llr(1))*sin(a*PI)
    v(3) = 0.0_r_def

    @assertEqual(v, u)

    v0 = 5.0_r_def
    h = 30000.0_r_def
    u = analytic_wind(xyz, initial_wind_profile_constant_shear_uv, 3, (/ u0, v0, h /))
    v(1) = u0*xyz(3)/h
    v(2) = v0*xyz(3)/h
    v(3) = 0.0_r_def 

    @assertEqual(v, u)

    tol=10.0E-12_r_def
    llr = (/ 0.0_r_def, 0.0_r_def, 6371229.0_r_def /)
    u = analytic_wind(llr,initial_wind_profile_vortex,0)
    @assertEqual(0.0_r_def, u(1), tol)

    llr = (/ 0.0_r_def, PI/8.0_r_def, 6371229.0_r_def /)
    u = analytic_wind(llr,initial_wind_profile_vortex,0)

    @assertEqual(-9.08019127578551_r_def, u(1), tol)
    @assertEqual(0.0_r_def, u(2), tol)

    llr = (/ PI/8.0_r_def, 0.0_r_def, 6371229.0_r_def /)
    u = analytic_wind(llr,initial_wind_profile_vortex,0)

    @assertEqual(0.0_r_def, u(1), tol)
    @assertEqual(9.08019127578551_r_def, u(2), tol)

  end subroutine test_all

end module analytic_wind_profiles_mod_test
