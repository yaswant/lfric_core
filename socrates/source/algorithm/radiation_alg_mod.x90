!------------------------------------------------------------------------------
! (c) Crown copyright 2018 Met Office. All rights reserved.
! The file LICENCE, distributed with this code, contains details of the terms
! under which the code may be used.
!------------------------------------------------------------------------------
! @brief Control code for the radiative transfer scheme

module radiation_alg_mod
  
use field_mod,                    only: field_type
use field_collection_mod,         only: field_collection_type
use mr_indices_mod,               only: nummr, imr_v, imr_cl, imr_ci
use constants_mod,                only: r_def

use sw_kernel_mod, only: sw_kernel_type
use lw_kernel_mod, only: lw_kernel_type
use timestepping_config_mod, only: dt

implicit none
private

!------------------------------------------------------------------------------
! Contained functions/subroutines
!------------------------------------------------------------------------------
public :: radiation_alg_step

contains

! @param[in,out] dtheta_rad             Potential temperature increment
! @param[in]     theta                  Potential temperature field
! @param[in]     exner                  Exner pressure field in density space
! @param[in]     mr                     Mixing ratios
! @param[in,out] jules_ancils           Ancillary fields for Jules
! @param[in,out] jules_prognostics      Prognostic fields for Jules
! @param[in,out] derived_fields         Derived fields
! @param[in,out] cloud_fields           Cloud fields
! @param[in,out] twod_fields            2D fields
! @param[in,out] physics_incs           Physics increments
! @param[in]     height_w3              Height of w3 levels above surface
! @param[in]     height_wth             Height of wth levels above surface
! @param[in]     tile_sw_direct_albedo  Tile SW direct albedos
! @param[in]     tile_sw_diffuse_albedo Tile SW diffuse albedos
! @param[in]     tile_lw_albedo         Tile LW albedos (1 - emissivity)

subroutine radiation_alg_step(dtheta_rad, theta, exner, mr,                  &
                              jules_ancils, jules_prognostics,               &
                              derived_fields, cloud_fields, twod_fields,     &
                              physics_incs, height_w3, height_wth,           &
                              tile_sw_direct_albedo, tile_sw_diffuse_albedo, &
                              tile_lw_albedo)

  implicit none

  type( field_type ), intent( inout ) :: dtheta_rad
  type( field_type ), intent( in )    :: theta, exner, mr(nummr)
  type( field_type ), intent( in )    :: height_w3, height_wth
  type( field_type ), intent( in )    :: tile_sw_direct_albedo
  type( field_type ), intent( in )    :: tile_sw_diffuse_albedo
  type( field_type ), intent( in )    :: tile_lw_albedo
  type( field_collection_type ), intent(inout) :: jules_ancils
  type( field_collection_type ), intent(inout) :: jules_prognostics
  type( field_collection_type ), intent(inout) :: derived_fields
  type( field_collection_type ), intent(inout) :: cloud_fields
  type( field_collection_type ), intent(inout) :: twod_fields
  type( field_collection_type ), intent(inout) :: physics_incs

  real(r_def) :: dt_again

  ! Unpacked fields from collections
  type( field_type ), pointer :: tile_fraction       => null()
  type( field_type ), pointer :: tile_temperature    => null()

  type( field_type ), pointer :: exner_in_wth        => null()
  type( field_type ), pointer :: rho_in_wth          => null()
  type( field_type ), pointer :: theta_in_w3         => null()

  type( field_type ), pointer :: area_fraction       => null()
  type( field_type ), pointer :: liquid_fraction     => null()
  type( field_type ), pointer :: ice_fraction        => null()

  type( field_type ), pointer :: tstar               => null()
  type( field_type ), pointer :: cos_zenith_angle    => null()
  type( field_type ), pointer :: lit_fraction        => null()
  type( field_type ), pointer :: stellar_irradiance  => null()

  type( field_type ), pointer :: lw_down_surf        => null()
  type( field_type ), pointer :: sw_down_surf        => null()
  type( field_type ), pointer :: sw_direct_surf      => null()
  type( field_type ), pointer :: sw_down_blue_surf   => null()
  type( field_type ), pointer :: sw_direct_blue_surf => null()
  type( field_type ), pointer :: lw_up_tile          => null()
  type( field_type ), pointer :: sw_up_tile          => null()
  type( field_type ), pointer :: sw_up_blue_tile     => null()
  type( field_type ), pointer :: sw_heating_rate     => null()
  type( field_type ), pointer :: lw_heating_rate     => null()


  tile_fraction    =>      jules_ancils%get_field('tile_fraction')

  tile_temperature => jules_prognostics%get_field('tile_temperature')
  lw_up_tile       => jules_prognostics%get_field('lw_up_tile')
  sw_up_tile       => jules_prognostics%get_field('sw_up_tile')
  sw_up_blue_tile  => jules_prognostics%get_field('sw_up_blue_tile')

  exner_in_wth        => derived_fields%get_field('exner_in_wth')
  rho_in_wth          => derived_fields%get_field('rho_in_wth')
  theta_in_w3         => derived_fields%get_field('theta_in_w3')

  area_fraction       =>   cloud_fields%get_field('area_fraction')
  liquid_fraction     =>   cloud_fields%get_field('liquid_fraction')
  ice_fraction        =>   cloud_fields%get_field('ice_fraction')

  tstar               =>    twod_fields%get_field('tstar')
  cos_zenith_angle    =>    twod_fields%get_field('cos_zenith_angle')
  lit_fraction        =>    twod_fields%get_field('lit_fraction')
  stellar_irradiance  =>    twod_fields%get_field('stellar_irradiance')

  lw_down_surf        =>   physics_incs%get_field('lw_down_surf')
  sw_down_surf        =>   physics_incs%get_field('sw_down_surf')
  sw_direct_surf      =>   physics_incs%get_field('sw_direct_surf')
  sw_down_blue_surf   =>   physics_incs%get_field('sw_down_blue_surf')
  sw_direct_blue_surf =>   physics_incs%get_field('sw_direct_blue_surf')
  sw_heating_rate     =>   physics_incs%get_field('sw_heating_rate')
  lw_heating_rate     =>   physics_incs%get_field('lw_heating_rate')


  call invoke( sw_kernel_type(                                       &
                 sw_heating_rate, sw_down_surf, sw_direct_surf,      &
                 sw_down_blue_surf, sw_direct_blue_surf,             &
                 sw_up_tile, sw_up_blue_tile,                        &
                 theta, exner, exner_in_wth, rho_in_wth,             &
                 height_w3, height_wth,                              &
                 cos_zenith_angle, lit_fraction, stellar_irradiance, &
                 mr(imr_v), mr(imr_cl), mr(imr_ci),                  &
                 area_fraction, liquid_fraction, ice_fraction,       &
                 tile_fraction,                                      &
                 tile_sw_direct_albedo, tile_sw_diffuse_albedo ) )

  call invoke( lw_kernel_type(                                        &
                 lw_heating_rate, lw_down_surf, lw_up_tile,           &
                 theta, theta_in_w3, exner, exner_in_wth, rho_in_wth, &
                 height_w3, height_wth, tstar,                        &
                 mr(imr_v), mr(imr_cl), mr(imr_ci),                   &
                 area_fraction, liquid_fraction, ice_fraction,        &
                 tile_fraction, tile_temperature,                     &
                 tile_lw_albedo ) )

  ! first calculate the total temperature increment
  dt_again=dt
  call invoke(aX_plus_bY(dtheta_rad, dt,       sw_heating_rate,  &
                                     dt_again, lw_heating_rate), &
  ! then convert it to a theta increment
              inc_X_divideby_Y(dtheta_rad, exner_in_wth) )

end subroutine radiation_alg_step

end module radiation_alg_mod
