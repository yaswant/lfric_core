!-----------------------------------------------------------------------------
! (c) Crown copyright 2023 Met Office. All rights reserved.
! The file LICENCE, distributed with this code, contains details of the terms
! under which the code may be used.
!-----------------------------------------------------------------------------
!> @brief   Initialisation and stepping of the fake nl model for testing
!!
!> @details Handles creation and init of prognostic state fields and
!>          propagation of the fake nl model_data for JEDI-LFRIC interface
!>          testing.
!>
module jedi_lfric_fake_nl_mod

  use constants_mod,                  only : r_def
  use driver_model_data_mod,          only : model_data_type
  use field_collection_mod,           only : field_collection_type
  use field_mod,                      only : field_type
  use fs_continuity_mod,              only : W2, W3, Wtheta
  use log_mod,                        only : log_event,       &
                                             LOG_LEVEL_INFO
  use mesh_mod,                       only : mesh_type
  use jedi_lfric_utils_mod,           only : add_real_field

  use jedi_lfric_increment_alg_mod,   only : jedi_lfric_increment_alg
  !> @todo: Test code should not appear in the component
  !> @{
  use jedi_lfric_tests_config_mod,    only : test_field
  !> @}

  implicit none

  private
  public create_fake_nl_model_data, initialise_fake_nl_model_data, step_fake_nl

  contains

  !> @brief  Create the fake nl DA model model_data
  !>
  !> @param[in]    mesh        Mesh to use for field initialisation
  !> @param[in]    mesh2d      2D Mesh to use for field initialisation
  !> @param[inout] model_data  model_data to store the fake nl DA model
  !!                           fields in
  subroutine create_fake_nl_model_data( mesh, mesh2d, model_data )

    implicit none

    type(mesh_type), intent(in), pointer        :: mesh
    type(mesh_type), intent(in), pointer        :: mesh2d
    type( model_data_type ), intent(inout)      :: model_data

    type(field_collection_type), pointer        :: depository

    call log_event( 'jedi_lfric_tests: Initialising miniapp ...', LOG_LEVEL_INFO )

    call model_data%add_empty_field_collection("depository")
    nullify(depository)
    depository => model_data%get_field_collection("depository")

    ! Create fields and add them to model_data
    call add_real_field( depository, mesh,   Wtheta, "theta"    )
    call add_real_field( depository, mesh,   W3,     "rho"      )
    call add_real_field( depository, mesh2d, W3,     "u10m"     )
    call add_real_field( depository, mesh,   W3,     "exner"    )
    call add_real_field( depository, mesh,   W3,     "u_in_w3"  )
    call add_real_field( depository, mesh,   W3,     "v_in_w3"  )
    call add_real_field( depository, mesh,   Wtheta, "w_in_wth" )
    call add_real_field( depository, mesh,   Wtheta, "m_v"      )
    call add_real_field( depository, mesh,   Wtheta, "m_cl"     )
    call add_real_field( depository, mesh,   Wtheta, "m_r"      )
    call add_real_field( depository, mesh,   Wtheta, "m_ci"     )

    call log_event( 'jedi_lfric_tests: Miniapp initialised', LOG_LEVEL_INFO )

  end subroutine create_fake_nl_model_data

  !> @brief  Initialise the fake nl DA model model_data
  !>
  !> @param[inout] model_data  model_data to initialise
  subroutine initialise_fake_nl_model_data( model_data )

    implicit none

    type( model_data_type ), intent(inout)      :: model_data

    call log_event( 'jedi_lfric_tests: initialise_fake_nl_model_data - start', LOG_LEVEL_INFO )

    ! Initialise fields in model_data
    call initialise_real_field( model_data, "theta" )
    call initialise_real_field( model_data, "rho"   )
    call initialise_real_field( model_data, "u10m"  )

    call log_event( 'jedi_lfric_tests: initialise_fake_nl_model_data - end', LOG_LEVEL_INFO )

  end subroutine initialise_fake_nl_model_data

  !> @brief Performs a single timestep of the fake nl model
  !>
  !> @param [inout] model_data  The model data instance
  subroutine step_fake_nl( model_data )

    implicit none

    type(model_data_type), intent(inout) :: model_data

    type(field_collection_type), pointer :: depository => null()
    type(field_type),            pointer :: working_field => null()

    depository => model_data%get_field_collection("depository")
    call depository%get_field( test_field, working_field )

    call jedi_lfric_increment_alg( working_field )

  end subroutine step_fake_nl

  !> @brief  Initialise real-valued fields to zero
  !>
  !> @param[inout] model_data  The model data object to add a field to
  !> @param[in]    field_name  The name of the new field
  subroutine initialise_real_field(model_data, field_name)
    implicit none

    type(model_data_type), intent(inout)  :: model_data
    character(*),          intent(in)     :: field_name

    type(field_collection_type), pointer  :: depository => null()
    type(field_type),            pointer  :: field_ptr

    ! get field and set equal to 0
    depository => model_data%get_field_collection("depository")
    call depository % get_field(field_name, field_ptr)
    call invoke(setval_c(field_ptr, 0.0_r_def))

  end subroutine initialise_real_field

end module jedi_lfric_fake_nl_mod
