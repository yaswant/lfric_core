!-----------------------------------------------------------------------------
! Copyright (c) 2017,  Met Office, on behalf of HMSO and Queen's Printer
! For further details please refer to the file LICENCE which you
! should have received as part of this distribution.
!-----------------------------------------------------------------------------

!> Test the Wtheta mass columnwise matrix application.
!> For this apply a know matrix both in the columnwise representation
!> in columnwise_operator_type and in a dense representation with matmul()
module columnwise_op_app_kernel_mod_test

  use constants_mod,                 only : i_def, r_solver
  use get_unit_test_m3x3_dofmap_mod, only : get_wtheta_m3x3_dofmap
  use get_unit_test_m3x3_q3x3x3_sizes_mod, &
       only : get_wtheta_m3x3_q3x3x3_size
  use get_unit_test_m3x3_cma_data_mod, &
       only : get_wtheta_m3x3_cma_data
  use funit

  implicit none

  private
  public :: test_all

contains

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  @Test
  subroutine test_all()

    use sci_columnwise_op_app_kernel_mod, only : &
                                                columnwise_op_app_kernel_code

    implicit none

    real(kind=r_solver), parameter :: tol = 1.0e-6_r_solver

    integer(kind=i_def) :: i, cell

    ! Required for calling mass matrix kernel

    integer(kind=i_def) :: ndf, undf, ncells
    integer(kind=i_def) :: unused
    integer(kind=i_def) :: nlayers = 3
    integer(kind=i_def) :: nrow, ncol, bw
    integer(kind=i_def) :: alpha, beta, g_m, g_p

    integer(kind=i_def), allocatable :: map(:,:)
    integer(kind=i_def), allocatable :: indirection_map(:)
    integer(kind=i_def), allocatable :: column_banded_map(:,:)

    real(kind=r_solver), allocatable :: lhs_data(:), rhs_data(:)

    real(kind=r_solver), allocatable    :: lhs_direct(:)
    real(kind=r_solver), allocatable :: columnwise_matrix(:,:,:)

    real(kind=r_solver), allocatable, dimension(:,:) :: densematrix

    call get_wtheta_m3x3_q3x3x3_size(ndf, undf, ncells, unused, unused, unused, unused, nlayers=nlayers)

    cell = 1
    call get_wtheta_m3x3_dofmap( map )

    ! Fill RHS with random data
    allocate(rhs_data(undf), lhs_data(undf) )
    call random_number(rhs_data(:))
    lhs_data(:) = 0.0_r_solver

    call get_wtheta_m3x3_cma_data(ndf, undf, ncells, nlayers, map, &
                                  alpha, beta, g_m, g_p, bw,   &
                                  ncol, nrow, indirection_map, column_banded_map )

    ! Check some sizes for Wtheta-Wtheta ...
    @assertEqual(ndf, 2)
    @assertEqual(alpha, 1)
    @assertEqual(g_m, 1)
    @assertEqual(bw, 3)

    allocate(lhs_direct(nrow))
    lhs_direct(:) = 0.0_r_solver

    ! Construct banded and dense matrix representation,
    ! fill with (scaled) theta mass matrix.
    allocate(densematrix(nrow,ncol))
    densematrix(:,:) = 0.0_r_solver

    allocate( columnwise_matrix( bw, nrow, ncells ) )
    columnwise_matrix(:,:,:) = 0.0_r_solver

    do i = 1, nrow
       if (i == 1) then
          columnwise_matrix(2,i,cell) = 1.0_r_solver/3.0_r_solver
          columnwise_matrix(3,i,cell) = 1.0_r_solver/6.0_r_solver
          densematrix(i,i)   = 1.0_r_solver/3.0_r_solver
          densematrix(i,i+1) = 1.0_r_solver/6.0_r_solver
       else if (i == nrow) then
          columnwise_matrix(1,i,cell) = 1.0_r_solver/6.0_r_solver
          columnwise_matrix(2,i,cell) = 1.0_r_solver/3.0_r_solver
          densematrix(i,i-1) = 1.0_r_solver/6.0_r_solver
          densematrix(i,i)   = 1.0_r_solver/3.0_r_solver
       else
          columnwise_matrix(1,i,cell) = 1.0_r_solver/6.0_r_solver
          columnwise_matrix(2,i,cell) = 2.0_r_solver/3.0_r_solver
          columnwise_matrix(3,i,cell) = 1.0_r_solver/6.0_r_solver
          densematrix(i,i-1) = 1.0_r_solver/6.0_r_solver
          densematrix(i,i)   = 2.0_r_solver/3.0_r_solver
          densematrix(i,i+1) = 1.0_r_solver/6.0_r_solver
       end if
    end do

    call columnwise_op_app_kernel_code(cell,                      &
                                       ncells,                    &
                                       lhs_data,                  &
                                       rhs_data,                  &
                                       columnwise_matrix,         &
                                       nrow,                      &
                                       ncol,                      &
                                       bw,                        &
                                       alpha,                     &
                                       beta,                      &
                                       g_m,                       &
                                       g_p,                       &
                                       ndf, undf, map,            &
                                       indirection_map,           &
                                       ndf, undf, map,            &
                                       indirection_map )

    lhs_direct(1:nrow) &
       = matmul( densematrix, rhs_data(1:ncol) )

    ! Compare the two results
    do i = 1, nrow
       @assertEqual(lhs_direct(i), lhs_data(i), tol)
    end do

    deallocate(densematrix)
    deallocate(lhs_direct)
    deallocate(map)
    deallocate(indirection_map)
    deallocate(column_banded_map)
    deallocate(lhs_data, rhs_data)
    deallocate(columnwise_matrix)

  end subroutine test_all

end module columnwise_op_app_kernel_mod_test
