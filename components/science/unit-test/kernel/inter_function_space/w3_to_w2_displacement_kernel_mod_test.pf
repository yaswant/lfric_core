!-----------------------------------------------------------------------------
! (c) Crown copyright 2024 Met Office. All rights reserved.
! The file LICENCE, distributed with this code, contains details of the terms
! under which the code may be used.
!-----------------------------------------------------------------------------

!> Test the kernel to compute errors in W3 to W2 averaging
module w3_to_w2_displacement_kernel_mod_test

    use constants_mod,         only: i_def, r_def, PI, l_def
    use reference_element_mod, only: S, E, N, W
    use pFUnit_Mod

  implicit none

  private
  public :: set_up, tear_down, test_all

contains

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  @before
  subroutine set_up()

    use base_mesh_config_mod,      only : geometry_spherical, &
                                          topology_fully_periodic
    use extrusion_config_mod,      only : method_uniform, &
                                          stretching_method_linear
    use finite_element_config_mod, only : cellshape_quadrilateral, &
                                          coord_system_native
    use feign_config_mod,          only : feign_base_mesh_config,      &
                                          feign_extrusion_config,      &
                                          feign_finite_element_config, &
                                          feign_planet_config
    use sci_chi_transform_mod,     only : init_chi_transforms

    implicit none

    call feign_base_mesh_config( file_prefix='foo',                &
                                 prime_mesh_name='unit_test',      &
                                 geometry=geometry_spherical,      &
                                 prepartitioned=.false.,           &
                                 topology=topology_fully_periodic, &
                                 fplane=.false., f_lat_deg=0.0_r_def )

    call feign_extrusion_config( method=method_uniform,                      &
                                 planet_radius=1900000.0_r_def,              &
                                 domain_height=10.0_r_def,                   &
                                 number_of_layers=5_i_def,                   &
                                 stretching_method=stretching_method_linear, &
                                 stretching_height=15.0_r_def )

    call feign_finite_element_config(              &
             cellshape=cellshape_quadrilateral,    &
             element_order_h=0_i_def,              &
             element_order_v=0_i_def,              &
             rehabilitate=.true.,                  &
             coord_order = 0_i_def,                &
             coord_system=coord_system_native )

    call feign_planet_config( scaling_factor=1.0_r_def )

    call init_chi_transforms()

  end subroutine set_up

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  @after
  subroutine tear_down()

    use configuration_mod,        only: final_configuration
    use sci_chi_transform_mod,    only: final_chi_transforms

    implicit none

    call final_configuration()
    call final_chi_transforms()

  end subroutine tear_down

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  @test
  subroutine test_all()

    use sci_w3_to_w2_displacement_kernel_mod, only: w3_to_w2_displacement_code
    use get_unit_test_w2hnodal_basis_mod,  only: get_wchi_w2hnodal_basis
    use get_unit_test_w3nodal_basis_mod,   only: get_wchi_w3nodal_basis

    implicit none

    real(r_def),  parameter :: tol = 1.0e-2_r_def
    real(r_def),  parameter :: dalpha = 0.05_r_def
    real(r_def),  parameter :: dbeta = 0.05_r_def
    real(r_def),  parameter :: alpha0 = -PI/4.0_r_def
    real(r_def),  parameter :: beta0 = PI/6.0_r_def
    real(r_def),  parameter :: dz = 2.0_r_def

    ! Non-periodic 3x3x3 domain
    integer(i_def), parameter   :: ncells = 2
    integer(i_def), parameter   :: nlayers = 1
    integer(i_def), parameter   :: ndf_w2h = 4
    integer(i_def), parameter   :: undf_w2h = 7
    integer(i_def), parameter   :: ndf_chi = 8
    integer(i_def), parameter   :: undf_chi = ndf_chi*nlayers*ncells
    integer(i_def), parameter   :: ndf_w3 = 1
    integer(i_def), parameter   :: undf_w3 = ncells*nlayers
    integer(i_def)              :: map_w2h(ndf_w2h,ncells)
    integer(i_def)              :: map_w3(ndf_w3,ncells)
    integer(i_def)              :: map_chi(ndf_chi,ncells)
    real(r_def),    allocatable :: basis_chi_w3(:,:,:)
    real(r_def),    allocatable :: basis_chi_w2h(:,:,:)

    ! Fields
    real(r_def)  :: displacement(undf_w2h)
    real(r_def)  :: chi_1(undf_chi)
    real(r_def)  :: chi_2(undf_chi)
    real(r_def)  :: chi_3(undf_chi)
    real(r_def)  :: panel_id(undf_w3)
    real(r_def)  :: dummy_w3(undf_w3)
    real(r_def)  :: answer, phi

    integer(i_def) :: df_w2h, cell

    ! ------------------------------------------------------------------------ !
    ! Make DoF maps
    ! ------------------------------------------------------------------------ !
    ! Two cells
    map_w3 = reshape([1 , 2], [ndf_w3, ncells])
    map_w2h = reshape([1, 2, 3, 4, 5, 1, 6, 7], [ndf_w2h, ncells])
    map_chi = reshape([1, 2, 3, 4, 5, 6, 7, 8, &
                       9, 10, 11, 12, 13, 14, 15, 16], [ndf_chi, ncells])

    ! ------------------------------------------------------------------------ !
    ! Get basis functions
    ! ------------------------------------------------------------------------ !

    call get_wchi_w3nodal_basis(basis_chi_w3)
    call get_wchi_w2hnodal_basis(basis_chi_w2h)

    ! ------------------------------------------------------------------------ !
    ! Set up initial chi field
    ! ------------------------------------------------------------------------ !

    chi_1 = (/ alpha0, alpha0 + dalpha, alpha0, alpha0 + dalpha, &
               alpha0, alpha0 + dalpha, alpha0, alpha0 + dalpha, &
               beta0, beta0 + dbeta, beta0, beta0 + dbeta,       &
               beta0, beta0 + dbeta, beta0, beta0 + dbeta /)
    chi_2 = (/ beta0, beta0, beta0 + dbeta, beta0 + dbeta,       &
               beta0, beta0, beta0 + dbeta, beta0 + dbeta,       &
               alpha0, alpha0, alpha0 + dalpha, alpha0 + dalpha, &
               alpha0, alpha0, alpha0 + dalpha, alpha0 + dalpha /)
    chi_3 = (/ 0.0_r_def, 0.0_r_def, 0.0_r_def, 0.0_r_def, dz, dz, dz, dz, &
               0.0_r_def, 0.0_r_def, 0.0_r_def, 0.0_r_def, dz, dz, dz, dz /)
    panel_id(1) = 1.0_r_def
    panel_id(2) = 4.0_r_def

    ! ------------------------------------------------------------------------ !
    ! Set up answer
    ! ------------------------------------------------------------------------ !

    ! Approximate angle between alpha and beta coordinates at (alpha0=-pi/4, beta0=pi/6)
    phi = 0.361367_r_def
    answer = 0.5_r_def*dalpha/dbeta*sin(phi)

    ! ------------------------------------------------------------------------ !
    ! Run
    ! ------------------------------------------------------------------------ !

    ! Initialise data
    displacement(:) = 0.0_r_def

    do cell = 1, ncells

      call w3_to_w2_displacement_code( nlayers,                            &
                                       displacement,                       &
                                       chi_1, chi_2, chi_3,                &
                                       panel_id,                           &
                                       dummy_w3,                           &
                                       ndf_w2h, undf_w2h, map_w2h(:,cell), &
                                       ndf_chi, undf_chi, map_chi(:,cell), &
                                       basis_chi_w2h, basis_chi_w3,        &
                                       ndf_w3, undf_w3, map_w3(:,cell),    &
                                       ndf_w3, undf_w3, map_w3(:,cell) )
    end do

    ! ------------------------------------------------------------------------ !
    ! Check
    ! ------------------------------------------------------------------------ !

    ! Only check the first DoF as this is the only shared value between panels
    @assertEqual(answer, displacement(1), tol)

    deallocate(basis_chi_w3)
    deallocate(basis_chi_w2h)

  end subroutine test_all

end module w3_to_w2_displacement_kernel_mod_test
