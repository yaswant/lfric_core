!-----------------------------------------------------------------------------
! (C) Crown copyright 2020 Met Office. All rights reserved.
! The file LICENCE, distributed with this code, contains details of the terms
! under which the code may be used.
!-----------------------------------------------------------------------------

!> Test the time axis implementation
module time_axis_mod_test

  use pFUnit_Mod

  use constants_mod,                  only : i_def, r_def, str_def
  use lfric_xios_time_axis_mod,       only : time_axis_type

  implicit none

  private

  public :: test_time_axis

  @TestCase
  type, extends(TestCase), public :: time_axis_test_type
    private
    type(time_axis_type) :: test_t_ax
  contains
    procedure setUp
    procedure tearDown
    procedure test_time_axis
  end type time_axis_test_type

contains
  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  subroutine setUp( this )

    implicit none

    class(time_axis_test_type), intent(inout) :: this

  end subroutine setUp

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  subroutine tearDown( this )

    implicit none

    class(time_axis_test_type), intent(inout) :: this

  end subroutine tearDown

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  @test
  subroutine test_time_axis( this )

    implicit none

    class(time_axis_test_type), intent(inout) :: this

    ! Set up time axis attributes
    real(r_def),    dimension(6) :: test_data
    integer(i_def), dimension(6) :: test_data_index
    character(str_def)           :: test_name
    character(str_def)           :: test_units

    ! Set up test  variables
    real(r_def) :: time
    real(r_def),    dimension(2) :: t_window
    integer(i_def), dimension(2) :: i_window
    character(str_def)           :: name_output
    character(str_def)           :: unit_output

    test_data = (/ 15.0, 75.0, 135.0, 195.0, 255.0, 315.0 /)
    test_data_index = (/ 1, 2, 3, 4, 5, 6 /)
    test_name = "test_time_axis_name"
    test_units = "hours"

    ! Initialise object
    call this%test_t_ax%initialise( test_data, test_data_index, &
                                    trim(test_name), test_units )

    ! Test name and time units getter
    unit_output = this%test_t_ax%get_units()
    name_output = this%test_t_ax%get_name()

    @assertEqual( test_units, unit_output )
    @assertEqual( test_name, name_output )

    ! Test time window functions
    t_window = this%test_t_ax%get_time_window()
    i_window = this%test_t_ax%get_index_window()
    @assertEqual( t_window, test_data(1:2) )
    @assertEqual( i_window, test_data_index(1:2) )

    ! Test cycle function
    call this%test_t_ax%shift_forward()

    t_window = this%test_t_ax%get_time_window()
    i_window = this%test_t_ax%get_index_window()
    @assertEqual( t_window, test_data(2:3) )
    @assertEqual( i_window, test_data_index(2:3) )

    ! Test align time function
    time = 205.0
    call this%test_t_ax%align(time)
    t_window = this%test_t_ax%get_time_window()

    @assertEqual( t_window, test_data(4:5) )

    time = 0.0
    call this%test_t_ax%align(time)
    t_window = this%test_t_ax%get_time_window()

    @assertEqual( t_window(1), test_data(6) )
    @assertEqual( t_window(2), test_data(1) )

  end subroutine test_time_axis

end module time_axis_mod_test