!-----------------------------------------------------------------------------
! (C) Crown copyright 2025 Met Office. All rights reserved.
! The file LICENCE, distributed with this code, contains details of the terms
! under which the code may be used.
!-----------------------------------------------------------------------------

!> Test creating mesh objects with tiling
!>
module create_mesh_mod_test

  use constants_mod,              only : i_def, r_def, l_def, str_def, &
                                         str_max_filename
  use mesh_collection_mod,        only : mesh_collection_type,         &
                                         mesh_collection
  use global_mesh_collection_mod, only : global_mesh_collection_type,  &
                                         global_mesh_collection
  use local_mesh_collection_mod,  only : local_mesh_collection_type,   &
                                         local_mesh_collection
  use mesh_mod,                   only : mesh_type
  use extrusion_mod,              only : extrusion_type, &
                                         PRIME_EXTRUSION
  use extrusion_config_mod,       only : METHOD_UNIFORM
  use feign_config_mod,           only : feign_multigrid_config, &
                                         feign_partitioning_config
  use create_mesh_mod,            only : create_extrusion, &
                                         create_mesh
  use pfunit

  implicit none

  private
  public :: test_single_cell_tiling

  @TestCase
  type, extends(TestCase), public :: create_mesh_test_type
    private
  contains
    procedure setUp
    procedure tearDown
  end type create_mesh_test_type

contains

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  subroutine setUp( this )

    implicit none

    class(create_mesh_test_type), intent(inout) :: this

    mesh_collection = mesh_collection_type()
    global_mesh_collection = global_mesh_collection_type()
    local_mesh_collection = local_mesh_collection_type()

    call feign_multigrid_config( chain_mesh_tags        = [ 'dummy' ], &
                                 multigrid_chain_nitems = 1_i_def,     &
                                 n_coarsesmooth         = 1_i_def,     &
                                 n_postsmooth           = 1_i_def,     &
                                 n_presmooth            = 1_i_def,     &
                                 smooth_relaxation      = 0.0_r_def )

  end subroutine setUp

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  subroutine tearDown( this )

    implicit none

    class(create_mesh_test_type), intent(inout) :: this

    call mesh_collection%clear()
    call global_mesh_collection%clear()
    call local_mesh_collection%clear()

  end subroutine tearDown

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  subroutine initialise_mesh_collections( filename, mesh_name, xyprocs, &
                                          max_stencil_depth,            &
                                          generate_inner_halos,         &
                                          local_rank, total_ranks )

    use global_mesh_mod,     only : global_mesh_type
    use local_mesh_mod,      only : local_mesh_type
    use ugrid_mesh_data_mod, only : ugrid_mesh_data_type
    use partition_mod,       only : partition_type,        &
                                    partitioner_interface, &
                                    partitioner_planar

    implicit none

    character(len = str_max_filename), intent(in) :: filename
    character(str_def), intent(in)                :: mesh_name
    integer(i_def), intent(in)                    :: xyprocs(2)
    integer(i_def), intent(in)                    :: max_stencil_depth
    logical(l_def), intent(in)                    :: generate_inner_halos
    integer(i_def), intent(in)                    :: local_rank
    integer(i_def), intent(in)                    :: total_ranks
    ! Local variables
    type(ugrid_mesh_data_type)                    :: ugrid_mesh_data
    type(global_mesh_type)                        :: global_mesh
    type(global_mesh_type), pointer               :: global_mesh_ptr
    type(local_mesh_type)                         :: local_mesh
    type(partition_type)                          :: partition
    procedure(partitioner_interface), pointer     :: partitioner_ptr
    integer(i_def)                                :: local_mesh_id

    ! Reset in case of repeated initialisation
    call mesh_collection%clear()
    call global_mesh_collection%clear()
    call local_mesh_collection%clear()

    nullify( global_mesh_ptr )
    nullify( partitioner_ptr )

    ! Read mesh from file and initialise mesh collections
    call ugrid_mesh_data%read_from_file( trim( filename ), mesh_name )
    global_mesh = global_mesh_type( ugrid_mesh_data )
    call ugrid_mesh_data%clear()
    call global_mesh_collection%add_new_global_mesh( global_mesh )
    global_mesh_ptr => global_mesh_collection % get_global_mesh( mesh_name )

    partitioner_ptr => partitioner_planar
    partition = partition_type( global_mesh_ptr,      &
                                partitioner_ptr,      &
                                xyprocs(1),           &
                                xyprocs(2),           &
                                max_stencil_depth,    &
                                generate_inner_halos, &
                                local_rank,           &
                                total_ranks )

    call local_mesh%initialise( global_mesh_ptr, partition )
    call local_mesh%init_cell_owner()
    local_mesh_id = local_mesh_collection%add_new_local_mesh( local_mesh )
    call global_mesh%clear()
    call local_mesh%clear()

  end subroutine initialise_mesh_collections

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  ! Test 1x1 tiling only, which is equivalent to colouring - other tiling
  ! setups require MPI
  @Test
  subroutine test_single_cell_tiling( this )

    use partitioning_config_mod, only : panel_decomposition_auto, &
                                        partitioner_planar

    implicit none

    class(create_mesh_test_type), intent(inout) :: this
    class(extrusion_type),          allocatable :: extrusion
    type(mesh_type), pointer                    :: mesh
    integer(i_def)                              :: xyprocs(2)
    integer(i_def)                              :: max_stencil_depth
    logical(l_def)                              :: generate_inner_halos
    character(str_max_filename), parameter      :: filename = &
                                             'data/mesh_BiP8x8-750x250.nc'
    character(str_def), parameter               :: mesh_name = 'unit_test'
    integer(i_def)                              :: colour
    integer(i_def)                              :: last_cell
    integer(i_def)                              :: last_tile

    xyprocs = 1
    max_stencil_depth = 2
    generate_inner_halos = .true.

    call initialise_mesh_collections( filename , mesh_name, xyprocs,           &
                                      max_stencil_depth, generate_inner_halos, &
                                      0_i_def, 1_i_def)

    call feign_partitioning_config( coarsen_multigrid_tiles   = .false., &
                                    generate_inner_halos      = .true.,  &
                                    inner_halo_tiles          = .false., &
                                    max_tiled_multigrid_level = 1_i_def, &
                                    panel_decomposition       =          &
                                               panel_decomposition_auto, &
                                    panel_xproc               = 1_i_def, &
                                    panel_yproc               = 1_i_def, &
                                    partitioner               =          &
                                                     partitioner_planar, &
                                    tile_size_x               = 1_i_def, &
                                    tile_size_y               = 1_i_def )

    allocate( extrusion, source=create_extrusion( METHOD_UNIFORM,   &
                                                  100.0_r_def,      &
                                                  10000.0_r_def,    &
                                                  10_i_def,         &
                                                  PRIME_EXTRUSION ) )

    ! Check that exactly one mesh is added to the collection
    @assertEqual( mesh_collection%n_meshes(), 0 )
    call create_mesh( mesh_name, extrusion )
    @assertEqual( mesh_collection%n_meshes(), 1 )

    ! Check that mesh name is correct
    nullify( mesh )
    mesh => mesh_collection%get_mesh_by_name( mesh_name )
    @assertTrue( associated( mesh ) )

    ! 1x1 tiling is fully equivalent to colouring - expect same number
    ! of colours and matching numbers of tiles and cells for each colour
    @assertEqual( mesh%get_ncolours(), mesh%get_ntilecolours() )
    do colour = 1, mesh%get_ntilecolours()
      last_cell = mesh%get_last_halo_cell_per_colour_deepest( colour )
      last_tile = mesh%get_last_halo_tile_per_colour_deepest( colour )
      @assertEqual( last_cell, last_tile )
    end do

    deallocate( extrusion )

  end subroutine test_single_cell_tiling

end module create_mesh_mod_test
