!-----------------------------------------------------------------------------
! (c) Crown copyright 2021 Met Office. All rights reserved.
! The file LICENCE, distributed with this code, contains details of the terms
! under which the code may be used.
!-----------------------------------------------------------------------------

!> @brief Reconstruct a W3 field at W2 points for use in the transport scheme.
!> @todo There are number of workarounds in this algorithm for aspects which
!!       aren't yet supported by PSyclone/LFRic:
!!       1. Issue #868 Multidata fields are not natively supported in PSyclone.
!!                     Currently we treat these as normal fields but need to
!!                     additionally pass in ndata_h or ndata_v to the kernels
!!                     as extra integer arguments.
!!       2. Issue #1246 Region stencils require both the global maximum size of
!!                      the stencil and local column size of the stencil.
!!                      PSyclone currently only passes in the local size and so
!!                      until this is resolved we also pass in the global maximum
!!                      size as an integer argument.
module tl_reconstruct_w3_field_alg_mod

  use combine_w2_field_kernel_mod,    only: combine_w2_field_kernel_type
  use constants_mod,                  only: r_def, i_def, l_def
  use field_mod,                      only: field_type
  use finite_element_config_mod,      only: element_order
  use function_space_collection_mod,  only: function_space_collection
  use function_space_mod,             only: function_space_type
  use mesh_mod,                       only: mesh_type
  use fs_continuity_mod,              only: W2, W2h, W2v, W3
  use poly1d_w3_reconstruction_kernel_mod, &
                                      only: poly1d_w3_reconstruction_kernel_type
  use tl_poly1d_vert_w3_reconstruction_kernel_mod, &
                                      only: tl_poly1d_vert_w3_reconstruction_kernel_type
  use poly2d_w3_reconstruction_kernel_mod, &
                                      only: poly2d_w3_reconstruction_kernel_type
  use transport_config_mod,           only: operators,            &
                                            fv_horizontal_order,  &
                                            fv_vertical_order,    &
                                            operators_fv,         &
                                            operators_fem,        &
                                            oned_reconstruction
  use log_mod,                        only: log_event,         &
                                            LOG_LEVEL_ERROR
  use transport_enumerated_types_mod, only: direction_v,               &
                                            direction_h,               &
                                            direction_3d,              &
                                            vertical_monotone_koren,   &
                                            equation_form_consistent
  use transport_metadata_mod,         only: transport_metadata_type
  use transport_runtime_alg_mod,      only: transport_runtime_type
  use transport_runtime_collection_mod, &
                                      only: get_transport_runtime
  use reconstruct_w3_field_alg_mod,   only: get_flux_stencil_extent,         &
                                            get_flux_2d_stencil_size,        &
                                            get_flux_ndata_h,                &
                                            get_flux_ndata_v,                &
                                            get_reversible_flux_ndata_v,     &
                                            get_flux_coeffs,                 &
                                            get_vert_flux_coeffs,            &
                                            get_reversible_vert_flux_coeffs, &
                                            hori_w3_reconstruct_alg

  implicit none

  private

  public :: tl_reconstruct_w3_field_alg
  public :: tl_vert_w3_reconstruct_alg

contains


  !=============================================================================
  !> @brief Reconstruct a W3 field at W2 points for tangent linear app.
  !> @details Reconstruct a W3 field (field_old) at W2 points (field_new) using
  !!          desired spatial reconstruction for tangent linear app.
  !!          Options for this are either FE or FV reconstructions.
  !> @param[in,out] field_new           ACTIVE  Reconstructed field at W2 points
  !> @param[in]     field_old           ACTIVE  Initial W3 field
  !> @param[in]     ls_field_old        PASSIVE Initial W3 field
  !> @param[in]     ls_wind             PASSIVE Advecting wind to determine upwind direction
  !> @param[in]     direction           Splitting direction (h, v, or 3d) to
  !!                                    compute reconstruction
  !> @param[in]     transport_metadata  Contains transport configuration options
  !> @param[in]     final_rk_stage      Whether this is the last Runge-Kutta stage
  subroutine tl_reconstruct_w3_field_alg(field_new, field_old, ls_field_old, &
                                         ls_wind, direction,                 &
                                         transport_metadata, final_rk_stage)

    implicit none

    type(field_type),              intent(in)    :: field_old
    type(field_type),              intent(in)    :: ls_field_old
    type(field_type),              intent(in)    :: ls_wind
    type(field_type),              intent(inout) :: field_new
    integer(kind=i_def),           intent(in)    :: direction
    type(transport_metadata_type), intent(in)    :: transport_metadata
    logical(kind=l_def),           intent(in)    :: final_rk_stage

    type(mesh_type),              pointer :: mesh => null()
    type(field_type),             pointer :: ls_wind_v
    type(field_type),             pointer :: ls_wind_h
    type(field_type)                      :: field_new_h, field_new_v
    type(function_space_type),    pointer :: w2v_fs => null()
    type(function_space_type),    pointer :: w2h_fs => null()
    type(transport_runtime_type), pointer :: transport_runtime => null()

    mesh => field_new%get_mesh()

    select case(operators)

      case default
        call log_event( "Gungho: Unrecognized option for operator.", LOG_LEVEL_ERROR )

      case(operators_fv)

        ! Set default value to be 0 and then update the mass flux
        ! in cases where the wind is nonzero using an upwind reconstruction
        call invoke( setval_c( field_new, 0.0_r_def) )

        select case ( direction )
        case ( direction_3d )
          transport_runtime => get_transport_runtime(mesh)
          if (transport_metadata%get_equation_form() == equation_form_consistent) then
            call log_event('T.L. consistent transport not implemented', LOG_LEVEL_ERROR)
          else
            ls_wind_h => transport_runtime%get_horizontal_advecting_wind(mesh%get_id())
            ls_wind_v => transport_runtime%get_vertical_advecting_wind(mesh%get_id())
          end if

          w2h_fs => function_space_collection%get_fs( mesh, element_order, W2h )
          w2v_fs => function_space_collection%get_fs( mesh, element_order, W2v )

          call field_new_h%initialise( vector_space=w2h_fs )
          call field_new_v%initialise( vector_space=w2v_fs )

          ! Set default value to be 0 and then update the mass flux
          ! in cases where the wind is nonzero using an upwind reconstruction
          call invoke( setval_c(field_new_h, 0.0_r_def), &
                       setval_c(field_new_v, 0.0_r_def) )

          ! Reconstruct W3 field separately in horizontal and vertical W2
          call hori_w3_reconstruct_alg(field_new_h, field_old, ls_wind_h, &
                                       transport_metadata)
          call tl_vert_w3_reconstruct_alg(field_new_v, field_old, ls_wind_v, &
                                          ls_field_old, transport_metadata,  &
                                          final_rk_stage)

          ! Combine horizontal and vertical components of W2 field
          call invoke( combine_w2_field_kernel_type(field_new, field_new_h, field_new_v) )

          nullify( w2h_fs, w2v_fs, ls_wind_h, ls_wind_v, transport_runtime )

        case ( direction_h )
          call hori_w3_reconstruct_alg(field_new, field_old, ls_wind, &
                                       transport_metadata)
        case ( direction_v )
          call tl_vert_w3_reconstruct_alg(field_new, field_old, ls_wind,    &
                                          ls_field_old, transport_metadata, &
                                          final_rk_stage)
        case default
          call log_event('T.L.: W3 reconstruction at W2: direction not recognised', LOG_LEVEL_ERROR)
        end select

      case(operators_fem)
        call log_event( "No Tangent linear for operators_fem", LOG_LEVEL_ERROR )
    end select

    nullify(mesh)

  end subroutine tl_reconstruct_w3_field_alg

  !> @brief Tangent linear reconstruction of a W3 field in W2v
  !> @param[in,out] field_new           The resulting vertical W2 field
  !> @param[in]     field_old           The input W3 field
  !> @param[in]     ls_wind             The linearised vertical wind (in W2v)
  !> @param[in]     ls_field_old        The linearised input W3 field
  !> @param[in]     transport_metadata  Contains transport configuration options
  !> @param[in]     final_rk_stage      Whether this is the last Runge-Kutta stage
  subroutine tl_vert_w3_reconstruct_alg(field_new, field_old, ls_wind,    &
                                        ls_field_old, transport_metadata, &
                                        final_rk_stage)

    implicit none

    type(field_type),              intent(inout) :: field_new
    type(field_type),              intent(in)    :: field_old
    type(field_type),              intent(in)    :: ls_wind
    type(field_type),              intent(in)    :: ls_field_old
    type(transport_metadata_type), intent(in)    :: transport_metadata
    logical(kind=l_def),           intent(in)    :: final_rk_stage

    type(mesh_type),     pointer :: mesh => null()
    integer(kind=i_def)          :: mesh_id
    integer(kind=i_def), pointer :: ndata_v => null()
    type(field_type),    pointer :: vert_flux_coeffs => null()
    integer(kind=i_def)          :: vertical_order
    logical(kind=l_def)          :: logspace
    logical(kind=l_def)          :: reversible
    integer(kind=i_def)          :: monotonicity

    reversible = ( transport_metadata%get_reversible() .and. final_rk_stage )
    logspace = transport_metadata%get_log_space()
    monotonicity = transport_metadata%get_vertical_monotone()

    mesh => field_new%get_mesh()
    mesh_id = mesh%get_id()

    ! Compute vertical components of mass flux
    if ( reversible ) then
      ndata_v => get_reversible_flux_ndata_v(mesh_id)
      vert_flux_coeffs => get_reversible_vert_flux_coeffs(mesh_id)
      vertical_order = fv_vertical_order - 1_i_def
    else
      ndata_v => get_flux_ndata_v(mesh_id)
      vert_flux_coeffs => get_vert_flux_coeffs(mesh_id)
      vertical_order = fv_vertical_order
    end if

    if ( monotonicity == vertical_monotone_koren ) then
      call log_event('T.L. vertical Koren not implemented', LOG_LEVEL_ERROR)

    else
      ! The logspace interpolation makes this different to non-linear kernel
      call invoke( name="W3_1dv_reconstruction",                        &
        tl_poly1d_vert_w3_reconstruction_kernel_type( field_new,        &
                                                      field_old,        &
                                                      ls_wind,          &
                                                      ls_field_old,     &
                                                      vert_flux_coeffs, &
                                                      ndata_v,          &
                                                      vertical_order,   &
                                                      logspace) )
    end if

    nullify(mesh, ndata_v, vert_flux_coeffs)

  end subroutine tl_vert_w3_reconstruct_alg

end module tl_reconstruct_w3_field_alg_mod
