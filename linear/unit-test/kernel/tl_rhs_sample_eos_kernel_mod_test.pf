!-----------------------------------------------------------------------------
! (C) Crown copyright 2021 Met Office. All rights reserved.
! The file LICENCE, distributed with this code, contains details of the terms
! under which the code may be used.
!-----------------------------------------------------------------------------

!>@brief Test the tangent linear computation of the lhs of the EoS for nonlinear equations by
!! sampling
module tl_rhs_sample_eos_kernel_mod_test

  use constants_mod,                 only : i_def, r_def
  use pFUnit_Mod

  implicit none

  private
  public :: tl_rhs_sample_eos_test_type, test_all

  @TestCase
  type, extends(TestCase) :: tl_rhs_sample_eos_test_type
    private
  contains
    procedure setUp
    procedure tearDown
    procedure test_all
  end type tl_rhs_sample_eos_test_type

  real(r_def), parameter :: bvf_square = 0.0001_r_def
  real(r_def), parameter :: theta_surf = 300.0_r_def
  real(r_def), parameter :: pert_width_scaling = 1.0_r_def
  real(r_def), parameter :: pert_centre = 120.0_r_def
  integer(i_def),                                                  &
               parameter :: profile_size       = 4
  real(r_def), parameter :: profile_data(4)    = (/ 280.0_r_def,   &
                                                    300.0_r_def,   &
                                                    305.0_r_def,   &
                                                    400.0_r_def /)
  real(r_def), parameter :: profile_heights(4) = (/ 500.0_r_def,   &
                                                    4.0e3_r_def,   &
                                                    4.0e3_r_def,   &
                                                    40.0e3_r_def /)


  real(r_def), parameter :: gravity  = 10.0_r_def
  real(r_def), parameter :: radius   = 6000000_r_def
  real(r_def), parameter :: omega    = 8.0E-5_r_def
  real(r_def), parameter :: p_zero   = 100000.0_r_def
  real(r_def), parameter :: rd       = 300.0_r_def
  real(r_def), parameter :: cp       = 1000.0_r_def
  real(r_def), parameter :: scaling  = 1.0_r_def
  real(r_def), parameter :: kappa    = rd/cp
contains

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  subroutine setUp( this )

    use base_mesh_config_mod,      only : geometry_planar,    &
                                          topology_fully_periodic
    use finite_element_config_mod, only : cellshape_quadrilateral, &
                                          coord_system_xyz
    use feign_config_mod,          only : feign_base_mesh_config,           &
                                          feign_initial_temperature_config, &
                                          feign_finite_element_config
    use initial_temperature_config_mod, &
                                   only : perturb_none

    implicit none

    class(tl_rhs_sample_eos_test_type), intent(inout) :: this

    call feign_base_mesh_config( filename='foo',                      &
                                 prime_mesh_name='unit_test',         &
                                 geometry=geometry_planar,            &
                                 offline_partitioning=.false.,        &
                                 topology=topology_fully_periodic,    &
                                 fplane=.false., f_lat_deg=0.0_r_def )
    call feign_finite_element_config( cellshape=cellshape_quadrilateral, &
                                      coord_order=0_i_def,               &
                                      coord_system=coord_system_xyz,     &
                                      element_order=0_i_def,             &
                                      rehabilitate=.true.,               &
                                      vorticity_in_w1=.false. )
    call feign_initial_temperature_config( bvf_square=bvf_square,                 &
                                           theta_surf=theta_surf,                 &
                                           pert_centre=pert_centre,               &
                                           pert_width_scaling=pert_width_scaling, &
                                           perturb=perturb_none,                  &
                                           profile_size=profile_size,             &
                                           profile_data=profile_data,             &
                                           profile_heights=profile_heights )

  end subroutine setUp

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  subroutine tearDown( this )

    use configuration_mod,        only: final_configuration

    implicit none

    class(tl_rhs_sample_eos_test_type), intent(inout) :: this

    call final_configuration()

  end subroutine tearDown

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  @test
  subroutine test_all( this )

    use tl_rhs_sample_eos_kernel_mod, only : tl_rhs_sample_eos_code

    use get_unit_test_m3x3_q3x3x3_sizes_mod, only: get_w3_m3x3_q3x3x3_size,    &
                                                   get_wtheta_m3x3_q3x3x3_size

    use get_unit_test_m3x3_dofmap_mod, only: get_w3_m3x3_dofmap,     &
                                             get_wtheta_m3x3_dofmap

    use get_unit_test_q3x3x3_basis_mod,  only: get_w3_q3x3x3_basis
    use get_unit_test_w3nodal_basis_mod, only: get_wtheta_w3nodal_basis

    implicit none

    class(tl_rhs_sample_eos_test_type), intent(inout) :: this

    real(r_def), parameter :: tol = 1.0e-7_r_def
    real(r_def), parameter :: dx = 2.0_r_def, &
                              dy = 2.0_r_def, &
                              dz = 2.5_r_def
    real(r_def)            :: answer

    ! Required for calling kernel
    real(r_def), allocatable :: rhs_eos_data(:)
    real(r_def), allocatable :: exner_data(:)
    real(r_def), allocatable :: rho_data(:)
    real(r_def), allocatable :: theta_data(:)
    real(r_def), allocatable :: moist_dyn_gas_data(:)
    real(r_def), allocatable :: ls_exner_data(:)
    real(r_def), allocatable :: ls_rho_data(:)
    real(r_def), allocatable :: ls_theta_data(:)
    real(r_def), allocatable :: ls_moist_dyn_gas_data(:)

    integer(i_def)            :: nlayers, ncells
    integer(i_def)            :: ndf_wtheta, undf_wtheta
    integer(i_def)            :: ndf_w3, undf_w3
    integer(i_def)            :: dim_space, dim_space_diff
    integer(i_def)            :: nqp_h, nqp_v

    integer(i_def), allocatable :: map_w3(:,:)
    integer(i_def), allocatable :: map_wtheta(:,:)

    real(r_def), allocatable :: basis_w3(:,:,:,:)
    real(r_def), allocatable :: basis_wtheta(:,:,:)

    integer(i_def) :: k
    integer(i_def) :: cell

    ! Use a 3 layer mesh
    nlayers = 3_i_def

    ! Get sizes
    call get_w3_m3x3_q3x3x3_size( ndf_w3, undf_w3, ncells, &
                                  dim_space, dim_space_diff, &
                                  nqp_h, nqp_v, &
                                  nlayers )
    call get_wtheta_m3x3_q3x3x3_size( ndf_wtheta, undf_wtheta, ncells, &
                                      dim_space, dim_space_diff, &
                                      nqp_h, nqp_v, &
                                      nlayers )
   ! Get basis functions
    call get_w3_q3x3x3_basis(basis_w3)
    call get_wtheta_w3nodal_basis(basis_wtheta)

    ! Get dofmaps
    call get_w3_m3x3_dofmap(map_w3)
    call get_wtheta_m3x3_dofmap(map_wtheta)

    ! Create the data arrays
    allocate(rhs_eos_data(undf_w3))
    allocate(exner_data(undf_w3))
    allocate(theta_data(undf_wtheta))
    allocate(moist_dyn_gas_data(undf_wtheta))
    allocate(rho_data(undf_w3))
    allocate(ls_exner_data(undf_w3))
    allocate(ls_theta_data(undf_wtheta))
    allocate(ls_moist_dyn_gas_data(undf_wtheta))
    allocate(ls_rho_data(undf_w3))

    cell = 1_i_def

    ! Initialise the data
    rhs_eos_data(:) = 0.0_r_def
    exner_data(:) = 1.0_r_def
    rho_data(:)   = 1.0_r_def
    theta_data(:) = 1.0_r_def
    moist_dyn_gas_data(:) = 1.0_r_def
    ls_rho_data(:)   = 1.0_r_def
    ls_exner_data(:) = 1.0_r_def
    ls_theta_data(:) = 300.0_r_def
    ls_moist_dyn_gas_data(:) = 1.0_r_def

    call tl_rhs_sample_eos_code( nlayers,               &
                                 rhs_eos_data,          &
                                 exner_data,            &
                                 rho_data,              &
                                 theta_data,            &
                                 moist_dyn_gas_data,    &
                                 ls_exner_data,         &
                                 ls_rho_data,           &
                                 ls_theta_data,         &
                                 ls_moist_dyn_gas_data, &
                                 kappa,                 &
                                 rd,                    &
                                 p_zero,                &
                                 ndf_w3,                &
                                 undf_w3,               &
                                 map_w3(:,cell),        &
                                 basis_w3,              &
                                 ndf_wtheta,            &
                                 undf_wtheta,           &
                                 map_wtheta(:,cell),    &
                                 basis_wtheta )

!     Denoting kappa as K (not vertical layers) k=Rd/Cp = 300/1000=0.3
!     ls_theta_m = ls_theta * ls_moist = 300 *1 = 300
!     theta_m = ls_theta * moist + theta * ls_moist = 300 * 1 + 1* 1 = 301
!     ls_rho = 1
!     rho = 1
!
!
!     ls_rhs_eos = ( -p0/Rd * (ls_exner)** ( (1 - k) / k )/(ls_rho * ls_theta) )
!              = (- ( 100000./300.) * 1.**(0.7/0.3) / ( 1. * 300.) )
!              = -1.11111111111111
!     rhs_eos = - ls_rhs_eos *( ((1-k)/k) * exner/ls_exner - rho / ls_rho - theta / ls_theta )
!           = 1.11111111111111 * ((0.7/0.3)*(1./1.) -  1./1. - 301./300.)
!           = 0.36666666666667


    k = 1
    answer = 0.36666666666667_r_def
    @assertEqual(answer, rhs_eos_data(map_w3(1,cell)+k), tol)

    deallocate(rhs_eos_data)
    deallocate(exner_data)
    deallocate(theta_data)
    deallocate(moist_dyn_gas_data)
    deallocate(rho_data)
    deallocate(ls_exner_data)
    deallocate(ls_theta_data)
    deallocate(ls_moist_dyn_gas_data)
    deallocate(ls_rho_data)
    deallocate(map_w3)
    deallocate(map_wtheta)
    deallocate(basis_w3)
    deallocate(basis_wtheta)

  end subroutine test_all

end module tl_rhs_sample_eos_kernel_mod_test
