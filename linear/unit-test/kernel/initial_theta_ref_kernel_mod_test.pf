!-----------------------------------------------------------------------------
! (C) Crown copyright 2022 Met Office. All rights reserved.
! The file LICENCE, distributed with this code, contains details of the terms
! under which the code may be used.
!-----------------------------------------------------------------------------

!>@ brief Test the reference temperature vertical profile.
module initial_theta_ref_kernel_mod_test

  use constants_mod,                       only : i_def, r_def
  use get_unit_test_m3x3_q3x3x3_sizes_mod, only : get_w0_m3x3_q3x3x3_size,     &
                                                  get_wtheta_m3x3_q3x3x3_size, &
                                                  get_w3_m3x3_q3x3x3_size
  use get_unit_test_m3x3_dofmap_mod,       only : get_w0_m3x3_dofmap,          &
                                                  get_wtheta_m3x3_dofmap,      &
                                                  get_w3_m3x3_dofmap
  use get_unit_test_wthetanodal_basis_mod, only : get_w0_wthetanodal_basis
  use get_unit_test_3x3x3_chi_mod,         only : get_w0_3x3x3_field
  use pFUnit_Mod

  implicit none

  private
  public :: initial_theta_ref_test_type, test_all

  @TestCase
  type, extends(TestCase) :: initial_theta_ref_test_type
    private
  contains
    procedure setUp
    procedure tearDown
    procedure test_all
  end type initial_theta_ref_test_type

  real(kind=r_def), parameter :: gravity       = 10.0_r_def
  real(kind=r_def), parameter :: planet_radius = 14_r_def
  real(kind=r_def), parameter :: omega         = 8.0E-5_r_def
  real(kind=r_def), parameter :: p_zero        = 100000.0_r_def
  real(kind=r_def), parameter :: rd            = 300.0_r_def
  real(kind=r_def), parameter :: cp            = 1000.0_r_def
  real(kind=r_def), parameter :: scaling       = 1.0_r_def

  real(kind=r_def), parameter :: bvf_square = 0.0001_r_def
  real(kind=r_def), parameter :: theta_surf = 300.0_r_def
  real(kind=r_def), parameter :: pert_width_scaling = 1.0_r_def
  real(kind=r_def), parameter :: pert_centre = 120.0_r_def

  real(kind=r_def), parameter :: surface_pressure = 1000.0e2_r_def

  integer(kind=i_def),                                                  &
                    parameter :: profile_size       = 4
  real(kind=r_def), parameter :: profile_data(4)    = (/ 280.0_r_def,   &
                                                         300.0_r_def,   &
                                                         305.0_r_def,   &
                                                         400.0_r_def /)
  real(kind=r_def), parameter :: profile_heights(4) = (/ 500.0_r_def,   &
                                                         4.0e3_r_def,   &
                                                         4.0e3_r_def,   &
                                                         40.0e3_r_def /)

contains

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  subroutine setUp( this )

    use base_mesh_config_mod,        only : geometry_planar,               &
                                            topology_fully_periodic
    use chi_transform_mod,           only : init_chi_transforms
    use finite_element_config_mod,   only : cellshape_quadrilateral,       &
                                            coord_system_xyz
    use initial_pressure_config_mod, only : method_balanced
    use initial_temperature_config_mod, &
                                     only : perturb_none

    use feign_config_mod,            only : feign_finite_element_config,   &
                                            feign_base_mesh_config,        &
                                            feign_idealised_config,        &
                                            feign_planet_config,           &
                                            feign_initial_pressure_config, &
                                            feign_initial_temperature_config

    implicit none

    class(initial_theta_ref_test_type), intent(inout) :: this

    call feign_base_mesh_config( filename='foo',                   &
                                 prime_mesh_name='unit_test',      &
                                 geometry=geometry_planar,         &
                                 offline_partitioning=.false.,     &
                                 topology=topology_fully_periodic, &
                                 fplane=.false., f_lat_deg=0.0_r_def )

    call feign_finite_element_config(           &
             cellshape=cellshape_quadrilateral, &
             coord_order=0_i_def,               &
             coord_system=coord_system_xyz,     &
             element_order=0_i_def,             &
             rehabilitate=.true.,               &
             vorticity_in_w1=.false. )

    call feign_planet_config(                   &
             gravity=gravity,                   &
             radius=planet_radius,              &
             omega=omega,                       &
             rd=rd,                             &
             cp=cp,                             &
             p_zero=p_zero,                     &
             scaling_factor=scaling )

    call feign_initial_pressure_config( method=method_balanced, &
                                        surface_pressure=surface_pressure )

    call feign_initial_temperature_config( bvf_square=bvf_square,                 &
                                           theta_surf=theta_surf,                 &
                                           pert_centre=pert_centre,               &
                                           pert_width_scaling=pert_width_scaling, &
                                           perturb=perturb_none,                  &
                                           profile_size=profile_size,             &
                                           profile_data=profile_data,             &
                                           profile_heights=profile_heights )

    call init_chi_transforms()

  end subroutine setUp

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  subroutine tearDown( this )

    use configuration_mod, only: final_configuration
    use chi_transform_mod, only: final_chi_transforms

    implicit none

    class(initial_theta_ref_test_type), intent(inout) :: this

    call final_configuration()
    call final_chi_transforms()

  end subroutine tearDown

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  @Test
  subroutine test_all( this )

    use initial_theta_ref_kernel_mod, only : initial_theta_ref_code
    use idealised_config_mod,         only : test_gravity_wave

    implicit none

    class(initial_theta_ref_test_type), intent(inout) :: this

    real(kind=r_def), parameter :: tol = 1.0e-12_r_def
    real(kind=r_def), parameter :: dx = 6000.0_r_def
    real(kind=r_def), parameter :: dy = 1000.0_r_def
    real(kind=r_def), parameter :: dz = 2000.0_r_def

    ! Fields
    real(kind=r_def), allocatable :: theta(:)
    real(kind=r_def), allocatable :: chi1(:), chi2(:), chi3(:)
    real(kind=r_def), allocatable :: panel_id(:)

    real(kind=r_def) :: answer

    integer(kind=i_def) :: i, j, k, cell

    integer(kind=i_def) :: nlayers, ncells
    integer(kind=i_def) :: ndf_pid, undf_pid
    integer(kind=i_def) :: ndf_w0, undf_w0
    integer(kind=i_def) :: ndf_wtheta, undf_wtheta
    integer(kind=i_def) :: nqp_h, nqp_v
    integer(kind=i_def) :: dim_space, dim_space_diff

    ! Dofmaps
    integer(kind=i_def), allocatable :: map_w0(:,:)
    integer(kind=i_def), allocatable :: map_wtheta(:,:)
    integer(kind=i_def), allocatable :: map_pid(:,:)

    ! Basis functions
    real(kind=r_def),    allocatable :: basis_w0(:,:,:)

    nlayers = 3
    call get_w0_m3x3_q3x3x3_size( ndf_w0, undf_w0, ncells,   &
                                  dim_space, dim_space_diff, &
                                  nqp_h, nqp_v,              &
                                  nlayers )
    call get_wtheta_m3x3_q3x3x3_size( ndf_wtheta, undf_wtheta, ncells, &
                                      dim_space, dim_space_diff,       &
                                      nqp_h, nqp_v,                    &
                                      nlayers )
    call get_w3_m3x3_q3x3x3_size( ndf_pid, undf_pid, ncells, &
                                  dim_space, dim_space_diff, &
                                  nqp_h, nqp_v, 1 )

    call get_w0_m3x3_dofmap(map_w0)
    call get_wtheta_m3x3_dofmap(map_wtheta)
    call get_w3_m3x3_dofmap(map_pid, 1)

    ! Get canned basis functions
    call get_w0_wthetanodal_basis(basis_w0)

    ! Compute coordinates
    allocate(chi1(undf_w0))
    allocate(chi2(undf_w0))
    allocate(chi3(undf_w0))

    allocate(panel_id(undf_pid))
    panel_id(:) = 1.0_r_def
    call get_w0_3x3x3_field(chi1, chi2, chi3, dx, dy, dz, &
                            map_w0, nlayers)

    cell = 1

    ! Create the data
    allocate( theta( undf_wtheta ) )
    theta(:) = 0.0_r_def

    call initial_theta_ref_code( nlayers,             &
                                 theta,               &
                                 chi1,                &
                                 chi2,                &
                                 chi3,                &
                                 panel_id,            &
                                 test_gravity_wave,   &
                                 ndf_wtheta,          &
                                 undf_wtheta,         &
                                 map_wtheta(:, cell), &
                                 ndf_w0,              &
                                 undf_w0,             &
                                 map_w0(:, cell),     &
                                 basis_w0,            &
                                 ndf_pid,             &
                                 undf_pid,            &
                                 map_pid(:, cell)     &
                                 )


    ! theta = Ts * exp ( z * bvf_square/gravity )
    ! For k = 1, z = 2000,
    ! theta = 300 * exp ( 2000 * 0.0001/10 ) = 306.060402008

    k = 1
    answer = 306.06040200802676_r_def
    @assertEqual(answer, theta(map_wtheta(1, cell)+k), tol )

    deallocate( theta )
    deallocate( chi1 )
    deallocate( chi2 )
    deallocate( chi3 )
    deallocate( panel_id )
    deallocate( map_w0 )
    deallocate( map_wtheta )
    deallocate( basis_w0 )

  end subroutine test_all

end module initial_theta_ref_kernel_mod_test
