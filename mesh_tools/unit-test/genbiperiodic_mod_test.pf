!-----------------------------------------------------------------------------
! Copyright (c) 2017,  Met Office, on behalf of HMSO and Queen's Printer
! For further details please refer to the file LICENCE.original which you
! should have received as part of this distribution.
!-----------------------------------------------------------------------------
!> @brief     Tests for src/dynamo/ugrid/genbiperiodic_mod.F90
!>
!> @details   [1]  Constructor can be called with program default args.
!>            [2]  Constructor fails with invalid args.
!>            [3]  Type generates mesh with appropriate dimensions for args.
!>            [4]  Mesh contains correct values at fixed points for fixed args.
!>            [5]  Mesh connectivity has correct dimensions.
!>            [6]  Mesh connectivity has correct values at fixed points.
!>            [7]  Mesh coords have correct values at fixed points.
!>
module genbiperiodic_mod_test

  use constants_mod,     only: i_def, r_def, str_def, str_long
  use genbiperiodic_mod, only: genbiperiodic_type
  use pFUnit_Mod

  implicit none

  private
  public :: test_all

  @TestCase
  type, extends(TestCase), public :: genbiperiodic_test_type
    private
    integer(i_def), allocatable :: faces_on_face(:,:)
    integer(i_def), allocatable :: nodes_on_face(:,:)
    integer(i_def), allocatable :: edges_on_face(:,:)
    integer(i_def), allocatable :: nodes_on_edge(:,:)
    real(r_def),    allocatable :: coords(:,:)
  contains
    procedure setUp
    procedure tearDown
    procedure test_all
  end type genbiperiodic_test_type

  character(str_def), parameter :: mesh_name = 'unit_test'
  integer(i_def),     parameter :: nx = 5
  integer(i_def),     parameter :: ny = 4

contains

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  subroutine setUp( this )

    implicit none

    class(genbiperiodic_test_type), intent(inout) :: this

    allocate( this%faces_on_face(4, nx*ny) )
    allocate( this%nodes_on_face(4, nx*ny) )
    allocate( this%edges_on_face(4, nx*ny) )
    allocate( this%nodes_on_edge(2, 2*nx*ny) )
    allocate( this%coords(2, nx*ny) )

  end subroutine setUp

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  subroutine tearDown( this )

    implicit none

    class(genbiperiodic_test_type), intent(inout) :: this

    deallocate( this%faces_on_face )
    deallocate( this%nodes_on_face )
    deallocate( this%edges_on_face )
    deallocate( this%nodes_on_edge )
    deallocate( this%coords )

  end subroutine tearDown

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  @test
  subroutine test_all( this )

    implicit none

    class(genbiperiodic_test_type), intent(inout) :: this

    type(genbiperiodic_type) :: bpgen
    integer(i_def)           :: nodes, edges, faces
    integer(i_def)           :: nodes_per_face, edges_per_face
    integer(i_def)           :: nodes_per_edge, max_faces_per_node

    character(str_def)  :: test_str1, test_str2
    character(str_long) :: test_str3

    bpgen = genbiperiodic_type(mesh_name, nx, ny)

    ! Test metadata retrieval
    call bpgen%get_metadata( mesh_name        = test_str1, &
                             mesh_class       = test_str2, &
                             generator_inputs = test_str3 )
    @assertEqual( mesh_name, trim(test_str1) )
    @assertEqual( 'plane', trim(test_str2) )
    @assertEqual( 'cells_in_x=5;cells_in_y=4;cell_width=6000.00;cell_height=2000.00', trim(test_str3) )

    ! Generate the mesh and connectivity
    call bpgen%generate()

    ! Retrieve calculated dimensions
    call bpgen%get_dimensions(nodes, edges, faces, nodes_per_face, &
                              edges_per_face, nodes_per_edge)

    ! Mesh has correct dimensions for arguments
    @assertEqual(nodes, nx*ny, "Incorrect number of vertices for mesh dimensions")
    @assertEqual(edges, 2*nx*ny, "Incorrect number of edges for mesh dimensions")
    @assertEqual(faces, nx*ny, "Incorrect number of faces for mesh dimensions")

    ! Mesh has correct element dimensions
    @assertEqual(edges_per_face, 4, "Number of edges per face does not correspond to quad mesh")
    @assertEqual(nodes_per_edge, 2, "Number of vertices per edge does not correspond to quad mesh")

    ! Retrieve mesh connectivity
    call bpgen%get_connectivity(this%nodes_on_face, this%nodes_on_edge, &
                                this%edges_on_face, this%faces_on_face)

    ! Mesh has expected vertex values on certain faces
    @assertEqual(this%nodes_on_face(:,1), (/ 1,2,3,4 /), "Incorrect vertex sequence on faces.")
    @assertEqual(this%nodes_on_face(:,20), (/ 9,4,17,20 /), "Incorrect vertex sequence on faces.")

    ! Mesh connectivity is complete
    @assertTrue(minval(this%faces_on_face, 2) /= 0, "Incomplete faces_on_face connectivity.")
    @assertTrue(minval(this%nodes_on_face, 2) /= 0, "Incomplete nodes_on_face connectivity.")
    @assertTrue(minval(this%edges_on_face, 2) /= 0, "Incomplete edges_on_face connectivity.")
    @assertTrue(minval(this%nodes_on_edge, 2) /= 0, "Incomplete nodes_on_edge connectivity.")

    ! Mesh connectivity has expected values at certain points
    @assertEqual(this%faces_on_face(:,1), (/ 5,6,2,16 /), "Incorrect faces_on_face connectivity.")
    @assertEqual(this%faces_on_face(:,11), (/ 15,16,12,6 /), "Incorrect faces_on_face connectivity.")

    @assertEqual(this%edges_on_face(:,1), (/ 14,3,2,1 /), "Incorrect edges_on_face connectivity.")
    @assertEqual(this%edges_on_face(:,20), (/ 39,13,40,35 /), "Incorrect edges_on_face connectivity.")

    @assertEqual(this%nodes_on_edge(:,1), (/ 4,3 /), "Incorrect nodes_on_edge connectivity.")
    @assertEqual(this%nodes_on_edge(:,40), (/ 17,4 /), "Incorrect nodes_on_edge connectivity.")

    ! Retrieve mesh coordinates
    call bpgen%get_coordinates(this%coords)

    ! Mesh coordinates have expected values at certain points
    @assertTrue(this%coords(:,1) == (/ -12000.0_r_def, 4000.0_r_def /), "Incorrect mesh coordinates.")
    @assertTrue(this%coords(:,20) == (/ 12000.0_r_def, 0.0_r_def /), "Incorrect mesh coordinates.")

  end subroutine test_all

end module genbiperiodic_mod_test
