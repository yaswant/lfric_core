!-------------------------------------------------------------------------------
! (c) Crown copyright 2017 Met Office. All rights reserved.
! The file LICENCE, distributed with this code, contains details of the terms
! under which the code may be used.
!-------------------------------------------------------------------------------
!> @brief Interface to the PC2 cloud scheme

module pc2_initiation_alg_mod

   use field_mod,               only: field_type
   use field_collection_mod,    only: field_collection_type

   use geometric_constants_mod, only: get_height
   use fs_continuity_mod,       only: Wtheta

   implicit none

   private
   public pc2_initiation_alg

contains
  !>@brief Run pc2_initiation
  !>@details This algorithm calls the PC2 initiation kernel which assesses the
  !>         current model state fields. If certain conditions are met, the
  !>         kernel will create cloud from clear sky or break up an overcast
  !>         scene.
  !>
  !>         Moisture and Cloud fields are updated accordingly and
  !>         PC2 increments to potential temperature returned.
  !>
  !>         See UMDP30 for full scheme details.
  !>@param[in,out] mr                (in,out) Current Mixing ratios
  !>@param[in]     theta             (in)     Current Potential temperature
  !>@param[in]     exner_w3          (in)     Current value of enxer in w3 space
  !>@param[in]     exner_wth         (in)     Current value of exner in wtheta
  !>@param[in]     mr_n              (in)     Start of timestep mixing ratios
  !>@param[in]     theta_n           (in)     Start of timestep pot temp
  !>@param[in]     derived_fields    (in)     Group of derived fields
  !>@param[in]     turbulence_fields (in)     Fields for turbulence scheme
  !>@param[in]     cloud_fields      (in,out) Fields for cloud scheme
  !>@param[in,out] dtheta_pc2_inc    (in,out) Potential temperature response

  subroutine pc2_initiation_alg( mr,                &
                                 theta,             &
                                 exner_w3,          &
                                 exner_wth,         &
                                 mr_n,              &
                                 theta_n,           &
                                 derived_fields,    &
                                 turbulence_fields, &
                                 cloud_fields,      &
                                 dtheta_pc2_inc     )

    use pc2_initiation_kernel_mod, only: pc2_initiation_kernel_type
    use mr_indices_mod,            only: imr_v, imr_cl, imr_ci, nummr
    use io_config_mod,             only: subroutine_timers
    use timer_mod,                 only: timer

    implicit none

    type( field_type ), intent( in )    :: theta
    type( field_type ), intent( in )    :: exner_w3
    type( field_type ), intent( inout ) :: mr   ( nummr )
    type( field_type ), intent( in )    :: mr_n ( nummr ), theta_n
    type( field_type ), intent( in )    :: exner_wth

    type( field_collection_type ), intent(in) :: derived_fields
    type( field_collection_type ), intent(in) :: turbulence_fields
    type( field_collection_type ), intent(in) :: cloud_fields

    type( field_type ), intent(inout) :: dtheta_pc2_inc
    type( field_type )                :: dqv_pc2_inc
    type( field_type )                :: dqcl_pc2_inc
    type( field_type )                :: dqcf_pc2_inc
    type( field_type )                :: dcfl_pc2_inc
    type( field_type )                :: dcff_pc2_inc
    type( field_type )                :: dbcf_pc2_inc

    ! Temporary unpacked fields from collections
    type( field_type ), pointer :: exner_n_wth  => null()

    ! For PC2
    type( field_type ), pointer :: liquid_fraction => null()
    type( field_type ), pointer :: ice_fraction    => null()
    type( field_type ), pointer :: bulk_fraction   => null()
    type( field_type ), pointer :: area_fraction   => null()
    type( field_type ), pointer :: rh_crit         => null()

    type( field_type ), pointer :: dsldzm     => null()
    type( field_type ), pointer :: wvar       => null()
    type( field_type ), pointer :: tau_dec_bm => null()
    type( field_type ), pointer :: tau_hom_bm => null()
    type( field_type ), pointer :: tau_mph_bm => null()

    type( field_type ), pointer :: sskew_bm   => null()
    type( field_type ), pointer :: svar_bm    => null()
    type( field_type ), pointer :: svar_tb    => null()


    type( field_type ), pointer :: zlcl_mixed      => null()
    type( field_type ), pointer :: r_cumulus       => null()

    type( field_type ), pointer :: height_wth => null()

    if ( subroutine_timers ) call timer("pc2_initiation_alg")

    ! Unpack fields
    exner_n_wth     => derived_fields%get_field('exner_in_wth')

    bulk_fraction   => cloud_fields%get_field('bulk_fraction')
    liquid_fraction => cloud_fields%get_field('liquid_fraction')
    ice_fraction    => cloud_fields%get_field('ice_fraction')
    area_fraction   => cloud_fields%get_field('area_fraction')
    rh_crit         => cloud_fields%get_field('rh_crit')

    tau_dec_bm      => cloud_fields%get_field('tau_dec_bm')
    tau_hom_bm      => cloud_fields%get_field('tau_hom_bm')
    tau_mph_bm      => cloud_fields%get_field('tau_mph_bm')

    sskew_bm        => cloud_fields%get_field('sskew_bm')
    svar_bm         => cloud_fields%get_field('svar_bm')
    svar_tb         => cloud_fields%get_field('svar_tb')

    zlcl_mixed      => turbulence_fields%get_field('z_lcl')
    r_cumulus       => turbulence_fields%get_field('cumulus')

    dsldzm          => turbulence_fields%get_field('dsldzm')
    wvar            => turbulence_fields%get_field('wvar')

    height_wth      => get_height(Wtheta, theta%get_mesh_id())

    call theta%copy_field_properties(dqv_pc2_inc)
    call theta%copy_field_properties(dqcl_pc2_inc)
    call theta%copy_field_properties(dqcf_pc2_inc)
    call theta%copy_field_properties(dcfl_pc2_inc)
    call theta%copy_field_properties(dcff_pc2_inc)
    call theta%copy_field_properties(dbcf_pc2_inc)

    call invoke( name="update_from_pc2_initiation",              &
                 pc2_initiation_kernel_type ( mr(imr_v),         &
                                              mr(imr_cl),        &
                                              mr(imr_ci),        &
                                              liquid_fraction,   &
                                              ice_fraction,      &
                                              bulk_fraction,     &
                                              theta,             &
                                              exner_wth,         &
                                              exner_w3,          &
                                              dsldzm,            &
                                              wvar,              &
                                              tau_dec_bm,        &
                                              tau_hom_bm,        &
                                              tau_mph_bm,        &
                      ! Next 4 are start of timestep qv, qcl, theta
                      ! and exner to find RHt at start of timestep
                                              mr_n(imr_v),       &
                                              mr_n(imr_cl),      &
                                              theta_n,           &
                                              exner_n_wth,       &
                                              zlcl_mixed,        &
                                              r_cumulus,         &
                                              height_wth,        &
                                                ! Responses
                                              dtheta_pc2_inc,    &
                                              dqv_pc2_inc,       &
                                              dqcl_pc2_inc,      &
                                              dqcf_pc2_inc,      &
                                              dcfl_pc2_inc,      &
                                              dcff_pc2_inc,      &
                                              dbcf_pc2_inc,      &
                                              sskew_bm,          &
                                              svar_bm,           &
                                              svar_tb,           &
                                              rh_crit       ) ,  &
                                                                 !
                ! Update all field (apart from theta)
                ! with increments from initiation
                                                                 !
                inc_X_plus_Y(mr(imr_v),       dqv_pc2_inc),      &
                inc_X_plus_Y(mr(imr_cl),      dqcl_pc2_inc),     &
                inc_X_plus_Y(mr(imr_ci),      dqcf_pc2_inc),     &
                inc_X_plus_Y(liquid_fraction, dcfl_pc2_inc),     &
                inc_X_plus_Y(ice_fraction,    dcff_pc2_inc),     &
                inc_X_plus_Y(bulk_fraction,   dbcf_pc2_inc),     &
                ! area_fraction = bulk_fraction
                setval_X    (area_fraction,   bulk_fraction)     )

    if ( subroutine_timers ) call timer("pc2_initiation_alg")

  end subroutine pc2_initiation_alg

end module pc2_initiation_alg_mod
