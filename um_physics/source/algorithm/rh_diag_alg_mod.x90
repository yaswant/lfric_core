!-------------------------------------------------------------------------------
! (c) Crown copyright 2021 Met Office. All rights reserved.
! The file LICENCE, distributed with this code, contains details of the terms
! under which the code may be used.
!-------------------------------------------------------------------------------
!> @brief Holds code to calculate and output RH diagnostic

module rh_diag_alg_mod

  use field_mod,               only: field_type
  use io_config_mod,           only: use_xios_io
  use constants_mod,           only: r_def, l_def
  use initialise_diagnostics_mod, only: init_diag => init_diagnostic_field
  use mr_indices_mod,          only : nummr, imr_v

  implicit none
  private

  public :: rh_diag_alg

contains

  !> @details Output diagnostics of RH over water and ice
  !> @param[in]  exner       exner pressure in wth space
  !> @param[in]  theta       potential temperature
  !> @param[in]  mr          Mixing ratios

  subroutine rh_diag_alg(exner, theta, mr)

    use msat_kernel_mod, only: msat_kernel_type

    implicit none

    ! Arguments

    type( field_type ), intent(in) :: exner
    type( field_type ), intent(in) :: theta
    type( field_type ), intent(in) :: mr(nummr)

    ! Define RH field
    type( field_type ) :: rh_water, rh_ice, msat_water, msat_ice
    logical(l_def) :: water_only

    if (init_diag(rh_water, 'processed__rh_water') .and. use_xios_io) then

      call rh_water%copy_field_properties(msat_water)

      ! Calculate RH over water
      water_only = .true.
      call invoke(msat_kernel_type(msat_water, exner, theta, water_only), &
                  X_divideby_Y(rh_water, mr(imr_v), msat_water),          &
                  inc_a_times_X(100.0_r_def, rh_water) )

      ! Output diagnostic
      call rh_water%write_field(rh_water%get_name())

    end if

    if (init_diag(rh_ice, 'processed__rh_ice') .and. use_xios_io) then

      call rh_ice%copy_field_properties(msat_ice)

      ! Calculate RH over ice
      water_only = .false.
      call invoke(msat_kernel_type(msat_ice, exner, theta, water_only), &
                  X_divideby_Y(rh_ice, mr(imr_v), msat_ice),            &
                  inc_a_times_X(100.0_r_def, rh_ice) )

      ! Output diagnostic
      call rh_ice%write_field(rh_ice%get_name())

    end if

  end subroutine rh_diag_alg

end module rh_diag_alg_mod
