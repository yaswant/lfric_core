!-----------------------------------------------------------------------------
! Copyright (c) 2017,  Met Office, on behalf of HMSO and Queen's Printer
! For further details please refer to the file LICENCE.original which you
! should have received as part of this distribution.
!-----------------------------------------------------------------------------

!> @brief pFunit tests for the mesh map module
!>
module mesh_map_mod_test

  use constants_mod,        only: r_def, i_def, str_max_filename, str_def
  use extrusion_config_mod, only: extrusion_method_uniform
  use global_mesh_mod,      only: global_mesh_type
  use global_mesh_map_mod,  only: global_mesh_map_type
  use global_mesh_map_collection_mod,  only: global_mesh_map_collection_type
  use partition_mod,        only: partition_type, partitioner_interface, &
                                  partitioner_cubedsphere_serial
  use mesh_mod,             only: mesh_type
  use mesh_map_mod,         only: mesh_map_type
  use mesh_collection_mod,  only: mesh_collection_type, mesh_collection
  use global_mesh_collection_mod, only: global_mesh_collection_type, &
                                        global_mesh_collection
  use pFUnit_Mod                                            


  implicit none

  private
  public :: mesh_map_test_type,      &
            test_coarse_to_fine_map, &
            test_fine_to_coarse_map
  
  @testCase
  type, extends( TestCase ) :: mesh_map_test_type
    private


    type(global_mesh_type), pointer :: coarse_global_mesh => null()
    type(global_mesh_type), pointer :: fine_global_mesh   => null()
    type(mesh_type),        pointer :: coarse_mesh => null()
    type(mesh_type),        pointer :: fine_mesh   => null()

    integer(i_def), allocatable :: coarse_lid_gid_map(:)
    integer(i_def), allocatable :: fine_lid_gid_map(:)
    
  contains
    procedure :: setUp
    procedure :: tearDown
    procedure :: test_coarse_to_fine_map
    procedure :: test_fine_to_coarse_map
  end type mesh_map_test_type

  character(str_def), parameter :: mesh_name = 'unit_test'
  character(str_max_filename), parameter :: filenames(2) = &
                                    [ 'data/mesh_C4.nc ',  &! 4x4 panel
                                      'data/mesh_C8.nc ']   ! 8x8 panel

  integer(i_def), parameter :: npanels     = 6_i_def
  integer(i_def), parameter :: local_rank  = 0_i_def
  integer(i_def), parameter :: total_ranks = 1_i_def
  integer(i_def), parameter :: max_stencil_depth = 0_i_def
  integer(i_def), parameter :: xproc       = 1_i_def
  integer(i_def), parameter :: yproc       = 1_i_def
  integer(i_def), parameter :: nlayers     = 5_i_def
  real(r_def),    parameter :: domain_top  = 10000.0_r_def

  real(r_def), parameter :: gravity = 10.0_r_def
  real(r_def), parameter :: radius  = 6371229.0_r_def
  real(r_def), parameter :: omega   = 8.0E-5_r_def
  real(r_def), parameter :: p_zero  = 100000.0_r_def
  real(r_def), parameter :: rd      = 300.0_r_def
  real(r_def), parameter :: cp      = 1000.0_r_def
  real(r_def), parameter :: scaling = 125.0_r_def

  integer(i_def), parameter :: CoarseToFineMap(4,96) = reshape( &
      [   1,    2,   9,    10,            3,    4,   11,   12,  &
          5,    6,   13,   14,            7,    8,   15,   16,  &
         17,   18,   25,   26,           19,   20,   27,   28,  &
         21,   22,   29,   30,           23,   24,   31,   32,  &
         33,   34,   41,   42,           35,   36,   43,   44,  &
         37,   38,   45,   46,           39,   40,   47,   48,  &
         49,   50,   57,   58,           51,   52,   59,   60,  &
         53,   54,   61,   62,           55,   56,   63,   64,  &
         65,   66,   73,   74,           67,   68,   75,   76,  &
         69,   70,   77,   78,           71,   72,   79,   80,  &
         81,   82,   89,   90,           83,   84,   91,   92,  &
         85,   86,   93,   94,           87,   88,   95,   96,  &
         97,   98,  105,  106,           99,  100,  107,  108,  &
        101,  102,  109,  110,          103,  104,  111,  112,  &
        113,  114,  121,  122,          115,  116,  123,  124,  &
        117,  118,  125,  126,          119,  120,  127,  128,  &
        129,  130,  137,  138,          131,  132,  139,  140,  &
        133,  134,  141,  142,          135,  136,  143,  144,  &
        145,  146,  153,  154,          147,  148,  155,  156,  &
        149,  150,  157,  158,          151,  152,  159,  160,  &
        161,  162,  169,  170,          163,  164,  171,  172,  &
        165,  166,  173,  174,          167,  168,  175,  176,  &
        177,  178,  185,  186,          179,  180,  187,  188,  &
        181,  182,  189,  190,          183,  184,  191,  192,  &
        193,  194,  201,  202,          195,  196,  203,  204,  &
        197,  198,  205,  206,          199,  200,  207,  208,  &
        209,  210,  217,  218,          211,  212,  219,  220,  &
        213,  214,  221,  222,          215,  216,  223,  224,  &
        225,  226,  233,  234,          227,  228,  235,  236,  &
        229,  230,  237,  238,          231,  232,  239,  240,  &
        241,  242,  249,  250,          243,  244,  251,  252,  &
        245,  246,  253,  254,          247,  248,  255,  256,  &
        257,  258,  265,  266,          259,  260,  267,  268,  &
        261,  262,  269,  270,          263,  264,  271,  272,  &
        273,  274,  281,  282,          275,  276,  283,  284,  &
        277,  278,  285,  286,          279,  280,  287,  288,  &
        289,  290,  297,  298,          291,  292,  299,  300,  &
        293,  294,  301,  302,          295,  296,  303,  304,  &
        305,  306,  313,  314,          307,  308,  315,  316,  &
        309,  310,  317,  318,          311,  312,  319,  320,  &
        321,  322,  329,  330,          323,  324,  331,  332,  &
        325,  326,  333,  334,          327,  328,  335,  336,  &
        337,  338,  345,  346,          339,  340,  347,  348,  &
        341,  342,  349,  350,          343,  344,  351,  352,  &
        353,  354,  361,  362,          355,  356,  363,  364,  &
        357,  358,  365,  366,          359,  360,  367,  368,  &
        369,  370,  377,  378,          371,  372,  379,  380,  &
        373,  374,  381,  382,          375,  376,  383,  384 ], (/4,96/))

  integer(i_def), parameter :: FineToCoarseMap(1,384) = reshape(              &
    [ 1,  1,  2,  2,  3,  3,  4,  4,  1,  1,  2,  2,  3,  3,  4,  4,  5,  5,  &
      6,  6,  7,  7,  8,  8,  5,  5,  6,  6,  7,  7,  8,  8,  9,  9,  10, 10, &
      11, 11, 12, 12, 9,  9,  10, 10, 11, 11, 12, 12, 13, 13, 14, 14, 15, 15, &
      16, 16, 13, 13, 14, 14, 15, 15, 16, 16, 17, 17, 18, 18, 19, 19, 20, 20, &
      17, 17, 18, 18, 19, 19, 20, 20, 21, 21, 22, 22, 23, 23, 24, 24, 21, 21, &
      22, 22, 23, 23, 24, 24, 25, 25, 26, 26, 27, 27, 28, 28, 25, 25, 26, 26, &
      27, 27, 28, 28, 29, 29, 30, 30, 31, 31, 32, 32, 29, 29, 30, 30, 31, 31, &
      32, 32, 33, 33, 34, 34, 35, 35, 36, 36, 33, 33, 34, 34, 35, 35, 36, 36, &
      37, 37, 38, 38, 39, 39, 40, 40, 37, 37, 38, 38, 39, 39, 40, 40, 41, 41, &
      42, 42, 43, 43, 44, 44, 41, 41, 42, 42, 43, 43, 44, 44, 45, 45, 46, 46, &
      47, 47, 48, 48, 45, 45, 46, 46, 47, 47, 48, 48, 49, 49, 50, 50, 51, 51, &
      52, 52, 49, 49, 50, 50, 51, 51, 52, 52, 53, 53, 54, 54, 55, 55, 56, 56, &
      53, 53, 54, 54, 55, 55, 56, 56, 57, 57, 58, 58, 59, 59, 60, 60, 57, 57, &
      58, 58, 59, 59, 60, 60, 61, 61, 62, 62, 63, 63, 64, 64, 61, 61, 62, 62, &
      63, 63, 64, 64, 65, 65, 66, 66, 67, 67, 68, 68, 65, 65, 66, 66, 67, 67, &
      68, 68, 69, 69, 70, 70, 71, 71, 72, 72, 69, 69, 70, 70, 71, 71, 72, 72, &
      73, 73, 74, 74, 75, 75, 76, 76, 73, 73, 74, 74, 75, 75, 76, 76, 77, 77, &
      78, 78, 79, 79, 80, 80, 77, 77, 78, 78, 79, 79, 80, 80, 81, 81, 82, 82, &
      83, 83, 84, 84, 81, 81, 82, 82, 83, 83, 84, 84, 85, 85, 86, 86, 87, 87, &
      88, 88, 85, 85, 86, 86, 87, 87, 88, 88, 89, 89, 90, 90, 91, 91, 92, 92, &
      89, 89, 90, 90, 91, 91, 92, 92, 93, 93, 94, 94, 95, 95, 96, 96, 93, 93, &
      94, 94, 95, 95, 96, 96 ], (/1,384/) )

contains

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  subroutine setUp( this )

    use base_mesh_config_mod,      only : base_mesh_geometry_spherical, &
                                          base_mesh_partitioner_cubedsphere
    use finite_element_config_mod, only : finite_element_cellshape_quadrilateral
    use gungho_feign_config_mod,   only : feign_base_mesh_config, &
                                          feign_planet_config,    &
                                          feign_extrusion_config
    use reference_element_mod,     only : reference_cube, reference_element

    implicit none

    class( mesh_map_test_type ), intent( inout ) :: this

    type(global_mesh_type), pointer :: global_mesh => null()
    type(partition_type)   :: partition
    integer(i_def)         :: mesh_id

    integer(i_def)         :: mesh_B_id
    
    integer(i_def)         :: ncells
    integer(i_def)         :: i,j
    integer(i_def)         :: global_mesh_id
    integer(i_def)         :: npanels


    procedure (partitioner_interface), pointer :: partitioner_ptr => null()

    call feign_base_mesh_config                               &
             ( filename='foo', prime_mesh_name=mesh_name,     &
               geometry=base_mesh_geometry_spherical,         &
               partitioner=base_mesh_partitioner_cubedsphere, &
               fplane=.false., f_lat_deg=0.0_r_def )

    call feign_extrusion_config( extrusion_method_uniform, domain_top, nlayers )

    call feign_planet_config( gravity=gravity, radius=radius, omega=omega, &
                              rd=rd, cp=cp, p_zero=p_zero,                 &
                              scaling_factor=scaling )

    reference_element = finite_element_cellshape_quadrilateral
    call reference_cube()

    partitioner_ptr => partitioner_cubedsphere_serial

    global_mesh_collection = global_mesh_collection_type()
    mesh_collection        = mesh_collection_type()

    npanels = 6

    ! Loop over the files
    do i=1, size(filenames)

      ! Get the global mesh
      global_mesh_id =  global_mesh_collection %             &
                          add_new_global_mesh( filenames(i), &
                                               mesh_name, npanels )
      global_mesh    => global_mesh_collection % &
                          get_global_mesh( global_mesh_id )

      ! Set pointers to the coarse and fine global meshes
      if (i==1) then
        this%coarse_global_mesh => global_mesh_collection % &
                                     get_global_mesh( global_mesh_id )
      else
        this%fine_global_mesh   => global_mesh_collection % &
                                     get_global_mesh( global_mesh_id )
      end if

      ! Generate the local 3d meshes
      partition = partition_type( global_mesh, partitioner_ptr,    &
                                  xproc, yproc, max_stencil_depth, &
                                  local_rank, total_ranks )

      mesh_id = mesh_collection%add_new_mesh( global_mesh, partition, &
                                              nlayers, domain_top,    &
                                              extrusion_method_uniform )

      ncells = partition%get_num_cells_in_layer()

      ! Set pointers to the coarse and fine local 3d meshes
      ! and generate to lid to gid maps
      if ( i==1 ) then

        this%coarse_mesh => mesh_collection%get_mesh( mesh_id )
        allocate(this%coarse_lid_gid_map(ncells))
        do j=1, ncells
          this%coarse_lid_gid_map(j) = partition%get_gid_from_lid(j)
        end do

      else

        this%fine_mesh   => mesh_collection%get_mesh( mesh_id )
        allocate(this%fine_lid_gid_map(ncells))
        do j=1, ncells
          this%fine_lid_gid_map(j) = partition%get_gid_from_lid(j)
        end do

      end if
    end do

  end subroutine setUp

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  subroutine tearDown( this )

    use reference_element_mod, only : deallocate_reference

    implicit none

    class( mesh_map_test_type ), intent( inout ) :: this

    call mesh_collection%clear()
    call global_mesh_collection%clear()

    call deallocate_reference()

  end subroutine tearDown

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  @test
  subroutine test_coarse_to_fine_map( this )

    implicit none

    class( mesh_map_test_type ), intent( inout ) :: this

    type (mesh_map_type) :: mesh_map

    integer(i_def) :: test_integer
    integer(i_def), allocatable :: test_1d_array(:)
    integer(i_def), allocatable :: test_2d_array(:,:)
    integer(i_def), allocatable :: data(:,:)

    integer(i_def), allocatable :: map(:,:)

    integer(i_def), pointer :: test_1d_array_ptr(:)
    integer(i_def), pointer :: test_2d_array_ptr(:,:)
    integer(i_def) :: nsource_cells
    integer(i_def) :: ntarget_cells_per_source_cell
    integer(i_def) :: fine_global_mesh_id

    type(global_mesh_map_type), pointer :: CoarseToFine_global_mesh_map => null()


    fine_global_mesh_id = this%fine_global_mesh%get_id()

    CoarseToFine_global_mesh_map => &
         this%coarse_global_mesh%get_global_mesh_map(fine_global_mesh_id)

    ntarget_cells_per_source_cell = &
        CoarseToFine_global_mesh_map%get_ntarget_cells_per_source_cell()
    nsource_cells                 = &
        CoarseToFine_global_mesh_map%get_nsource_cells()
    allocate(map(ntarget_cells_per_source_cell,nsource_cells))

    map(:,:) = CoarseToFineMap(:,:) 
    mesh_map = mesh_map_type( this%coarse_mesh%get_id(), &
                              this%fine_mesh%get_id(),   &
                              map )


    !-------------------------------------------------------------------
    ! Test get_source_id
    test_integer = mesh_map%get_source_id()
    @assertEqual ( this%coarse_mesh%get_id(), test_integer )

    !-------------------------------------------------------------------
    ! Test get_target_id
    test_integer = mesh_map%get_target_id()
    @assertEqual ( this%fine_mesh%get_id(), test_integer )

    !-------------------------------------------------------------------
    ! Test get_cell_factor
    test_integer = mesh_map%get_ntarget_cells_per_source_cell()
    @assertEqual ( 4, test_integer )

    !-------------------------------------------------------------------
    ! Test get_nsource_cells
    test_integer = mesh_map%get_nsource_cells()
    @assertEqual ( 96, test_integer )

    !-------------------------------------------------------------------
    ! Test get_map_from_cell
    test_1d_array_ptr => mesh_map%get_cell_map(11)
    @assertEqual ( [37,38,45,46], test_1d_array_ptr )
    nullify(test_1d_array_ptr)


    !-------------------------------------------------------------------
    ! Test get_full_map
    test_2d_array_ptr => mesh_map%get_whole_cell_map()
    @assertEqual ( CoarseToFineMap, test_2d_array_ptr )
    nullify(test_2d_array_ptr)
    deallocate(map)

    call mesh_map%clear()

  end subroutine test_coarse_to_fine_map



  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  @test
  subroutine test_fine_to_coarse_map( this )

    implicit none

    class( mesh_map_test_type ), intent( inout ) :: this

    type (mesh_map_type) :: mesh_map

    integer(i_def) :: test_integer
    integer(i_def), allocatable :: test_1d_array(:)
    integer(i_def), allocatable :: test_2d_array(:,:)
    integer(i_def), allocatable :: data(:,:)

    integer(i_def), pointer :: test_1d_array_ptr(:)
    integer(i_def), pointer :: test_2d_array_ptr(:,:)
    integer(i_def) :: coarse_global_mesh_id
    integer(i_def), allocatable :: map(:,:)
    integer(i_def) :: nsource_cells
    integer(i_def) :: ntarget_cells_per_source_cell

    type(global_mesh_map_type), pointer :: FineToCoarse_global_mesh_map => null()

    coarse_global_mesh_id =  this%coarse_global_mesh%get_id()

    FineToCoarse_global_mesh_map => &
         this%fine_global_mesh%get_global_mesh_map(coarse_global_mesh_id)

    ntarget_cells_per_source_cell = &
        FineToCoarse_global_mesh_map%get_ntarget_cells_per_source_cell()
    nsource_cells                 = &
        FineToCoarse_global_mesh_map%get_nsource_cells()
    allocate(map(ntarget_cells_per_source_cell,nsource_cells))

    map(:,:) = FineToCoarseMap(:,:) 
    mesh_map = mesh_map_type( this%fine_mesh%get_id(),   &
                              this%coarse_mesh%get_id(), &
                              map )

    !-------------------------------------------------------------------
    ! Test get_source_id
    test_integer = mesh_map%get_source_id()
    @assertEqual ( this%fine_mesh%get_id(), test_integer )

    !-------------------------------------------------------------------
    ! Test get_target_id
    test_integer = mesh_map%get_target_id()
    @assertEqual ( this%coarse_mesh%get_id(), test_integer )

    !-------------------------------------------------------------------
    ! Test get_cell_factor
    test_integer = mesh_map%get_ntarget_cells_per_source_cell()
    @assertEqual ( 1, test_integer )

    !-------------------------------------------------------------------
    ! Test get_nsource_cells
    test_integer = mesh_map%get_nsource_cells()
    @assertEqual ( 384, test_integer )

    !-------------------------------------------------------------------
    ! Test get_map_from_cell
    test_1d_array_ptr => mesh_map%get_cell_map(11)
    @assertEqual ( [2], test_1d_array_ptr )
    nullify(test_1d_array_ptr)


    !-------------------------------------------------------------------
    ! Test get_full_map
    test_2d_array_ptr => mesh_map%get_whole_cell_map()
    @assertEqual ( FineToCoarseMap, test_2d_array_ptr )
    nullify(test_2d_array_ptr)

    call mesh_map%clear()

  end subroutine test_fine_to_coarse_map

end module mesh_map_mod_test
