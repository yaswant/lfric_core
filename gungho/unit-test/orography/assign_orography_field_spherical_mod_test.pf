!-----------------------------------------------------------------------
! (C) Crown copyright 2017 Met Office. All rights reserved.
! For further details please refer to the file COPYRIGHT.txt
! which you should have received as part of this distribution.
!-----------------------------------------------------------------------
!> @brief Test suite to verify assignment of orography profile to spherical
!>        coordinates using Witch-of-Agnesi mountain.
!-------------------------------------------------------------------------------
module assign_orography_field_spherical_mod_test

  use constants_mod,              only : i_def, r_def, PI, str_def, &
                                         str_max_filename
  use global_mesh_collection_mod, only : global_mesh_collection_type, &
                                         global_mesh_collection
  use global_mesh_mod,            only : global_mesh_type
  use mesh_collection_mod,        only : mesh_collection_type, &
                                         mesh_collection
  use analytic_orography_mod,     only : orography_type

  use pFUnit_Mod

  implicit none

  private
  public :: setUp, tearDown, test_assign_orography_field_spherical

  @testCase
  type, public, extends(TestCase) :: assign_orography_field_spherical_test_type
    real(r_def) :: mountain_height
    real(r_def) :: half_width 
    real(r_def) :: lambda_centre_dec
    real(r_def) :: phi_centre_dec
    real(r_def) :: lambda_focus_dec
    real(r_def) :: phi_focus_dec
  contains
    procedure     :: setUp
    procedure     :: tearDown
    procedure     :: test_assign_orography_field_spherical
  end type assign_orography_field_spherical_test_type

  character(str_def), parameter   :: mesh_name     = 'unit_test'
  character(len=str_max_filename) :: filename      = "data/mesh_C16.nc"
  real(r_def),        parameter   :: domain_top    = 10000.0_r_def
  integer(i_def),     parameter   :: npanels       = 6_i_def
  integer(i_def),     parameter   :: nlayers       = 10_i_def
  integer(i_def),     parameter   :: element_order = 0
  integer(i_def),     parameter   :: total_ranks   = 1
  integer(i_def),     parameter   :: local_rank    = 0
  integer(i_def),     parameter   :: xproc         = 1
  integer(i_def),     parameter   :: yproc         = 1

  real(r_def), parameter :: gravity = 10.0_r_def
  real(r_def), parameter :: radius  = 6371229.0_r_def
  real(r_def), parameter :: omega   = 8.0E-5_r_def
  real(r_def), parameter :: p_zero  = 100000.0_r_def
  real(r_def), parameter :: rd      = 300.0_r_def
  real(r_def), parameter :: cp      = 1000.0_r_def
  real(r_def), parameter :: scaling = 125.0_r_def
 
contains

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  subroutine setUp(this)

    use reference_element_mod,          only : reference_cube, reference_element
    use agnesi_orography_spherical_mod, only : agnesi_spherical_type
    use finite_element_config_mod,      only : finite_element_cellshape_quadrilateral
    use base_mesh_config_mod,           only : base_mesh_geometry_spherical, &
                                               base_mesh_partitioner_cubedsphere
    use gungho_feign_config_mod,        only :                  &
                                        feign_base_mesh_config, &
                                        feign_planet_config,    &
                                        feign_orography_agnesi_spherical_config
    use orography_agnesi_spherical_config_mod, only :              &
                                        lambda_centre, phi_centre, &
                                        lambda_focus, phi_focus

    implicit none

    class(assign_orography_field_spherical_test_type), intent(inout) :: this

    reference_element = finite_element_cellshape_quadrilateral
    call reference_cube()

    ! Create top level collections
    global_mesh_collection = global_mesh_collection_type()
    mesh_collection = mesh_collection_type()

    ! Set mesh configuration
    call feign_base_mesh_config(                                              &
                         filename        = filename,                          & 
                         prime_mesh_name = mesh_name,                         &
                         geometry        = base_mesh_geometry_spherical,      &
                         partitioner     = base_mesh_partitioner_cubedsphere, &
                         fplane          = .false.,                           &
                         f_lat_deg       = 0.0_r_def )

    ! Set planet parameters
    call feign_planet_config( gravity        = gravity, &
                              radius         = radius,  &
                              omega          = omega,   & 
                              rd             = rd,      &
                              cp             = cp,      &  
                              p_zero         = p_zero,  &
                              scaling_factor = scaling )

    ! Set Witch-of-Agnesi spherical configuration
    this%mountain_height   = 100.0_r_def
    this%half_width        = 2500.0_r_def
    this%lambda_centre_dec = 1.5_r_def
    this%phi_centre_dec    = 0.0_r_def
    this%lambda_focus_dec  = 1.5_r_def
    this%phi_focus_dec     = 0.25_r_def
    call feign_orography_agnesi_spherical_config(                           &
                                mountain_height   = this%mountain_height,   &
                                half_width        = this%half_width,        &
                                lambda_centre_dec = this%lambda_centre_dec, &
                                phi_centre_dec    = this%phi_centre_dec,    &
                                lambda_focus_dec  = this%lambda_focus_dec,  &
                                phi_focus_dec     = this%phi_focus_dec )

    ! Initialise Witch-of-Agnesi spherical orography type
    allocate( orography_type, source = agnesi_spherical_type(       &
                                              this%mountain_height, &
                                              this%half_width,      &
                                              lambda_centre,        &
                                              phi_centre,           &
                                              lambda_focus,         &
                                              phi_focus ) )

  end subroutine setUp

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  subroutine tearDown(this)

    use reference_element_mod, only : deallocate_reference
    implicit none

    class(assign_orography_field_spherical_test_type), intent(inout) :: this

    call deallocate_reference()
    call global_mesh_collection%clear()
    call mesh_collection%clear()

  end subroutine tearDown

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  @test
  subroutine test_assign_orography_field_spherical(this)

    use mesh_mod,                       only : mesh_type
    use partition_mod,                  only : partition_type,        &
                                               partitioner_interface, &
                                               partitioner_cubedsphere_serial
    use extrusion_config_mod,           only : extrusion_method_uniform
    use planet_config_mod,              only : scaled_radius
    use assign_orography_field_mod,     only : assign_orography_spherical
    use coord_transform_mod,            only : xyz2llr, llr2xyz
    use orography_helper_functions_mod, only : eta2z_linear

    implicit none
    ! Arguments
    class(assign_orography_field_spherical_test_type), intent(inout) :: this
    ! Mesh and partition types
    integer(i_def)                            :: global_mesh_id
    integer(i_def)                            :: mesh_id
    type(global_mesh_type),           pointer :: global_mesh => null()
    type(mesh_type),                  pointer :: mesh => null()
    type(partition_type)                      :: partition
    procedure(partitioner_interface), pointer :: partitioner_ptr => null()
    ! Test parameters
    integer(i_def), parameter :: ndf = 1, undf = nlayers*ndf
    integer(i_def)            :: itest, i, map(1)
    real(r_def), parameter    :: tol = 1.0e-3_r_def
    real(r_def)               :: height_flat_surface, height_domain_top
    real(r_def)               :: chi_1(undf), chi_2(undf), chi_3(undf)
    real(r_def)               :: chi_1_cart(undf), chi_2_cart(undf), chi_3_cart(undf)
    real(r_def)               :: eta(0:nlayers)
    real(r_def)               :: vertex_coords_2d(3,1), chi_surf, chi_test

    ! Add global mesh
    global_mesh_id =  global_mesh_collection%add_new_global_mesh( filename,  &
                                                                  mesh_name, &
                                                                  npanels )
    global_mesh    => global_mesh_collection%get_global_mesh( global_mesh_id )

    ! Assign partitioner
    partitioner_ptr => partitioner_cubedsphere_serial
    partition = partition_type( global_mesh,     &
                                partitioner_ptr, &
                                xproc,           &
                                yproc,           &
                                1,               &
                                local_rank,      &
                                total_ranks )

    ! Construct a mesh which is  uniform in vertical for purpose of the test
    mesh_id = mesh_collection%add_new_mesh( global_mesh, &
                                            partition,   &
                                            nlayers,     &
                                            domain_top,  &
                                            extrusion_method_uniform )
    mesh => mesh_collection%get_mesh( mesh_id )

    ! Set map for chi coordinates
    map(1)  = 1

    ! Test assigning orography for selected number of points
    i = 905
    call global_mesh%get_vert_coords(i,vertex_coords_2d(:,1))
    ! Create horizontal chi_1 and chi_2 fields from mesh coordinates
    chi_1 = vertex_coords_2d(1,1)
    chi_2 = vertex_coords_2d(2,1)
    ! Get physical height of flat domain surface (scaled_radius in spherical
    ! coordinates)
    height_flat_surface = scaled_radius
    ! Get domain top
    height_domain_top = domain_top + height_flat_surface
    ! Create uniform chi_3 field
    call mesh%get_eta(eta) 
    do i = 1, undf
      chi_3(i) = eta2z_linear(eta(i-1), height_flat_surface, height_domain_top)
    end do
    ! Transform coordinate field to (x, y, z) as it is currently in the model
    do i = 1, undf
      call llr2xyz(chi_1(i), chi_2(i), chi_3(i), &
                   chi_1_cart(i), chi_2_cart(i), chi_3_cart(i))
    end do
    ! Test spherical orography assignment
    call assign_orography_spherical( nlayers, ndf, undf, map, &
                                     height_flat_surface,     &
                                     height_domain_top,       &
                                     chi_1_cart, chi_2_cart, chi_3_cart )
    ! Transform coordinate field back to (long, lat, r) for comparison
    do i = 1, undf
      call xyz2llr(chi_1_cart(i), chi_2_cart(i), chi_3_cart(i), &
                   chi_1(i), chi_2(i), chi_3(i))
    end do

    ! Test selected chi_3
    itest = 1
    chi_surf = chi_3(itest)
    chi_test = this%mountain_height + height_flat_surface
    @assertEqual( chi_surf, chi_test, tol )

    itest = 5
    chi_surf = chi_3(itest)
    chi_test = 55029.832_r_def
    @assertEqual( chi_surf, chi_test, tol )

    return
  end subroutine test_assign_orography_field_spherical

end module assign_orography_field_spherical_mod_test
