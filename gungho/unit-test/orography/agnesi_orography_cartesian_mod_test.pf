!-----------------------------------------------------------------------
! (C) Crown copyright 2017 Met Office. All rights reserved.
! For further details please refer to the file COPYRIGHT.txt
! which you should have received as part of this distribution.
!-----------------------------------------------------------------------
!> @brief Test suite to verify calculation of Witch-of-Agnesi mountain orography 
!>        profile in Cartesian coordinates.
!-------------------------------------------------------------------------------
module agnesi_orography_cartesian_mod_test

  use constants_mod,              only : i_def, r_def, str_def, str_max_filename
  use global_mesh_collection_mod, only : global_mesh_collection_type, &
                                         global_mesh_collection
  use global_mesh_mod,            only : global_mesh_type
  use mesh_collection_mod,        only : mesh_collection_type, &
                                         mesh_collection
  use analytic_orography_mod,     only : orography_type

  use pFUnit_Mod

  implicit none

  private
  public :: setUp, tearDown, test_agnesi_orography_cartesian

  @testCase
  type, public, extends(TestCase) :: agnesi_orography_cartesian_test_type
    real(r_def)         :: mountain_height
    real(r_def)         :: half_width_x
    real(r_def)         :: half_width_y
    real(r_def)         :: x_centre
    real(r_def)         :: y_centre
    integer(kind=i_def) :: direction
  contains
    procedure     :: setUp
    procedure     :: tearDown
    procedure     :: test_agnesi_orography_cartesian
  end type agnesi_orography_cartesian_test_type

  character(str_def), parameter   :: mesh_name     = 'unit_test'
  character(len=str_max_filename) :: filename      = "data/mesh_BiP40x40-5x5.nc"
  real(r_def),        parameter   :: domain_top    = 1000.0_r_def
  integer(i_def),     parameter   :: npanels       = 1_i_def
  integer(i_def),     parameter   :: nlayers       = 1_i_def
  integer(i_def),     parameter   :: element_order = 0
  integer(i_def),     parameter   :: total_ranks   = 1
  integer(i_def),     parameter   :: local_rank    = 0
  integer(i_def),     parameter   :: xproc         = 1
  integer(i_def),     parameter   :: yproc         = 1

contains

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  subroutine setUp(this)

    use reference_element_mod,          only : reference_cube, reference_element
    use agnesi_orography_cartesian_mod, only : agnesi_cartesian_type
    use finite_element_config_mod,      only : finite_element_cellshape_quadrilateral
    use base_mesh_config_mod,           only : base_mesh_geometry_planar, &
                                               base_mesh_partitioner_planar
    use gungho_feign_config_mod,        only :                    &
                                        feign_base_mesh_config,   &
                                        feign_domain_size_config, &
                                        feign_orography_agnesi_cartesian_config
    use orography_agnesi_cartesian_config_mod, only : &
                                        orography_agnesi_cartesian_direction_x

    implicit none

    class(agnesi_orography_cartesian_test_type), intent(inout) :: this

    reference_element = finite_element_cellshape_quadrilateral
    call reference_cube()

    ! Create top level collections
    global_mesh_collection = global_mesh_collection_type()
    mesh_collection = mesh_collection_type()

    ! Set mesh configuration
    call feign_base_mesh_config(                                         &
                         filename        = filename,                     &
                         prime_mesh_name = mesh_name,                    &
                         geometry        = base_mesh_geometry_planar,    &
                         partitioner     = base_mesh_partitioner_planar, &
                         fplane          = .false.,                      &
                         f_lat_deg       = 0.0_r_def )

    ! Set domain size
    call feign_domain_size_config( -100.0_r_def, 100.0_r_def, &
                                   -100.0_r_def, 100.0_r_def )

    ! Set Witch-of-Agnesi Cartesian configuration
    this%mountain_height = 100.0_r_def
    this%half_width_x    = 10.0_r_def
    this%half_width_y    = 10.0_r_def
    this%x_centre        = -10.0_r_def
    this%y_centre        = 20.0_r_def
    this%direction       = int(orography_agnesi_cartesian_direction_x, i_def)
    call feign_orography_agnesi_cartesian_config(      &
               mountain_height = this%mountain_height, &
               half_width_x    = this%half_width_x,    &
               half_width_y    = this%half_width_y,    &
               x_centre        = this%x_centre,        &
               y_centre        = this%y_centre,        &
               direction       = orography_agnesi_cartesian_direction_x )

    ! Initialise Witch-of-Agnesi Cartesian orography type
    allocate( orography_type, source = agnesi_cartesian_type(       &
                                              this%mountain_height, &
                                              this%half_width_x,    &
                                              this%half_width_y,    &
                                              this%x_centre,        &
                                              this%y_centre,        &
                                              this%direction ) )

  end subroutine setUp

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  subroutine tearDown(this)

    use reference_element_mod, only : deallocate_reference
    implicit none

    class(agnesi_orography_cartesian_test_type), intent(inout) :: this

    call deallocate_reference()
    call global_mesh_collection%clear()
    call mesh_collection%clear()

  end subroutine tearDown

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  @test
  subroutine test_agnesi_orography_cartesian(this)

    use mesh_mod,                              only : mesh_type
    use partition_mod,                         only : partition_type,        &
                                                      partitioner_interface, &
                                                      partitioner_biperiodic
    use extrusion_config_mod,                  only : extrusion_method_uniform
    use mesh_constructor_helper_functions_mod, only : domain_size_type
    use orography_helper_functions_mod,        only : calc_domain_size_horizontal

    implicit none
    ! Arguments
    class(agnesi_orography_cartesian_test_type), intent(inout) :: this
    ! Mesh, partition and domain size types
    integer(i_def)                            :: global_mesh_id
    integer(i_def)                            :: mesh_id
    type(global_mesh_type),           pointer :: global_mesh => null()
    type(mesh_type),                  pointer :: mesh => null()
    type(domain_size_type)                    :: domain_size 
    type(partition_type)                      :: partition
    procedure(partitioner_interface), pointer :: partitioner_ptr => null()
    ! Test parameters
    real(r_def), parameter :: tol = 1.0e-3_r_def
    real(r_def)            :: vertex_coords_2d(3,1), chi_surf, chi_test
    integer(i_def)         :: itest

    ! Add global mesh
    global_mesh_id =  global_mesh_collection%add_new_global_mesh( filename,  &
                                                                  mesh_name, &
                                                                  npanels )
    global_mesh    => global_mesh_collection%get_global_mesh( global_mesh_id )

    ! Assign partitioner
    partitioner_ptr => partitioner_biperiodic
    partition = partition_type( global_mesh,     &
                                partitioner_ptr, &
                                xproc,           &
                                yproc,           &
                                1,               &
                                local_rank,      &
                                total_ranks )

    ! Construct a mesh which is uniform in vertical for purpose of the test
    mesh_id = mesh_collection%add_new_mesh( global_mesh, &
                                            partition,   &
                                            nlayers,     &
                                            domain_top,  &
                                            extrusion_method_uniform )
    mesh => mesh_collection%get_mesh( mesh_id )

    ! Get domain size required for the biperiodic coordinate transform
    domain_size = mesh%get_domain_size()
    ! Calculate horizontal domain size from the domain_size object
    call calc_domain_size_horizontal(domain_size%minimum%x, &
                                     domain_size%maximum%x, &
                                     domain_size%minimum%y, &
                                     domain_size%maximum%y)

    ! Calculate surface height for selected number of points
    itest = 699
    call global_mesh%get_vert_coords(itest,vertex_coords_2d(:,1))
    chi_surf = orography_type%analytic_orography(vertex_coords_2d(1,1), &
                                                 vertex_coords_2d(2,1))
    chi_test = this%mountain_height
    @assertEqual( chi_surf, chi_test, tol )

    itest = 541
    call global_mesh%get_vert_coords(itest,vertex_coords_2d(:,1))
    chi_surf = orography_type%analytic_orography(vertex_coords_2d(1,1), &
                                                 vertex_coords_2d(2,1))
    chi_test = 50.0_r_def
    @assertEqual( chi_surf, chi_test, tol )

    return
  end subroutine test_agnesi_orography_cartesian

end module agnesi_orography_cartesian_mod_test
