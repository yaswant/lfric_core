!-----------------------------------------------------------------------------
! Copyright (c) 2017,  Met Office, on behalf of HMSO and Queen's Printer
! For further details please refer to the file LICENCE.original which you
! should have received as part of this distribution.
!-----------------------------------------------------------------------------

!> Test the computation of the rotation component of RHS of momentum equation
!>
module rotation_kernel_mod_test

  use constants_mod,                 only : i_def, r_def, PI
  use function_space_collection_mod, only : function_space_collection_type, &
                                            function_space_collection
  use mesh_collection_mod,           only : mesh_collection_type, &
                                            mesh_collection
  use field_mod,                     only : field_type, &
                                            field_proxy_type
  use fs_continuity_mod,             only : W0, W2
  use function_space_mod,            only : function_space_type
  use pFUnit_Mod
  use quadrature_mod,                only : quadrature_type, GAUSSIAN

  implicit none

  private
  public :: rotation_test_type, test_all

  @TestCase
  type, extends(TestCase) :: rotation_test_type
    private
    type(quadrature_type)              :: qr
    type(function_space_type), pointer :: w0_fs => null()
    type(function_space_type), pointer :: w2_fs => null()
    type(field_type)                   :: chi(3)
    type(field_type)                   :: r_u
    real(r_def), allocatable           :: basis_w2(:,:,:,:), &
                                          basis_w0(:,:,:,:), &
                                          diff_basis_w0(:,:,:,:)
    integer(i_def)                     :: mesh_id
  contains
    procedure setUp
    procedure tearDown
    procedure test_all
  end type rotation_test_type

  integer(i_def), parameter :: element_order = 0

  real(r_def), parameter :: f_lat = PI/4.0_r_def

  real(r_def), parameter :: gravity = 10.0_r_def
  real(r_def), parameter :: radius  = 6000000_r_def
  real(r_def), parameter :: omega   = 7.292116E-5_r_def
  real(r_def), parameter :: p_zero  = 100000.0_r_def
  real(r_def), parameter :: rd      = 300.0_r_def
  real(r_def), parameter :: cp      = 1000.0_r_def
  real(r_def), parameter :: scaling = 1.0_r_def

contains

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  subroutine setUp( this )

    use base_mesh_config_mod,      only : base_mesh_geometry_planar, &
                                          base_mesh_partitioner_planar
    use finite_element_config_mod, only : finite_element_cellshape_quadrilateral
    use gungho_feign_config_mod,   only : feign_base_mesh_config, &
                                          feign_planet_config
    use mesh_mod,                  only : PLANE_BI_PERIODIC
    use reference_element_mod,     only : reference_cube, reference_element

    implicit none

    class(rotation_test_type), intent(inout) :: this

    type(field_proxy_type)    :: chi_p(3)
    type(field_proxy_type)    :: r_u_proxy


    integer(i_def) :: nqp_h, nqp_v
    integer(i_def) :: ndf_w0, ndf_w2
    integer(i_def) :: undf_w0, undf_w2
    integer(i_def) :: dim_w0, diff_dim_w0, dim_w2

    integer :: i

    call feign_planet_config( gravity=gravity, radius=radius, omega=omega, &
                              rd=rd, cp=cp, p_zero=p_zero,                 &
                              scaling_factor=scaling )

    call feign_base_mesh_config( filename='foo', prime_mesh_name='unit_test', &
                                 geometry=base_mesh_geometry_planar,          &
                                 partitioner=base_mesh_partitioner_planar,    &
                                 fplane=.false., f_lat_deg=f_lat )

    reference_element = finite_element_cellshape_quadrilateral
    call reference_cube()

    ! Create top level mesh collection
    mesh_collection = mesh_collection_type()
    ! Create top level function space collection
    function_space_collection = function_space_collection_type()

    this%qr = quadrature_type( 3, GAUSSIAN )

    nqp_h = this%qr%get_nqp_h()
    nqp_v = this%qr%get_nqp_v()

    ! Dummy mesh mod has 9 cells, 3 layers and is uniform in vertical
    this%mesh_id = mesh_collection%add_unit_test_mesh( PLANE_BI_PERIODIC )

    this%w0_fs => function_space_collection%get_fs(this%mesh_id, &
                                                   element_order, &
                                                   W0)
    this%w2_fs => function_space_collection%get_fs(this%mesh_id, &
                                                   element_order, &
                                                   W2)

    this%r_u = field_type( vector_space = this%w2_fs )
    r_u_proxy  = this%r_u%get_proxy()

    ndf_w2  = r_u_proxy%vspace%get_ndf( )
    dim_w2  = r_u_proxy%vspace%get_dim_space( )
    undf_w2 = r_u_proxy%vspace%get_undf()

    allocate(this%basis_w2(dim_w2,ndf_w2,nqp_h,nqp_v))

    do i = 1,3
      this%chi(i) = field_type( vector_space = this%w0_fs )
      chi_p(i)    = this%chi(i)%get_proxy()
    end do

    ndf_w0      = chi_p(1)%vspace%get_ndf( )
    dim_w0      = chi_p(1)%vspace%get_dim_space( )
    diff_dim_w0 = chi_p(1)%vspace%get_dim_space_diff( )
    undf_w0     = chi_p(1)%vspace%get_undf()

    allocate(this%basis_w0(dim_w0,ndf_w0,nqp_h,nqp_v))
    allocate(this%diff_basis_w0(diff_dim_w0,ndf_w0,nqp_h,nqp_v))

  end subroutine setUp

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  subroutine tearDown( this )

    use reference_element_mod, only : deallocate_reference

    implicit none

    class(rotation_test_type), intent(inout) :: this

    deallocate( this%basis_w2, this%basis_w0, this%diff_basis_w0 )

    call function_space_collection%clear()
    call deallocate_reference()
    call mesh_collection%clear()

  end subroutine tearDown

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  @test
  subroutine test_all( this )

    use rotation_kernel_mod, only : rotation_code
    use mesh_mod,                   only : mesh_type

    implicit none

    class(rotation_test_type), intent(inout) :: this

    real(r_def), parameter :: dx = 6000.0_r_def, &
                              dy = 1000.0_r_def
    real(r_def), parameter :: tol = 1.0e-3_r_def

    type(mesh_type), pointer :: mesh => null()

    type(field_type) :: u

    type(field_proxy_type) :: chi_p(3)
    type(field_proxy_type) :: r_u_proxy, u_proxy


    real(kind=r_def) :: answer

    integer :: i, j, k, cell, df, err

    real(r_def) :: dz, dz_test(3)

    real(kind=r_def) :: junk1, junk2

    integer                 :: nlayers, nqp_h, nqp_v
    integer                 :: ndf_w0, ndf_w2
    integer                 :: undf_w0, undf_w2
    integer, pointer        :: map_w2(:), map_w0(:) => null()

    real(kind=r_def), pointer :: xp(:,:) => null()
    real(kind=r_def), pointer :: zp(:) => null()
    real(kind=r_def), pointer :: wh(:), wv(:) => null()

    mesh => mesh_collection%get_mesh( this%mesh_id )
    call mesh%get_dz(dz_test)
    dz = dz_test(1)


    do i = 1,3
      chi_p(i) = this%chi(i)%get_proxy()
    end do

    ! Compute coordinates
    cell = 1
    do j = 1,3
      do i = 1,3
        map_w0 => chi_p(1)%vspace%get_cell_dofmap( cell )
        do k = 0,3
          chi_p(1)%data(map_w0(1) + k) = real(i-1)*dx
          chi_p(2)%data(map_w0(1) + k) = real(j-1)*dy
          chi_p(3)%data(map_w0(1) + k) = real(k)  *dz
        end do
        cell = cell + 1
      end do
    end do

    ! Test kernel
    u   = field_type( vector_space = this%w2_fs )

    r_u_proxy  = this%r_u%get_proxy()
    u_proxy    = u%get_proxy()

    nlayers = r_u_proxy%vspace%get_nlayers()
    nqp_h = this%qr%get_nqp_h()
    nqp_v = this%qr%get_nqp_v()
    zp => this%qr%get_xqp_v()
    xp => this%qr%get_xqp_h()
    wh => this%qr%get_wqp_h()
    wv => this%qr%get_wqp_v()

    ndf_w2  = r_u_proxy%vspace%get_ndf( )
    undf_w2 = r_u_proxy%vspace%get_undf()

    ndf_w0      = chi_p(1)%vspace%get_ndf( )
    undf_w0     = chi_p(1)%vspace%get_undf()

    call r_u_proxy%vspace%compute_basis_function( &
         this%basis_w2, ndf_w2, nqp_h, nqp_v, xp, zp)

    call chi_p(1)%vspace%compute_basis_function( &
         this%basis_w0, ndf_w0, nqp_h, nqp_v, xp, zp)

    call chi_p(1)%vspace%compute_diff_basis_function( &
         this%diff_basis_w0, ndf_w0, nqp_h, nqp_v, xp, zp)

    cell = 1

    map_w2 => r_u_proxy%vspace%get_cell_dofmap( cell )
    map_w0 => chi_p(1)%vspace%get_cell_dofmap( cell )

    ! Create the data
    u_proxy%data(:)   = 0.0_r_def
    r_u_proxy%data(:) = 0.0_r_def
    do k = 0,nlayers -1
      u_proxy%data(map_w2(1)+k) = 2.0_r_def
      u_proxy%data(map_w2(3)+k) = 2.0_r_def
    end do

    call rotation_code( nlayers,                                              &
                        r_u_proxy%data,                                       &
                        u_proxy%data,                                         &
                        chi_p(1)%data,                                        &
                        chi_p(2)%data,                                        &
                        chi_p(3)%data,                                        &
                        ndf_w2, undf_w2,                                      &
                        map_w2, this%basis_w2,                                     &
                        ndf_w0, undf_w0,                                      &
                        map_w0, this%basis_w0, this%diff_basis_w0,                      &
                        nqp_h, nqp_v, wh, wv                                  &
                        )

    ! Simple test, if u = constant that the correct f-F_plane rotation
    ! terms are obtained in the vertical component
    answer = -u_proxy%data(map_w2(1))/dy*2.0_r_def*omega*cos(f_lat)

    @assertEqual(answer, r_u_proxy%data(map_w2(5)+1), tol)
    @assertEqual(answer, r_u_proxy%data(map_w2(6)+1), tol)

  end subroutine test_all

end module rotation_kernel_mod_test
