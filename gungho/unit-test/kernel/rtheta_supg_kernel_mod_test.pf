!-----------------------------------------------------------------------------
! Copyright (c) 2017,  Met Office, on behalf of HMSO and Queen's Printer
! For further details please refer to the file LICENCE.original which you
! should have received as part of this distribution.
!-----------------------------------------------------------------------------

!> Test the rtheta computation
!>
module rtheta_supg_kernel_mod_test

  use constants_mod,                 only : i_def, r_def
  use function_space_collection_mod, only : function_space_collection_type, &
                                            function_space_collection
  use mesh_collection_mod,           only : mesh_collection_type, &
                                            mesh_collection
  use field_mod,                     only : field_type, field_proxy_type
  use fs_continuity_mod,             only : W0, W2, W3
  use function_space_mod,            only : function_space_type
  use pFUnit_Mod
  use quadrature_mod,                only : quadrature_type, GAUSSIAN

  implicit none

  private
  public :: test_all

  @TestCase
  type, extends(TestCase), public :: rtheta_supg_test_type
    private
    type(quadrature_type)              :: qr
    type(function_space_type), pointer :: w0_fs => null()
    type(function_space_type), pointer :: w2_fs => null()
    type(function_space_type), pointer :: w3_fs => null()
    type(field_type)                   :: r_theta, f, rho
    real(r_def), allocatable           :: basis_w2(:,:,:,:), &
                                          basis_w0(:,:,:,:), &
                                          basis_w3(:,:,:,:), &
                                          diff_basis_w0(:,:,:,:)
    real(r_def), allocatable           :: answer(:)
  contains
    procedure setUp
    procedure tearDown
    procedure test_all
  end type rtheta_supg_test_type

  integer(i_def), parameter :: element_order = 0

  real(r_def),    parameter :: dt = 10.0
  real(r_def), parameter    :: alpha   = 0.5_r_def
  real(r_def), parameter    :: tau_u   = 0.5_r_def
  real(r_def), parameter    :: tau_t   = 0.5_r_def
  real(r_def), parameter    :: tau_r   = 0.5_r_def
  integer(i_def), parameter :: inner_iterations = 2
  integer(i_def), parameter :: outer_iterations = 2
  integer(i_def) :: mesh_id
  integer(i_def), parameter :: spinup_period = 0
  logical,        parameter :: spinup_winds  = .false.
  logical,        parameter :: spinup_alpha  = .false.

contains

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  subroutine setUp( this )

    use finite_element_config_mod, only : finite_element_shape_quadrilateral
    use mesh_mod,                  only : PLANE_BI_PERIODIC
    use reference_element_mod,     only : reference_cube, reference_element
    use gungho_feign_config_mod,   only : feign_timestepping_config
    use timestepping_config_mod,   only : timestepping_method_semi_implicit, &
                                          timestepping_runge_kutta_method_ssp3

    implicit none

    class(rtheta_supg_test_type), intent(inout) :: this

    type(field_proxy_type)    :: r_theta_proxy, f_proxy, rho_proxy

    integer(i_def) :: nqp_h, nqp_v
    integer(i_def) :: ndf_w0, undf_w0, dim_w0, dim_diff_w0
    integer(i_def) :: ndf_w2, dim_w2
    integer(i_def) :: ndf_w3, dim_w3

    call feign_timestepping_config( timestepping_method_semi_implicit,   &
                                    dt,                                  &
                                    alpha,                               &
                                    tau_u,                               &
                                    tau_t,                               &
                                    tau_r,                               &
                                    outer_iterations,                    &
                                    inner_iterations,                    &
                                    timestepping_runge_kutta_method_ssp3,&
                                    spinup_period,                       &
                                    spinup_alpha,                        &
                                    spinup_winds                         &
                                    )

    reference_element = finite_element_shape_quadrilateral
    call reference_cube()

    ! Create top level mesh collection
    mesh_collection = mesh_collection_type()
    ! Create top level function space collection
    function_space_collection = function_space_collection_type()

    this%qr  = quadrature_type(3, GAUSSIAN)

    nqp_h=this%qr%get_nqp_h()
    nqp_v=this%qr%get_nqp_v()

    ! Dummy mesh mod has 9 cells, 3 layers and is uniform in vertical
    mesh_id = mesh_collection%add_unit_test_mesh( PLANE_BI_PERIODIC )

    this%w0_fs => function_space_collection%get_fs(mesh_id, element_order, W0)
    this%w2_fs => function_space_collection%get_fs(mesh_id, element_order, W2)
    this%w3_fs => function_space_collection%get_fs(mesh_id, element_order, W3)

    this%f  = field_type( vector_space = this%w2_fs )
    f_proxy = this%f%get_proxy()
    ndf_w2  = f_proxy%vspace%get_ndf( )
    dim_w2  = f_proxy%vspace%get_dim_space( )

    allocate( this%basis_w2(dim_w2,ndf_w2,nqp_h,nqp_v) )

    this%r_theta  = field_type( vector_space = this%w0_fs )
    r_theta_proxy = this%r_theta%get_proxy()
    ndf_w0        = r_theta_proxy%vspace%get_ndf( )
    dim_w0        = r_theta_proxy%vspace%get_dim_space( )
    dim_diff_w0   = r_theta_proxy%vspace%get_dim_space_diff( )
    undf_w0       = r_theta_proxy%vspace%get_undf()

    allocate( this%basis_w0(dim_w0,ndf_w0,nqp_h,nqp_v) )
    allocate( this%diff_basis_w0(dim_diff_w0,ndf_w0,nqp_h,nqp_v) )
    allocate( this%answer(undf_w0) )

    this%rho  = field_type( vector_space = this%w3_fs )
    rho_proxy = this%rho%get_proxy()
    ndf_w3    = rho_proxy%vspace%get_ndf( )
    dim_w3    = rho_proxy%vspace%get_dim_space( )

    allocate( this%basis_w3(dim_w3,ndf_w3,nqp_h,nqp_v) )

  end subroutine setUp

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  subroutine tearDown( this )

    use reference_element_mod, only : deallocate_reference

    implicit none

    class(rtheta_supg_test_type), intent(inout) :: this

    deallocate( this%basis_w2,      &
                this%basis_w0,      &
                this%diff_basis_w0, &
                this%basis_w3 )
    deallocate( this%answer )

    call function_space_collection%clear()
    call deallocate_reference()
    call mesh_collection%clear()

  end subroutine tearDown

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  @test
  subroutine test_all( this )

    use rtheta_supg_kernel_mod,     only : rtheta_supg_code

    implicit none

    class(rtheta_supg_test_type), intent(inout) :: this

    real(r_def), parameter :: dx = 6000.0_r_def, &
                              dy = 1000.0_r_def, &
                              dz = 2000.0_r_def
    real(r_def) :: tol = 1.0e-6_r_def


    integer :: i, j, k, cell
    integer(i_def) :: err

    ! Required for calling kernel
    type(field_type)  :: theta, theta_n, chi(3)

    integer                 :: nlayers, nqp_h, nqp_v
    integer                 :: ndf_w0, undf_w0
    integer                 :: ndf_w2, undf_w2
    integer                 :: ndf_w3, undf_w3

    integer, pointer        :: map_w0(:) => null()
    integer, pointer        :: map_w2(:) => null()
    integer, pointer        :: map_w3(:) => null()

    type(field_proxy_type)        :: r_theta_proxy, theta_proxy, &
                                       theta_n_proxy, &
                                       f_proxy, rho_proxy, &
                                       chi_proxy(3)

    real(kind=r_def), pointer :: xp(:,:) => null()
    real(kind=r_def), pointer :: zp(:)   => null()
    real(kind=r_def), pointer :: wh(:)   => null(), &
                                 wv(:)   => null()


    ! Make a coordinate field with a function space
    do i = 1,3
      chi(i) = field_type( vector_space = this%w0_fs )
      chi_proxy(i) = chi(i)%get_proxy()
    end do
    ! Compute coordinates
    cell = 1
    do j = 1,3
      do i = 1,3
        map_w0 => chi_proxy(1)%vspace%get_cell_dofmap( cell )
        do k = 0,3
          chi_proxy(1)%data(map_w0(1) + k) = real(i-1)*dx
          chi_proxy(2)%data(map_w0(1) + k) = real(j-1)*dy
          chi_proxy(3)%data(map_w0(1) + k) = real(k)  *dz
        end do
        cell = cell + 1
      end do
    end do

    ! Test kernel
    theta   = field_type( vector_space = this%w0_fs )
    theta_n = field_type( vector_space = this%w0_fs )

    r_theta_proxy   = this%r_theta%get_proxy()
    theta_proxy     = theta%get_proxy()
    theta_n_proxy   = theta_n%get_proxy()
    f_proxy         = this%f%get_proxy()
    rho_proxy       = this%rho%get_proxy()

    nlayers = r_theta_proxy%vspace%get_nlayers()

    nqp_h=this%qr%get_nqp_h()
    nqp_v=this%qr%get_nqp_v()
    zp=>this%qr%get_xqp_v()
    xp=>this%qr%get_xqp_h()
    wh=>this%qr%get_wqp_h()
    wv=>this%qr%get_wqp_v()

    ndf_w2      = f_proxy%vspace%get_ndf( )
    undf_w2     = f_proxy%vspace%get_undf()

    ndf_w0      = r_theta_proxy%vspace%get_ndf( )
    undf_w0     = r_theta_proxy%vspace%get_undf()

    ndf_w3      = rho_proxy%vspace%get_ndf( )
    undf_w3     = rho_proxy%vspace%get_undf()


    call f_proxy%vspace%compute_basis_function(                     &
        this%basis_w2, ndf_w2, nqp_h, nqp_v, xp, zp)

    call r_theta_proxy%vspace%compute_basis_function(this%basis_w0, ndf_w0,      & 
         nqp_h, nqp_v, xp, zp)

    call r_theta_proxy%vspace%compute_diff_basis_function(                  &
         this%diff_basis_w0, ndf_w0, nqp_h, nqp_v, xp, zp)

    call rho_proxy%vspace%compute_basis_function(                     &
        this%basis_w3, ndf_w3, nqp_h, nqp_v, xp, zp)


    ! Create the data
    r_theta_proxy%data(:) = 0.0_r_def
    theta_proxy%data(:)   = 300.0_r_def
    theta_n_proxy%data(:) = 300.0_r_def
    f_proxy%data(:)       = 2.0_r_def
    rho_proxy%data(:)     = 0.5_r_def


    do cell = 1, r_theta_proxy%vspace%get_ncell()
       map_w0 => r_theta_proxy%vspace%get_cell_dofmap( cell )
       map_w2 => f_proxy%vspace%get_cell_dofmap( cell )
       map_w3 => rho_proxy%vspace%get_cell_dofmap( cell )


       call rtheta_supg_code( nlayers, &
                              r_theta_proxy%data, &
                              theta_proxy%data, &
                              theta_n_proxy%data, &
                              f_proxy%data, &
                              rho_proxy%data, &
                              chi_proxy(1)%data, &
                              chi_proxy(2)%data, &
                              chi_proxy(3)%data, &
                              ndf_w0, undf_w0, &
                              map_w0, &
                              this%basis_w0, &
                              this%diff_basis_w0, &
                              ndf_w2, undf_w2, &
                              map_w2, &
                              this%basis_w2, &
                              ndf_w3, undf_w3, &
                              map_w3, &
                              this%basis_w3, &
                              ndf_w0, undf_w0, &
                              map_w0, &
                              this%diff_basis_w0, &
                              nqp_h, nqp_v, wh, wv &
                              )
    end do

    ! Simple test, check that when theta=const, f/rho.grad(theta) = 0    
    this%answer(:) = 0.0_r_def
    @assertEqual( this%answer, r_theta_proxy%data(:), tol )

  end subroutine test_all

end module rtheta_supg_kernel_mod_test
