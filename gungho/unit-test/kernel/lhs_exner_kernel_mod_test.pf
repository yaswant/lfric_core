!-----------------------------------------------------------------------------
! Copyright (c) 2017,  Met Office, on behalf of HMSO and Queen's Printer
! For further details please refer to the file LICENCE.original which you
! should have received as part of this distribution.
!-----------------------------------------------------------------------------

!> Test the lhs of the equation of state computation
!>
module lhs_exner_kernel_mod_test

  use constants_mod,                 only : i_def, r_def

  use get_unit_test_m3x3_q3x3x3_sizes_mod, only : get_w0_m3x3_q3x3x3_size,     &
                                                  get_wtheta_m3x3_q3x3x3_size, &
                                                  get_w3_m3x3_q3x3x3_size

  use get_unit_test_m3x3_dofmap_mod,       only : get_w0_m3x3_dofmap,     &
                                                  get_wtheta_m3x3_dofmap, &
                                                  get_w3_m3x3_dofmap

  use get_unit_test_q3x3x3_quadrature_mod, &
     only : get_gaussian_q3x3x3_quadrature_weights_xy, &
            get_gaussian_q3x3x3_quadrature_weights_z

  use get_unit_test_q3x3x3_basis_mod, only: get_w0_q3x3x3_basis,       &
                                            get_w0_q3x3x3_diff_basis,  &
                                            get_wtheta_q3x3x3_basis,   &
                                            get_w3_q3x3x3_basis

  use get_unit_test_3x3x3_chi_mod,    only : get_w0_3x3x3_field
  use pFUnit_Mod

  use yaxt,                          only : xt_initialize, xt_finalize
  use mpi_mod,                       only : store_comm, clear_comm

  implicit none

  private
  public :: lhs_exner_test_type, test_all

  @TestCase
  type, extends(MPITestCase) :: lhs_exner_test_type
    private
  contains
    procedure setUp
    procedure tearDown
    procedure test_all
  end type lhs_exner_test_type

  real(r_def), parameter :: bvf_square = 0.0001_r_def
  real(r_def), parameter :: theta_surf = 300.0_r_def

  real(r_def), parameter :: gravity  = 10.0_r_def
  real(r_def), parameter :: radius   = 6000000_r_def
  real(r_def), parameter :: omega    = 8.0E-5_r_def
  real(r_def), parameter :: p_zero   = 100000.0_r_def
  real(r_def), parameter :: rd       = 300.0_r_def
  real(r_def), parameter :: cp       = 1000.0_r_def
  real(r_def), parameter :: scaling  = 1.0_r_def

contains

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  subroutine setUp( this )

    use feign_config_mod,          only : feign_formulation_config,         &
                                          feign_planet_config
    use mesh_mod,                  only : PLANE_BI_PERIODIC

    implicit none

    class(lhs_exner_test_type), intent(inout) :: this

    ! Initialise YAXT
    call xt_initialize(this%getMpiCommunicator())
    !Store the MPI communicator for later use
    call store_comm(this%getMpiCommunicator())

    call feign_formulation_config( dlayer_on=.false.,            &
                                   dry_static_adjust=.false.,    &
                                   exner_from_eos=.false.,       &
                                   init_exner_bt=.false.,        &
                                   l_multigrid=.false.,          &
                                   rotating=.true.,              &
                                   set_hydrostatic_init=.false., &
                                   shallow=.false.,              &
                                   transport_only=.false.,       &
                                   use_moisture=.false.,         &
                                   use_physics=.false.,          &
                                   use_wavedynamics=.false.,     &
                                   si_momentum_equation=.true.,  &
                                   vector_invariant=.false.,     &
                                   moisture_conservation=.false. )

    call feign_planet_config( gravity=gravity, radius=radius, omega=omega, &
                              rd=rd, cp=cp, p_zero=p_zero,                 &
                              scaling_factor=scaling )

  end subroutine setUp

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  subroutine tearDown( this )

    use configuration_mod,        only: final_configuration

    implicit none

    class(lhs_exner_test_type), intent(inout) :: this

    call final_configuration()

    ! Finalise YAXT
    call xt_finalize()
    ! Clear the stored MPI communicator
    call clear_comm()

  end subroutine tearDown

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  @Test( npes=[1] )
  subroutine test_all( this )

    use lhs_exner_kernel_mod,     only: lhs_exner_code
    use planet_config_mod,        only: kappa
    use calc_exner_pointwise_mod, only: calc_exner_pointwise

    implicit none

    class(lhs_exner_test_type), intent(inout) :: this

    real(r_def), parameter :: dx = 6000.0_r_def
    real(r_def), parameter :: dy = 1000.0_r_def
    real(r_def), parameter :: dz = 2000.0_r_def

    real(r_def), parameter :: tol = 1.0e-8_r_def
    real(r_def)            :: answer

    ! Fields
    real(r_def), allocatable :: lhs_exner(:)
    real(r_def), allocatable :: rho(:)
    real(r_def), allocatable :: exner(:)
    real(r_def), allocatable :: theta(:)
    real(r_def), allocatable :: rho_ref(:)
    real(r_def), allocatable :: exner_ref(:)
    real(r_def), allocatable :: theta_ref(:)
    real(r_def), allocatable :: chi1(:), chi2(:), chi3(:)

    integer(i_def) :: i, j, k, cell, err

    ! Sizes
    integer(i_def)            :: nlayers, ncells, nqp_h, nqp_v
    integer(i_def)            :: ndf_w0, ndf_w3, ndf_wtheta
    integer(i_def)            :: undf_w0, undf_w3, undf_wtheta
    integer(i_def)            :: dim_space, dim_space_diff

    ! Dofmaps
    integer(i_def), allocatable :: map_w0(:,:)
    integer(i_def), allocatable :: map_w3(:,:)
    integer(i_def), allocatable :: map_wtheta(:,:)

   ! Basis functions
    real(r_def),    allocatable :: basis_wtheta(:,:,:,:)
    real(r_def),    allocatable :: diff_basis_w0(:,:,:,:)
    real(r_def),    allocatable :: basis_w3(:,:,:,:)

    ! Weights
    real(r_def),    allocatable :: wh(:)
    real(r_def),    allocatable :: wv(:)

    nlayers = 3
    call get_w0_m3x3_q3x3x3_size( ndf_w0, undf_w0, ncells,     &
                                  dim_space, dim_space_diff,   &
                                  nqp_h, nqp_v,                &
                                  nlayers )
    call get_w3_m3x3_q3x3x3_size( ndf_w3, undf_w3, ncells,   &
                                  dim_space, dim_space_diff, &
                                  nqp_h, nqp_v,              &
                                  nlayers )
    call get_wtheta_m3x3_q3x3x3_size( ndf_wtheta, undf_wtheta, ncells, &
                                      dim_space, dim_space_diff,       &
                                      nqp_h, nqp_v,                    &
                                      nlayers )

    call get_w0_m3x3_dofmap(map_w0)
    call get_w3_m3x3_dofmap(map_w3)
    call get_wtheta_m3x3_dofmap(map_wtheta)

    call get_gaussian_q3x3x3_quadrature_weights_xy( wh )
    call get_gaussian_q3x3x3_quadrature_weights_z( wv )

    ! Get canned basis functions
    call get_wtheta_q3x3x3_basis(basis_wtheta)
    call get_w0_q3x3x3_diff_basis(diff_basis_w0)
    call get_w3_q3x3x3_basis(basis_w3)

    ! Compute coordinates
    allocate(chi1(undf_w0))
    allocate(chi2(undf_w0))
    allocate(chi3(undf_w0))

    call get_w0_3x3x3_field(chi1, chi2, chi3, dx, dy, dz, &
                            map_w0, nlayers)

    ! Create the data
    allocate( lhs_exner( undf_w3 ) )
    allocate( exner( undf_w3 ) )
    allocate( rho( undf_w3 ) )
    allocate( theta( undf_wtheta ) )
    allocate( exner_ref( undf_w3 ) )
    allocate( rho_ref( undf_w3 ) )
    allocate( theta_ref( undf_wtheta ) )

    rho(:) = 0.1_r_def
    exner(:) = 0.2_r_def
    exner_ref(:) = 1.2_r_def
    theta(:) = 3.0_r_def
    rho_ref(:) = 1.2_r_def
    theta_ref(:) = 300.0_r_def
    lhs_exner(:) = 0.0_r_def

    cell = 5

    call lhs_exner_code( nlayers,                          &
                         lhs_exner,                        &
                         theta,                            &
                         rho,                              &
                         exner,                            &
                         theta_ref,                        &
                         rho_ref,                          &
                         exner_ref,                        &
                         chi1,                             &
                         chi2,                             &
                         chi3,                             &
                         ndf_w3, undf_w3,                  &
                         map_w3(:,cell), basis_w3,         &
                         ndf_wtheta, undf_wtheta,          &
                         map_wtheta(:,cell), basis_wtheta, &
                         ndf_w0, undf_w0,                  &
                         map_w0(:,cell), diff_basis_w0,    &
                         nqp_h, nqp_v, wh, wv              &
                         )

    k = 1
    answer = (1.0_r_def -kappa)/kappa*exner(1)/exner_ref(1) &
           - rho(1)/rho_ref(1) &
           - theta(1)/theta_ref(1)
    @assertEqual(answer, lhs_exner(map_w3(1, cell)+k)/(dx*dy*dz), tol)

    deallocate(lhs_exner)
    deallocate(theta)
    deallocate(exner)
    deallocate(rho)
    deallocate(theta_ref)
    deallocate(exner_ref)
    deallocate(rho_ref)
    deallocate(chi1)
    deallocate(chi2)
    deallocate(chi3)
    deallocate(map_w0)
    deallocate(map_w3)
    deallocate(map_wtheta)
    deallocate(basis_wtheta)
    deallocate(diff_basis_w0)
    deallocate(basis_w3)
    deallocate(wv)
    deallocate(wh)

  end subroutine test_all

end module lhs_exner_kernel_mod_test
