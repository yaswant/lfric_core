!-----------------------------------------------------------------------------
! Copyright (c) 2017,  Met Office, on behalf of HMSO and Queen's Printer
! For further details please refer to the file LICENCE.original which you
! should have received as part of this distribution.
!-----------------------------------------------------------------------------

!> Test the lhs of the equation of state computation
!>
module lhs_exner_kernel_mod_test

  use constants_mod,                 only : i_def, r_def
  use function_space_collection_mod, only : function_space_collection_type, &
                                            function_space_collection
  use mesh_collection_mod,           only : mesh_collection_type, &
                                            mesh_collection
  use field_mod,                     only : field_type, &
                                            field_proxy_type
  use function_space_mod,            only : function_space_type
  use fs_continuity_mod,             only : W0, W3
  use pFUnit_Mod
  use quadrature_mod,                only : quadrature_type, GAUSSIAN

  implicit none

  private
  public :: lhs_exner_test_type, test_all

  @TestCase
  type, extends(TestCase) :: lhs_exner_test_type
    private
    type(quadrature_type)              :: qr
    type(function_space_type), pointer :: w0_fs => null()
    type(function_space_type), pointer :: w3_fs => null()
    type(field_type)                   :: lhs_exner
    type(field_type)                   :: exner, rho, theta
    type(field_type)                   :: rho_ref, theta_ref
    real(r_def), allocatable           :: basis_w3(:,:,:,:), &
                                          basis_w0(:,:,:,:), &
                                          diff_basis_w0(:,:,:,:)
  contains
    procedure setUp
    procedure tearDown
    procedure test_all
  end type lhs_exner_test_type

  integer(i_def), parameter :: element_order = 0

  real(r_def), parameter :: bvf_square = 0.0001_r_def

  real(r_def), parameter :: gravity = 10.0_r_def
  real(r_def), parameter :: radius  = 6000000_r_def
  real(r_def), parameter :: omega   = 8.0E-5_r_def
  real(r_def), parameter :: p_zero  = 100000.0_r_def
  real(r_def), parameter :: rd      = 300.0_r_def
  real(r_def), parameter :: cp      = 1000.0_r_def
  real(r_def), parameter :: scaling = 1.0_r_def

contains

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  subroutine setUp( this )

    use base_mesh_config_mod,      only : base_mesh_geometry_planar, &
                                          base_mesh_partitioner_planar
    use finite_element_config_mod, only : finite_element_cellshape_quadrilateral
    use gungho_feign_config_mod,   only : feign_base_mesh_config,           &
                                          feign_initial_temperature_config, &
                                          feign_planet_config
    use mesh_mod,                  only : PLANE_BI_PERIODIC
    use reference_element_mod,     only : reference_cube, reference_element

    implicit none

    class(lhs_exner_test_type), intent(inout) :: this

    type(field_proxy_type)    :: exner_proxy, theta_proxy

    integer(i_def) :: nqp_h, nqp_v
    integer(i_def) :: ndf_w0, ndf_w3
    integer(i_def) :: undf_w0, undf_w3
    integer(i_def) :: dim_w0, diff_dim_w0, dim_w3
    integer(i_def) :: mesh_id

    call feign_base_mesh_config( filename='foo', prime_mesh_name='unit_test', &
                                 geometry=base_mesh_geometry_planar,          &
                                 partitioner=base_mesh_partitioner_planar,    &
                                 fplane=.false., f_lat_deg=0.0_r_def )

    call feign_initial_temperature_config( bvf_square )

    call feign_planet_config( gravity=gravity, radius=radius, omega=omega, &
                              rd=rd, cp=cp, p_zero=p_zero,                 &
                              scaling_factor=scaling )

    reference_element = finite_element_cellshape_quadrilateral
    call reference_cube()

    ! Create top level mesh collection
    mesh_collection = mesh_collection_type()
    ! Create top level function space collection
    function_space_collection = function_space_collection_type()

    this%qr = quadrature_type( 3, GAUSSIAN )

    nqp_h=this%qr%get_nqp_h()
    nqp_v=this%qr%get_nqp_v()

    ! Dummy mesh mod has 9 cells, 3 layers and is uniform in vertical
    mesh_id = mesh_collection%add_unit_test_mesh( PLANE_BI_PERIODIC )

    this%w0_fs => function_space_collection%get_fs(mesh_id, element_order, W0)
    this%w3_fs => function_space_collection%get_fs(mesh_id, element_order, W3)

    this%rho   = field_type( vector_space = this%w3_fs )
    this%lhs_exner  = field_type( vector_space = this%w3_fs )
    this%rho_ref  = field_type( vector_space = this%w3_fs )
    this%exner  = field_type( vector_space = this%w3_fs )
    exner_proxy = this%exner%get_proxy()

    ndf_w3  = exner_proxy%vspace%get_ndf( )
    dim_w3  = exner_proxy%vspace%get_dim_space( )
    undf_w3 = exner_proxy%vspace%get_undf()
    allocate(this%basis_w3(dim_w3,ndf_w3,nqp_h,nqp_v))

    this%theta_ref = field_type( vector_space = this%w0_fs )
    this%theta = field_type( vector_space = this%w0_fs )
    theta_proxy = this%theta%get_proxy()

    ndf_w0      = theta_proxy%vspace%get_ndf( )
    dim_w0      = theta_proxy%vspace%get_dim_space( )
    diff_dim_w0 = theta_proxy%vspace%get_dim_space_diff( )
    undf_w0     = theta_proxy%vspace%get_undf()
    allocate(this%basis_w0(dim_w0,ndf_w0,nqp_h,nqp_v))
    allocate(this%diff_basis_w0(diff_dim_w0,ndf_w0,nqp_h,nqp_v))

  end subroutine setUp

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  subroutine tearDown( this )

    use reference_element_mod, only : deallocate_reference

    implicit none

    class(lhs_exner_test_type), intent(inout) :: this

    deallocate( this%basis_w3,                     &
                this%basis_w0, this%diff_basis_w0 )

    call deallocate_reference()
    call function_space_collection%clear()
    call mesh_collection%clear()

  end subroutine tearDown

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  @test
  subroutine test_all( this )

    use lhs_exner_kernel_mod,     only: lhs_exner_code
    use planet_config_mod,        only: kappa
    use calc_exner_pointwise_mod, only: calc_exner_pointwise

    implicit none

    class(lhs_exner_test_type), intent(inout) :: this

    real(r_def), parameter :: dx = 6000.0_r_def, &
                              dy = 1000.0_r_def, &
                              dz = 2000.0_r_def
    real(r_def), parameter :: tol = 1.0e-8_r_def
    real(r_def)            :: answer

    type(field_type) :: chi(3)
    type(field_proxy_type )   :: chi_p(3)

    integer(i_def) :: i, j, k, cell, err

    integer(i_def)            :: nlayers, nqp_h, nqp_v
    integer(i_def)            :: ndf_w0, ndf_w3
    integer(i_def)            :: undf_w0, undf_w3
    integer(i_def)            :: element_order
    integer(i_def), pointer   :: map_w3(:), map_w0(:) => null()

    type(field_proxy_type)    :: lhs_exner_proxy 
    type(field_proxy_type)    :: rho_proxy, exner_proxy, theta_proxy
    type(field_proxy_type)    :: rho_ref_proxy, theta_ref_proxy

    real(kind=r_def), pointer :: xp(:,:) => null()
    real(kind=r_def), pointer :: zp(:) => null()
    real(kind=r_def), pointer :: wh(:), wv(:) => null()
    real(kind=r_def)          :: exner_ref


    ! Make a coordinate field with a function space
    do i = 1,3
      chi(i) = field_type( vector_space = this%w0_fs )
      chi_p(i) = chi(i)%get_proxy()
    end do

    ! Compute coordinates
    cell = 1
    do j = 1,3
      do i = 1,3
        map_w0 => chi_p(1)%vspace%get_cell_dofmap( cell )
        do k = 0,3
          chi_p(1)%data(map_w0(1) + k) = real(i-1)*dx
          chi_p(2)%data(map_w0(1) + k) = real(j-1)*dy
          chi_p(3)%data(map_w0(1) + k) = real(k)  *dz
        end do
        cell = cell + 1
      end do
    end do

    ! Test kernel
    rho_proxy  = this%rho%get_proxy()
    exner_proxy  = this%exner%get_proxy()
    theta_proxy = this%theta%get_proxy()
    lhs_exner_proxy = this%lhs_exner%get_proxy()
    rho_ref_proxy = this%rho_ref%get_proxy()
    theta_ref_proxy = this%theta_ref%get_proxy()

    nlayers = exner_proxy%vspace%get_nlayers()
    nqp_h=this%qr%get_nqp_h()
    nqp_v=this%qr%get_nqp_v()
    zp=>this%qr%get_xqp_v()
    xp=>this%qr%get_xqp_h()
    wh=>this%qr%get_wqp_h()
    wv=>this%qr%get_wqp_v()

    ndf_w3  = exner_proxy%vspace%get_ndf( )
    undf_w3 = exner_proxy%vspace%get_undf()

    ndf_w0  = theta_proxy%vspace%get_ndf( )
    undf_w0 = theta_proxy%vspace%get_undf()

    call exner_proxy%vspace%compute_basis_function( &
         this%basis_w3, ndf_w3, nqp_h, nqp_v, xp, zp )

    call theta_proxy%vspace%compute_basis_function( &
         this%basis_w0, ndf_w0, nqp_h, nqp_v, xp, zp )

    call theta_proxy%vspace%compute_diff_basis_function( &
         this%diff_basis_w0, ndf_w0, nqp_h, nqp_v, xp, zp )

    cell = 5

    map_w3 => exner_proxy%vspace%get_cell_dofmap( cell )
    map_w0 => theta_proxy%vspace%get_cell_dofmap( cell )

    ! Create the data
    rho_proxy%data(:) = 0.1_r_def
    exner_proxy%data(:) = 0.2_r_def
    theta_proxy%data(:) = 3.0_r_def
    rho_ref_proxy%data(:) = 1.2_r_def
    theta_ref_proxy%data(:) = 300.0_r_def
    lhs_exner_proxy%data(:) = 0.0_r_def

    call lhs_exner_code( nlayers,                                   &
                         lhs_exner_proxy%data,                      &
                         theta_proxy%data,                          &
                         rho_proxy%data,                            &
                         exner_proxy%data,                          &
                         theta_ref_proxy%data,                      &
                         rho_ref_proxy%data,                        &
                         chi_p(1)%data,                             &
                         chi_p(2)%data,                             &
                         chi_p(3)%data,                             &
                         ndf_w3, undf_w3,                           &
                         map_w3, this%basis_w3,                     &
                         ndf_w0, undf_w0,                           &
                         map_w0, this%basis_w0,                     &
                         ndf_w0, undf_w0,                           &
                         map_w0, this%diff_basis_w0,                        &
                         nqp_h, nqp_v, wh, wv                       &
                         )

    k = 1
    exner_ref =  calc_exner_pointwise(rho_ref_proxy%data(1), &
                                      theta_ref_proxy%data(1))

    answer = (1.0_r_def - kappa)/kappa*exner_proxy%data(1)/exner_ref &
           - rho_proxy%data(1)/rho_ref_proxy%data(1) &
           - theta_proxy%data(1)/theta_ref_proxy%data(1)
    @assertEqual(answer, lhs_exner_proxy%data(map_w3(1)+k)/(dx*dy*dz), tol)

  end subroutine test_all

end module lhs_exner_kernel_mod_test
