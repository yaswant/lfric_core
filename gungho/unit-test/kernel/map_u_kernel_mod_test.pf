!-----------------------------------------------------------------------------
! (C) Crown copyright 2019 Met Office. All rights reserved.
! The file LICENCE, distributed with this code, contains details of the terms
! under which the code may be used.
!-----------------------------------------------------------------------------

module map_u_kernel_mod_test

  use constants_mod,                 only : i_def, r_def
  use function_space_collection_mod, only : function_space_collection_type, &
                                            function_space_collection
  use mesh_collection_mod,           only : mesh_collection_type, &
                                            mesh_collection
  use halo_routing_collection_mod,   only : halo_routing_collection_type, &
                                            halo_routing_collection
  use field_mod,                     only : field_type, field_proxy_type
  use function_space_mod,            only : function_space_type
  use fs_continuity_mod,             only : W0, W2, W3, WTHETA

  use pFUnit_Mod
  use quadrature_xyoz_mod,           only : quadrature_xyoz_type, &
                                               quadrature_xyoz_proxy_type
  use quadrature_rule_gaussian_mod,  only : quadrature_rule_gaussian_type
  use yaxt,                          only : xt_initialize, xt_finalize
  use mpi_mod,                       only : store_comm, clear_comm

  implicit none

  private
  public :: map_u_test_type, test_all

  @TestCase
  type, extends(MPITestCase) :: map_u_test_type
    private
    type( quadrature_xyoz_type )       :: qr
    type(quadrature_xyoz_proxy_type)   :: qr_proxy
    type(function_space_type), pointer :: w0_fs => null()
    type(function_space_type), pointer :: w2_fs => null()
    type(function_space_type), pointer :: w3_fs => null()
    type(function_space_type), pointer :: wt_fs => null()
    type(field_type)                   :: chi1
    type(field_type)                   :: chi2
    type(field_type)                   :: chi3
    type(field_type)                   :: r_u, u_lon, u_lat, u_up
    real(r_def), allocatable           :: basis_w2(:,:,:,:), &
                                          basis_w3(:,:,:,:), &
                                          basis_wt(:,:,:,:), &
                                          basis_w0(:,:,:,:), &
                                          diff_basis_w0(:,:,:,:)
  contains
    procedure setUp
    procedure tearDown
    procedure test_all
  end type map_u_test_type

  integer(i_def), parameter :: element_order = 0

contains

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  subroutine setUp( this )

    use base_mesh_config_mod,      only : geometry_planar, &
                                          partitioner_planar
    use finite_element_config_mod, only : cellshape_quadrilateral
    use feign_config_mod,          only : feign_base_mesh_config
    use mesh_mod,                  only : PLANE_BI_PERIODIC

    implicit none

    class(map_u_test_type), intent(inout) :: this

    type(field_proxy_type) :: chi_p(3)
    type(field_proxy_type) :: r_u_proxy
    type(field_proxy_type) :: u_lon_proxy
    type(field_proxy_type) :: u_lat_proxy
    type(field_proxy_type) :: u_up_proxy
    type( quadrature_rule_gaussian_type ) :: quadrature_rule

    integer(i_def) :: nqp_h, nqp_v
    integer(i_def) :: ndf_w0, ndf_w2, ndf_w3, ndf_wt
    integer(i_def) :: undf_w0, undf_w2, undf_w3, undf_wt
    integer(i_def) :: diff_dim_w0, dim_w0, dim_w2, dim_w3, dim_wt
    integer(i_def) :: mesh_id

    integer :: i

    ! Initialise YAXT
    call xt_initialize( this%getMpiCommunicator() )
    !Store the MPI communicator for later use
    call store_comm( this%getMpiCommunicator() )

    call feign_base_mesh_config( filename='foo',                      &
                                 prime_mesh_name='unit_test',         &
                                 geometry=geometry_planar,            &
                                 partitioner=partitioner_planar,      &
                                 fplane=.false., f_lat_deg=0.0_r_def )

    ! Create top level mesh collection
    mesh_collection = mesh_collection_type()
    ! Create top level function space collection
    function_space_collection = function_space_collection_type()
    ! Create top level halo_routing collection
    halo_routing_collection = halo_routing_collection_type()

    this%qr = quadrature_xyoz_type( 3, quadrature_rule )

    this%qr_proxy = this%qr%get_quadrature_proxy()
    nqp_h=this%qr_proxy%np_xy
    nqp_v=this%qr_proxy%np_z

    ! Dummy mesh mod has 9 cells, 3 layers
    mesh_id = mesh_collection%add_unit_test_mesh( PLANE_BI_PERIODIC )

    this%w0_fs => function_space_collection%get_fs( mesh_id, element_order, W0 )
    this%w2_fs => function_space_collection%get_fs( mesh_id, element_order, W2 )
    this%w3_fs => function_space_collection%get_fs( mesh_id, element_order, W3 )
    this%wt_fs => function_space_collection%get_fs( mesh_id, element_order, Wtheta )

    call this%r_u%initialise( vector_space = this%w2_fs )
    r_u_proxy  = this%r_u%get_proxy()

    ndf_w2      = r_u_proxy%vspace%get_ndf()
    dim_w2      = r_u_proxy%vspace%get_dim_space()
    undf_w2     = r_u_proxy%vspace%get_undf()

    call this%u_lon%initialise( vector_space = this%w3_fs )
    u_lon_proxy = this%u_lon%get_proxy()
    call this%u_lat%initialise( vector_space = this%w3_fs )
    u_lat_proxy = this%u_lat%get_proxy()
    call this%u_up%initialise( vector_space = this%wt_fs )
    u_up_proxy  = this%u_up%get_proxy()

    ndf_w3      = u_lon_proxy%vspace%get_ndf()
    dim_w3      = u_lon_proxy%vspace%get_dim_space()
    undf_w3     = u_lon_proxy%vspace%get_undf()

    ndf_wt      = u_up_proxy%vspace%get_ndf()
    dim_wt      = u_up_proxy%vspace%get_dim_space()
    undf_wt     = u_up_proxy%vspace%get_undf()

    allocate(this%basis_w2(dim_w2,ndf_w2,nqp_h,nqp_v))
    allocate(this%basis_w3(dim_w3,ndf_w3,nqp_h,nqp_v))
    allocate(this%basis_wt(dim_wt,ndf_wt,nqp_h,nqp_v))

    ! make a coordinate field with a function space
    call this%chi1%initialise( vector_space = this%w0_fs )
    call this%chi2%initialise( vector_space = this%w0_fs )
    call this%chi3%initialise( vector_space = this%w0_fs )
    chi_p(1) = this%chi1%get_proxy()
    chi_p(2) = this%chi2%get_proxy()
    chi_p(3) = this%chi3%get_proxy()

    ndf_w0      = chi_p(1)%vspace%get_ndf()
    dim_w0      = chi_p(1)%vspace%get_dim_space()
    diff_dim_w0 = chi_p(1)%vspace%get_dim_space_diff()
    undf_w0     = chi_p(1)%vspace%get_undf()

    allocate(this%basis_w0(dim_w0,ndf_w0,nqp_h,nqp_v))
    allocate(this%diff_basis_w0(diff_dim_w0,ndf_w0,nqp_h,nqp_v))

  end subroutine setUp

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  subroutine tearDown( this )

    use configuration_mod,        only: final_configuration

    implicit none

    class(map_u_test_type), intent(inout) :: this

    deallocate(this%basis_w2, this%basis_w0, this%diff_basis_w0, &
               this%basis_w3, this%basis_wt )

    call function_space_collection%clear()
    call mesh_collection%clear()
    call halo_routing_collection%clear()
    call final_configuration()

    ! Finalise YAXT
    call xt_finalize()
    ! Clear the stored MPI communicator
    call clear_comm()

  end subroutine tearDown

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  @Test( npes=[1] )
  subroutine test_all( this )

    use map_u_kernel_mod, only : map_u_code

   implicit none

    class(map_u_test_type), intent(inout) :: this

    real(r_def), parameter :: tol = 1.0e-3_r_def
    real(r_def), parameter :: dx = 6000.0_r_def, &
                              dy = 1000.0_r_def, &
                              dz = 2000.0_r_def
    real(r_def), parameter :: initial_time = 0.0_r_def

    type(field_proxy_type)   :: chi_p(3)

    real(kind=r_def) :: answer

    integer :: i, j, k, cell, err
    ! Required for calling kernel
    type(field_type) :: r_u

    integer                 :: nlayers, nqp_h, nqp_v
    integer                 :: ndf_w0, ndf_w2, ndf_w3, ndf_wt
    integer                 :: undf_w0, undf_w2, undf_w3, undf_wt
    integer, pointer        :: map_w2(:) => null()
    integer, pointer        :: map_w0(:) => null()
    integer, pointer        :: map_w3(:) => null()
    integer, pointer        :: map_wt(:) => null()

    type(field_proxy_type)        :: r_u_proxy
    type(field_proxy_type)        :: u_lon_proxy
    type(field_proxy_type)        :: u_lat_proxy
    type(field_proxy_type)        :: u_up_proxy

    real(kind=r_def), pointer :: xp(:,:) => null()
    real(kind=r_def), pointer :: zp(:) => null()
    real(kind=r_def), pointer :: wh(:), wv(:) => null()


    ! make a coordinate field with a function space
    chi_p(1) = this%chi1%get_proxy()
    chi_p(2) = this%chi2%get_proxy()
    chi_p(3) = this%chi3%get_proxy()

    ! Compute coordinates
    cell = 1
    do j = 1,3
      do i = 1,3
        map_w0 => chi_p(1)%vspace%get_cell_dofmap( cell )
        do k = 0,3
          chi_p(1)%data(map_w0(1) + k) = real(i-1)*dx
          chi_p(2)%data(map_w0(1) + k) = real(j-1)*dy
          chi_p(3)%data(map_w0(1) + k) = real(k)  *dz
        end do
        cell = cell + 1
      end do
    end do

    ! Test kernel
    r_u_proxy  = this%r_u%get_proxy()
    u_lon_proxy  = this%u_lon%get_proxy()
    u_lat_proxy  = this%u_lat%get_proxy()
    u_up_proxy  = this%u_up%get_proxy()

    nlayers = r_u_proxy%vspace%get_nlayers()
    nqp_h=this%qr_proxy%np_xy
    nqp_v=this%qr_proxy%np_z
    xp=>this%qr_proxy%points_xy
    zp=>this%qr_proxy%points_z
    wh=>this%qr_proxy%weights_xy
    wv=>this%qr_proxy%weights_z

    ndf_w2      = r_u_proxy%vspace%get_ndf()
    undf_w2     = r_u_proxy%vspace%get_undf()

    ndf_w0      = chi_p(1)%vspace%get_ndf()
    undf_w0     = chi_p(1)%vspace%get_undf()

    ndf_w3      = u_lon_proxy%vspace%get_ndf()
    undf_w3     = u_lon_proxy%vspace%get_undf()

    ndf_wt      = u_up_proxy%vspace%get_ndf()
    undf_wt     = u_up_proxy%vspace%get_undf()

    call r_u_proxy%vspace%compute_basis_function( &
         this%basis_w2, ndf_w2, nqp_h, nqp_v, xp, zp )

    call u_lon_proxy%vspace%compute_basis_function( &
         this%basis_w3, ndf_w3, nqp_h, nqp_v, xp, zp )

    call u_up_proxy%vspace%compute_basis_function( &
         this%basis_wt, ndf_wt, nqp_h, nqp_v, xp, zp )

    call chi_p(1)%vspace%compute_basis_function( &
         this%basis_w0, ndf_w0, nqp_h, nqp_v, xp, zp )

    call chi_p(1)%vspace%compute_diff_basis_function( &
         this%diff_basis_w0, ndf_w0, nqp_h, nqp_v, xp, zp )

    cell = 1

    map_w2 => r_u_proxy%vspace%get_cell_dofmap( cell )
    map_w3 => u_lon_proxy%vspace%get_cell_dofmap( cell )
    map_wt => u_lat_proxy%vspace%get_cell_dofmap( cell )
    map_w0 => chi_p(1)%vspace%get_cell_dofmap( cell )

    ! Create the data
    r_u_proxy%data(:) = 0.0_r_def

    u_lon_proxy%data(:) = 1.0_r_def
    u_lat_proxy%data(:) = -1.0_r_def
    u_up_proxy%data(:)  = 0.0_r_def

    call map_u_code( nlayers,                                   &
                     r_u_proxy%data,                            &
                     u_lon_proxy%data,                          &
                     u_lat_proxy%data,                          &
                     u_up_proxy%data,                           &
                     chi_p(1)%data,                             &
                     chi_p(2)%data,                             &
                     chi_p(3)%data,                             &
                     ndf_w2, undf_w2,                           &
                     map_w2, this%basis_w2,                     &
                     ndf_w3, undf_w3, map_w3, this%basis_w3,    &
                     ndf_wt, undf_wt, map_wt, this%basis_wt,    &
                     ndf_w0, undf_w0,                           &
                     map_w0, this%basis_w0, this%diff_basis_w0, &
                     nqp_h, nqp_v, wh, wv                       &
                     )

    answer = 3000.0_r_def

    do k = 0,2
      @assertEqual(answer, r_u_proxy%data(map_w2(1)+k), tol)
      @assertEqual(answer, r_u_proxy%data(map_w2(3)+k), tol)
    end do

    answer = 500.0_r_def

    do k = 0,2
      @assertEqual(answer, r_u_proxy%data(map_w2(2)+k), tol)
      @assertEqual(answer, r_u_proxy%data(map_w2(4)+k), tol)
    end do

    answer = 0.0_r_def

    do k = 0,2
      @assertEqual(answer, r_u_proxy%data(map_w2(5)+k), tol)
      @assertEqual(answer, r_u_proxy%data(map_w2(6)+k), tol)
    end do

  end subroutine test_all

end module map_u_kernel_mod_test
