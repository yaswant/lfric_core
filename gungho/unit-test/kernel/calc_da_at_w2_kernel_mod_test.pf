!-----------------------------------------------------------------------------
! Copyright (c) 2017,  Met Office, on behalf of HMSO and Queen's Printer
! For further details please refer to the file LICENCE.original which you
! should have received as part of this distribution.
!-----------------------------------------------------------------------------

!> Test the computation of face areas
!>
module calc_dA_at_w2_kernel_mod_test

  use constants_mod,                 only : i_def, r_def

  use get_unit_test_m3x3_q3x3x3_sizes_mod, only : get_w0_m3x3_q3x3x3_size, &
                                                  get_w2_m3x3_q3x3x3_size

  use get_unit_test_m3x3_dofmap_mod,       only : get_w0_m3x3_dofmap, &
                                                  get_w2_m3x3_dofmap

  use get_unit_test_q3x3x3_basis_mod,      only : get_w0_q3x3x3_diff_basis

  use yaxt,                          only : xt_initialize, xt_finalize
  use mpi_mod,                       only : store_comm, clear_comm
  use pFUnit_Mod

  implicit none

  private
  public :: test_all

  @TestCase
  type, extends(MPITestCase), public :: calc_dA_at_w2_test_type
    private
  contains
    procedure setUp
    procedure tearDown
    procedure test_all
  end type calc_dA_at_w2_test_type

contains

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  subroutine setUp( this )

    use feign_config_mod,          only : feign_formulation_config

    implicit none

    class(calc_dA_at_w2_test_type), intent(inout) :: this

    ! Initialise YAXT
    call xt_initialize(this%getMpiCommunicator())
    ! Store the MPI communicator for later use
    call store_comm(this%getMpiCommunicator())

    call feign_formulation_config( dlayer_on=.false.,            &
                                   dry_static_adjust=.false.,    &
                                   exner_from_eos=.false.,       &
                                   init_exner_bt=.false.,        &
                                   rotating=.true.,              &
                                   l_multigrid=.false.,          &
                                   set_hydrostatic_init=.false., &
                                   shallow=.false.,              &
                                   transport_only=.false.,       &
                                   use_moisture=.false.,         &
                                   use_physics=.false.,          &
                                   use_wavedynamics=.false.,     &
                                   si_momentum_equation=.true.,  &
                                   vector_invariant=.false.,     &
                                   moisture_conservation=.false. )

  end subroutine setUp

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  subroutine tearDown( this )

    implicit none

    class(calc_dA_at_w2_test_type), intent(inout) :: this

    ! Finalise YAXT
    call xt_finalize()
    ! Clear the stored MPI communicator
    call clear_comm()

  end subroutine tearDown

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  @Test( npes=[1] )
  subroutine test_all( this )

    use calc_dA_at_w2_kernel_mod, only : calc_dA_at_w2_code

    implicit none

    class(calc_dA_at_w2_test_type), intent(inout) :: this

    real(kind=r_def), parameter :: tol  = 1.0e-8_r_def
    real(kind=r_def), parameter :: dx   = 4.0_r_def
    real(kind=r_def), parameter :: dy   = 7.0_r_def
    real(kind=r_def), parameter :: dz   = 13.0_r_def
    real(kind=r_def), parameter :: dzdx = 0.75   ! slope to mimic orography

    ! Reference element parameters for the faces of the cube.
    integer(i_def), parameter :: W=1 !< "West" face of the cell.
    integer(i_def), parameter :: S=2 !< "South" face of the cell.
    integer(i_def), parameter :: E=3 !< "East" face of the cell.
    integer(i_def), parameter :: N=4 !< "North" face of the cell.
    integer(i_def), parameter :: B=5 !< "Bottom" face of the cell.
    integer(i_def), parameter :: T=6 !< "Top" face of the cell.

    ! Fields
    real(r_def), allocatable :: dA_w2(:)
    real(r_def), allocatable :: chi1(:), chi2(:), chi3(:)

    ! Dofmaps
    integer(i_def), allocatable :: map_w0(:,:)
    integer(i_def), allocatable :: map_w2(:,:)

    ! Basis function
    real(r_def), allocatable :: diff_basis_w0(:,:,:,:)

    integer(kind=i_def) :: i, j, k, cell

    ! Sizes
    integer(i_def) :: nlayers, ncells
    integer(i_def) :: ndf_w0, undf_w0, ndf_w2, undf_w2
    integer(i_def) :: dim_space, dim_space_diff
    integer(i_def) :: nqp_h, nqp_v

    nlayers = 3
    call get_w0_m3x3_q3x3x3_size( ndf_w0, undf_w0, ncells,   &
                                  dim_space, dim_space_diff, &
                                  nqp_h, nqp_v,              &
                                  nlayers )
    call get_w2_m3x3_q3x3x3_size( ndf_w2, undf_w2, ncells,   &
                                  dim_space, dim_space_diff, &
                                  nqp_h, nqp_v,              &
                                  nlayers )

    call get_w0_m3x3_dofmap(map_w0)
    call get_w2_m3x3_dofmap(map_w2)

    call get_w0_q3x3x3_diff_basis(diff_basis_w0)

    ! Compute coordinates
    allocate(chi1(undf_w0))
    allocate(chi2(undf_w0))
    allocate(chi3(undf_w0))

    cell = 1
    do j = 1,3
      do i = 1,3
        do k = 0,3
          chi1(map_w0( 1 ,cell ) + k) = real(i-1,r_def) * dx
          chi2(map_w0( 1, cell ) + k) = real(j-1,r_def) * dy
          chi3(map_w0( 1, cell ) + k) = real(k,r_def)   * dz +      &
                                        real(i-1,r_def) * dzdx * dx
        end do
        cell = cell + 1
      end do
    end do

    ! Create the data
    allocate( dA_w2( undf_w2 ) )
    dA_w2(:) = 0.0_r_def

    cell = 1

    call calc_dA_at_w2_code( nlayers,                  &
                             dA_w2,                    &
                             chi1,                     &
                             chi2,                     &
                             chi3,                     &
                             ndf_w2,                   &
                             undf_w2,                  &
                             map_w2(:, cell),          &
                             ndf_w0,                   &
                             undf_w0,                  &
                             map_w0(:, cell),          &
                             diff_basis_w0 )

    ! dx = 4, dy = 7, dz = 13, dzdx=.75, hence areas are
    ! N,S: 7*13 = 91
    ! E,W: 4*13 = 52
    ! T,B: 5*7  = 35
    @assertEqual(91.0_r_def, dA_w2( map_w2( N, cell) ), tol)
    @assertEqual(91.0_r_def, dA_w2( map_w2( S, cell) ), tol)
    @assertEqual(52.0_r_def, dA_w2( map_w2( E, cell) ), tol)
    @assertEqual(52.0_r_def, dA_w2( map_w2( W, cell) ), tol)
    @assertEqual(35.0_r_def, dA_w2( map_w2( T, cell) ), tol)
    @assertEqual(35.0_r_def, dA_w2( map_w2( B, cell) ), tol)

    deallocate(dA_w2)
    deallocate(chi1)
    deallocate(chi2)
    deallocate(chi3)
    deallocate(map_w0)
    deallocate(map_w2)
    deallocate(diff_basis_w0)

  end subroutine test_all

end module calc_dA_at_w2_kernel_mod_test
