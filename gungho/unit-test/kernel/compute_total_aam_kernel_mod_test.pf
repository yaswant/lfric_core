!-----------------------------------------------------------------------------
! Copyright (c) 2017,  Met Office, on behalf of HMSO and Queen's Printer
! For further details please refer to the file LICENCE.original which you
! should have received as part of this distribution.
!-----------------------------------------------------------------------------

!> Test the vorticity rhs computation
!>
module compute_total_aam_kernel_mod_test

  use constants_mod,                 only : i_def, r_def
  use function_space_collection_mod, only : function_space_collection_type, &
                                            function_space_collection
  use mesh_collection_mod,           only : mesh_collection_type, &
                                            mesh_collection
  use field_mod,                     only : field_type,      &
                                            field_proxy_type
  use function_space_mod,            only : function_space_type
  use fs_continuity_mod,             only : W0, W2, W3
  use pFUnit_Mod
  use quadrature_mod,                only : quadrature_type, GAUSSIAN

  implicit none

  private
  public :: compute_total_aam_kernel_test_type, test_all

  @TestCase
  type, extends(TestCase) :: compute_total_aam_kernel_test_type
    private
    type(quadrature_type)              :: qr
    type(function_space_type), pointer :: w0_fs => null()
    type(function_space_type), pointer :: w2_fs => null()
    type(function_space_type), pointer :: w3_fs => null()
    type(field_type)                   :: chi(3)
    type(field_type)                   :: u, rho
    real(r_def), allocatable           :: basis_w3(:,:,:,:), &
                                          basis_w2(:,:,:,:), &
                                          basis_w0(:,:,:,:), &
                                          diff_basis_w0(:,:,:,:)
  contains
    procedure setUp
    procedure tearDown
    procedure test_all
  end type compute_total_aam_kernel_test_type

  integer(i_def), parameter :: element_order = 0

  real(r_def), parameter :: gravity = 10.0_r_def
  real(r_def), parameter :: radius  = 6000000_r_def
  real(r_def), parameter :: omega   = 8.0E-5_r_def
  real(r_def), parameter :: p_zero  = 100000.0_r_def
  real(r_def), parameter :: rd      = 300.0_r_def
  real(r_def), parameter :: cp      = 1000.0_r_def
  real(r_def), parameter :: scaling = 1.0_r_def

contains

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  subroutine setUp( this )

    use base_mesh_config_mod,      only : base_mesh_geometry_planar, &
                                          base_mesh_partitioner_planar
    use finite_element_config_mod, only : finite_element_cellshape_quadrilateral
    use gungho_feign_config_mod,   only : feign_base_mesh_config, &
                                          feign_planet_config
    use function_space_mod,        only : function_space_type
    use mesh_mod,                  only : PLANE_BI_PERIODIC
    use reference_element_mod,     only : reference_cube,   &
                                          reference_element

    implicit none

    class(compute_total_aam_kernel_test_type), intent(inout) :: this

    integer(i_def), parameter :: element_order = 0

    type(field_proxy_type)    :: chi_proxy(3)
    type(field_proxy_type)    :: rho_proxy, &
                                 u_proxy

    integer(i_def) :: ndf_w0, ndf_w2, ndf_w3
    integer(i_def) :: undf_w0, undf_w2, undf_w3
    integer(i_def) :: dim_w0, diff_dim_w0, dim_w2, dim_w3
    integer(i_def) :: nqp_h, nqp_v

    integer(i_def) :: mesh_id

    integer :: i, err

    call feign_base_mesh_config( filename='foo', prime_mesh_name='unit_test', &
                                 geometry=base_mesh_geometry_planar,          &
                                 partitioner=base_mesh_partitioner_planar,    &
                                 fplane=.false., f_lat_deg=0.0_r_def )

    call feign_planet_config( gravity=gravity, radius=radius, omega=omega, &
                              rd=rd, cp=cp, p_zero=p_zero,                 &
                              scaling_factor=scaling )

    reference_element = finite_element_cellshape_quadrilateral
    call reference_cube()

    ! Create top level mesh collection
    mesh_collection = mesh_collection_type()
    ! Create top level function space collection
    function_space_collection = function_space_collection_type()

    this%qr = quadrature_type( 3, GAUSSIAN )

    nqp_h=this%qr%get_nqp_h()
    nqp_v=this%qr%get_nqp_v()

    ! Dummy mesh mod has 9 cells, 3 layers and is uniform in vertical
    mesh_id = mesh_collection%add_unit_test_mesh( PLANE_BI_PERIODIC )

    this%w0_fs => function_space_collection%get_fs(mesh_id, element_order, W0)
    this%w2_fs => function_space_collection%get_fs(mesh_id, element_order, W2)
    this%w3_fs => function_space_collection%get_fs(mesh_id, element_order, W3)

    this%rho  = field_type( vector_space = this%w3_fs )
    rho_proxy = this%rho%get_proxy()

    ndf_w3  = rho_proxy%vspace%get_ndf()
    dim_w3  = rho_proxy%vspace%get_dim_space()
    undf_w3 = rho_proxy%vspace%get_undf()

    this%u  = field_type( vector_space = this%w2_fs )
    u_proxy = this%u%get_proxy()

    ndf_w2      = u_proxy%vspace%get_ndf()
    dim_w2      = u_proxy%vspace%get_dim_space()
    undf_w2     = u_proxy%vspace%get_undf()

    ! Make a coordinate field with a function space
    do i = 1,3
      this%chi(i) = field_type( vector_space = this%w0_fs )
      chi_proxy(i) = this%chi(i)%get_proxy()
    end do

    ndf_w0      = chi_proxy(1)%vspace%get_ndf()
    dim_w0      = chi_proxy(1)%vspace%get_dim_space()
    diff_dim_w0 = chi_proxy(1)%vspace%get_dim_space_diff()
    undf_w0     = chi_proxy(1)%vspace%get_undf()

    allocate( this%basis_w3(dim_w3,ndf_w3,nqp_h,nqp_v) )
    allocate( this%basis_w2(dim_w2,ndf_w2,nqp_h,nqp_v) )
    allocate( this%basis_w0(dim_w0,ndf_w0,nqp_h,nqp_v) )
    allocate( this%diff_basis_w0(diff_dim_w0,ndf_w0,nqp_h,nqp_v) )

  end subroutine setUp

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  subroutine tearDown( this )

    use reference_element_mod, only : deallocate_reference
    implicit none

    class(compute_total_aam_kernel_test_type), intent(inout) :: this

    deallocate(this%basis_w3, this%basis_w2,  &
               this%basis_w0, this%diff_basis_w0)

    call mesh_collection%clear()
    call function_space_collection%clear()
    call deallocate_reference()

  end subroutine tearDown

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  @test
  subroutine test_all( this )

    use compute_total_aam_kernel_mod, only : compute_total_aam_code

    implicit none

    class(compute_total_aam_kernel_test_type), intent(inout) :: this

    real(r_def), parameter :: answer = 0.0_r_def
    real(r_def), parameter :: tol    = 1.0e-6_r_def
    real(r_def), parameter :: dx     = 4.0_r_def, &
                              dy     = 3.0_r_def, &
                              dz     = 2.0_r_def

    type(field_type)          :: aam
    type(field_proxy_type)    :: chi_proxy(3)
    type(field_proxy_type)    :: rho_proxy, &
                                 u_proxy,   &
                                 aam_proxy

    integer,                   pointer :: map_w3(:) => null(), &
                                          map_w2(:) => null(), &
                                          map_w0(:) => null()
    real(r_def),               pointer :: xp(:,:) => null()
    real(r_def),               pointer :: zp(:) => null()
    real(r_def),               pointer :: wh(:) => null(), wv(:) => null()

    integer :: i, j, k, cell

    integer          :: nlayers, nqp_h, nqp_v
    integer          :: ndf_w0, ndf_w2, ndf_w3
    integer          :: undf_w0, undf_w2, undf_w3


    ! Make a coordinate field with a function space
    do i = 1,3
      chi_proxy(i) = this%chi(i)%get_proxy()
    end do
    ! Compute coordinates
    cell = 1
    do j = 1,3
      do i = 1,3
        map_w0 => chi_proxy(1)%vspace%get_cell_dofmap( cell )
        do k = 0,3
          chi_proxy(1)%data(map_w0(1) + k) = real(i-1)*dx
          chi_proxy(2)%data(map_w0(1) + k) = real(j-1)*dy
          chi_proxy(3)%data(map_w0(1) + k) = real(k)  *dz
        end do
        cell = cell + 1
      end do
    end do

    ! Test aam computation
    aam = field_type(  vector_space = this%w3_fs )

    u_proxy    = this%u%get_proxy()
    rho_proxy  = this%rho%get_proxy()
    aam_proxy  = aam%get_proxy()

    nlayers = rho_proxy%vspace%get_nlayers()
    nqp_h=this%qr%get_nqp_h()
    nqp_v=this%qr%get_nqp_v()
    zp=>this%qr%get_xqp_v()
    xp=>this%qr%get_xqp_h()
    wh=>this%qr%get_wqp_h()
    wv=>this%qr%get_wqp_v()

    ndf_w3  = rho_proxy%vspace%get_ndf()
    undf_w3 = rho_proxy%vspace%get_undf()

    ndf_w2  = u_proxy%vspace%get_ndf()
    undf_w2 = u_proxy%vspace%get_undf()

    ndf_w0  = chi_proxy(1)%vspace%get_ndf()
    undf_w0 = chi_proxy(1)%vspace%get_undf()

    call rho_proxy%vspace%compute_basis_function( &
         this%basis_w3, ndf_w3, nqp_h, nqp_v, xp, zp)

    call u_proxy%vspace%compute_basis_function( &
         this%basis_w2, ndf_w2, nqp_h, nqp_v, xp, zp)

    call chi_proxy(1)%vspace%compute_basis_function( &
         this%basis_w0, ndf_w0, nqp_h, nqp_v, xp, zp)

    call chi_proxy(1)%vspace%compute_diff_basis_function( &
         this%diff_basis_w0, ndf_w0, nqp_h, nqp_v, xp, zp)

    cell = 1
    ! Create the data
    map_w3 => rho_proxy%vspace%get_cell_dofmap( cell )
    map_w2 => u_proxy%vspace%get_cell_dofmap( cell )
    map_w0 => chi_proxy(1)%vspace%get_cell_dofmap( cell )
    u_proxy%data(:) = 0.0_r_def
    rho_proxy%data(:) = 1.5_r_def
    aam_proxy%data(:) = 0.0_r_def

    call compute_total_aam_code(                                           &
                                nlayers,                                   &
                                aam_proxy%data,                            &
                                u_proxy%data,                              &
                                rho_proxy%data,                            &
                                chi_proxy(1)%data,                         &
                                chi_proxy(2)%data,                         &
                                chi_proxy(3)%data,                         &
                                ndf_w3, undf_w3,                           &
                                map_w3, this%basis_w3,                     &
                                ndf_w2, undf_w2,                           &
                                map_w2, this%basis_w2,                     &
                                ndf_w0, undf_w0,                           &
                                map_w0, this%basis_w0, this%diff_basis_w0, &
                                nqp_h, nqp_v, wh, wv                       &
                                )

    @assertEqual(answer, aam_proxy%data(map_w3(1)), tol)

  end subroutine test_all

end module compute_total_aam_kernel_mod_test
