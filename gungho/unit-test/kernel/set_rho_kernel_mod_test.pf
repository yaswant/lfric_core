!-----------------------------------------------------------------------------
! Copyright (c) 2017,  Met Office, on behalf of HMSO and Queen's Printer
! For further details please refer to the file LICENCE.original which you
! should have received as part of this distribution.
!-----------------------------------------------------------------------------

!> Test the initial rho computation
!>
module set_rho_kernel_mod_test

  use constants_mod,                 only : i_def, r_def
  use function_space_collection_mod, only : function_space_collection_type, &
                                            function_space_collection
  use mesh_collection_mod,           only : mesh_collection_type, &
                                            mesh_collection
  use field_mod,                     only : field_type,      &
                                            field_proxy_type
  use function_space_mod,            only : function_space_type, W0, W3
  use fs_continuity_mod,             only : W0, W3
  use pFUnit_Mod
  use quadrature_mod,                only : quadrature_type, GAUSSIAN

  implicit none 

  private
  public :: set_rho_kernel_test_type, test_all

  @TestCase
  type, extends(TestCase) :: set_rho_kernel_test_type
    private
    type(quadrature_type)              :: qr
    type(function_space_type), pointer :: w0_fs => null()
    type(function_space_type), pointer :: w3_fs => null()
    type(field_type)                   :: chi(3)
    type(field_type)                   :: rho
    real(r_def)                        :: time
    real(r_def), allocatable           :: basis_rho(:,:,:,:), &
                                          basis_chi(:,:,:,:), &
                                          diff_basis_chi(:,:,:,:)
    integer(i_def)                     :: mesh_id
  contains
    procedure setUp
    procedure tearDown
    procedure test_all
  end type set_rho_kernel_test_type

  integer(i_def), parameter :: element_order = 0

  real(r_def), parameter :: gravity = 9.80665_r_def
  real(r_def), parameter :: radius  = 6371229.0_r_def
  real(r_def), parameter :: omega   = 8.0E-5_r_def
  real(r_def), parameter :: p_zero  = 100000.0_r_def
  real(r_def), parameter :: rd      = 287.05_r_def
  real(r_def), parameter :: cp      = 1005.0_r_def
  real(r_def), parameter :: scaling = 1.0_r_def

  real(r_def), parameter :: tracer_max        = 2.0_r_def
  real(r_def), parameter :: tracer_background = 0.1_r_def
  real(r_def), parameter :: y1                = 0.0_r_def
  real(r_def), parameter :: y2                = 0.0_r_def

  real(r_def), parameter :: bvf_square = 0.0001_r_def

contains

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  subroutine setUp( this )


    use base_mesh_config_mod,      only : base_mesh_geometry_planar, &
                                          base_mesh_partitioner_planar
    use finite_element_config_mod, only : finite_element_cellshape_quadrilateral
    use gungho_feign_config_mod,   only : feign_base_mesh_config,           &
                                          feign_idealised_config,           &
                                          feign_initial_density_config,     &
                                          feign_initial_temperature_config, &
                                          feign_planet_config
    use idealised_config_mod,      only : idealised_test_gravity_wave
    use mesh_mod,                  only : PLANE_BI_PERIODIC
    use reference_element_mod,     only : reference_cube, reference_element

    implicit none

    class(set_rho_kernel_test_type), intent(inout) :: this

    type(field_proxy_type)    :: rho_p
    type(field_proxy_type)    :: chi_p(3)


    integer(i_def) :: ndf_rho, undf_rho, dim_rho, &
                      ndf_chi, undf_chi, dim_chi, dim_diff_chi, &
                      nqp_h, nqp_v

    integer :: i

    call feign_base_mesh_config( filename='foo', prime_mesh_name='unit_test', &
                                 geometry=base_mesh_geometry_planar,          &
                                 partitioner=base_mesh_partitioner_planar,    &
                                 fplane=.false., f_lat_deg=0.0_r_def )

    call feign_idealised_config( idealised_test_gravity_wave )
    call feign_initial_density_config( r1=0.0_r_def, x1=0.0_r_def, &
                                       tracer_background=tracer_background, tracer_max=tracer_max, &
                                       r2=0.0_r_def, x2=0.0_r_def, y1=y1, y2=y2, &
                                       sample_init_rho=.false. )
    call feign_initial_temperature_config( bvf_square )

    call feign_planet_config( gravity=gravity, radius=radius, omega=omega, &
                              rd=rd, cp=cp, p_zero=p_zero,                 &
                              scaling_factor=scaling )

    reference_element = finite_element_cellshape_quadrilateral
    call reference_cube()

    ! Create top level mesh collection
    mesh_collection = mesh_collection_type()
    ! Create top level function space collection
    function_space_collection = function_space_collection_type()

    this%qr = quadrature_type( 3, GAUSSIAN )

    nqp_h=this%qr%get_nqp_h()
    nqp_v=this%qr%get_nqp_v()

    ! Dummy mesh mod has 9 cells, 3 layers and is uniform in vertical
    this%mesh_id = mesh_collection%add_unit_test_mesh( PLANE_BI_PERIODIC )

    this%w0_fs => function_space_collection%get_fs( this%mesh_id, &
                                                    element_order, &
                                                    W0 )
    this%w3_fs => function_space_collection%get_fs( this%mesh_id, &
                                                    element_order, &
                                                    W3 )

    this%rho = field_type( vector_space = this%w3_fs )
    rho_p    = this%rho%get_proxy()

    ndf_rho  = rho_p%vspace%get_ndf( )
    undf_rho = rho_p%vspace%get_undf( )
    dim_rho  = rho_p%vspace%get_dim_space( )

    do i = 1,3
      this%chi(i) = field_type( vector_space = this%w0_fs )
      chi_p(i)    = this%chi(i)%get_proxy()
    end do

    ndf_chi  = chi_p(1)%vspace%get_ndf( )
    undf_chi = chi_p(1)%vspace%get_undf( )
    dim_chi  = chi_p(1)%vspace%get_dim_space( )
    dim_diff_chi  = chi_p(1)%vspace%get_dim_space_diff( )

    allocate( this%basis_rho(dim_rho, ndf_rho, nqp_h, nqp_v), &
              this%basis_chi(dim_chi, ndf_chi, nqp_h, nqp_v), &
              this%diff_basis_chi(dim_diff_chi, ndf_chi, nqp_h, nqp_v) )

  end subroutine setUp

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  subroutine tearDown( this )

    use reference_element_mod, only : deallocate_reference

    implicit none

    class(set_rho_kernel_test_type), intent(inout) :: this

    deallocate( this%basis_rho, this%basis_chi )

    call function_space_collection%clear()
    call mesh_collection%clear()
    call deallocate_reference()

  end subroutine tearDown

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  @test
  subroutine test_all( this )

    use set_rho_kernel_mod,     only : set_rho_code
    use mesh_mod,                   only : mesh_type

    implicit none

    class(set_rho_kernel_test_type), intent(inout) :: this

    real(r_def), parameter :: tol = 1.0e-6_r_def
    real(r_def), parameter :: dx = 6000.0_r_def, &
                              dy = 2000.0_r_def

    ! Information for testing initial rho
    type(field_proxy_type)    :: rho_p
    type(field_proxy_type )   :: chi_p(3)
    real(kind=r_def)          :: time

    type(mesh_type),pointer   :: mesh => null()

    integer          :: ndf_rho, undf_rho, &
                        ndf_chi, undf_chi, &
                        nqp_h, nqp_v


    integer, pointer :: map_rho(:) => null(), &
                        map_chi(:) => null()

    real(kind=r_def), pointer :: xp(:,:) => null()
    real(kind=r_def), pointer :: zp(:)   => null()
    real(kind=r_def), pointer :: wh(:), wv(:) => null()

    real(r_def) :: answer(3)

    integer :: i, j, k, cell
    integer(i_def) :: err

    real(r_def) :: dz, dz_test(3)

    mesh => mesh_collection%get_mesh( this%mesh_id )
    call mesh%get_dz(dz_test)
    dz = dz_test(1)

    do i = 1,3
      chi_p(i) = this%chi(i)%get_proxy()
    end do

    ! Compute coordinates
    cell = 1
    do j = 1,3
      do i = 1,3
        map_chi => chi_p(1)%vspace%get_cell_dofmap( cell )
        do k = 0,3
          chi_p(1)%data(map_chi(1) + k) = real(i-1)*dx
          chi_p(2)%data(map_chi(1) + k) = real(j-1)*dy
          chi_p(3)%data(map_chi(1) + k) = real(k)  *dz
        end do
        cell = cell + 1
      end do
    end do

    ! Test initial rho
    rho_p = this%rho%get_proxy()

    ndf_rho  = rho_p%vspace%get_ndf( )
    undf_rho = rho_p%vspace%get_undf( )

    ndf_chi  = chi_p(1)%vspace%get_ndf( )
    undf_chi = chi_p(1)%vspace%get_undf( )

    nqp_h=this%qr%get_nqp_h()
    nqp_v=this%qr%get_nqp_v()
    zp=>this%qr%get_xqp_v()
    xp=>this%qr%get_xqp_h()
    wh=>this%qr%get_wqp_h()
    wv=>this%qr%get_wqp_v()

    call rho_p%vspace%compute_basis_function( &
         this%basis_rho, ndf_rho, nqp_h, nqp_v, xp, zp)

    call chi_p(1)%vspace%compute_basis_function( &
         this%basis_chi, ndf_chi, nqp_h, nqp_v, xp, zp)

    call chi_p(1)%vspace%compute_diff_basis_function( &
         this%diff_basis_chi, ndf_chi, nqp_h, nqp_v, xp, zp)

    time = 0.0_r_def
    cell = 1
    map_rho => rho_p%vspace%get_cell_dofmap( cell )
    map_chi => chi_p(1)%vspace%get_cell_dofmap( cell )
    call set_rho_code( rho_p%vspace%get_nlayers(), &
                       rho_p%data,                 &
                       chi_p(1)%data,              &
                       chi_p(2)%data,              &
                       chi_p(3)%data,              &
                       time,                       &
                       ndf_rho,                    &
                       undf_rho,                   &
                       map_rho,                    &
                       this%basis_rho,             &
                       ndf_chi,                    &
                       undf_chi,                   &
                       map_chi,                    &
                       this%basis_chi,             &
                       this%diff_basis_chi,        &
                       nqp_h,                      &
                       nqp_v,                      &
                       wh,                         &
                       wv )

    answer(1) = 1.059869_r_def
    answer(2) = 0.8758241_r_def
    answer(3) = 0.7175226_r_def
    do i = 1,3
      @assertEqual(answer(i), rho_p%data(i), tol)
    end do

  end subroutine test_all

end module set_rho_kernel_mod_test
