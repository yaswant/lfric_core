!-----------------------------------------------------------------------------
! Copyright (c) 2017,  Met Office, on behalf of HMSO and Queen's Printer
! For further details please refer to the file LICENCE.original which you
! should have received as part of this distribution.
!-----------------------------------------------------------------------------

module sample_poly_adv_kernel_mod_test

  use constants_mod,                 only : i_def, r_def
  use function_space_collection_mod, only : function_space_collection_type, &
                                            function_space_collection
  use mesh_collection_mod,           only : mesh_collection_type, &
                                            mesh_collection

  use field_mod,                     only : field_type,      &
                                            field_proxy_type
  use fs_continuity_mod,             only : W2, Wtheta, W0
  use function_space_mod,            only : function_space_type
  use pFUnit_Mod
  use stencil_dofmap_mod,            only : stencil_dofmap_type, STENCIL_CROSS

  implicit none

  private
  public :: test_all

  @TestCase
  type, extends(TestCase), public :: sample_poly_adv_test_type
    private
    type(function_space_type), pointer :: w2_fs => null()
    type(function_space_type), pointer :: wt_fs => null()
    type(function_space_type), pointer :: w0_fs => null()
    type(field_type)                   :: theta, u, adv, mt_lumped_inv 
    type(field_type)                   :: chi(3)
    real(r_def), allocatable           :: basis_u(:,:,:)
    real(r_def), allocatable           :: basis_x(:,:,:)
    real(r_def), allocatable           :: diff_basis_x(:,:,:)
    type(stencil_dofmap_type), pointer :: stencil => null()
    type(stencil_dofmap_type), pointer :: stencil_x => null()
    integer(i_def)                     :: mesh_id
  contains
    procedure setUp
    procedure tearDown
    procedure test_all
  end type sample_poly_adv_test_type

  integer(i_def), parameter :: element_order = 0

contains

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  subroutine setUp( this )

    use finite_element_config_mod, only : finite_element_cellshape_quadrilateral
    use mesh_mod,                  only : PLANE_BI_PERIODIC
    use reference_element_mod,     only : reference_cube, reference_element
    use gungho_feign_config_mod,   only : feign_transport_config, &
                                          feign_base_mesh_config
    use base_mesh_config_mod,      only : base_mesh_geometry_planar, &
                                          base_mesh_partitioner_planar
    use transport_config_mod,      only : transport_scheme_method_of_lines, &
                                          transport_operators_fv
    implicit none

    class(sample_poly_adv_test_type), intent(inout) :: this

    type(field_proxy_type)    :: u_p, theta_p, mt_lumped_inv_p
    type(field_proxy_type)    :: chi_p(3)
    integer(i_def) :: dim_u, ndf_u, ndf_t
    integer(i_def) :: ndf_chi, undf_chi, dim_chi, dim_diff_chi
    integer :: i

    call feign_base_mesh_config( filename='foo', prime_mesh_name='unit_test', &
                                 geometry=base_mesh_geometry_planar,          &
                                 partitioner=base_mesh_partitioner_planar,    &
                                 fplane=.false., f_lat_deg=0.0_r_def )

    reference_element = finite_element_cellshape_quadrilateral
    call reference_cube()

    call feign_transport_config(scheme = transport_scheme_method_of_lines, &
                                operators = transport_operators_fv,        &
                                fv_flux_order = 0,                         &
                                fv_advective_order = 1,                    &
                                consistent_metric = .true.,                &
				enforce_monotonicity = .false. )

    ! Create top level mesh collection
    mesh_collection = mesh_collection_type()
    ! Create top level function space collection
    function_space_collection = function_space_collection_type()

    ! Dummy mesh mod has 9 cells, 3 layers and is uniform in vertical
    this%mesh_id = mesh_collection%add_unit_test_mesh( PLANE_BI_PERIODIC )

    this%w2_fs => function_space_collection%get_fs(this%mesh_id, element_order, W2)
    this%wt_fs => function_space_collection%get_fs(this%mesh_id, element_order, Wtheta)
    this%w0_fs => function_space_collection%get_fs(this%mesh_id, element_order, W0)

    this%u = field_type( vector_space = this%w2_fs )
    u_p    = this%u%get_proxy()
    ndf_u  = u_p%vspace%get_ndf( )    
    dim_u  = u_p%vspace%get_dim_space( )

    this%theta = field_type( vector_space = this%wt_fs )
    this%adv   = field_type( vector_space = this%wt_fs )
    this%mt_lumped_inv = field_type( vector_space = this%wt_fs )

    do i = 1,3
      this%chi(i) = field_type( vector_space = this%w0_fs )
      chi_p(i)    = this%chi(i)%get_proxy()
    end do
    theta_p    = this%theta%get_proxy()
    ndf_t      = theta_p%vspace%get_ndf( )    
    this%stencil => theta_p%vspace%get_stencil_dofmap(STENCIL_CROSS,1)

    ndf_chi  = chi_p(1)%vspace%get_ndf( )
    undf_chi = chi_p(1)%vspace%get_undf( )
    dim_chi  = chi_p(1)%vspace%get_dim_space( )
    dim_diff_chi  = chi_p(1)%vspace%get_dim_space_diff( )
    this%stencil_x => chi_p(1)%vspace%get_stencil_dofmap(STENCIL_CROSS,1)

    allocate( this%basis_x(dim_chi, ndf_chi, ndf_t), &
              this%diff_basis_x(dim_diff_chi, ndf_chi, ndf_t) )

    allocate( this%basis_u(dim_u, ndf_u, ndf_t) )

  end subroutine setUp

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  subroutine tearDown( this )

    use reference_element_mod, only : deallocate_reference

    implicit none

    class(sample_poly_adv_test_type), intent(inout) :: this

    deallocate( this%basis_u, this%basis_x, this%diff_basis_x )

    call function_space_collection%clear()
    call mesh_collection%clear()
    call deallocate_reference()

  end subroutine tearDown

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  @test
  subroutine test_all( this )

    use sample_poly_adv_kernel_mod,     only : sample_poly_adv_code, &
                                               sample_poly_adv_init
    use function_space_mod,             only : BASIS, DIFF_BASIS
    use mesh_mod,                       only : mesh_type

    implicit none

    class(sample_poly_adv_test_type), intent(inout) :: this
    real(r_def), parameter :: dx = 6000.0_r_def, &
                              dy = 2000.0_r_def
    real(r_def), parameter :: tol = 1.0e-12_r_def
    type(field_proxy_type )   :: chi_p(3)
    real(r_def) :: answer
    integer     :: ndf_chi, undf_chi

    integer(i_def) :: cell, i, j, k

    ! Variables for testing sample_poly_adv

    integer          :: ndf_theta, undf_theta, dim_u, &
                        ndf_u, undf_u, ndf_adv
    integer          :: df_u, df_adv, df_x
    type(mesh_type),pointer   :: mesh => null()
    integer, pointer          :: stencil_map(:,:,:) => null()
    integer, pointer          :: stencil_map_x(:,:,:) => null()
    integer, pointer          :: map_u(:,:) => null() 
    integer, pointer          :: map_chi(:) => null() 
    integer, pointer          :: map_theta(:,:) => null() 
    integer, pointer          :: map_wx(:,:) => null() 
    real(kind=r_def), pointer :: nodes_adv(:,:) => null()

    type(field_proxy_type)   :: adv_p, theta_p, u_p, mt_lumped_inv_p
    real(r_def) :: dz, dz_test(3)

    mesh => mesh_collection%get_mesh( this%mesh_id )
    call mesh%get_dz(dz_test)
    dz = dz_test(1)

    do i = 1,3
      chi_p(i) = this%chi(i)%get_proxy()
    end do

    ! Compute coordinates
    cell = 1
    do j = 1,3
      do i = 1,3
        map_chi => chi_p(1)%vspace%get_cell_dofmap( cell )
        do k = 0,3
          chi_p(1)%data(map_chi(1) + k) = real(i-1)*dx
          chi_p(2)%data(map_chi(1) + k) = real(j-1)*dy
          chi_p(3)%data(map_chi(1) + k) = real(k)  *dz
        end do
        cell = cell + 1
      end do
    end do
    ndf_chi  = chi_p(1)%vspace%get_ndf( )
    undf_chi = chi_p(1)%vspace%get_undf( )

    ! Test adv computation

    adv_p     = this%adv%get_proxy()
    theta_p   = this%theta%get_proxy()
    u_p       = this%u%get_proxy()
    mt_lumped_inv_p=this%mt_lumped_inv%get_proxy()

    ndf_u  = u_p%vspace%get_ndf( )
    undf_u = u_p%vspace%get_undf( )
    dim_u  = u_p%vspace%get_dim_space( )

    ndf_theta  = theta_p%vspace%get_ndf( )
    undf_theta = theta_p%vspace%get_undf( )

    ndf_adv   = adv_p%vspace%get_ndf()
    nodes_adv => adv_p%vspace%get_nodes()

    ! Evaluate the basis function
    do df_adv = 1, ndf_adv
      do df_u = 1, ndf_u
        this%basis_u(:,df_u,df_adv) = u_p%vspace%call_function(BASIS,df_u,nodes_adv(:,df_adv))
      end do
      do df_x = 1, ndf_chi
        this%basis_x(:,df_x,df_adv) = chi_p(1)%vspace%call_function(BASIS,df_x,nodes_adv(:,df_adv))
        this%diff_basis_x(:,df_x,df_adv) = chi_p(1)%vspace%call_function(DIFF_BASIS,df_x,nodes_adv(:,df_adv))
      end do
    end do

    stencil_map => this%stencil%get_whole_dofmap()
    stencil_map_x => this%stencil_x%get_whole_dofmap()
    map_u => u_p%vspace%get_whole_dofmap()
    map_theta => theta_p%vspace%get_whole_dofmap()
    map_wx => chi_p(1)%vspace%get_whole_dofmap()

    ! Create the data
    cell = 5
    u_p%data(:) = 0.0_r_def
    theta_p%data(:) = 2.0_r_def       
    u_p%data(map_u(1,cell)+0) = 2.0_r_def
    u_p%data(map_u(3,cell)+0) = 3.0_r_def
    theta_p%data(stencil_map(1,1,cell)) = 3.0_r_def
    theta_p%data(stencil_map(1,2,cell)) = 1.0_r_def
    theta_p%data(stencil_map(1,4,cell)) = 5.0_r_def

    ! This isn't used unless enforce_monotonicity=.true., 
    ! so set to something sensible
    mt_lumped_inv_p%data(:) = 1.0_r_def 

    ! Intitialise kernel
    call sample_poly_adv_init(1, adv_p%vspace%get_nlayers())
    adv_p%data(:) = 0.0_r_def

    call sample_poly_adv_code(adv_p%vspace%get_nlayers(), &
                              adv_p%data, & 
                              theta_p%data, &
                              5, &
                              stencil_map(:,:,cell),&
                              u_p%data, &
                              mt_lumped_inv_p%data, &
                              chi_p(1)%data, &
                              5, &
                              stencil_map_x(:,:,cell), &
                              chi_p(2)%data, &
                              chi_p(3)%data, &
                              ndf_theta, &
                              undf_theta, &
                              map_theta(:,cell), &
                              ndf_u, &
                              undf_u, & 
                              map_u(:,cell), &
                              this%basis_u, &
                              ndf_chi, &
                              undf_chi, &                              
                              map_wx(:,cell), &
                              this%basis_x, &
                              this%diff_basis_x &
                              )
     ! u.grad(theta) = 5, but need extra half factor to account for 
     ! weighting of mass matrix term: int(1-z,z=0..1) = 1/2
     ! that is not included here but would be for a full run
     answer = 2.5_r_def 
     @assertEqual(answer, adv_p%data(stencil_map(1,1,cell) ), tol)
     answer = 0.0_r_def 
     @assertEqual(answer, adv_p%data(stencil_map(1,1,cell)+1), tol)

  end subroutine test_all

end module sample_poly_adv_kernel_mod_test
