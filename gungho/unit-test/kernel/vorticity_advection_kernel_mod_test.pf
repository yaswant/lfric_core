!-----------------------------------------------------------------------------
! Copyright (c) 2017,  Met Office, on behalf of HMSO and Queen's Printer
! For further details please refer to the file LICENCE.original which you
! should have received as part of this distribution.
!-----------------------------------------------------------------------------

!> Test the vorticity advection kernel computation
!>
module vorticity_advection_kernel_mod_test

  use constants_mod,                 only : i_def, r_def
  use function_space_collection_mod, only : function_space_collection_type, &
                                            function_space_collection
  use mesh_collection_mod,           only : mesh_collection_type, &
                                            mesh_collection
  use field_mod,                     only : field_type, &
                                            field_proxy_type
  use fs_continuity_mod,             only : W0, W1, W2, W3
  use function_space_mod,            only : function_space_type
  use pFUnit_Mod
  use quadrature_mod,                only : quadrature_type, GAUSSIAN

  implicit none

  private
  public :: vorticity_advection_test_type, test_all

  @TestCase
  type, extends(TestCase) :: vorticity_advection_test_type
    private
    type(quadrature_type)              :: qr
    type(function_space_type), pointer :: w0_fs => null()
    type(function_space_type), pointer :: w1_fs => null()
    type(function_space_type), pointer :: w2_fs => null()
    type(function_space_type), pointer :: w3_fs => null()
    type(field_type)                   :: chi(3)
    type(field_type)                   :: r_u, rho, xi
    real(r_def), allocatable           :: basis_w3(:,:,:,:), &
                                          basis_w2(:,:,:,:), &
                                          basis_w1(:,:,:,:), &
                                          diff_basis_w0(:,:,:,:)
  contains
    procedure setUp
    procedure tearDown
    procedure test_all
  end type vorticity_advection_test_type

  integer(i_def), parameter :: element_order = 0

contains

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  subroutine setUp( this )

    use base_mesh_config_mod,      only : base_mesh_geometry_planar, &
                                          base_mesh_partitioner_planar
    use finite_element_config_mod, only : finite_element_cellshape_quadrilateral
    use gungho_feign_config_mod,   only : feign_base_mesh_config
    use mesh_mod,                  only : PLANE_BI_PERIODIC
    use reference_element_mod,     only : reference_cube, reference_element
    use reference_profile_mod,     only : reference_profile

    implicit none

    class(vorticity_advection_test_type), intent(inout) :: this

    type(field_proxy_type)    :: chi_p(3)
    type(field_proxy_type)    :: r_u_proxy, rho_proxy, xi_proxy

    integer(i_def) :: nqp_h, nqp_v
    integer(i_def) :: ndf_w0, ndf_w1, ndf_w2, ndf_w3
    integer(i_def) :: undf_w0, undf_w1, undf_w2, undf_w3
    integer(i_def) :: dim_w1, diff_dim_w0, dim_w0, dim_w2, dim_w3
    integer(i_def) :: mesh_id

    integer(i_def) :: i

    call feign_base_mesh_config( filename='foo', prime_mesh_name='unit_test', &
                                 geometry=base_mesh_geometry_planar,          &
                                 partitioner=base_mesh_partitioner_planar,    &
                                 fplane=.false., f_lat_deg=0.0_r_def )

    reference_element = finite_element_cellshape_quadrilateral
    call reference_cube()

    ! Create top level mesh collection
    mesh_collection = mesh_collection_type()
    ! Create top level function space collection
    function_space_collection = function_space_collection_type()

    this%qr = quadrature_type( 3, GAUSSIAN )

    nqp_h=this%qr%get_nqp_h()
    nqp_v=this%qr%get_nqp_v()

    ! Dummy mesh mod has 9 cells, 3 layers and is uniform in vertical
    mesh_id = mesh_collection%add_unit_test_mesh( PLANE_BI_PERIODIC )

    this%w0_fs => function_space_collection%get_fs(mesh_id, element_order, W0)
    this%w1_fs => function_space_collection%get_fs(mesh_id, element_order, W1)
    this%w2_fs => function_space_collection%get_fs(mesh_id, element_order, W2)
    this%w3_fs => function_space_collection%get_fs(mesh_id, element_order, W3)

    this%rho  = field_type( vector_space = this%w3_fs )
    rho_proxy = this%rho%get_proxy()

    ndf_w3  = rho_proxy%vspace%get_ndf( )
    dim_w3  = rho_proxy%vspace%get_dim_space( )
    undf_w3 = rho_proxy%vspace%get_undf()
    allocate(this%basis_w3(dim_w3,ndf_w3,nqp_h,nqp_v))

    this%r_u  = field_type( vector_space = this%w2_fs )
    r_u_proxy = this%r_u%get_proxy()

    ndf_w2  = r_u_proxy%vspace%get_ndf( )
    dim_w2  = r_u_proxy%vspace%get_dim_space( )
    undf_w2 = r_u_proxy%vspace%get_undf()
    allocate(this%basis_w2(dim_w2,ndf_w2,nqp_h,nqp_v))

    this%xi  = field_type( vector_space = this%w1_fs )
    xi_proxy = this%xi%get_proxy()

    ndf_w1  = xi_proxy%vspace%get_ndf( )
    dim_w1  = xi_proxy%vspace%get_dim_space( )
    undf_w1 = xi_proxy%vspace%get_undf()
    allocate(this%basis_w1(dim_w1,ndf_w1,nqp_h,nqp_v))

    ! Make a coordinate field with a function space
    do i = 1,3
      this%chi(i) = field_type( vector_space = this%w0_fs )
      chi_p(i)    = this%chi(i)%get_proxy()
    end do

    ndf_w0      = chi_p(1)%vspace%get_ndf( )
    diff_dim_w0 = chi_p(1)%vspace%get_dim_space_diff( )
    dim_w0      = chi_p(1)%vspace%get_dim_space()
    undf_w0     = chi_p(1)%vspace%get_undf()
    allocate(this%diff_basis_w0(diff_dim_w0,ndf_w0,nqp_h,nqp_v))

  end subroutine setUp

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  subroutine tearDown( this )

    use reference_element_mod, only : deallocate_reference

    implicit none

    class(vorticity_advection_test_type), intent(inout) :: this

    deallocate( this%basis_w3,     &
                this%basis_w2,     &
                this%basis_w1,     &
                this%diff_basis_w0 )

    call function_space_collection%clear()
    call deallocate_reference()
    call mesh_collection%clear()

  end subroutine tearDown

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  @test
  subroutine test_all( this )

    use vorticity_advection_kernel_mod, only : vorticity_advection_code

    implicit none

    class(vorticity_advection_test_type), intent(inout) :: this

    real(r_def), parameter :: dx = 6000.0_r_def, &
                              dy = 1000.0_r_def, &
                              dz = 2000.0_r_def
    real(r_def), parameter :: tol = 1.0e-3_r_def
    real(r_def), parameter :: answer = 0.0_r_def

    type(field_type)          :: mass_flux
    type(field_proxy_type)    :: chi_p(3)
    type(field_proxy_type)    :: r_u_proxy, rho_proxy, &
                                 f_proxy, xi_proxy


    integer :: i, j, k, cell, err

    integer                 :: nlayers, nqp_h, nqp_v
    integer                 :: ndf_w0, ndf_w1, ndf_w2, ndf_w3
    integer                 :: undf_w0, undf_w1, undf_w2, undf_w3

    integer, pointer        :: map_w0(:) => null()
    integer, pointer        :: map_w1(:) => null()
    integer, pointer        :: map_w2(:) => null()
    integer, pointer        :: map_w3(:) => null()

    real(kind=r_def), pointer :: xp(:,:) => null()
    real(kind=r_def), pointer :: zp(:) => null()
    real(kind=r_def), pointer :: wh(:), wv(:) => null()


    do i = 1,3
      chi_p(i) = this%chi(i)%get_proxy()
    end do
    ! Compute coordinates
    cell = 1
    do j = 1,3
      do i = 1,3
        map_w0 => chi_p(1)%vspace%get_cell_dofmap( cell )
        do k = 0,3
          chi_p(1)%data(map_w0(1) + k) = real(i-1)*dx
          chi_p(2)%data(map_w0(1) + k) = real(j-1)*dy
          chi_p(3)%data(map_w0(1) + k) = real(k)  *dz
        end do
        cell = cell + 1
      end do
    end do

    ! Test kernel
    mass_flux = field_type( vector_space = this%w2_fs )

    r_u_proxy  = this%r_u%get_proxy()
    f_proxy    = mass_flux%get_proxy()
    rho_proxy  = this%rho%get_proxy()
    xi_proxy   = this%xi%get_proxy()

    nlayers = rho_proxy%vspace%get_nlayers()
    nqp_h=this%qr%get_nqp_h()
    nqp_v=this%qr%get_nqp_v()
    zp=>this%qr%get_xqp_v()
    xp=>this%qr%get_xqp_h()
    wh=>this%qr%get_wqp_h()
    wv=>this%qr%get_wqp_v()

    ndf_w3  = rho_proxy%vspace%get_ndf( )
    undf_w3 = rho_proxy%vspace%get_undf()

    ndf_w2      = r_u_proxy%vspace%get_ndf( )
    undf_w2     = r_u_proxy%vspace%get_undf()

    ndf_w1  = xi_proxy%vspace%get_ndf( )
    undf_w1 = xi_proxy%vspace%get_undf()

    ndf_w0      = chi_p(1)%vspace%get_ndf( )
    undf_w0     = chi_p(1)%vspace%get_undf()

    call rho_proxy%vspace%compute_basis_function( &
                                 this%basis_w3, ndf_w3, nqp_h, nqp_v, xp, zp )

    call r_u_proxy%vspace%compute_basis_function( &
                                 this%basis_w2, ndf_w2, nqp_h, nqp_v, xp, zp )

    call xi_proxy%vspace%compute_basis_function( &
                                 this%basis_w1, ndf_w1, nqp_h, nqp_v, xp, zp )

    call chi_p(1)%vspace%compute_diff_basis_function( &
                            this%diff_basis_w0, ndf_w0, nqp_h, nqp_v, xp, zp )

    cell = 1

    map_w3 => rho_proxy%vspace%get_cell_dofmap( cell )
    map_w2 => r_u_proxy%vspace%get_cell_dofmap( cell )
    map_w1 => xi_proxy%vspace%get_cell_dofmap( cell )
    map_w0 => chi_p(1)%vspace%get_cell_dofmap( cell )

    ! Create the data
    r_u_proxy%data(:) = 0.0_r_def
    f_proxy%data(:)   = 2.0_r_def
    xi_proxy%data(:)  = 0.0_r_def
    rho_proxy%data(:) = 1.5_r_def

    call vorticity_advection_code( nlayers,                    &
                                   r_u_proxy%data,             &
                                   f_proxy%data,               &
                                   rho_proxy%data,             &
                                   xi_proxy%data,              &
                                   chi_p(1)%data,              &
                                   chi_p(2)%data,              &
                                   chi_p(3)%data,              &
                                   ndf_w2, undf_w2,            &
                                   map_w2, this%basis_w2,      &
                                   ndf_w3, undf_w3,            &
                                   map_w3, this%basis_w3,      &
                                   ndf_w1, undf_w1,            &
                                   map_w1, this%basis_w1,      &
                                   ndf_w0, undf_w0,            &
                                   map_w0, this%diff_basis_w0, &
                                   nqp_h, nqp_v, wh, wv        &
                                   )

    do k = 0,2
      @assertEqual(answer, r_u_proxy%data(map_w2(5)+k), tol)
      @assertEqual(answer, r_u_proxy%data(map_w2(6)+k), tol)
    end do

  end subroutine test_all

end module vorticity_advection_kernel_mod_test
