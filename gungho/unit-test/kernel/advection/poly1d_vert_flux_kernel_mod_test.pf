!-----------------------------------------------------------------------------
! (C) Crown copyright 2018 Met Office. All rights reserved.
! The file LICENCE, distributed with this code, contains details of the terms
! under which the code may be used.
!-----------------------------------------------------------------------------

!> Test the flux computation using 1d polynomials 
module poly1d_vert_flux_kernel_mod_test

  use constants_mod, only : i_def, r_def
  use pFUnit_Mod

  implicit none

  private
  public :: test_all

  @TestCase
  type, extends(TestCase), public :: poly1d_vert_flux_test_type
    private
  contains
    procedure test_all
  end type poly1d_vert_flux_test_type

contains

  @Test
  subroutine test_all( this )

    use poly1d_vert_flux_kernel_mod, only: poly1d_vert_flux_code

    implicit none 

    class(poly1d_vert_flux_test_type), intent(inout) :: this

    real(r_def), parameter :: tol = 1.0e-12_r_def
    real(r_def)            :: answer

    integer(i_def), parameter :: nfaces_v = 2
    integer(i_def), parameter :: nlayers = 3
    integer(i_def), parameter :: ndf_w2 = 6
    integer(i_def), parameter :: ndf_w3 = 1
    integer(i_def), parameter :: undf_w2 = 4*nlayers + 2*(nlayers+1)
    integer(i_def), parameter :: undf_w3 = ndf_w3*nlayers
    integer(i_def), parameter :: order = 2

    integer(i_def), dimension(ndf_w2) :: map_w2
    integer(i_def), dimension(ndf_w3) :: map_w3

    real(r_def), dimension(3,ndf_w2,ndf_w2) :: basis_w2
    real(r_def), dimension(3,6)             :: out_face_normal

    real(r_def), dimension(undf_w2)           :: flux
    real(r_def), dimension(undf_w2)           :: wind
    real(r_def), dimension(undf_w3)           :: density
    real(r_def), dimension(order+1,2,undf_w3) :: coeff

    integer(i_def) :: df, k
    
    map_w3(1) = 1
    do df = 1,4
      map_w2(df) = 1 + (df-1)*nlayers
    end do
    map_w2(5) = 1 + 4*nlayers
    map_w2(6) = map_w2(5) + 1

    out_face_normal = 0.0_r_def
    out_face_normal(1,1) = -1.0_r_def
    out_face_normal(2,2) = -1.0_r_def
    out_face_normal(1,3) =  1.0_r_def
    out_face_normal(2,4) =  1.0_r_def
    out_face_normal(3,5) = -1.0_r_def
    out_face_normal(3,6) =  1.0_r_def

    wind(:) = 1.0_r_def
    
    basis_w2 = 0.0_r_def
    basis_w2(1,1,1) = 1.0_r_def
    basis_w2(2,2,2) = -1.0_r_def
    basis_w2(1,3,3) = 1.0_r_def
    basis_w2(2,4,4) = -1.0_r_def
    basis_w2(3,5,5) = 1.0_r_def
    basis_w2(3,6,6) = 1.0_r_def
   
    do k = 1,nlayers
      density(k) = real(k,r_def)
    end do

    ! Quadratic fit for most of the domain
    coeff(1,1,:) =  2.0_r_def/6.0_r_def
    coeff(2,1,:) =  5.0_r_def/6.0_r_def
    coeff(3,1,:) = -1.0_r_def/6.0_r_def
    coeff(1,2,:) = -1.0_r_def/6.0_r_def
    coeff(2,2,:) =  5.0_r_def/6.0_r_def
    coeff(3,2,:) =  2.0_r_def/6.0_r_def
    ! Constant fit for lowest level
    coeff(1,1,1) =  1.0_r_def
    coeff(1,2,1) =  1.0_r_def

    call poly1d_vert_flux_code( nlayers,              &
                                flux,                 &
                                wind,                 &
                                density,              &
                                coeff,                &
                                ndf_w2,               &
                                undf_w2,              &
                                map_w2,               &
                                basis_w2,             &
                                ndf_w3,               &
                                undf_w3,              &
                                map_w3,               &
                                order,                &
                                nfaces_v,             &
                                out_face_normal )
    answer = 1.0_r_def
    @assertEqual(answer, flux(map_w2(5)+1) , tol)
    answer = 2.5_r_def
    @assertEqual(answer, flux(map_w2(5)+2) , tol)

  end subroutine test_all

end module poly1d_vert_flux_kernel_mod_test
