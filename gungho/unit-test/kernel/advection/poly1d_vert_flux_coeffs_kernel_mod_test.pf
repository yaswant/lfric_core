!-----------------------------------------------------------------------------
! (C) Crown copyright 2018 Met Office. All rights reserved.
! The file LICENCE, distributed with this code, contains details of the terms
! under which the code may be used.
!-----------------------------------------------------------------------------

!> Test the computation of the vertical flux coefficients 
module poly1d_vert_flux_coeffs_kernel_mod_test

  use constants_mod, only: i_def, r_def
  use pFUnit_Mod
  use yaxt,          only: xt_initialize, xt_finalize
  use mpi_mod,       only: store_comm, clear_comm

  implicit none

  private
  public :: test_all

  @TestCase
  type, extends(MPITestCase), public :: poly1d_vert_flux_coeffs_test_type
    private
  contains
    procedure setUp
    procedure test_all
    procedure tearDown
  end type poly1d_vert_flux_coeffs_test_type

contains

  subroutine setUp( this )
    use feign_config_mod,     only: feign_base_mesh_config, &
                                    feign_planet_config
    use base_mesh_config_mod, only: geometry_planar, &
                                    partitioner_planar
    implicit none 

    class(poly1d_vert_flux_coeffs_test_type), intent(inout) :: this

    ! Initialise YAXT
    call xt_initialize(this%getMpiCommunicator())
    ! Store the MPI communicator for later use
    call store_comm(this%getMpiCommunicator())

    call feign_base_mesh_config( filename='foo', prime_mesh_name='unit_test', &
                                 geometry=geometry_planar,                    &
                                 partitioner=partitioner_planar,              &
                                 fplane=.false., f_lat_deg=0.0_r_def )


  end subroutine setUp

  @Test( npes=[1] )
  subroutine test_all( this )

    use poly1d_vert_flux_coeffs_kernel_mod, only: poly1d_vert_flux_coeffs_code
    implicit none 

    class(poly1d_vert_flux_coeffs_test_type), intent(inout) :: this

    real(r_def), parameter    :: tol = 1.0e-12_r_def
    real(r_def), dimension(3) :: answer

    integer(i_def), parameter :: nfaces_v = 2
    integer(i_def), parameter :: nlayers = 3
    integer(i_def), parameter :: ndf_w3 = 1
    integer(i_def), parameter :: ndf_wx = 8
    integer(i_def), parameter :: undf_w3 = ndf_w3*nlayers
    integer(i_def), parameter :: undf_wx = ndf_wx*nlayers
    integer(i_def), parameter :: global_order = 2
    integer(i_def), parameter :: nqp_h = 1
    integer(i_def), parameter :: nqp_v = 3
    integer(i_def), parameter :: nqp_f = 1
    integer(i_def), dimension(ndf_w3) :: map_w3
    integer(i_def), dimension(ndf_wx) :: map_wx

    real(r_def), dimension(global_order+1,2,undf_w3) :: coeff
    real(r_def), dimension(undf_w3) :: mdw3
    real(r_def), dimension(undf_wx) :: x, y, z

    real(r_def), dimension(nqp_h) :: wqp_h
    real(r_def), dimension(nqp_v) :: wqp_v
    real(r_def), dimension(nqp_f) :: wqp_f

    real(r_def), dimension(1,ndf_wx,nqp_h,nqp_v) :: basis_wx
    real(r_def), dimension(1,ndf_wx,nqp_f,2)     :: face_basis_wx

    real(r_def), parameter :: dx = 2.0_r_def
    real(r_def), parameter :: dy = 1.0_r_def
    real(r_def), parameter :: dz = 3.0_r_def

    real(r_def), dimension(3) :: xq

    integer(i_def) :: k, df

    xq(:) = 0.5_r_def + 0.5_r_def*(/ -sqrt(3.0_r_def/5.0_r_def), 0.0_r_def, sqrt(3.0_r_def/5.0_r_def) /)
    wqp_v(:) = (/5.0_r_def, 8.0_r_def, 5.0_r_def/)/18.0_r_def
 
    map_w3(1) = 1

    do df = 1,ndf_wx
      map_wx(df) = 1 + (df-1)*nlayers
    end do

    mdw3(:) = dx*dy*dz

    x(:) = 0.0_r_def
    y(:) = 0.0_r_def
    z(:) = 0.0_r_def
    do k = 0,nlayers-1      
      x(map_wx(2)+k) = dx
      x(map_wx(4)+k) = dx
      y(map_wx(3)+k) = dy
      y(map_wx(4)+k) = dy

      do df = 1,4
        x(map_wx(4+df)+k) = x(map_wx(df)+k)
        y(map_wx(4+df)+k) = y(map_wx(df)+k)
        z(map_wx(df)+k)   = k*dz
        z(map_wx(4+df)+k) = z(map_wx(df)+k) + dz
      end do
    end do

    face_basis_wx(:,:,:,:) = 0.0_r_def
    do df = 1,4
      face_basis_wx(1,df,:,1) = 0.25_r_def
      face_basis_wx(1,4+df,:,2) = 0.25_r_def
    end do
    do k = 1,3
      do df = 1,4
        basis_wx(1,df,:,k) = 0.25_r_def*(1.0_r_def - xq(k))
        basis_wx(1,4+df,:,k) = 0.25_r_def*xq(k)
      end do
    end do

    wqp_h(1) = 1.0_r_def
    wqp_f(1) = 1.0_r_def

    call poly1d_vert_flux_coeffs_code(nlayers,                   &
                                      coeff,                     &
                                      mdw3,                      &
                                      x, y, z,                   &
                                      ndf_w3,                    &
                                      undf_w3,                   &
                                      map_w3,                    &
                                      ndf_wx,                    &
                                      undf_wx,                   &
                                      map_wx,                    &
                                      basis_wx,                  &
                                      face_basis_wx,             &
                                      global_order,              &
                                      nfaces_v,                  &
                                      nqp_h, nqp_v, wqp_h, wqp_v,&
                                      nfaces_v, nqp_f, wqp_f )

    answer = (/ 2.0_r_def, 5.0_r_def, -1.0_r_def /)/6.0_r_def
    @assertEqual(answer, coeff(:,1,2), tol)
    answer = (/ -1.0_r_def, 5.0_r_def, 2.0_r_def /)/6.0_r_def
    @assertEqual(answer, coeff(:,2,2), tol)

    answer(1) = 1.0_r_def
    @assertEqual(answer(1), coeff(1,2,1), tol)
    @assertEqual(answer(1), coeff(1,1,3), tol)
  end subroutine test_all

  subroutine tearDown( this )
    implicit none 

    class(poly1d_vert_flux_coeffs_test_type), intent(inout) :: this
    ! Finalise YAXT
    call xt_finalize()
    ! Clear the stored MPI communicator
    call clear_comm()
  end subroutine tearDown

end module poly1d_vert_flux_coeffs_kernel_mod_test
