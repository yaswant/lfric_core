!-----------------------------------------------------------------------------
! (c) Crown copyright 2020 Met Office. All rights reserved.
! The file LICENCE, distributed with this code, contains details of the terms
! under which the code may be used.
!-----------------------------------------------------------------------------
!

!> Test the vertical_sl_theta_kernel.
!>
module vertical_sl_theta_kernel_mod_test

  use constants_mod, only : i_def, r_def, l_def, r_tran
  use pFUnit_Mod
  use transport_enumerated_types_mod, only : vertical_monotone_none,        &
                                             vertical_monotone_strict,      &
                                             vertical_monotone_order_high

  implicit none

  private

  public :: test_all

  @TestCase
  type, public, extends(TestCase) :: vertical_sl_theta_test_type
    private
  contains
    procedure setUp
    procedure tearDown
    procedure test_all
  end type vertical_sl_theta_test_type

contains
 !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  subroutine setUp( this )

  use feign_config_mod,      only : feign_transport_config
  use transport_config_mod,  only : operators_fv,                  &
                                    runge_kutta_method_ssp2,       &
                                    slice_order_cubic,             &
                                    vertical_sl_order_cubic,       &
                                    max_vert_cfl_calc_uniform,     &
                                    horizontal_monotone,           &
                                    vertical_monotone,             &
                                    vertical_monotone_order,       &
                                    theta_variable_dry

    implicit none

    class(vertical_sl_theta_test_type), intent(inout) :: this

    call feign_transport_config( operators = operators_fv,                                 &
                                 runge_kutta_method = runge_kutta_method_ssp2,             &
                                 fv_vertical_order = 2,                                    &
                                 fv_horizontal_order = 2,                                  &
                                 broken_w2_projection = .false.,                           &
                                 consistent_metric  = .false.,                             &
                                 oned_reconstruction = .false.,                            &
                                 cfl_mol_1d_stab = 1.0_r_def,                              &
                                 cfl_mol_2d_stab = 1.0_r_def,                              &
                                 cfl_mol_3d_stab = 1.0_r_def,                              &
                                 cheap_update = .false.,                                   &
                                 slice_order = slice_order_cubic,                          &
                                 vertical_sl_order = vertical_sl_order_cubic,              &
                                 theta_variable=theta_variable_dry,                        &
                                 use_density_predictor = .false.,                          &
                                 cap_density_predictor = 0.001_r_def,                      &
                                 min_val_abs_tol = -1.0e-12_r_def,                         &
                                 min_val_max_iterations = 100,                             &
                                 adjust_theta = .false.,                                   &
                                 adjust_theta_above = 0.0_r_def,                           &
                                 dry_field_name='tracer',                                  &
                                 profile_size = 1,                                         &
                                 field_names = (/'tracer'/),                               &
                                 equation_form = (/1/),                                    &
                                 scheme = (/1/),                                           &
                                 splitting = (/1/),                                        &
                                 horizontal_method = (/1/),                                &
                                 vertical_method = (/1/),                                  &
                                 horizontal_monotone = (/1/),                              &
                                 vertical_monotone = (/1/),                                &
                                 vertical_monotone_order = (/1/),                          &
                                 log_space = (/.false./),                                  &
                                 enforce_min_value = (/.false./),                          &
                                 min_value = (/0.0_r_def/),                                &
                                 max_vert_cfl_calc = max_vert_cfl_calc_uniform,            &
                                 reversible = (/.false./),                                 &
                                 transport_ageofair=.false.,                               &
                                 ageofair_reset_level = 10,                                &
                                 extended_mesh = .false.                                   &
                                 )

  end subroutine setUp

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  subroutine tearDown( this )

    use configuration_mod,  only: final_configuration

    implicit none

    class(vertical_sl_theta_test_type), intent(inout) :: this

    call final_configuration()

  end subroutine tearDown

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  @Test
  subroutine test_all( this )

    use, intrinsic :: iso_fortran_env,      only : real64
    use vertical_sl_theta_kernel_mod, only: vertical_sl_theta_code
    use transport_config_mod,         only: vertical_sl_order_cubic

    implicit none

    class(vertical_sl_theta_test_type), intent(inout) :: this
    real(r_tran), parameter :: tol = 1.0e-12_r_tran
    real(r_tran)            :: use_tol
    integer(i_def), parameter :: nlayers = 5
    integer(i_def), parameter :: ndf_w2 = 2
    integer(i_def), parameter :: nfaces_h  = 4
    integer(i_def), parameter :: undf_w2 = nlayers + 1
    integer(i_def), parameter :: ndf_wtheta = 2
    integer(i_def), parameter :: undf_wtheta = nlayers + 1
    integer(i_def), dimension(ndf_w2)     :: map_w2
    integer(i_def), dimension(ndf_wtheta) :: map_wtheta
    real(r_tran), dimension(undf_w2)       :: dep_pts_z
    real(r_tran), dimension(undf_wtheta)   :: theta_in, theta_in2, theta_out, z_edges
    real(r_tran), dimension(4) :: cubic_param, cubic_coeff
    real(r_tran)     :: x
    integer(i_def)  :: i, im1, ip1, ip2, df, sl_order
    logical(l_def)  :: log_space
    integer(i_def)  :: monotone, monotone_order
    ! Create the dof map for any number of layers
    sl_order = vertical_sl_order_cubic
    map_w2(1) = 1
    map_w2(2) = 2
    ! Theta has two dofs, and follows the same pattern as the wind field
    map_wtheta(:) = (/ 1, 2 /)
    ! Get canned information for the reference cube
    theta_in(:) = 0.0_r_tran
    cubic_param(:) = (/ -1.0_r_tran, 2.0_r_tran, 1.0_r_tran,  2.0_r_tran /)
    cubic_coeff(:) = (/ -1.0_r_tran, 9.0_r_tran, 9.0_r_tran, -1.0_r_tran /)
    cubic_coeff(:) = cubic_coeff(:)/16_r_tran
    dep_pts_z = -0.5_r_tran
    ! Set top and bottom departure distance (w=0)
    dep_pts_z(map_w2(1))= 0.0_r_tran
    dep_pts_z(map_w2(1)+nlayers) = 0.0_r_tran

    do i = 1, nlayers + 1
      z_edges(i) = real(i-1,r_tran)/real(nlayers,r_tran)
    end do

    do i = 1, nlayers + 1
      x  = z_edges(i)
      theta_in(i) = cubic_param(1)*x**3 + cubic_param(2)*x**2 + &
                    cubic_param(3)*x    + cubic_param(4)
    end do
    do i = 1, nlayers + 1
       im1 = max(i-1,1)
       ip1 = min(i+1,nlayers+1)
       ip2 = min(i+2,nlayers+1)
       theta_out(i) = cubic_coeff(1)*theta_in(im1) + cubic_coeff(2)*theta_in(i) &
                    + cubic_coeff(3)*theta_in(ip1) + cubic_coeff(4)*theta_in(ip2)
    end do
    theta_out(1)         = theta_in(1)
    theta_out(nlayers+1) = theta_in(nlayers+1)
    theta_out(nlayers)   = 0.5_r_tran*theta_in(nlayers) +  &
                           0.5_r_tran*theta_in(nlayers+1)
    ! Theta_in gets overwitten by the new values on exit
    monotone = vertical_monotone_none
    monotone_order = vertical_monotone_order_high
    log_space = .false.
    theta_in2(:)=theta_in(:)

    call vertical_sl_theta_code( nlayers,                             &
                                 dep_pts_z,                           &
                                 theta_in,                            &
                                 z_edges,                             &
                                 sl_order,                            &
                                 monotone,                            &
                                 monotone_order,                      &
                                 log_space,                           &
                                 ndf_w2, undf_w2, map_w2,             &
                                 ndf_wtheta, undf_wtheta, map_wtheta  )

    if ( r_tran == real64 ) then
       use_tol = tol
    else
       use_tol = 10.0_r_tran*spacing(maxval(theta_in(:)))
    end if
    do i = 1,nlayers + 1
      @assertEqual(theta_out(i), theta_in(i) ,use_tol)
    end do

    ! redo the test with mono on

    monotone = vertical_monotone_strict
    monotone_order = vertical_monotone_order_high

    call vertical_sl_theta_code( nlayers,                             &
                                 dep_pts_z,                           &
                                 theta_in2,                           &
                                 z_edges,                             &
                                 sl_order,                            &
                                 monotone,                            &
                                 monotone_order,                      &
                                 log_space,                           &
                                 ndf_w2, undf_w2, map_w2,             &
                                 ndf_wtheta, undf_wtheta, map_wtheta  )

    if ( r_tran == real64 ) then
       use_tol = tol
    else
       use_tol = 10.0_r_tran*spacing(maxval(theta_in2(:)))
    end if
    do i = 1,nlayers + 1
      @assertEqual(theta_out(i), theta_in2(i) ,use_tol)
    end do

  end subroutine test_all
end module vertical_sl_theta_kernel_mod_test
