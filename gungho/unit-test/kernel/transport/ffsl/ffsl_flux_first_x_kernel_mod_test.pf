!-----------------------------------------------------------------------------
! (c) Crown copyright 2022 Met Office. All rights reserved.
! The file LICENCE, distributed with this code, contains details of the terms
! under which the code may be used.
!-----------------------------------------------------------------------------
!> @brief Test the first 1D PPM flux computation in the x direction

module ffsl_flux_first_x_kernel_mod_test

  use constants_mod, only : i_def, r_def
  use pFUnit_Mod

  implicit none

  private

  public :: test_all

  @TestCase
  type, public, extends(TestCase) :: ffsl_flux_first_x_test_type
    private
  contains
    procedure setUp
    procedure tearDown
    procedure test_all
  end type ffsl_flux_first_x_test_type

contains
 !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  subroutine setUp( this )

    use feign_config_mod,   only : feign_subgrid_config
    use subgrid_config_mod, only : calculate_detj_upwind

    implicit none

    class(ffsl_flux_first_x_test_type), intent(inout) :: this

    call feign_subgrid_config( calculate_detj = calculate_detj_upwind, &
                               dep_pt_stencil_extent = 2,              &
                               inner_order = 2,                        &
                               outer_order = 2                         &
                               )

  end subroutine setUp

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  subroutine tearDown( this )

    use configuration_mod, only: final_configuration

    implicit none

    class(ffsl_flux_first_x_test_type), intent(inout) :: this

    call final_configuration()

  end subroutine tearDown
  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

  @Test
  subroutine test_all( this )

    use, intrinsic :: iso_fortran_env, only: real64
    use ffsl_flux_first_x_kernel_mod,  only: ffsl_flux_first_x_code

    implicit none

    class(ffsl_flux_first_x_test_type), intent(inout) :: this

    real(kind=r_def), parameter :: tol = 1.0e-12_r_def   ! r_def 64-bit
    real(kind=r_def)            :: answer, use_tol

    integer(kind=i_def), parameter :: nlayers = 1
    integer(kind=i_def), parameter :: ndf_w3 = 1
    integer(kind=i_def), parameter :: ndf_w2 = 4
    integer(kind=i_def), parameter :: stencil_size = 9
    integer(kind=i_def), parameter :: stencil_extent = ( stencil_size - 1_i_def ) / 2_i_def
    integer(kind=i_def), parameter :: undf_w2 = ndf_w2*nlayers
    integer(kind=i_def), parameter :: undf_w3 = ndf_w3*nlayers*stencil_size
    integer(kind=i_def), parameter :: order = 2

    integer(kind=i_def), dimension(ndf_w3)              :: map_w3
    integer(kind=i_def), dimension(ndf_w2)              :: map_w2
    integer(kind=i_def), dimension(ndf_w3,stencil_size) :: stencil_map

    real(kind=r_def), dimension(undf_w3) :: field
    real(kind=r_def), dimension(undf_w2) :: dep_pts
    real(kind=r_def), dimension(undf_w2) :: flux

    real(kind=r_def) :: dt

    ! Set up maps
    stencil_map(1,:) = (/ 5, 4, 3, 2, 1, 6, 7, 8, 9 /)
    map_w2(:) = (/ 1, 2, 3, 4 /)
    map_w3(1) = 1

    ! Set up field to have quadratic profile
    field(:) = (/ 17.0_r_def, 10.0_r_def, 5.0_r_def, &
                  2.0_r_def,  1.0_r_def,  2.0_r_def, &
                  5.0_r_def,  10.0_r_def, 17.0_r_def /)

    ! Initialise flux to zero before each test
    flux(:) = 0.0_r_def

    ! Test with integer departure distance
    dep_pts(:) = 1.0_r_def
    dt = 1.0_r_def
    call ffsl_flux_first_x_code( nlayers,        &
                                 flux,           &
                                 field,          &
                                 stencil_size,   &
                                 stencil_map,    &
                                 dep_pts,        &
                                 order,          &
                                 stencil_extent, &
                                 dt,             &
                                 ndf_w2,         &
                                 undf_w2,        &
                                 map_w2,         &
                                 ndf_w3,         &
                                 undf_w3,        &
                                 map_w3 )
    ! Get correct tolerance
    if ( r_def == real64 ) then
      use_tol = tol
    else
      use_tol = 10.0_r_def*spacing( maxval( flux(1:4) ) )
    end if
    answer = 2.0_r_def
    @assertEqual(answer, flux(1), use_tol)
    answer = 1.0_r_def
    @assertEqual(answer, flux(3), use_tol)

    ! Test with positive distance
    dep_pts(:) = 0.4_r_def
    dt = 1.0_r_def
    flux(:) = 0.0_r_def
    call ffsl_flux_first_x_code( nlayers,        &
                                 flux,           &
                                 field,          &
                                 stencil_size,   &
                                 stencil_map,    &
                                 dep_pts,        &
                                 order,          &
                                 stencil_extent, &
                                 dt,             &
                                 ndf_w2,         &
                                 undf_w2,        &
                                 map_w2,         &
                                 ndf_w3,         &
                                 undf_w3,        &
                                 map_w3 )
    answer = 0.568_r_def
    @assertEqual(answer, flux(1), use_tol)
    answer = 0.408_r_def
    @assertEqual(answer, flux(3), use_tol)

    ! Test with negative distance
    dep_pts(:) = -0.4_r_def
    dt = 1.0_r_def
    flux(:) = 0.0_r_def
    call ffsl_flux_first_x_code( nlayers,        &
                                 flux,           &
                                 field,          &
                                 stencil_size,   &
                                 stencil_map,    &
                                 dep_pts,        &
                                 order,          &
                                 stencil_extent, &
                                 dt,             &
                                 ndf_w2,         &
                                 undf_w2,        &
                                 map_w2,         &
                                 ndf_w3,         &
                                 undf_w3,        &
                                 map_w3 )
    answer = -0.408_r_def
    @assertEqual(answer, flux(1), use_tol)
    answer = -0.568_r_def
    @assertEqual(answer, flux(3), use_tol)

  end subroutine test_all

end module ffsl_flux_first_x_kernel_mod_test
