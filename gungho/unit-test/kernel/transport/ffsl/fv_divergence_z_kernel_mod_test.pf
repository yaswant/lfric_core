!-----------------------------------------------------------------------------
! (c) Crown copyright 2022 Met Office. All rights reserved.
! The file LICENCE, distributed with this code, contains details of the terms
! under which the code may be used.
!-----------------------------------------------------------------------------

!> Test the finite-volume divergence calculation in the z direction.
!>
module fv_divergence_z_kernel_mod_test

  use constants_mod, only : i_def, r_def
  use pFUnit_Mod

  implicit none

  private
  public :: test_all

  @TestCase
  type, extends(TestCase), public :: fv_divergence_z_test_type
    private
    real(kind=r_def), allocatable :: mass_divergence(:)
    real(kind=r_def), allocatable :: mass_flux(:)

    integer(kind=i_def), allocatable :: map_w2(:)
    integer(kind=i_def), allocatable :: map_w3(:)

  contains
    procedure setUp
    procedure tearDown
    procedure test_all
  end type fv_divergence_z_test_type

    integer(kind=i_def), parameter :: nlayers = 1
    integer(kind=i_def), parameter :: ndf_w2  = 6
    integer(kind=i_def), parameter :: undf_w2 = ndf_w2
    integer(kind=i_def), parameter :: ndf_w3  = 1
    integer(kind=i_def), parameter :: undf_w3 = ndf_w3

contains

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  subroutine setUp( this )

    implicit none

    class(fv_divergence_z_test_type), intent(inout) :: this
    integer(kind=i_def) :: i

    allocate( this%mass_flux(undf_w2) )
    allocate( this%mass_divergence(undf_w3) )
    allocate( this%map_w2(ndf_w2) )
    allocate( this%map_w3(ndf_w3) )

    this%map_w2 = (/ (i, i=1,ndf_w2) /)
    this%map_w3 = 1

  end subroutine setUp

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  subroutine tearDown( this )

    implicit none

    class(fv_divergence_z_test_type), intent(inout) :: this

    deallocate( this%mass_flux )
    deallocate( this%mass_divergence )
    deallocate( this%map_w2 )
    deallocate( this%map_w3 )

  end subroutine tearDown

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  @Test
  subroutine test_all( this )

    use fv_divergence_z_kernel_mod, only : fv_divergence_z_code

    implicit none

    class(fv_divergence_z_test_type), intent(inout) :: this

    real(kind=r_def), parameter :: tol = 1.0e-6_r_def

    this%mass_flux = (/ 0.0_r_def, 0.0_r_def, 0.0_r_def, 0.0_r_def, 2.0_r_def, 8.0_r_def /)
    ! The fluxes have the following vertical setup:    !
    !          8.0
    !
    !          2.0
    ! Hence the z-direction divergence will be 6.

    call fv_divergence_z_code( nlayers,              &
                               this%mass_divergence, &
                               this%mass_flux,       &
                               ndf_w3,               &
                               undf_w3,              &
                               this%map_w3,          &
                               ndf_w2,               &
                               undf_w2,              &
                               this%map_w2 )
    @assertEqual(this%mass_divergence(1), 6.0_r_def, tol)

    this%mass_flux = (/ 0.0_r_def, 0.0_r_def, 0.0_r_def, 0.0_r_def, 1.0_r_def, -3.0_r_def /)
    ! The fluxes have the following vertical setup:
    !          -3.0
    !
    !           1.0
    ! Hence the z-direction divergence will be -4.

    call fv_divergence_z_code( nlayers,              &
                               this%mass_divergence, &
                               this%mass_flux,       &
                               ndf_w3,               &
                               undf_w3,              &
                               this%map_w3,          &
                               ndf_w2,               &
                               undf_w2,              &
                               this%map_w2 )
    @assertEqual(this%mass_divergence(1), -4.0_r_def, tol)

  end subroutine test_all

end module fv_divergence_z_kernel_mod_test
