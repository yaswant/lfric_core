!-----------------------------------------------------------------------------
! Copyright (c) 2017,  Met Office, on behalf of HMSO and Queen's Printer
! For further details please refer to the file LICENCE.original which you
! should have received as part of this distribution.
!-----------------------------------------------------------------------------

!> Test the held suarez forcing kernel
!>
module held_suarez_kernel_mod_test

  use constants_mod,                 only: i_def, r_def, PI
  use function_space_collection_mod, only: function_space_collection_type, &
                                           function_space_collection
  use mesh_collection_mod,           only : mesh_collection_type, &
                                            mesh_collection
  use field_mod,                     only: field_type,      &
                                           field_proxy_type
  use fs_continuity_mod,             only: W0, W2, W3
  use function_space_mod,            only: function_space_type
  use pFUnit_Mod
  use quadrature_mod,                only: quadrature_type, GAUSSIAN
  use coord_transform_mod,           only: llr2xyz
  implicit none

  private
  public :: test_all

  @TestCase
  type, extends(TestCase), public :: held_suarez_test_type
    private
    type(quadrature_type)              :: qr
    type(function_space_type), pointer :: w0_fs => null()
    type(function_space_type), pointer :: w2_fs => null()
    type(function_space_type), pointer :: w3_fs => null()
    type(field_type)                   :: du, dtheta, u, theta, rho
    type(field_type)                   :: chi(3)
    real(r_def), allocatable           :: basis_w2(:,:,:,:), &
                                          basis_w0(:,:,:,:), &
                                          basis_w3(:,:,:,:), &
                                          diff_basis_w0(:,:,:,:)
    real(r_def), allocatable           :: answer_du(:),answer_dtheta(:)
  contains
    procedure setup
    procedure tearDown
    procedure test_all
  end type held_suarez_test_type

  integer(i_def), parameter :: element_order = 0

  real(r_def), parameter :: gravity = 9.80665_r_def
  real(r_def), parameter :: radius  = 6371229.0_r_def
  real(r_def), parameter :: omega   = 7.292116E-5_r_def
  real(r_def), parameter :: p_zero  = 100000.0_r_def
  real(r_def), parameter :: rd      = 287.05_r_def
  real(r_def), parameter :: cp      = 1005.0_r_def
  real(r_def), parameter :: scaling = 1.0_r_def

contains

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  subroutine setUp( this )

    use base_mesh_config_mod,      only : base_mesh_geometry_planar, &
                                          base_mesh_partitioner_planar
    use finite_element_config_mod, only : finite_element_cellshape_quadrilateral
    use gungho_feign_config_mod,   only : feign_base_mesh_config, &
                                          feign_planet_config
    use function_space_mod,        only : function_space_type
    use mesh_mod,                  only : PLANE_BI_PERIODIC
    use reference_element_mod,     only : reference_cube,   &
                                          reference_element
    implicit none

    class(held_suarez_test_type), intent(inout) :: this


    type(field_proxy_type) :: u_proxy, theta_proxy, rho_proxy, du_proxy, dtheta_proxy



    integer(i_def)                 :: nqp_h, nqp_v
    integer(i_def)                 :: ndf_w0, undf_w0, dim_w0, dim_diff_w0
    integer(i_def)                 :: ndf_w2, undf_w2, dim_w2
    integer(i_def)                 :: ndf_w3, undf_w3, dim_w3

    integer(i_def)                 :: mesh_id

    integer(i_def)                 :: i

    call feign_base_mesh_config( filename='foo', prime_mesh_name='unit_test', &
                                 geometry=base_mesh_geometry_planar,          &
                                 partitioner=base_mesh_partitioner_planar,    &
                                 fplane=.false., f_lat_deg=0.0_r_def )

    call feign_planet_config( gravity=gravity, radius=radius, omega=omega, &
                              rd=rd, cp=cp, p_zero=p_zero,                 &
                              scaling_factor=scaling )

    reference_element = finite_element_cellshape_quadrilateral
    call reference_cube()

    ! Create top level mesh collection
    mesh_collection = mesh_collection_type()
    ! Create top level function space collection
    function_space_collection = function_space_collection_type()

    this%qr  = quadrature_type(3, GAUSSIAN)

    nqp_h=this%qr%get_nqp_h()
    nqp_v=this%qr%get_nqp_v()

    ! Dummy mesh mod has 9 cells, 3 layers and is uniform in vertical
    mesh_id = mesh_collection%add_unit_test_mesh( PLANE_BI_PERIODIC )

    this%w0_fs => function_space_collection%get_fs(mesh_id, element_order, W0)
    this%w2_fs => function_space_collection%get_fs(mesh_id, element_order, W2)
    this%w3_fs => function_space_collection%get_fs(mesh_id, element_order, W3)

    this%u  = field_type( vector_space = this%w2_fs )
    u_proxy = this%u%get_proxy()
    ndf_w2  = u_proxy%vspace%get_ndf( )
    dim_w2  = u_proxy%vspace%get_dim_space( )
    undf_w2 = u_proxy%vspace%get_undf()
    allocate( this%basis_w2(dim_w2,ndf_w2,nqp_h,nqp_v) )
    allocate( this%answer_du(undf_w2) )

    this%theta  = field_type( vector_space = this%w0_fs )
    theta_proxy = this%theta%get_proxy()
    ndf_w0        = theta_proxy%vspace%get_ndf( )
    dim_w0        = theta_proxy%vspace%get_dim_space( )
    dim_diff_w0   = theta_proxy%vspace%get_dim_space_diff( )
    undf_w0       = theta_proxy%vspace%get_undf()
    allocate( this%basis_w0(dim_w0,ndf_w0,nqp_h,nqp_v) )
    allocate( this%diff_basis_w0(dim_diff_w0,ndf_w0,nqp_h,nqp_v) )
    allocate( this%answer_dtheta(undf_w0) )

    this%rho  = field_type( vector_space = this%w3_fs )
    rho_proxy = this%rho%get_proxy()
    ndf_w3    = rho_proxy%vspace%get_ndf( )
    dim_w3    = rho_proxy%vspace%get_dim_space( )
    allocate( this%basis_w3(dim_w3,ndf_w3,nqp_h,nqp_v) )

    this%du  = field_type( vector_space = this%w2_fs )
    this%dtheta  = field_type( vector_space = this%w0_fs )

    do i = 1,3
      this%chi(i) = field_type( vector_space = this%w0_fs )
    end do

  end subroutine setUp

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  subroutine tearDown( this )

    use reference_element_mod, only : deallocate_reference

    implicit none

    class(held_suarez_test_type), intent(inout) :: this

    deallocate( this%basis_w2,      &
                this%basis_w0,      &
                this%diff_basis_w0, &
                this%basis_w3 )
    deallocate( this%answer_du, this%answer_dtheta )

    call function_space_collection%clear()
    call deallocate_reference()
    call mesh_collection%clear()
    
  end subroutine tearDown

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  @test
  subroutine test_all( this )

    use held_suarez_kernel_mod,          only : held_suarez_code
    use planet_config_mod,               only : scaled_radius

    implicit none

    class(held_suarez_test_type), intent(inout) :: this

    real(r_def), parameter :: tol = 1.0e-6_r_def

    integer :: cell
    integer(i_def) :: err

    integer                 :: nlayers, nqp_h, nqp_v
    integer                 :: ndf_w0, undf_w0
    integer                 :: ndf_w2, undf_w2
    integer                 :: ndf_w3, undf_w3

    integer, pointer        :: map_w0(:) => null()
    integer, pointer        :: map_w2(:) => null()
    integer, pointer        :: map_w3(:) => null()

    type(field_proxy_type)        :: u_proxy, theta_proxy, rho_proxy, &
                                     du_proxy, dtheta_proxy, chi_proxy(3)

    real(kind=r_def), pointer :: xp(:,:) => null()
    real(kind=r_def), pointer :: zp(:)   => null()
    real(kind=r_def), pointer :: wh(:)   => null(), &
                                 wv(:)   => null()

    real(r_def), parameter :: dz = 10000.0_r_def
    real(r_def) ::x,y,z, lat, lon, rad
    integer, pointer        ::  map_chi(:)  => null()
    integer                 :: i, j, k
    real(r_def) :: norm_1, norm_2
    

    ! Test kernel
    theta_proxy   = this%theta%get_proxy()
    u_proxy       = this%u%get_proxy()
    dtheta_proxy  = this%dtheta%get_proxy()
    du_proxy      = this%du%get_proxy()
    rho_proxy     = this%rho%get_proxy()
    do i = 1,3
      chi_proxy(i) = this%chi(i)%get_proxy()
    end do

    ! Compute coordinates: These will provide columns at?
    cell=1
    do i = 0,2
      lon=(-1.0 + i*1.0)*PI/180.0
      do j=0,2
        map_chi => chi_proxy(1)%vspace%get_cell_dofmap( cell )
        lat=(-1.0 + j*1.0)*PI/180.0
        do k = 0,3
          rad=scaled_radius + k * dz
          call llr2xyz(lon,lat,rad,x,y,z)
          chi_proxy(1)%data(map_chi(1) + k) = x
          chi_proxy(2)%data(map_chi(1) + k) = y
          chi_proxy(3)%data(map_chi(1) + k) = z
        end do
        cell=cell+1
      end do
    end do
    
    nlayers = theta_proxy%vspace%get_nlayers()

    nqp_h=this%qr%get_nqp_h()
    nqp_v=this%qr%get_nqp_v()
    zp=>this%qr%get_xqp_v()
    xp=>this%qr%get_xqp_h()
    wh=>this%qr%get_wqp_h()
    wv=>this%qr%get_wqp_v()

    ndf_w2      = u_proxy%vspace%get_ndf( )
    undf_w2     = u_proxy%vspace%get_undf()

    ndf_w0      = theta_proxy%vspace%get_ndf( )
    undf_w0     = theta_proxy%vspace%get_undf()

    ndf_w3      = rho_proxy%vspace%get_ndf( )
    undf_w3     = rho_proxy%vspace%get_undf()

    call u_proxy%vspace%compute_basis_function(         &
        this%basis_w2, ndf_w2, nqp_h, nqp_v, xp, zp)

    call theta_proxy%vspace%compute_basis_function(     &
       this%basis_w0, ndf_w0, nqp_h, nqp_v, xp, zp)

    call theta_proxy%vspace%compute_diff_basis_function( &
         this%diff_basis_w0, ndf_w0, nqp_h, nqp_v, xp, zp)

    call rho_proxy%vspace%compute_basis_function(        &
        this%basis_w3, ndf_w3, nqp_h, nqp_v, xp, zp)

    ! Create the data
    theta_proxy%data(:) = 300.0_r_def
    u_proxy%data(:)     = 0.1_r_def
    rho_proxy%data(:)   = 1.0_r_def

! only test the interior cell (5)
    cell=5
    map_w0 => theta_proxy%vspace%get_cell_dofmap( cell )
    map_w2 => u_proxy%vspace%get_cell_dofmap( cell )
    map_w3 => rho_proxy%vspace%get_cell_dofmap( cell )

    call held_suarez_code( nlayers, &
                         du_proxy%data, &
                         dtheta_proxy%data, &
                         u_proxy%data, &
                         theta_proxy%data, &
                         rho_proxy%data, &
                         chi_proxy(1)%data, &
                         chi_proxy(2)%data, &
                         chi_proxy(3)%data, &
                         ndf_w2, undf_w2, &
                         map_w2, &
                         this%basis_w2, &
                         ndf_w0, undf_w0, &
                         map_w0, &
                         this%basis_w0, &
                         ndf_w3, undf_w3, &
                         map_w3, &
                         this%basis_w3, &
                         ndf_w0, undf_w0, &
                         map_w0, &
                         this%basis_w0, &
                         this%diff_basis_w0, &
                         nqp_h, nqp_v, wh, wv &
                         )

    ! Normalize answers of differing magnitudes to use the same tolerance in assertEqual
    norm_1 = tol/1.e-04
    norm_2 = tol/1.e-12
    this%answer_dtheta(1) = -0.1023480E-03 * norm_1
    this%answer_du(1) = 0.5786259E-10 * norm_2

    @assertEqual( this%answer_dtheta(1), dtheta_proxy%data(map_w0(1)) * norm_1, tol )
    @assertEqual( this%answer_du(1), du_proxy%data(map_w2(1)) * norm_2, tol )

  end subroutine test_all

end module held_suarez_kernel_mod_test
