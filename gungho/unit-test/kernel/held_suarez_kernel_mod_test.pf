!-----------------------------------------------------------------------------
! Copyright (c) 2017,  Met Office, on behalf of HMSO and Queen's Printer
! For further details please refer to the file LICENCE.original which you
! should have received as part of this distribution.
!-----------------------------------------------------------------------------

!> Test the held suarez forcing kernel
!>
module held_suarez_kernel_mod_test

  use constants_mod,                 only : i_def, r_def, PI
  use function_space_collection_mod, only : function_space_collection_type, &
                                            function_space_collection
  use mesh_collection_mod,           only : mesh_collection_type, &
                                            mesh_collection
  use halo_routing_collection_mod,   only : halo_routing_collection_type, &
                                            halo_routing_collection
  use field_mod,                     only : field_type,      &
                                            field_proxy_type
  use fs_continuity_mod,             only : W2, W3, Wtheta
  use function_space_mod,            only : function_space_type
  use feign_config_mod,              only : feign_base_mesh_config, &
                                            feign_planet_config, &
                                            feign_timestepping_config, &
                                            feign_physics_config
  use physics_config_mod,            only : radiation_placement_slow,       &
                                            blayer_placement_fast,          &
                                            convection_placement_fast,      &
                                            microphysics_placement_slow,    &
                                            spectral_gwd_placement_slow,    &
                                            orographic_drag_placement_slow, &
                                            lowest_level_gradient
  use pFUnit_Mod
  use quadrature_xyoz_mod,           only: quadrature_xyoz_type, &
                                           quadrature_xyoz_proxy_type
  use quadrature_rule_gaussian_mod,  only: quadrature_rule_gaussian_type
  use coord_transform_mod,           only: llr2xyz
  use yaxt,                          only : xt_initialize, xt_finalize
  use mpi_mod,                       only : store_comm, clear_comm

  implicit none

  private
  public :: test_all

  @TestCase
  type, extends(MPITestCase), public :: held_suarez_test_type
    private
    type( quadrature_xyoz_type )          :: qr
    type(quadrature_xyoz_proxy_type) :: qr_proxy
    type(function_space_type), pointer :: w2_fs => null()
    type(function_space_type), pointer :: w3_fs => null()
    type(function_space_type), pointer :: wth_fs => null()
    type(field_type)                   :: du, dtheta, u, theta, exner, w2_rmultiplicity
    type(field_type)                   :: chi1
    type(field_type)                   :: chi2
    type(field_type)                   :: chi3
    real(r_def), allocatable           :: answer_du(:),answer_dtheta(:)
  contains
    procedure setup
    procedure tearDown
    procedure test_all
  end type held_suarez_test_type

  integer(i_def), parameter :: element_order = 0

  real(r_def), parameter :: gravity  = 9.80665_r_def
  real(r_def), parameter :: radius   = 6371229.0_r_def
  real(r_def), parameter :: omega    = 7.292116E-5_r_def
  real(r_def), parameter :: p_zero   = 100000.0_r_def
  real(r_def), parameter :: rd       = 287.05_r_def
  real(r_def), parameter :: cp       = 1005.0_r_def
  real(r_def), parameter :: scaling  = 1.0_r_def
  real(r_def), parameter :: dt = 600.0_r_def
  real(r_def), parameter :: rsecs_in_day = 1.0_r_def/86400.0_r_def

contains

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  subroutine setUp( this )

    use base_mesh_config_mod,      only : geometry_planar, &
                                          partitioner_planar
    use timestepping_config_mod,   only : method_semi_implicit, &
                                          runge_kutta_method_ssp3
    use finite_element_config_mod, only : cellshape_quadrilateral
    use function_space_mod,        only : function_space_type
    use mesh_mod,                  only : PLANE_BI_PERIODIC, PLANE

    implicit none

    class(held_suarez_test_type), intent(inout) :: this


    type(field_proxy_type) :: u_proxy, theta_proxy, exner_proxy, &
                              du_proxy, dtheta_proxy, w2_rmultiplicity_proxy

    integer(i_def)         :: ndf_w2, undf_w2, dim_w2
    integer(i_def)         :: ndf_w3, undf_w3, dim_w3
    integer(i_def)         :: ndf_wth, undf_wth, dim_wth

    integer(i_def)         :: mesh_id

    integer(i_def)         :: i

    ! Initialise YAXT
    call xt_initialize(this%getMpiCommunicator())
    ! Store the MPI communicator for later use
    call store_comm(this%getMpiCommunicator())

    call feign_base_mesh_config( filename='foo',                      &
                                 prime_mesh_name='unit_test',         &
                                 geometry=geometry_planar,            &
                                 partitioner=partitioner_planar,      &
                                 fplane=.false., f_lat_deg=0.0_r_def )

    call feign_planet_config( gravity=gravity, radius=radius, omega=omega, &
                              rd=rd, cp=cp, p_zero=p_zero,                 &
                              scaling_factor=scaling )

    call feign_timestepping_config(method=method_semi_implicit,                       &
                                   dt=dt, alpha=0.5_r_def, tau_u=0.5_r_def,           &
                                   tau_t=0.5_r_def, tau_r=0.5_r_def,                  &
                                   outer_iterations=1_i_def, inner_iterations=1_i_def,&
                                   runge_kutta_method=runge_kutta_method_ssp3,        &
                                   spinup_period=0.0_r_def, spinup_alpha=.false.,     &
                                   spinup_winds=.false.    )

    ! Create top level mesh collection
    mesh_collection = mesh_collection_type()
    ! Create top level function space collection
    function_space_collection = function_space_collection_type()
    ! Create top level halo_routing collection
    halo_routing_collection = halo_routing_collection_type()

    ! Dummy mesh mod has 9 cells, 3 layers and is uniform in vertical
    mesh_id = mesh_collection%add_unit_test_mesh( PLANE_BI_PERIODIC )

    this%w2_fs => function_space_collection%get_fs(mesh_id, element_order, W2)
    this%w3_fs => function_space_collection%get_fs(mesh_id, element_order, W3)
    this%wth_fs => function_space_collection%get_fs(mesh_id, element_order, Wtheta)

    call this%u%initialise( vector_space = this%w2_fs )
    u_proxy = this%u%get_proxy()
    ndf_w2  = u_proxy%vspace%get_ndf( )
    dim_w2  = u_proxy%vspace%get_dim_space( )
    undf_w2 = u_proxy%vspace%get_undf()
    allocate( this%answer_du(undf_w2) )

    call this%theta%initialise( vector_space = this%wth_fs )
    theta_proxy = this%theta%get_proxy()
    ndf_wth        = theta_proxy%vspace%get_ndf( )
    dim_wth        = theta_proxy%vspace%get_dim_space( )
    undf_wth       = theta_proxy%vspace%get_undf()
    allocate( this%answer_dtheta(undf_wth) )

    call this%exner%initialise( vector_space = this%w3_fs )
    exner_proxy = this%exner%get_proxy()
    ndf_w3    = exner_proxy%vspace%get_ndf( )
    dim_w3    = exner_proxy%vspace%get_dim_space( )

    call this%w2_rmultiplicity%initialise( vector_space = this%w2_fs )
    w2_rmultiplicity_proxy = this%w2_rmultiplicity%get_proxy()

    call this%du%initialise( vector_space = this%w2_fs )
    call this%dtheta%initialise( vector_space = this%wth_fs )

    call this%chi1%initialise( vector_space = this%w3_fs )
    call this%chi2%initialise( vector_space = this%w3_fs )
    call this%chi3%initialise( vector_space = this%w3_fs )

  end subroutine setUp

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  subroutine tearDown( this )

    use configuration_mod,        only: final_configuration

    implicit none

    class(held_suarez_test_type), intent(inout) :: this

    deallocate( this%answer_du, this%answer_dtheta )

    call function_space_collection%clear()
    call mesh_collection%clear()
    call halo_routing_collection%clear()
    call final_configuration()

    ! Finalise YAXT
    call xt_finalize()
    ! Clear the stored MPI communicator
    call clear_comm()

    ! Finalise namelist
    call final_configuration()

  end subroutine tearDown

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  @Test( npes=[1] )
  subroutine test_all( this )

    use held_suarez_fv_kernel_mod,       only : held_suarez_fv_code
    use held_suarez_fv_wind_kernel_mod,  only : held_suarez_fv_wind_code
    use planet_config_mod,               only : scaled_radius

    implicit none

    class(held_suarez_test_type), intent(inout) :: this

    real(r_def) :: tol ! Tolerance to default precision

    integer(i_def) :: cell
    integer(i_def) :: err

    integer(i_def)          :: nlayers
    integer(i_def)          :: ndf_wchi, undf_wchi
    integer(i_def)          :: ndf_w2, undf_w2
    integer(i_def)          :: ndf_w3, undf_w3
    integer(i_def)          :: ndf_wth, undf_wth

    integer(i_def), pointer :: map_w2(:) => null()
    integer(i_def), pointer :: map_w3(:) => null()
    integer(i_def), pointer :: map_wth(:) => null()
    integer(i_def), pointer :: map_wchi(:)  => null()

    type(field_proxy_type)        :: u_proxy, theta_proxy, exner_proxy, &
                                     du_proxy, dtheta_proxy, chi_proxy(3), &
                                     w2_rmultiplicity_proxy

    real(r_def), parameter  :: dlat=1.0_r_def, dlon=1.0_r_def, dz = 10000.0_r_def
    real(r_def)             :: x, y, z, lat, lon, rad
    integer(i_def)          :: i, j, k, df, udf
    real(r_def)             :: lat0, lon0, rad0, x0, y0, z0

    ! Test kernel
    theta_proxy   = this%theta%get_proxy()
    u_proxy       = this%u%get_proxy()
    w2_rmultiplicity_proxy = this%w2_rmultiplicity%get_proxy()
    dtheta_proxy  = this%dtheta%get_proxy()
    du_proxy      = this%du%get_proxy()
    exner_proxy     = this%exner%get_proxy()
    chi_proxy(1) = this%chi1%get_proxy()
    chi_proxy(2) = this%chi2%get_proxy()
    chi_proxy(3) = this%chi3%get_proxy()
    do i = 1,3
      chi_proxy(i)%data(:) = 0.0_r_def
    end do

    nlayers = theta_proxy%vspace%get_nlayers()


    call feign_physics_config(                                      &
        blayer_placement       = blayer_placement_fast,             &
        radiation_placement    = radiation_placement_slow,          &
        convection_placement   = convection_placement_fast,         &
        lowest_level           = lowest_level_gradient,             &
        microphysics_placement = microphysics_placement_slow,       &
        spectral_gwd_placement = spectral_gwd_placement_slow,       &
        orographic_drag_placement = orographic_drag_placement_slow, &
        heldsuarez_thermo_placement = -1,                           &
        heldsuarez_wind_placement   = -1,                           &
        hs_random                   = .false.,                      &
        sample_physics_winds   = .true.                       )



    ndf_w2      = u_proxy%vspace%get_ndf( )
    undf_w2     = u_proxy%vspace%get_undf()

    ndf_wth      = theta_proxy%vspace%get_ndf( )
    undf_wth     = theta_proxy%vspace%get_undf()

    ndf_w3      = exner_proxy%vspace%get_ndf( )
    undf_w3     = exner_proxy%vspace%get_undf()

    ndf_wchi      = chi_proxy(1)%vspace%get_ndf( )
    undf_wchi     = chi_proxy(1)%vspace%get_undf()

    ! Compute coordinates: These will give equator at centre of cell 5
    ! only this column is used, so that the w2_multiplicity = 1
    ! Note we're using lowest order w3 for chi
    lat0=0.
    lon0=0.
    rad0=scaled_radius

    cell=1
    udf=1
    do i = 0,2
      do j=0,2
        map_wchi => chi_proxy(1)%vspace%get_cell_dofmap( cell )
        do k = 0,2
          do df=1,ndf_w3
            call llr2xyz(lon0 + (i-1)*dlon, lat0 + (j-1)*dlat, rad0+k*dz, x, y, z)
            chi_proxy(1)%data(map_wchi(df) + k) = x
            chi_proxy(2)%data(map_wchi(df) + k) = y
            chi_proxy(3)%data(map_wchi(df) + k) = z
          end do
        end do
        cell=cell+1
      end do
    end do

    ! Create the data
    theta_proxy%data(:)            = 300.0_r_def
    u_proxy%data(:)                = 10.0_r_def
    exner_proxy%data(:)            = 1.0_r_def
    w2_rmultiplicity_proxy%data(:) = 1.0_r_def

    ! Only test interior cell 5
    cell=5
    map_w2 => u_proxy%vspace%get_cell_dofmap( cell )
    map_w3 => exner_proxy%vspace%get_cell_dofmap( cell )
    map_wth => theta_proxy%vspace%get_cell_dofmap( cell )
    map_wchi => chi_proxy(1)%vspace%get_cell_dofmap( cell )

    call held_suarez_fv_code( nlayers,                      &
                              dtheta_proxy%data,            &
                              theta_proxy%data,             &
                              exner_proxy%data,             &
                              chi_proxy(1)%data,            &
                              chi_proxy(2)%data,            &
                              chi_proxy(3)%data,            &
                              ndf_wth, undf_wth, map_wth,   &
                              ndf_wchi, undf_wchi, map_wchi &
                            )

    ! du is incremented so need to initialize
    du_proxy%data(:)     = 0.0_r_def

    call held_suarez_fv_wind_code( nlayers,                      &
                                   du_proxy%data,                &
                                   u_proxy%data,                 &
                                   exner_proxy%data,             &
                                   w2_rmultiplicity_proxy%data,  &
                                   chi_proxy(1)%data,            &
                                   chi_proxy(2)%data,            &
                                   chi_proxy(3)%data,            &
                                   ndf_w2, undf_w2, map_w2,      &
                                   ndf_wth, undf_wth, map_wth,   &
                                   ndf_wchi, undf_wchi, map_wchi &
                                 )


    this%answer_dtheta(1) = 15.0_r_def*0.25_r_def*dt*rsecs_in_day
    this%answer_du(1)     = -dt*rsecs_in_day*10.0_r_def

    tol = spacing(this%answer_dtheta(1))
    @assertEqual( this%answer_dtheta(1), dtheta_proxy%data(map_wth(1)), tol )
    tol = spacing(this%answer_du(1))
    @assertEqual( this%answer_du(1), du_proxy%data(map_w2(2)), tol )

  end subroutine test_all

end module held_suarez_kernel_mod_test
