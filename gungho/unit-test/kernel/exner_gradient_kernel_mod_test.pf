!-----------------------------------------------------------------------------
! Copyright (c) 2017,  Met Office, on behalf of HMSO and Queen's Printer
! For further details please refer to the file LICENCE.original which you
! should have received as part of this distribution.
!-----------------------------------------------------------------------------

!> Test the exner gradient computation
!>
module exner_gradient_kernel_mod_test

  use constants_mod,                 only : i_def, r_def
  use function_space_collection_mod, only : function_space_collection_type, &
                                            function_space_collection
  use mesh_collection_mod,           only : mesh_collection_type, &
                                            mesh_collection
  use halo_routing_collection_mod,   only : halo_routing_collection_type, &
                                            halo_routing_collection
  use field_mod,                     only : field_type, &
                                            field_proxy_type
  use function_space_mod,            only : function_space_type
  use fs_continuity_mod,             only : W0, W2, W3
  use pFUnit_Mod
  use quadrature_xyoz_mod,               only: quadrature_xyoz_type, &
                                               quadrature_xyoz_proxy_type
  use quadrature_rule_gaussian_mod,      only: quadrature_rule_gaussian_type
  use yaxt,                          only : xt_initialize, xt_finalize
  use mpi_mod,                       only : store_comm, clear_comm

  implicit none

  private
  public :: exner_gradient_test_type, test_all

  @TestCase
  type, extends(MPITestCase) :: exner_gradient_test_type
    private
    type( quadrature_xyoz_type )          :: qr
    type(quadrature_xyoz_proxy_type) :: qr_proxy
    type(function_space_type), pointer :: w0_fs => null()
    type(function_space_type), pointer :: w2_fs => null()
    type(function_space_type), pointer :: w3_fs => null()
    type(field_type)                   :: r_u, exner, theta
    real(r_def), allocatable           :: basis_w3(:,:,:,:), &
                                          basis_w2(:,:,:,:), &
                                          basis_w0(:,:,:,:), &
                                          diff_basis_w0(:,:,:,:), &
                                          diff_basis_w2(:,:,:,:)
  contains
    procedure setUp
    procedure tearDown
    procedure test_all
  end type exner_gradient_test_type

  integer(i_def), parameter :: element_order = 0

  real(r_def), parameter :: bvf_square = 0.0001_r_def
  real(r_def), parameter :: theta_surf = 300.0_r_def
  real(r_def), parameter :: pert_width_scaling = 1.0_r_def

  real(r_def), parameter :: gravity  = 10.0_r_def
  real(r_def), parameter :: radius   = 6000000_r_def
  real(r_def), parameter :: omega    = 8.0E-5_r_def
  real(r_def), parameter :: p_zero   = 100000.0_r_def
  real(r_def), parameter :: rd       = 300.0_r_def
  real(r_def), parameter :: cp       = 1000.0_r_def
  real(r_def), parameter :: scaling  = 1.0_r_def

contains

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  subroutine setUp( this )

    use base_mesh_config_mod,      only : geometry_planar, &
                                          partitioner_planar
    use finite_element_config_mod, only : cellshape_quadrilateral
    use feign_config_mod,          only : feign_base_mesh_config,           &
                                          feign_initial_temperature_config, &
                                          feign_planet_config
    use mesh_mod,                  only : PLANE_BI_PERIODIC

    implicit none

    class(exner_gradient_test_type), intent(inout) :: this

    type(field_proxy_type)    :: r_u_proxy, exner_proxy, theta_proxy
    type( quadrature_rule_gaussian_type ) :: quadrature_rule

    integer(i_def) :: nqp_h, nqp_v
    integer(i_def) :: ndf_w0, ndf_w2, ndf_w3
    integer(i_def) :: undf_w0, undf_w2, undf_w3
    integer(i_def) :: dim_w0, diff_dim_w0, dim_w2, diff_dim_w2, dim_w3
    integer(i_def) :: mesh_id

    ! Initialise YAXT
    call xt_initialize(this%getMpiCommunicator())
    !Store the MPI communicator for later use
    call store_comm(this%getMpiCommunicator())

    call feign_base_mesh_config( filename='foo',                      &
                                 prime_mesh_name='unit_test',         &
                                 geometry=geometry_planar,            &
                                 partitioner=partitioner_planar,      &
                                 fplane=.false., f_lat_deg=0.0_r_def )


    call feign_initial_temperature_config( bvf_square=bvf_square, &
                                           theta_surf=theta_surf, &
                                           pert_width_scaling=pert_width_scaling )

    call feign_planet_config( gravity=gravity, radius=radius, omega=omega, &
                              rd=rd, cp=cp, p_zero=p_zero,                 &
                              scaling_factor=scaling )

    ! Create top level mesh collection
    mesh_collection = mesh_collection_type()
    ! Create top level function space collection
    function_space_collection = function_space_collection_type()
    ! Create top level halo_routing collection
    halo_routing_collection = halo_routing_collection_type()

    this%qr = quadrature_xyoz_type(3, quadrature_rule)

    this%qr_proxy = this%qr%get_quadrature_proxy()
    nqp_h=this%qr_proxy%np_xy
    nqp_v=this%qr_proxy%np_z

    ! Dummy mesh mod has 9 cells, 3 layers and is uniform in vertical
    mesh_id = mesh_collection%add_unit_test_mesh( PLANE_BI_PERIODIC )

    this%w0_fs => function_space_collection%get_fs(mesh_id, element_order, W0)
    this%w2_fs => function_space_collection%get_fs(mesh_id, element_order, W2)
    this%w3_fs => function_space_collection%get_fs(mesh_id, element_order, W3)

    call this%exner%initialise( vector_space = this%w3_fs )
    exner_proxy = this%exner%get_proxy()

    ndf_w3  = exner_proxy%vspace%get_ndf( )
    dim_w3  = exner_proxy%vspace%get_dim_space( )
    undf_w3 = exner_proxy%vspace%get_undf()
    allocate(this%basis_w3(dim_w3,ndf_w3,nqp_h,nqp_v))

    call this%r_u%initialise( vector_space = this%w2_fs )
    r_u_proxy = this%r_u%get_proxy()

    ndf_w2      = r_u_proxy%vspace%get_ndf( )
    dim_w2      = r_u_proxy%vspace%get_dim_space( )
    diff_dim_w2 = r_u_proxy%vspace%get_dim_space_diff( )
    undf_w2     = r_u_proxy%vspace%get_undf()
    allocate(this%basis_w2(dim_w2,ndf_w2,nqp_h,nqp_v))
    allocate(this%diff_basis_w2(diff_dim_w2,ndf_w2,nqp_h,nqp_v))

    call this%theta%initialise( vector_space = this%w0_fs )
    theta_proxy = this%theta%get_proxy()

    ndf_w0      = theta_proxy%vspace%get_ndf( )
    dim_w0      = theta_proxy%vspace%get_dim_space( )
    diff_dim_w0 = theta_proxy%vspace%get_dim_space_diff( )
    undf_w0     = theta_proxy%vspace%get_undf()
    allocate(this%basis_w0(dim_w0,ndf_w0,nqp_h,nqp_v))
    allocate(this%diff_basis_w0(diff_dim_w0,ndf_w0,nqp_h,nqp_v))

  end subroutine setUp

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  subroutine tearDown( this )

    use configuration_mod,        only: final_configuration

    implicit none

    class(exner_gradient_test_type), intent(inout) :: this

    deallocate( this%basis_w3,                     &
                this%basis_w2, this%diff_basis_w2, &
                this%basis_w0, this%diff_basis_w0 )

    call mesh_collection%clear()
    call function_space_collection%clear()
    call halo_routing_collection%clear()
    call final_configuration()

    ! Finalise YAXT
    call xt_finalize()
    ! Clear the stored MPI communicator
    call clear_comm()

  end subroutine tearDown

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  @Test( npes=[1] )
  subroutine test_all( this )

    use exner_gradient_kernel_mod, only : exner_gradient_code

    implicit none

    class(exner_gradient_test_type), intent(inout) :: this

    real(r_def), parameter :: dx = 6000.0_r_def, &
                              dy = 1000.0_r_def, &
                              dz = 2000.0_r_def
    real(r_def), parameter :: tol = 1.0e-8_r_def
    real(r_def), parameter :: answer = 0.0_r_def

    type(field_type) :: chi(3)
    type(field_proxy_type )   :: chi_p(3)

    integer(i_def) :: i, j, k, cell, err

    integer(i_def)           :: nlayers, nqp_h, nqp_v
    integer(i_def)           :: ndf_w0, ndf_w2, ndf_w3
    integer(i_def)           :: undf_w0, undf_w2, undf_w3
    integer(i_def)           :: element_order
    integer(i_def), pointer  :: map_w3(:), map_w2(:), map_w0(:) => null()

    type(field_proxy_type) :: r_u_proxy, exner_proxy, theta_proxy

    real(kind=r_def), pointer :: xp(:,:) => null()
    real(kind=r_def), pointer :: zp(:) => null()
    real(kind=r_def), pointer :: wh(:), wv(:) => null()


    ! Make a coordinate field with a function space
    do i = 1,3
      call chi(i)%initialise( vector_space = this%w0_fs )
      chi_p(i) = chi(i)%get_proxy()
    end do

    ! Compute coordinates
    cell = 1
    do j = 1,3
      do i = 1,3
        map_w0 => chi_p(1)%vspace%get_cell_dofmap( cell )
        do k = 0,3
          chi_p(1)%data(map_w0(1) + k) = real(i-1)*dx
          chi_p(2)%data(map_w0(1) + k) = real(j-1)*dy
          chi_p(3)%data(map_w0(1) + k) = real(k)  *dz
        end do
        cell = cell + 1
      end do
    end do

    ! Test kernel
    r_u_proxy  = this%r_u%get_proxy()
    exner_proxy  = this%exner%get_proxy()
    theta_proxy = this%theta%get_proxy()

    nlayers = exner_proxy%vspace%get_nlayers()
    nqp_h=this%qr_proxy%np_xy
    nqp_v=this%qr_proxy%np_z
    xp=>this%qr_proxy%points_xy
    zp=>this%qr_proxy%points_z
    wh=>this%qr_proxy%weights_xy
    wv=>this%qr_proxy%weights_z

    ndf_w3  = exner_proxy%vspace%get_ndf( )
    undf_w3 = exner_proxy%vspace%get_undf()

    ndf_w2  = r_u_proxy%vspace%get_ndf( )
    undf_w2 = r_u_proxy%vspace%get_undf()

    ndf_w0  = theta_proxy%vspace%get_ndf( )
    undf_w0 = theta_proxy%vspace%get_undf()

    call exner_proxy%vspace%compute_basis_function( &
         this%basis_w3, ndf_w3, nqp_h, nqp_v, xp, zp )

    call r_u_proxy%vspace%compute_basis_function( &
         this%basis_w2, ndf_w2, nqp_h, nqp_v, xp, zp )

    call r_u_proxy%vspace%compute_diff_basis_function( &
         this%diff_basis_w2, ndf_w2, nqp_h, nqp_v, xp, zp )

    call theta_proxy%vspace%compute_basis_function( &
         this%basis_w0, ndf_w0, nqp_h, nqp_v, xp, zp )

    call theta_proxy%vspace%compute_diff_basis_function( &
         this%diff_basis_w0, ndf_w0, nqp_h, nqp_v, xp, zp )

    cell = 1

    map_w3 => exner_proxy%vspace%get_cell_dofmap( cell )
    map_w2 => r_u_proxy%vspace%get_cell_dofmap( cell )
    map_w0 => theta_proxy%vspace%get_cell_dofmap( cell )

    ! Create the data
    r_u_proxy%data(:) = 0.0_r_def
    exner_proxy%data(:) = 0.8_r_def
    theta_proxy%data(:) = 300.0_r_def

    call exner_gradient_code( nlayers,                                   &
                              r_u_proxy%data,                            &
                              exner_proxy%data,                          &
                              theta_proxy%data,                          &
                              ndf_w2, undf_w2,                           &
                              map_w2, this%basis_w2, this%diff_basis_w2, &
                              ndf_w3, undf_w3,                           &
                              map_w3, this%basis_w3,                     &
                              ndf_w0, undf_w0,                           &
                              map_w0, this%basis_w0, this%diff_basis_w0, &
                              nqp_h, nqp_v, wh, wv                       &
                              )

    k = 1
    @assertEqual(answer, r_u_proxy%data(map_w2(5)+k), tol)
    @assertEqual(answer, r_u_proxy%data(map_w2(6)+k), tol)

  end subroutine test_all

end module exner_gradient_kernel_mod_test
