!-----------------------------------------------------------------------------
! Copyright (c) 2017,  Met Office, on behalf of HMSO and Queen's Printer
! For further details please refer to the file LICENCE.original which you
! should have received as part of this distribution.
!-----------------------------------------------------------------------------

!> Test the geopotential gradient computation
!>
module geopotential_gradient_kernel_mod_test

  use constants_mod,                 only : i_def, r_def
  use function_space_collection_mod, only : function_space_collection_type, &
                                            function_space_collection
  use mesh_collection_mod,           only : mesh_collection_type, &
                                            mesh_collection
  use field_mod,                     only : field_type,      &
                                            field_proxy_type
  use function_space_mod,            only : function_space_type
  use fs_continuity_mod,             only : W0, W2
  use pFUnit_Mod
  use quadrature_mod,                only : quadrature_type, GAUSSIAN

  implicit none

  private
  public :: geopotential_gradient_test_type, test_all

  @TestCase
  type, extends(TestCase) :: geopotential_gradient_test_type
    private
    type(quadrature_type)              :: qr
    type(function_space_type), pointer :: w0_fs => null()
    type(function_space_type), pointer :: w2_fs => null()
    type(field_type)                   :: r_u, phi
    real(r_def), allocatable           :: basis_w2(:,:,:,:), &
                                          diff_basis_w0(:,:,:,:)
  contains
    procedure setUp
    procedure tearDown
    procedure test_all
  end type geopotential_gradient_test_type

  integer(i_def), parameter :: element_order = 0

  real(r_def), parameter :: gravity = 10.0_r_def
  real(r_def), parameter :: radius  = 6000000_r_def
  real(r_def), parameter :: omega   = 8.0E-5_r_def
  real(r_def), parameter :: p_zero  = 100000.0_r_def
  real(r_def), parameter :: rd      = 300.0_r_def
  real(r_def), parameter :: cp      = 1000.0_r_def
  real(r_def), parameter :: scaling = 1.0_r_def

contains

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  subroutine setUp( this )

    use base_mesh_config_mod,      only : base_mesh_geometry_planar, &
                                          base_mesh_partitioner_planar
    use finite_element_config_mod, only : finite_element_cellshape_quadrilateral
    use gungho_feign_config_mod,   only : feign_base_mesh_config, &
                                          feign_planet_config
    use reference_element_mod,     only : reference_cube,  &
                                          reference_element
    use mesh_mod,                  only : PLANE_BI_PERIODIC

    implicit none

    class(geopotential_gradient_test_type), intent(inout) :: this

    type( field_proxy_type )  :: r_u_proxy, &
                                 phi_proxy


    integer(i_def) :: nqp_h, nqp_v
    integer(i_def) :: ndf_w0, ndf_w2
    integer(i_def) :: undf_w0, undf_w2
    integer(i_def) :: diff_dim_w0, dim_w2
    integer(i_def) :: mesh_id

    call feign_base_mesh_config( filename='foo', prime_mesh_name='unit_test', &
                                 geometry=base_mesh_geometry_planar,          &
                                 partitioner=base_mesh_partitioner_planar,    &
                                 fplane=.false., f_lat_deg=0.0_r_def )

    call feign_planet_config( gravity=gravity, radius=radius, omega=omega, &
                              rd=rd, cp=cp, p_zero=p_zero,                 &
                              scaling_factor=scaling )

    reference_element = finite_element_cellshape_quadrilateral
    call reference_cube()

    ! Create top level mesh collection
    mesh_collection = mesh_collection_type()
    ! Create top level function space collection
    function_space_collection = function_space_collection_type()

    this%qr = quadrature_type(3, GAUSSIAN)

    nqp_h=this%qr%get_nqp_h()
    nqp_v=this%qr%get_nqp_v()

    ! Dummy mesh mod has 9 cells, 3 layers and is uniform in vertical
    mesh_id = mesh_collection%add_unit_test_mesh( PLANE_BI_PERIODIC )

    this%w0_fs => function_space_collection%get_fs( mesh_id, element_order, W0 )
    this%w2_fs => function_space_collection%get_fs( mesh_id, element_order, W2 )

    this%r_u  = field_type( vector_space = this%w2_fs )
    r_u_proxy = this%r_u%get_proxy()

    ndf_w2  = r_u_proxy%vspace%get_ndf( )
    dim_w2  = r_u_proxy%vspace%get_dim_space( )
    undf_w2 = r_u_proxy%vspace%get_undf()

    allocate( this%basis_w2(dim_w2,ndf_w2,nqp_h,nqp_v) )

    this%phi  = field_type( vector_space = this%w0_fs )
    phi_proxy = this%phi%get_proxy()

    ndf_w0      = phi_proxy%vspace%get_ndf( )
    diff_dim_w0 = phi_proxy%vspace%get_dim_space_diff( )
    undf_w0     = phi_proxy%vspace%get_undf()

    allocate( this%diff_basis_w0(diff_dim_w0,ndf_w0,nqp_h,nqp_v) )

  end subroutine setUp

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  subroutine tearDown( this )

    use reference_element_mod, only : deallocate_reference

    implicit none

    class(geopotential_gradient_test_type), intent(inout) :: this

    deallocate( this%basis_w2, this%diff_basis_w0 )

    call function_space_collection%clear()
    call deallocate_reference()
    call mesh_collection%clear()

  end subroutine tearDown

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  @test
  subroutine test_all( this )

    use geopotential_gradient_kernel_mod, only : geopotential_gradient_code

    implicit none

    class(geopotential_gradient_test_type), intent(inout) :: this

    real(r_def), parameter :: tol = 1.0e-3_r_def
    real(r_def), parameter :: dx = 6000.0_r_def, &
                              dy = 1000.0_r_def, &
                              dz = 2000.0_r_def

    type(field_type)          :: chi(3)
    type(field_proxy_type )   :: chi_p(3)
    type(field_proxy_type)    :: r_u_proxy, &
                                 phi_proxy

    real(r_def) :: answer

    integer :: i, j, k, cell, err

    real(kind=r_def) :: junk1, junk2

    integer                 :: nlayers, nqp_h, nqp_v
    integer                 :: ndf_w0, ndf_w2
    integer                 :: undf_w0, undf_w2
    integer, pointer        :: map_w2(:), map_w0(:) => null()


    real(kind=r_def), pointer :: xp(:,:) => null()
    real(kind=r_def), pointer :: zp(:) => null()
    real(kind=r_def), pointer :: wh(:) => null(), wv(:) => null()


    ! Make a coordinate field with a function space
    do i = 1,3
      chi(i)   = field_type( vector_space = this%w0_fs )
      chi_p(i) = chi(i)%get_proxy()
    end do

    ! Compute coordinates
    cell = 1
    do j = 1,3
      do i = 1,3
        map_w0 => chi_p(1)%vspace%get_cell_dofmap( cell )
        do k = 0,3
          chi_p(1)%data(map_w0(1) + k) = real(i-1)*dx
          chi_p(2)%data(map_w0(1) + k) = real(j-1)*dy
          chi_p(3)%data(map_w0(1) + k) = real(k)  *dz
        end do
        cell = cell + 1
      end do
    end do

    ! Test kernel
    r_u_proxy  = this%r_u%get_proxy()
    phi_proxy  = this%phi%get_proxy()

    nlayers = r_u_proxy%vspace%get_nlayers()
    nqp_h=this%qr%get_nqp_h()
    nqp_v=this%qr%get_nqp_v()
    zp=>this%qr%get_xqp_v()
    xp=>this%qr%get_xqp_h()
    wh=>this%qr%get_wqp_h()
    wv=>this%qr%get_wqp_v()

    ndf_w2      = r_u_proxy%vspace%get_ndf( )
    undf_w2     = r_u_proxy%vspace%get_undf()

    ndf_w0      = phi_proxy%vspace%get_ndf( )
    undf_w0     = phi_proxy%vspace%get_undf()

    call r_u_proxy%vspace%compute_basis_function( &
         this%basis_w2, ndf_w2, nqp_h, nqp_v, xp, zp)

    call phi_proxy%vspace%compute_diff_basis_function( &
         this%diff_basis_w0, ndf_w0, nqp_h, nqp_v, xp, zp)

    cell = 1

    map_w2 => r_u_proxy%vspace%get_cell_dofmap( cell )
    map_w0 => phi_proxy%vspace%get_cell_dofmap( cell )

    ! Create the data
    phi_proxy%data(:) = gravity*chi_p(3)%data(:)
    r_u_proxy%data(:) = 0.0_r_def

    call geopotential_gradient_code( nlayers,                    &
                                     r_u_proxy%data,             &
                                     phi_proxy%data,             &
                                     ndf_w2, undf_w2,            &
                                     map_w2, this%basis_w2,      &
                                     ndf_w0, undf_w0,            &
                                     map_w0, this%diff_basis_w0, &
                                     nqp_h, nqp_v, wh, wv        &
                                     )

    answer = -gravity*dz
    k = 1
    @assertEqual(answer, r_u_proxy%data(map_w2(5)+k), tol)
    @assertEqual(answer, r_u_proxy%data(map_w2(6)+k), tol)

  end subroutine test_all

end module geopotential_gradient_kernel_mod_test
