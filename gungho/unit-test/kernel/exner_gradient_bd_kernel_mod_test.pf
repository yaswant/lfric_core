!-----------------------------------------------------------------------------
! Copyright (c) 2017,  Met Office, on behalf of HMSO and Queen's Printer
! For further details please refer to the file LICENCE.original which you
! should have received as part of this distribution.
!-----------------------------------------------------------------------------

!> Test the exner gradient boundary computation
!>
module exner_gradient_bd_kernel_mod_test

  use constants_mod,                 only : i_def, r_def
  use function_space_collection_mod, only : function_space_collection_type, &
                                            function_space_collection
  use mesh_collection_mod,           only : mesh_collection_type, &
                                            mesh_collection
  use field_mod,                     only : field_type, &
                                            field_proxy_type
  use function_space_mod,            only : function_space_type
  use fs_continuity_mod,             only : W0, W2, W3, Wtheta
  use pFUnit_Mod
  use quadrature_mod,                only : quadrature_type, GAUSSIAN
  use stencil_dofmap_mod,            only : stencil_dofmap_type, STENCIL_CROSS


  implicit none

  private
  public :: exner_gradient_bd_test_type, test_all

  @TestCase
  type, extends(TestCase) :: exner_gradient_bd_test_type
    private
    type(quadrature_type)              :: qr
    type(function_space_type), pointer :: wtheta_fs => null()
    type(function_space_type), pointer :: w0_fs => null()
    type(function_space_type), pointer :: w2_fs => null()
    type(function_space_type), pointer :: w3_fs => null()
    type(field_type)                   :: r_u_bd, exner, theta
    real(r_def), allocatable           :: basis_w3_face(:,:,:,:,:), &
                                          basis_w2_face(:,:,:,:,:), &
                                          basis_wtheta_face(:,:,:,:,:)

    type(stencil_dofmap_type), pointer :: cross_stencil_w2 => null()
    type(stencil_dofmap_type), pointer :: cross_stencil_w3 => null()

    integer, pointer        :: cross_stencil_w2_map(:,:,:) => null()

    integer, pointer        :: cross_stencil_w3_map(:,:,:) => null()
    integer                 :: cross_stencil_w3_size



  contains
    procedure setUp
    procedure tearDown
    procedure test_all
  end type exner_gradient_bd_test_type

  integer(i_def), parameter :: element_order = 0

  real(r_def), parameter :: gravity = 10.0_r_def
  real(r_def), parameter :: radius  = 6000000_r_def
  real(r_def), parameter :: omega   = 8.0E-5_r_def
  real(r_def), parameter :: p_zero  = 100000.0_r_def
  real(r_def), parameter :: rd      = 300.0_r_def
  real(r_def), parameter :: cp      = 1000.0_r_def
  real(r_def), parameter :: scaling = 1.0_r_def

  real(r_def), parameter :: bvf_square = 0.0001_r_def

contains

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  subroutine setUp( this )

    use base_mesh_config_mod,      only : base_mesh_geometry_planar, &
                                          base_mesh_partitioner_planar
    use finite_element_config_mod, only : finite_element_cellshape_quadrilateral
    use gungho_feign_config_mod,   only : feign_base_mesh_config,           &
                                          feign_initial_temperature_config, &
                                          feign_planet_config
    use mesh_mod,                  only : PLANE_BI_PERIODIC
    use reference_element_mod,     only : reference_cube, reference_element, nfaces_h

    implicit none

    class(exner_gradient_bd_test_type), intent(inout) :: this

    type(field_proxy_type)    :: r_u_bd_proxy, exner_proxy, theta_proxy

    integer(i_def) :: nqp_h, nqp_v, nqp_h_1d
    integer(i_def) :: ndf_wtheta, ndf_w2, ndf_w3
    integer(i_def) :: undf_wtheta, undf_w2, undf_w3
    integer(i_def) :: dim_wtheta, dim_w2, dim_w3
    integer(i_def) :: mesh_id

    call feign_base_mesh_config( filename='foo', prime_mesh_name='unit_test', &
                                 geometry=base_mesh_geometry_planar,          &
                                 partitioner=base_mesh_partitioner_planar,    &
                                 fplane=.false., f_lat_deg=0.0_r_def )

    call feign_initial_temperature_config( bvf_square )

    call feign_planet_config( gravity=gravity, radius=radius, omega=omega, &
                              rd=rd, cp=cp, p_zero=p_zero,                 &
                              scaling_factor=scaling )

    reference_element = finite_element_cellshape_quadrilateral
    call reference_cube()

    ! Create top level mesh collection
    mesh_collection = mesh_collection_type()
    ! Create top level function space collection
    function_space_collection = function_space_collection_type()

    this%qr = quadrature_type( 3, GAUSSIAN )

    nqp_h=this%qr%get_nqp_h()
    nqp_v=this%qr%get_nqp_v()

    ! Assumes same number of horizontal quad points in x and y
    nqp_h_1d = int(nqp_h**(1./2.))

    ! Dummy mesh mod has 9 cells, 3 layers and is uniform in vertical
    mesh_id = mesh_collection%add_unit_test_mesh( PLANE_BI_PERIODIC )

    this%wtheta_fs => function_space_collection%get_fs(mesh_id, element_order, Wtheta)
    this%w0_fs => function_space_collection%get_fs(mesh_id, element_order, W0)
    this%w2_fs => function_space_collection%get_fs(mesh_id, element_order, W2)
    this%w3_fs => function_space_collection%get_fs(mesh_id, element_order, W3)

    this%exner   = field_type( vector_space = this%w3_fs )
    exner_proxy = this%exner%get_proxy()

    ndf_w3  = exner_proxy%vspace%get_ndf( )
    dim_w3  = exner_proxy%vspace%get_dim_space( )
    undf_w3 = exner_proxy%vspace%get_undf()
    allocate( this%basis_w3_face(nfaces_h,dim_w3,ndf_w3,nqp_h_1d,nqp_v) )

    this%r_u_bd   = field_type( vector_space = this%w2_fs )
    r_u_bd_proxy = this%r_u_bd%get_proxy()

    ndf_w2      = r_u_bd_proxy%vspace%get_ndf( )
    dim_w2      = r_u_bd_proxy%vspace%get_dim_space( )
    undf_w2     = r_u_bd_proxy%vspace%get_undf()
    allocate( this%basis_w2_face(nfaces_h,dim_w2,ndf_w2,nqp_h_1d,nqp_v) )

    this%theta = field_type( vector_space = this%wtheta_fs )
    theta_proxy = this%theta%get_proxy()

    ndf_wtheta      = theta_proxy%vspace%get_ndf( )
    dim_wtheta      = theta_proxy%vspace%get_dim_space( )
    undf_wtheta     = theta_proxy%vspace%get_undf()
    allocate( this%basis_wtheta_face(nfaces_h,dim_wtheta,ndf_wtheta,nqp_h_1d,nqp_v) )

    this%cross_stencil_w2 => r_u_bd_proxy%vspace%get_stencil_dofmap(STENCIL_CROSS, 1)
    this%cross_stencil_w2_map => this%cross_stencil_w2%get_whole_dofmap()

    this%cross_stencil_w3 => exner_proxy%vspace%get_stencil_dofmap(STENCIL_CROSS, 1)
    this%cross_stencil_w3_map => this%cross_stencil_w3%get_whole_dofmap()
    this%cross_stencil_w3_size = this%cross_stencil_w3%get_size()

  end subroutine setUp

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  subroutine tearDown( this )

    use reference_element_mod, only : deallocate_reference

    implicit none

    class(exner_gradient_bd_test_type), intent(inout) :: this

    deallocate( this%basis_w3_face, &
                this%basis_w2_face, &
                this%basis_wtheta_face  )

    call deallocate_reference()
    call function_space_collection%clear()

  end subroutine tearDown

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  @test
  subroutine test_all( this )

    use exner_gradient_bd_kernel_mod, only : exner_gradient_bd_code
    use reference_element_mod,        only : nfaces_h
    use mesh_mod,                     only : mesh_type

    implicit none

    class(exner_gradient_bd_test_type), intent(inout) :: this

    real(r_def), parameter :: dx = 6000.0_r_def, &
                              dy = 1000.0_r_def, &
                              dz = 2000.0_r_def
    real(r_def), parameter :: tol = 1.0e-3_r_def

    type(field_type) :: chi(3)
    type(field_proxy_type) :: chi_p(3)

    real(r_def), parameter :: tol_v = 1.0e-8_r_def
    real(r_def), parameter :: tol_h = 1.0e-3_r_def
    real(r_def), parameter :: answer_v = 0.0_r_def
    real(r_def), parameter :: answer_h = 300000.0_r_def

    integer :: cell

    integer(i_def)           :: nlayers, nqp_h, nqp_v, nqp_h_1d
    integer(i_def)           :: ndf_wtheta, ndf_w2, ndf_w3
    integer(i_def)           :: undf_wtheta, undf_w2, undf_w3
    integer(i_def), pointer  :: map_w2(:), map_wtheta(:), map_w0(:) => null()

    integer,         pointer :: adjacent_face(:,:) => null()
    type(mesh_type), pointer :: mesh => null()

    integer                 :: ff, ii, jj, k, i, j

    type(field_proxy_type) :: r_u_bd_proxy, exner_proxy, theta_proxy

    real(kind=r_def), pointer :: xp(:,:), xp_f(:,:,:) => null()
    real(kind=r_def), pointer :: zp(:) => null()
    real(kind=r_def), pointer :: wh(:), wv(:) => null()

    ! Make a coordinate field with a function space
    do i = 1,3
      chi(i) = field_type( vector_space = this%w0_fs )
      chi_p(i) = chi(i)%get_proxy()
    end do

    ! Compute coordinates
    cell = 1
    do j = 1,3
      do i = 1,3
        map_w0 => chi_p(1)%vspace%get_cell_dofmap( cell )
        do k = 0,3
          chi_p(1)%data(map_w0(1) + k) = real(i-1)*dx
          chi_p(2)%data(map_w0(1) + k) = real(j-1)*dy
          chi_p(3)%data(map_w0(1) + k) = real(k)  *dz
        end do
        cell = cell + 1
      end do
    end do

    ! Test kernel
    r_u_bd_proxy  = this%r_u_bd%get_proxy()
    exner_proxy  = this%exner%get_proxy()
    theta_proxy = this%theta%get_proxy()

    nlayers = r_u_bd_proxy%vspace%get_nlayers()
    nqp_h=this%qr%get_nqp_h()
    nqp_v=this%qr%get_nqp_v()
    zp=>this%qr%get_xqp_v()
    xp=>this%qr%get_xqp_h()
    wh=>this%qr%get_wqp_h()
    wv=>this%qr%get_wqp_v()

    ! Assumes same number of horizontal quad points in x and y
    nqp_h_1d = int(nqp_h**(1./2.))

    allocate(xp_f(nfaces_h, nqp_h_1d, 2))

    ndf_w3  = exner_proxy%vspace%get_ndf( )
    undf_w3 = exner_proxy%vspace%get_undf()

    ndf_w2  = r_u_bd_proxy%vspace%get_ndf( )
    undf_w2 = r_u_bd_proxy%vspace%get_undf()

    ndf_wtheta  = theta_proxy%vspace%get_ndf( )
    undf_wtheta = theta_proxy%vspace%get_undf()

    ! Quadrature points on horizontal faces

    xp_f(1, :, :) = xp(1:nqp_h_1d, :)
    xp_f(1, :, 1) = 0.0_r_def

    xp_f(2, :, :) = xp(1:nqp_h - nqp_h_1d + 1:nqp_h_1d, :)
    xp_f(2, :, 2) = 0.0_r_def

    xp_f(3, :, :) = xp(nqp_h - nqp_h_1d + 1:nqp_h, :)
    xp_f(3, :, 1) = 1.0_r_def

    xp_f(4, :, :) = xp(nqp_h_1d:nqp_h:nqp_h_1d, :)
    xp_f(4, :, 2) = 1.0_r_def

    do ff = 1, nfaces_h

      call r_u_bd_proxy%vspace%compute_basis_function( &
        this%basis_w2_face(ff,:,:,:,:), ndf_w2, nqp_h_1d, nqp_v, xp_f(ff, :, :), zp)

      call exner_proxy%vspace%compute_basis_function( &
        this%basis_w3_face(ff,:,:,:,:), ndf_w3, nqp_h_1d, nqp_v, xp_f(ff, :,:), zp)

      call theta_proxy%vspace%compute_basis_function( &
        this%basis_wtheta_face(ff,:,:,:,:), ndf_wtheta, nqp_h_1d, nqp_v, xp_f(ff, :, :), zp)

    end do

    cell = 5

    map_w2 => r_u_bd_proxy%vspace%get_cell_dofmap( cell )
    map_wtheta => theta_proxy%vspace%get_cell_dofmap( cell )

    ! Create the data
    r_u_bd_proxy%data(:) = 0.0_r_def
    exner_proxy%data(:) = 1.0_r_def
    theta_proxy%data(:) = 300.0_r_def

    mesh => this%r_u_bd%get_mesh()
    adjacent_face => mesh%get_adjacent_face()

    call exner_gradient_bd_code( nlayers,                             &
                                 ndf_w2, undf_w2,                     &
                                 map_w2,                              &
                                 ndf_w3, undf_w3,                     &
                                 this%cross_stencil_w3_map(:,:,cell), &
                                 this%cross_stencil_w3_size,          &
                                 ndf_wtheta, undf_wtheta,             &
                                 map_wtheta,                          &
                                 r_u_bd_proxy%data,                   &
                                 exner_proxy%data,                    &
                                 theta_proxy%data,                    &
                                 nqp_v, nqp_h_1d, wv,                 &
                                 this%basis_w2_face,                  &
                                 this%basis_w3_face,                  &
                                 this%basis_wtheta_face,              &
                                 adjacent_face(:,cell))

    ! Test there is no contribution on the horizontal faces
    k = 1
    @assertEqual(answer_v, r_u_bd_proxy%data(map_w2(5)+k), tol_v)
    @assertEqual(answer_v, r_u_bd_proxy%data(map_w2(6)+k), tol_v)

    ! Test the contribution on a vertical face
    @assertEqual(answer_h, r_u_bd_proxy%data(map_w2(4)+k), tol_h)

    deallocate(xp_f)

  end subroutine test_all

end module exner_gradient_bd_kernel_mod_test
