!-----------------------------------------------------------------------------
! Copyright (c) 2017,  Met Office, on behalf of HMSO and Queen's Printer
! For further details please refer to the file LICENCE.original which you
! should have received as part of this distribution.
!-----------------------------------------------------------------------------
!> @brief Test suite to verify computation of colour map.
!>
!> @details Fixture provides ESMF init to allow creation of
!> prerequistes to perform colouring.
!>
!-------------------------------------------------------------------------------
module mesh_colouring_mod_test
!-------------------------------------------------------------------------------
  use constants_mod,     only : i_def, r_def, str_def, str_max_filename, IMDI
  use global_mesh_collection_mod, only: global_mesh_collection_type            &
                                      , global_mesh_collection
  use global_mesh_mod,            only: global_mesh_type
  use mesh_collection_mod,        only: mesh_collection_type,                  &
                                        mesh_collection

  use pFUnit_Mod
  use gungho_suite_fixture_mod,   only : get_esmf_handle

  implicit none

  private
  public setUp, tearDown, test_compute_colours_cubedsphere, &
                          test_compute_colours_biperiodic,  &
                          test_compute_colours_generic

  @testCase
  type, public, extends(TestCase) :: compute_colours_test_type
    integer       :: total_ranks, local_rank
    integer       :: xproc, yproc
    integer       :: num_layers
    integer       :: element_order
  contains
    procedure     :: setUp
    procedure     :: tearDown
    procedure     :: test_compute_colours_cubedsphere
    procedure     :: test_compute_colours_biperiodic
  end type compute_colours_test_type

  character(str_def), parameter :: mesh_name ='unit_test'
  
!-------------------------------------------------------------------------------
contains
!-------------------------------------------------------------------------------
subroutine setUp(this)

  use finite_element_config_mod, only : finite_element_cellshape_quadrilateral
  use ESMF
  use reference_element_mod,     only : reference_cube, reference_element

  implicit none

  class(compute_colours_test_type), intent(inout)  :: this
  type(ESMF_VM)  :: vm
  integer        :: rc, local_pet, pet_count

  call ESMF_VMGet(get_esmf_handle(),  &
                  localPet=local_pet, &
                  petCount=pet_count, &
                  rc=rc)
  if (rc  /=  ESMF_SUCCESS) call ESMF_Finalize(endflag=ESMF_END_ABORT)

  reference_element = finite_element_cellshape_quadrilateral
  call reference_cube()

  ! Create top level collections
  global_mesh_collection = global_mesh_collection_type()
  mesh_collection = mesh_collection_type()

  this%total_ranks = 1
  this%local_rank = 0
  this%xproc = 1
  this%yproc = 1
  this%num_layers = 5
  this%element_order = 0

end subroutine setUp
!-------------------------------------------------------------------------------
subroutine tearDown(this)

  use reference_element_mod, only : deallocate_reference
  implicit none

  class(compute_colours_test_type), intent(inout) :: this

  integer  :: rc

  call deallocate_reference()
  call global_mesh_collection%clear()
  call mesh_collection%clear()

end subroutine tearDown
!-------------------------------------------------------------------------------
@test
subroutine test_compute_colours_cubedsphere(this)

  use mesh_mod,             only : mesh_type
  use partition_mod,        only : partition_type, &
                                   partitioner_interface, &
                                   partitioner_cubedsphere_serial
  use extrusion_config_mod, only : extrusion_method_uniform

  implicit none

  class(compute_colours_test_type), intent(inout)  :: this

  character(len=str_max_filename)  :: filename
  integer                          :: total_colours, total_cells, colour_idx
  integer                          :: nc_colour, cic_colour, nc_colour_subset
  type(partition_type)             :: partition
  type(global_mesh_type), pointer  :: global_mesh => null()
  procedure(partitioner_interface), pointer  :: partitioner_ptr => null()

  integer(i_def)                   :: num_cells
  integer(i_def)                   :: num_colours
  integer(kind=i_def), pointer :: num_cell_per_colour(:)
  integer(kind=i_def), pointer :: cells_in_colour(:,:)

  integer(i_def)           :: npanels
  integer(i_def)           :: global_mesh_id
  integer(i_def)           :: mesh_id
  type(mesh_type), pointer :: mesh => null()
  integer(i_def)           :: err
  character( 200 ) :: message

  npanels = 6
  filename = "data/mesh_C32.nc"

  global_mesh_id =  global_mesh_collection%add_new_global_mesh( filename,  &
                                                                mesh_name, &
                                                                npanels )
  global_mesh    => global_mesh_collection%get_global_mesh( global_mesh_id )

  partitioner_ptr => partitioner_cubedsphere_serial

  partition = partition_type( global_mesh, &
                            partitioner_ptr, &
                            this%xproc, &
                            this%yproc, &
                            1, &
                            this%local_rank, &
                            this%total_ranks)
  ! Construct a mesh which is  uniform in vertical for purpose of the test
  mesh_id = mesh_collection%add_new_mesh( global_mesh,     &
                                          partition,       &
                                          this%num_layers, &
                                          10000.0_r_def,   &
                                          extrusion_method_uniform )
  mesh => mesh_collection%get_mesh( mesh_id )

  @assertTrue(mesh%is_coloured(), "Mesh uncoloured")

  num_cells = partition%get_num_cells_in_layer()

  call mesh%get_colours(num_colours, num_cell_per_colour, cells_in_colour)

  ! Is there storage for colour maps...
  @assertTrue(associated(cells_in_colour), "Colours not set for function space.")
  ! Is total number of colours used meaningful...
  write(message,'(A,I5)')"Failed to colour cubed-sphere with 4 colours. Used ",num_colours
  @assertTrue(num_colours == 4, message)

  ! Did we colour everything...
  total_cells = sum(num_cell_per_colour)
  @assertEqual(total_cells, num_cells, "Not all cells coloured.")

  ! Did we use all claimed colours...
  total_colours = size(cells_in_colour, 1)
  @assertEqual(total_colours, num_colours, "Reported used colours unequal to actual used total.")

  ! Does the total in each colour match the cells in the colour map...
  do colour_idx=1, num_colours
    ! Does the total in each colour match the cells in the colour map...
    nc_colour = num_cell_per_colour(colour_idx)
    cic_colour = count(cells_in_colour(colour_idx,:) /= 0)
    @assertEqual(nc_colour, cic_colour, "Mismatch between num-per-colour total and cell count.")
    ! For an unpartitioned mesh all the following should be equal to the number of cells in the colour
    ! So they are not terribly meaningful tests
    nc_colour_subset = mesh%get_last_edge_cell_per_colour(colour_idx)
    @assertEqual(nc_colour, nc_colour_subset, "Mismatch between num-per-colour total and no. upto last edge cell.")
    nc_colour_subset = mesh%get_last_halo_cell_per_colour(colour_idx,1)
    @assertEqual(nc_colour, nc_colour_subset, "Mismatch between num-per-colour total and no. upto last halo cell.")
  end do

  return
end subroutine test_compute_colours_cubedsphere
!-------------------------------------------------------------------------------
@test
subroutine test_compute_colours_biperiodic(this)

  use extrusion_config_mod, only : extrusion_method_uniform
  use mesh_mod,             only : mesh_type
  use partition_mod,        only : partition_type, &
                                   partitioner_interface, &
                                   partitioner_biperiodic

  implicit none

  class(compute_colours_test_type), intent(inout)  :: this

  character(len=str_max_filename)  :: filename
  integer                          :: total_colours, total_cells, colour_idx
  integer                          :: nc_colour, cic_colour, nc_colour_subset
  type(partition_type)             :: partition
  type(global_mesh_type), pointer  :: global_mesh => null()
  procedure(partitioner_interface), pointer  :: partitioner_ptr => null()

  integer(i_def)                   :: num_cells
  integer(i_def)                   :: num_colours
  integer(kind=i_def), pointer :: num_cell_per_colour(:)
  integer(kind=i_def), pointer :: cells_in_colour(:,:)

  integer(i_def)           :: npanels
  integer(i_def)           :: global_mesh_id
  integer(i_def)           :: mesh_id
  type(mesh_type), pointer :: mesh => null()
  integer(i_def)           :: err

  npanels = 1
  filename = "data/mesh_BiP8x8-6000x2000.nc"

  global_mesh_id =  global_mesh_collection%add_new_global_mesh( filename,  &
                                                                mesh_name, &
                                                                npanels )
  global_mesh    => global_mesh_collection%get_global_mesh( global_mesh_id )

  partitioner_ptr => partitioner_biperiodic

  partition = partition_type(global_mesh, &
                            partitioner_ptr, &
                            this%xproc, &
                            this%yproc, &
                            1, &
                            this%local_rank, &
                            this%total_ranks)

  ! Construct a mesh which is  uniform in vertical for purpose of the test
  mesh_id = mesh_collection%add_new_mesh( global_mesh,     &
                                          partition,       &
                                          this%num_layers, &
                                          10000.0_r_def,   &
                                          extrusion_method_uniform )
  mesh => mesh_collection%get_mesh( mesh_id )

  @assertTrue(mesh%is_coloured(), "Mesh uncoloured")

  num_cells = partition%get_num_cells_in_layer()

  call mesh%get_colours(num_colours, num_cell_per_colour, cells_in_colour)

  ! Is there storage for colour maps...
  @assertTrue(associated(cells_in_colour), "Colours not set for function space.")

  ! Is total number of colours used meaningful...
  @assertTrue(num_colours == 4, "Failed to colour biperiodic mesh with 4 colours")

  ! Did we colour everything...
  total_cells = sum(num_cell_per_colour)
  @assertEqual(total_cells, num_cells, "Not all cells coloured.")

  ! Did we use all claimed colours...
  total_colours = size(cells_in_colour, 1)
  @assertEqual(total_colours, num_colours, "Reported used colours unequal to actual used total.")

  ! Does the total in each colour match the cells in the colour map...
  do colour_idx=1, num_colours
    nc_colour = num_cell_per_colour(colour_idx)
    cic_colour = count(cells_in_colour(colour_idx,:) /= 0)
    @assertEqual(nc_colour, cic_colour, "Mismatch between num-per-colour total and cell count.")
    ! For an unpartitioned mesh all the following should be equal to the number of cells in the colour
    ! So they are not terribly meaningful tests
    nc_colour_subset = mesh%get_last_edge_cell_per_colour(colour_idx)
    @assertEqual(nc_colour, nc_colour_subset, "Mismatch between num-per-colour total and no. upto last edge cell.")
    nc_colour_subset = mesh%get_last_halo_cell_per_colour(colour_idx,1)
    @assertEqual(nc_colour, nc_colour_subset, "Mismatch between num-per-colour total and no. upto last halo cell.")
  end do


  return
end subroutine test_compute_colours_biperiodic
! This test uses a cubed sphere mesh but sneakily does not set panel number to 6 so
! forcing the colour algorithm to use the generic colour test
@test
subroutine test_compute_colours_generic(this)

  use mesh_mod,             only : mesh_type
  use partition_mod,        only : partition_type, &
                                   partitioner_interface, &
                                   partitioner_cubedsphere_serial
  use extrusion_config_mod, only : extrusion_method_uniform

  implicit none

  class(compute_colours_test_type), intent(inout)  :: this

  character(len=str_max_filename)  :: filename
  integer                          :: total_colours, total_cells, colour_idx
  integer                          :: nc_colour, cic_colour
  type(partition_type)             :: partition
  type(global_mesh_type), pointer  :: global_mesh => null()
  procedure(partitioner_interface), pointer  :: partitioner_ptr => null()

  integer(i_def)                   :: num_cells
  integer(i_def)                   :: num_colours
  integer(kind=i_def), pointer :: num_cell_per_colour(:)
  integer(kind=i_def), pointer :: cells_in_colour(:,:)

  integer(i_def)           :: npanels
  integer(i_def)           :: global_mesh_id
  integer(i_def)           :: mesh_id
  type(mesh_type), pointer :: mesh => null()
  integer(i_def)           :: err

  ! Force colour generator to use generic colour routine by not saying
  ! it's a cubed sphere
  npanels = IMDI
  filename = "data/mesh_C4.nc"
  global_mesh_id =  global_mesh_collection%add_new_global_mesh( filename,  &
                                                                mesh_name, &
                                                                npanels )
  global_mesh    => global_mesh_collection%get_global_mesh( global_mesh_id )

  partitioner_ptr => partitioner_cubedsphere_serial

  partition = partition_type( global_mesh, &
                            partitioner_ptr, &
                            this%xproc, &
                            this%yproc, &
                            1, &
                            this%local_rank, &
                            this%total_ranks)
  ! Construct a mesh which is  uniform in vertical for purpose of the test
  mesh_id = mesh_collection%add_new_mesh( global_mesh,     &
                                          partition,       &
                                          this%num_layers, &
                                          10000.0_r_def,   &
                                          extrusion_method_uniform )
  mesh => mesh_collection%get_mesh( mesh_id )

  @assertTrue(mesh%is_coloured(), "Mesh uncoloured")

  num_cells = partition%get_num_cells_in_layer()

  call mesh%get_colours(num_colours, num_cell_per_colour, cells_in_colour)

  ! Is there storage for colour maps...
  @assertTrue(associated(cells_in_colour), "Colours not set for function space.")

  ! Is total number of colours used meaningful...
  @assertTrue(num_colours > 0, "Non-positive used colour count.")

  ! Did we colour everything...
  total_cells = sum(num_cell_per_colour)
  @assertEqual(total_cells, num_cells, "Not all cells coloured.")

  ! Did we use all claimed colours...
  total_colours = size(cells_in_colour, 1)
  @assertEqual(total_colours, num_colours, "Reported used colours unequal to actual used total.")

  ! Does the total in each colour match the cells in the colour map...
  do colour_idx=1, num_colours
    nc_colour = num_cell_per_colour(colour_idx)
    cic_colour = count(cells_in_colour(colour_idx,:) /= 0)
    @assertEqual(nc_colour, cic_colour, "Mismatch between num-per-colour total and cell count.")
  end do


  return
end subroutine test_compute_colours_generic
!-------------------------------------------------------------------------------
!-------------------------------------------------------------------------------
end module mesh_colouring_mod_test
!-------------------------------------------------------------------------------
