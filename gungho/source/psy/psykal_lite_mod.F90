!-----------------------------------------------------------------------------
! Copyright (c) 2017,  Met Office, on behalf of HMSO and Queen's Printer
! For further details please refer to the file LICENCE.original which you
! should have received as part of this distribution.
!-----------------------------------------------------------------------------
!
!-------------------------------------------------------------------------------

!> @brief Provides an implementation of the PSy layer

!> @details Contains hand-rolled versions of the PSy layer that can be used for
!> simple testing and development of the scientific code

module psykal_lite_mod

  use field_mod,                    only : field_type, field_proxy_type
  use r_solver_field_mod,           only : r_solver_field_type, r_solver_field_proxy_type
  use scalar_mod,                   only : scalar_type
  use operator_mod,                 only : operator_type, operator_proxy_type, &
                                           r_solver_operator_type, r_solver_operator_proxy_type
  use constants_mod,                only : r_def, i_def, r_double, r_solver, cache_block
  use mesh_mod,                     only : mesh_type
  use function_space_mod,           only : BASIS, DIFF_BASIS

  use quadrature_xyoz_mod,          only : quadrature_xyoz_type, &
                                           quadrature_xyoz_proxy_type
  use quadrature_face_mod,          only : quadrature_face_type, &
                                           quadrature_face_proxy_type
  use departure_points_config_mod,  only : n_dep_pt_iterations

  implicit none
  public

contains

!> Non pointwise Kernels

  !-------------------------------------------------------------------------------
  !> io_mod uses this routine. However, because io_mod is not a algorithm its currently
  !> not clear if it should call into PSy. #1253 will address this point and remove
  !> once decided.
  subroutine invoke_nodal_coordinates_kernel(nodal_coords, chi )
    use nodal_coordinates_kernel_mod, only: nodal_coordinates_code
    use mesh_mod,                     only: mesh_type
    implicit none

    type(field_type), intent(inout)      :: nodal_coords(3)
    type(field_type), intent(in)         :: chi(3)

    type(field_proxy_type) :: x_p(3), chi_p(3)

    integer                 :: cell, nlayers
    integer                 :: ndf_chi, ndf_x
    integer                 :: undf_chi, undf_x
    integer                 :: dim_chi
    integer                 :: df_x, df_chi

    integer, pointer        :: map_chi(:) => null()
    integer, pointer        :: map_x(:) => null()
    real(kind=r_def), pointer :: nodes_x(:,:) => null()

    real(kind=r_def), allocatable  :: basis_chi(:,:,:)
    integer :: i
    type(mesh_type), pointer :: mesh => null()

    do i = 1,3
      x_p(i)   = nodal_coords(i)%get_proxy()
      chi_p(i) = chi(i)%get_proxy()
    end do

    nlayers = x_p(1)%vspace%get_nlayers()

    ndf_x  = x_p(1)%vspace%get_ndf( )
    undf_x = x_p(1)%vspace%get_undf()
    nodes_x => x_p(1)%vspace%get_nodes()

    ndf_chi  = chi_p(1)%vspace%get_ndf( )
    undf_chi = chi_p(1)%vspace%get_undf()
    dim_chi = chi_p(1)%vspace%get_dim_space( )

    ! Evaluate the basis function
    allocate(basis_chi(dim_chi, ndf_chi, ndf_x))
    do df_x = 1, ndf_x
      do df_chi = 1, ndf_chi
        basis_chi(:,df_chi,df_x) = chi_p(1)%vspace%call_function(BASIS,df_chi,nodes_x(:,df_x))
      end do
    end do

    if (chi_p(1)%is_dirty(depth=1)) then
       call chi_p(1)%halo_exchange(depth=1)
    end if
      !
    if (chi_p(2)%is_dirty(depth=1)) then
       call chi_p(2)%halo_exchange(depth=1)
    end if
      !
    if (chi_p(3)%is_dirty(depth=1)) then
       call chi_p(3)%halo_exchange(depth=1)
    end if
    mesh => x_p(1)%vspace%get_mesh()

    do cell = 1, mesh%get_last_halo_cell(1)
       map_x   => x_p(1)%vspace%get_cell_dofmap( cell )
       map_chi => chi_p(1)%vspace%get_cell_dofmap( cell )
       call nodal_coordinates_code(nlayers, &
                                   x_p(1)%data, &
                                   x_p(2)%data, &
                                   x_p(3)%data, &
                                   chi_p(1)%data, &
                                   chi_p(2)%data, &
                                   chi_p(3)%data, &
                                   ndf_x, undf_x, map_x, &
                                   ndf_chi, undf_chi, map_chi, &
                                   basis_chi &
                                  )
    end do

    call x_p(1)%set_dirty()
    call x_p(2)%set_dirty()
    call x_p(3)%set_dirty()

    deallocate(basis_chi)
  end subroutine invoke_nodal_coordinates_kernel

  !-------------------------------------------------------------------------------
  !> io_mod uses this routine. However, because io_mod is not a algorithm its currently
  !> not clear if it should call into PSy. #1253 will address this point and remove
  !> once decided.
  subroutine invoke_nodal_xyz_coordinates_kernel(nodal_coords, chi, panel_id)
    use nodal_xyz_coordinates_kernel_mod, only: nodal_xyz_coordinates_code
    use mesh_mod,                     only: mesh_type
    implicit none

    type(field_type), intent(inout)      :: nodal_coords(3)
    type(field_type), intent(in)         :: chi(3)
    type(field_type), intent(in)         :: panel_id

    type(field_proxy_type) :: x_p(3), chi_p(3), panel_id_proxy

    integer                 :: cell, nlayers
    integer                 :: ndf_chi, ndf_x, ndf_pid
    integer                 :: undf_chi, undf_x, undf_pid
    integer                 :: dim_chi
    integer                 :: df_x, df_chi

    integer, pointer        :: map_pid(:) => null()
    integer, pointer        :: map_chi(:) => null()
    integer, pointer        :: map_x(:) => null()
    real(kind=r_def), pointer :: nodes_x(:,:) => null()

    real(kind=r_def), allocatable  :: basis_chi(:,:,:)
    integer :: i
    type(mesh_type), pointer :: mesh => null()

    do i = 1,3
      x_p(i)   = nodal_coords(i)%get_proxy()
      chi_p(i) = chi(i)%get_proxy()
    end do

    panel_id_proxy = panel_id%get_proxy()

    nlayers = x_p(1)%vspace%get_nlayers()

    ndf_x  = x_p(1)%vspace%get_ndf()
    undf_x = x_p(1)%vspace%get_undf()
    nodes_x => x_p(1)%vspace%get_nodes()

    ndf_chi  = chi_p(1)%vspace%get_ndf()
    undf_chi = chi_p(1)%vspace%get_undf()
    dim_chi = chi_p(1)%vspace%get_dim_space()

    ndf_pid  = panel_id_proxy%vspace%get_ndf()
    undf_pid = panel_id_proxy%vspace%get_undf()

    ! Evaluate the basis function
    allocate(basis_chi(dim_chi, ndf_chi, ndf_x))
    do df_x = 1, ndf_x
      do df_chi = 1, ndf_chi
        basis_chi(:,df_chi,df_x) = chi_p(1)%vspace%call_function(BASIS,df_chi,nodes_x(:,df_x))
      end do
    end do

    if (chi_p(1)%is_dirty(depth=1)) then
       call chi_p(1)%halo_exchange(depth=1)
    end if
      !
    if (chi_p(2)%is_dirty(depth=1)) then
       call chi_p(2)%halo_exchange(depth=1)
    end if
      !
    if (chi_p(3)%is_dirty(depth=1)) then
       call chi_p(3)%halo_exchange(depth=1)
    end if
    if (panel_id_proxy%is_dirty(depth=1)) then
      call panel_id_proxy%halo_exchange(depth=1)
    end if

    mesh => x_p(1)%vspace%get_mesh()

    do cell = 1, mesh%get_last_halo_cell(1)
       map_x   => x_p(1)%vspace%get_cell_dofmap( cell )
       map_chi => chi_p(1)%vspace%get_cell_dofmap( cell )
       map_pid => panel_id_proxy%vspace%get_cell_dofmap( cell )
       call nodal_xyz_coordinates_code(nlayers, &
                                       x_p(1)%data, &
                                       x_p(2)%data, &
                                       x_p(3)%data, &
                                       chi_p(1)%data, &
                                       chi_p(2)%data, &
                                       chi_p(3)%data, &
                                       panel_id_proxy%data, &
                                       ndf_x, undf_x, map_x, &
                                       ndf_chi, undf_chi, map_chi, &
                                       basis_chi, &
                                       ndf_pid, undf_pid, map_pid &
                                      )
    end do

    call x_p(1)%set_dirty()
    call x_p(2)%set_dirty()
    call x_p(3)%set_dirty()

    deallocate(basis_chi)
  end subroutine invoke_nodal_xyz_coordinates_kernel

!-------------------------------------------------------------------------------
  subroutine invoke_compute_dof_level_kernel(level)

  use compute_dof_level_kernel_mod, only: compute_dof_level_code
  use mesh_mod,                     only: mesh_type
  implicit none

  type(field_type), intent(inout) :: level
  type(field_proxy_type) :: l_p
  integer :: cell, ndf, undf
  real(kind=r_def), pointer :: nodes(:,:) => null()
  integer, pointer :: map(:) => null()
  type(mesh_type), pointer :: mesh => null()
  l_p = level%get_proxy()
  undf = l_p%vspace%get_undf()
  ndf  = l_p%vspace%get_ndf()
  nodes => l_p%vspace%get_nodes( )

  mesh => l_p%vspace%get_mesh()
  do cell = 1,mesh%get_last_halo_cell(1)
    map => l_p%vspace%get_cell_dofmap(cell)
    call compute_dof_level_code(l_p%vspace%get_nlayers(),                 &
                                l_p%data,                                 &
                                ndf,                                      &
                                undf,                                     &
                                map,                                      &
                                nodes                                     &
                               )
  end do
  call l_p%set_dirty()

  end subroutine invoke_compute_dof_level_kernel

!-------------------------------------------------------------------------------
! Implemented in #965, kernel requires stencil support. Note that the w2_field is
! required to obtain the W2 stencil_cross which is used in determining
! orientation of cells in the halo
  subroutine invoke_mpi_calc_cell_orientation(w2_field,cell_orientation)

    use mesh_mod, only: mesh_type
    use stencil_dofmap_mod,               only : stencil_dofmap_type, &
                                                 STENCIL_CROSS
    use calc_cell_orientation_kernel_mod, only : calc_cell_orientation_code

    implicit none

    type(field_type), intent(in)      :: w2_field
    type(field_type), intent(inout)   :: cell_orientation

    integer                 :: cell, nlayers
    integer                 :: ndf_w3
    integer                 :: undf_w3
    integer                 :: ndf_w2
    integer, pointer        :: map_w3(:) => null()

    type(field_proxy_type) :: cell_orientation_proxy
    type(field_proxy_type) :: w2_field_proxy

    type(mesh_type), pointer           :: mesh => null()
    type(stencil_dofmap_type), pointer :: cross_stencil_w2 => null()
    type(stencil_dofmap_type), pointer :: cross_stencil_w3 => null()

    integer, pointer        :: cross_stencil_w2_map(:,:,:) => null()
    integer, pointer        :: cross_stencil_w3_map(:,:,:) => null()
    integer                 :: cross_stencil_w3_size


    cell_orientation_proxy = cell_orientation%get_proxy()
    w2_field_proxy = w2_field%get_proxy()
    mesh => w2_field_proxy%vspace%get_mesh()

    nlayers = cell_orientation_proxy%vspace%get_nlayers()
    ndf_w3  = cell_orientation_proxy%vspace%get_ndf( )
    undf_w3 = cell_orientation_proxy%vspace%get_undf()
    ndf_w2  = w2_field_proxy%vspace%get_ndf( )

    ! Obtain the stencil for core cells only
    cross_stencil_w2 => w2_field_proxy%vspace%get_stencil_dofmap(             &
                                        STENCIL_CROSS, mesh%get_halo_depth())
    cross_stencil_w2_map => cross_stencil_w2%get_whole_dofmap()

    cross_stencil_w3 => cell_orientation_proxy%vspace%get_stencil_dofmap(     &
                                        STENCIL_CROSS, mesh%get_halo_depth())
    cross_stencil_w3_map => cross_stencil_w3%get_whole_dofmap()
    cross_stencil_w3_size = cross_stencil_w3%get_size()

    do cell=1,mesh%get_last_edge_cell() ! Loop over core cells only
      map_w3 => cell_orientation_proxy%vspace%get_cell_dofmap(cell)

      call calc_cell_orientation_code(  nlayers,                              &
                                        cell_orientation_proxy%data,          &
                                        undf_w3,                              &
                                        ndf_w3,                               &
                                        map_w3,                               &
                                        ndf_w2,                               &
                                        cross_stencil_w3_size,                &
                                        cross_stencil_w2_map(:,:,cell),       &
                                        cross_stencil_w3_map(:,:,cell) )
    end do

  end subroutine invoke_mpi_calc_cell_orientation

  !----------------------------------------------------------------------------
  ! This requires a stencil of horizontal cells for the operators
  ! see PSyclone #1103: https://github.com/stfc/PSyclone/issues/1103
  ! The LFRic infrastructure for this will be introduced in #2532
  subroutine invoke_helmholtz_operator_kernel_type(helmholtz_operator, hb_lumped_inv, stencil_depth, u_normalisation, div_star, &
                                                   t_normalisation, ptheta2v, compound_div, m3_exner_star, p3theta, w2_mask)
    use helmholtz_operator_kernel_mod, only: helmholtz_operator_code
    use mesh_mod, only: mesh_type
    use stencil_2d_dofmap_mod, only: stencil_2d_cross
    use stencil_2d_dofmap_mod, only: stencil_2d_dofmap_type
    use reference_element_mod, only: reference_element_type
    implicit none

    type(r_solver_field_type), intent(in) :: helmholtz_operator(9)
    type(r_solver_field_type), intent(in) ::  hb_lumped_inv, u_normalisation, t_normalisation, w2_mask
    type(r_solver_operator_type), intent(in) :: div_star, ptheta2v, compound_div, m3_exner_star, p3theta
    integer(kind=i_def), intent(in) :: stencil_depth
    integer(kind=i_def) :: stencil_size
    integer(kind=i_def) cell
    integer(kind=i_def) nlayers
    type(r_solver_operator_proxy_type) div_star_proxy, ptheta2v_proxy, compound_div_proxy, m3_exner_star_proxy, p3theta_proxy
    type(r_solver_field_proxy_type) helmholtz_operator_proxy(9)
    type(r_solver_field_proxy_type) hb_lumped_inv_proxy, u_normalisation_proxy, t_normalisation_proxy, &
                           w2_mask_proxy
    integer(kind=i_def), pointer :: map_w2(:,:) => null(), map_w3(:,:) => null(), map_wtheta(:,:) => null()
    integer(kind=i_def) ndf_w3, undf_w3, ndf_w2, undf_w2, ndf_wtheta, undf_wtheta
    type(mesh_type), pointer :: mesh => null()
    INTEGER(KIND=i_def) :: hb_lumped_inv_max_branch_length
    integer(kind=i_def), pointer :: hb_lumped_inv_stencil_sizes(:,:) => null()
    integer(kind=i_def), pointer :: hb_lumped_inv_stencil_dofmap(:,:,:,:) => null()
    type(stencil_2d_dofmap_type), pointer :: hb_lumped_inv_stencil_map => null()
    integer(kind=i_def) :: i,j
    integer(kind=i_def), allocatable :: cell_stencil(:)
    integer(kind=i_def) nfaces_re_h
    class(reference_element_type), pointer :: reference_element => null()
    !
    ! Initialise field and/or operator proxies
    !
    helmholtz_operator_proxy(1) = helmholtz_operator(1)%get_proxy()
    helmholtz_operator_proxy(2) = helmholtz_operator(2)%get_proxy()
    helmholtz_operator_proxy(3) = helmholtz_operator(3)%get_proxy()
    helmholtz_operator_proxy(4) = helmholtz_operator(4)%get_proxy()
    helmholtz_operator_proxy(5) = helmholtz_operator(5)%get_proxy()
    helmholtz_operator_proxy(6) = helmholtz_operator(6)%get_proxy()
    helmholtz_operator_proxy(7) = helmholtz_operator(7)%get_proxy()
    helmholtz_operator_proxy(8) = helmholtz_operator(8)%get_proxy()
    helmholtz_operator_proxy(9) = helmholtz_operator(9)%get_proxy()
    hb_lumped_inv_proxy = hb_lumped_inv%get_proxy()
    u_normalisation_proxy = u_normalisation%get_proxy()
    div_star_proxy = div_star%get_proxy()
    t_normalisation_proxy = t_normalisation%get_proxy()
    ptheta2v_proxy = ptheta2v%get_proxy()
    compound_div_proxy = compound_div%get_proxy()
    m3_exner_star_proxy = m3_exner_star%get_proxy()
    p3theta_proxy = p3theta%get_proxy()
    w2_mask_proxy = w2_mask%get_proxy()
    !
    ! Initialise number of layers
    !
    nlayers = helmholtz_operator_proxy(1)%vspace%get_nlayers()
    !
    ! Create a mesh object
    !
    mesh => helmholtz_operator_proxy(1)%vspace%get_mesh()
    !
    ! Initialise stencil dofmaps
    !
    hb_lumped_inv_stencil_map => hb_lumped_inv_proxy%vspace%get_stencil_2D_dofmap(STENCIL_2D_CROSS,stencil_depth)
    hb_lumped_inv_max_branch_length = stencil_depth + 1
    hb_lumped_inv_stencil_dofmap => hb_lumped_inv_stencil_map%get_whole_dofmap()
    hb_lumped_inv_stencil_sizes => hb_lumped_inv_stencil_map%get_stencil_sizes()
    !
    ! Look-up dofmaps for each function space
    !
    map_w3 => helmholtz_operator_proxy(1)%vspace%get_whole_dofmap()
    map_w2 => hb_lumped_inv_proxy%vspace%get_whole_dofmap()
    map_wtheta => t_normalisation_proxy%vspace%get_whole_dofmap()
    !
    ! Initialise number of DoFs for w3
    !
    ndf_w3 = helmholtz_operator_proxy(1)%vspace%get_ndf()
    undf_w3 = helmholtz_operator_proxy(1)%vspace%get_undf()
    !
    ! Initialise number of DoFs for w2
    !
    ndf_w2 = hb_lumped_inv_proxy%vspace%get_ndf()
    undf_w2 = hb_lumped_inv_proxy%vspace%get_undf()
    !
    ! Initialise number of DoFs for wtheta
    !
    ndf_wtheta = t_normalisation_proxy%vspace%get_ndf()
    undf_wtheta = t_normalisation_proxy%vspace%get_undf()
    !
    ! Call kernels and communication routines
    !
    if (hb_lumped_inv_proxy%is_dirty(depth=1)) then
      call hb_lumped_inv_proxy%halo_exchange(depth=1)
    end if
    if (u_normalisation_proxy%is_dirty(depth=1)) then
      call u_normalisation_proxy%halo_exchange(depth=1)
    end if
    if (t_normalisation_proxy%is_dirty(depth=1)) then
      call t_normalisation_proxy%halo_exchange(depth=1)
    end if
    if (w2_mask_proxy%is_dirty(depth=1)) then
      call w2_mask_proxy%halo_exchange(depth=1)
    end if
    !
    ! Get the reference element and query its properties
    !
    reference_element => mesh%get_reference_element()
    nfaces_re_h = reference_element%get_number_horizontal_faces()
    !
    ! Create cell stencil of the correct size
    stencil_size =  1 + nfaces_re_h*stencil_depth
    allocate( cell_stencil( stencil_size ) )

    !$omp parallel default(shared), private(cell, cell_stencil, i, j)
    !$omp do schedule(static)
    do cell=1,mesh%get_last_edge_cell()
      !
      ! Populate cell_stencil array used for operators
      ! (this is the id of each cell in the stencil)
      cell_stencil(:) = 0
      cell_stencil(1) = cell
      j=0
      do i = 1,nfaces_re_h
        if (mesh%get_cell_next(i, cell) /= 0)then
          j=j+1
          cell_stencil(j+1) = mesh%get_cell_next(i, cell)
        end if
      end do
      call helmholtz_operator_code(stencil_size,                     &
                                   cell_stencil, nlayers,            &
                                   helmholtz_operator_proxy(1)%data, &
                                   helmholtz_operator_proxy(2)%data, &
                                   helmholtz_operator_proxy(3)%data, &
                                   helmholtz_operator_proxy(4)%data, &
                                   helmholtz_operator_proxy(5)%data, &
                                   helmholtz_operator_proxy(6)%data, &
                                   helmholtz_operator_proxy(7)%data, &
                                   helmholtz_operator_proxy(8)%data, &
                                   helmholtz_operator_proxy(9)%data, &
                                   hb_lumped_inv_proxy%data, &
                                   hb_lumped_inv_stencil_sizes(:,cell), &
                                   hb_lumped_inv_max_branch_length,  &
                                   hb_lumped_inv_stencil_dofmap(:,:,:,cell), &
                                   u_normalisation_proxy%data, &
                                   div_star_proxy%ncell_3d, &
                                   div_star_proxy%local_stencil, &
                                   t_normalisation_proxy%data, &
                                   ptheta2v_proxy%ncell_3d, &
                                   ptheta2v_proxy%local_stencil, &
                                   compound_div_proxy%ncell_3d, &
                                   compound_div_proxy%local_stencil, &
                                   m3_exner_star_proxy%ncell_3d, &
                                   m3_exner_star_proxy%local_stencil, &
                                   p3theta_proxy%ncell_3d, &
                                   p3theta_proxy%local_stencil, &
                                   w2_mask_proxy%data, &
                                   ndf_w3, undf_w3, map_w3(:,cell), &
                                   ndf_w2, undf_w2, map_w2(:,cell), &
                                   ndf_wtheta, undf_wtheta, map_wtheta(:,cell))
    end do
    !$omp end do
    !
    ! Set halos dirty/clean for fields modified in the above loop
    !
    !$omp master
    call helmholtz_operator_proxy(1)%set_dirty()
    call helmholtz_operator_proxy(2)%set_dirty()
    call helmholtz_operator_proxy(3)%set_dirty()
    call helmholtz_operator_proxy(4)%set_dirty()
    call helmholtz_operator_proxy(5)%set_dirty()
    call helmholtz_operator_proxy(6)%set_dirty()
    call helmholtz_operator_proxy(7)%set_dirty()
    call helmholtz_operator_proxy(8)%set_dirty()
    call helmholtz_operator_proxy(9)%set_dirty()
    !$omp end master
    !
    !$omp end parallel
    !
  end subroutine invoke_helmholtz_operator_kernel_type

  !----------------------------------------------------------------------------
  ! This requires a stencil of horizontal cells for the operators
  ! see PSyclone #1103: https://github.com/stfc/PSyclone/issues/1103
  ! The LFRic infrastructure for this will be introduced in #2532
  subroutine invoke_elim_helmholtz_operator_kernel_type(helmholtz_operator, hb_lumped_inv, stencil_depth, &
                                                   u_normalisation, div_star, &
                                                   m3_exner_star, Q32, &
                                                   w2_mask)
    use elim_helmholtz_operator_kernel_mod, only: elim_helmholtz_operator_code
    use mesh_mod, only: mesh_type
    use stencil_dofmap_mod, only: stencil_cross
    use stencil_dofmap_mod, only: stencil_dofmap_type
    use reference_element_mod, only: reference_element_type

    implicit none

    type(r_solver_field_type), intent(in) :: helmholtz_operator(9)
    type(r_solver_field_type), intent(in) :: hb_lumped_inv, u_normalisation, w2_mask
    type(r_solver_operator_type), intent(in) :: div_star, m3_exner_star, Q32
    integer(kind=i_def), intent(in) :: stencil_depth
    integer(kind=i_def) :: stencil_size
    integer(kind=i_def) cell
    integer(kind=i_def) nlayers
    type(r_solver_operator_proxy_type) div_star_proxy, m3_exner_star_proxy, Q32_proxy
    type(r_solver_field_proxy_type) helmholtz_operator_proxy(9)
    type(r_solver_field_proxy_type) hb_lumped_inv_proxy, u_normalisation_proxy, &
                           w2_mask_proxy
    integer(kind=i_def), pointer :: map_w2(:,:) => null(), map_w3(:,:) => null()
    integer(kind=i_def) ndf_w3, undf_w3, ndf_w2, undf_w2
    type(mesh_type), pointer :: mesh => null()
    integer(kind=i_def) hb_lumped_inv_stencil_size
    integer(kind=i_def), pointer :: hb_lumped_inv_stencil_dofmap(:,:,:) => null()
    type(stencil_dofmap_type), pointer :: hb_lumped_inv_stencil_map => null()
    integer(kind=i_def) :: i
    integer(kind=i_def), allocatable :: cell_stencil(:)
    integer(kind=i_def) nfaces_re_h
    class(reference_element_type), pointer :: reference_element => null()
    !
    ! Initialise field and/or operator proxies
    !
    helmholtz_operator_proxy(1) = helmholtz_operator(1)%get_proxy()
    helmholtz_operator_proxy(2) = helmholtz_operator(2)%get_proxy()
    helmholtz_operator_proxy(3) = helmholtz_operator(3)%get_proxy()
    helmholtz_operator_proxy(4) = helmholtz_operator(4)%get_proxy()
    helmholtz_operator_proxy(5) = helmholtz_operator(5)%get_proxy()
    helmholtz_operator_proxy(6) = helmholtz_operator(6)%get_proxy()
    helmholtz_operator_proxy(7) = helmholtz_operator(7)%get_proxy()
    helmholtz_operator_proxy(8) = helmholtz_operator(8)%get_proxy()
    helmholtz_operator_proxy(9) = helmholtz_operator(9)%get_proxy()
    hb_lumped_inv_proxy = hb_lumped_inv%get_proxy()
    u_normalisation_proxy = u_normalisation%get_proxy()
    div_star_proxy = div_star%get_proxy()
    m3_exner_star_proxy = m3_exner_star%get_proxy()
    Q32_proxy = Q32%get_proxy()
    w2_mask_proxy = w2_mask%get_proxy()
    !
    ! Initialise number of layers
    !
    nlayers = helmholtz_operator_proxy(1)%vspace%get_nlayers()
    !
    ! Create a mesh object
    !
    mesh => helmholtz_operator_proxy(1)%vspace%get_mesh()
    !
    ! Initialise stencil dofmaps
    !
    hb_lumped_inv_stencil_map => hb_lumped_inv_proxy%vspace%get_stencil_dofmap(STENCIL_CROSS,stencil_depth)
    hb_lumped_inv_stencil_dofmap => hb_lumped_inv_stencil_map%get_whole_dofmap()
    hb_lumped_inv_stencil_size = hb_lumped_inv_stencil_map%get_size()
    !
    ! Look-up dofmaps for each function space
    !
    map_w3 => helmholtz_operator_proxy(1)%vspace%get_whole_dofmap()
    map_w2 => hb_lumped_inv_proxy%vspace%get_whole_dofmap()
    !
    ! Initialise number of DoFs for w3
    !
    ndf_w3 = helmholtz_operator_proxy(1)%vspace%get_ndf()
    undf_w3 = helmholtz_operator_proxy(1)%vspace%get_undf()
    !
    ! Initialise number of DoFs for w2
    !
    ndf_w2 = hb_lumped_inv_proxy%vspace%get_ndf()
    undf_w2 = hb_lumped_inv_proxy%vspace%get_undf()
    !
    ! Call kernels and communication routines
    !
    if (hb_lumped_inv_proxy%is_dirty(depth=1)) then
      call hb_lumped_inv_proxy%halo_exchange(depth=1)
    end if
    if (u_normalisation_proxy%is_dirty(depth=1)) then
      call u_normalisation_proxy%halo_exchange(depth=1)
    end if
    if (w2_mask_proxy%is_dirty(depth=1)) then
      call w2_mask_proxy%halo_exchange(depth=1)
    end if
    !
    ! Get the reference element and query its properties
    !
    reference_element => mesh%get_reference_element()
    nfaces_re_h = reference_element%get_number_horizontal_faces()
    !
    ! Create cell stencil of the correct size
    stencil_size =  1 + nfaces_re_h*stencil_depth
    allocate( cell_stencil( stencil_size ) )

    !$omp parallel default(shared), private(cell, cell_stencil, i)
    !$omp do schedule(static)
    do cell=1,mesh%get_last_edge_cell()
      !
      ! Populate cell_stencil array used for operators
      ! (this is the id of each cell in the stencil)
      cell_stencil(1) = cell
      do i = 1,nfaces_re_h
        cell_stencil(i+1) = mesh%get_cell_next(i, cell)
      end do
      call elim_helmholtz_operator_code(stencil_size,                &
                                   cell_stencil, nlayers,            &
                                   helmholtz_operator_proxy(1)%data, &
                                   helmholtz_operator_proxy(2)%data, &
                                   helmholtz_operator_proxy(3)%data, &
                                   helmholtz_operator_proxy(4)%data, &
                                   helmholtz_operator_proxy(5)%data, &
                                   helmholtz_operator_proxy(6)%data, &
                                   helmholtz_operator_proxy(7)%data, &
                                   helmholtz_operator_proxy(8)%data, &
                                   helmholtz_operator_proxy(9)%data, &
                                   hb_lumped_inv_proxy%data, &
                                   hb_lumped_inv_stencil_size, &
                                   hb_lumped_inv_stencil_dofmap(:,:,cell), &
                                   u_normalisation_proxy%data, &
                                   div_star_proxy%ncell_3d, &
                                   div_star_proxy%local_stencil, &
                                   m3_exner_star_proxy%ncell_3d, &
                                   m3_exner_star_proxy%local_stencil, &
                                   Q32_proxy%ncell_3d, &
                                   Q32_proxy%local_stencil, &
                                   w2_mask_proxy%data, &
                                   ndf_w3, undf_w3, map_w3(:,cell), &
                                   ndf_w2, undf_w2, map_w2(:,cell))
    end do
    !$omp end do
    !
    ! Set halos dirty/clean for fields modified in the above loop
    !
    !$omp master
    call helmholtz_operator_proxy(1)%set_dirty()
    call helmholtz_operator_proxy(2)%set_dirty()
    call helmholtz_operator_proxy(3)%set_dirty()
    call helmholtz_operator_proxy(4)%set_dirty()
    call helmholtz_operator_proxy(5)%set_dirty()
    call helmholtz_operator_proxy(6)%set_dirty()
    call helmholtz_operator_proxy(7)%set_dirty()
    call helmholtz_operator_proxy(8)%set_dirty()
    call helmholtz_operator_proxy(9)%set_dirty()
    !$omp end master
    !
    !$omp end parallel
    !
  end subroutine invoke_elim_helmholtz_operator_kernel_type

  !----------------------------------------------------------------------------
  !> Requires GH_INC field to be halo swapped before updating. #
  !> Described by Issue #1292.
  !> https://github.com/stfc/PSyclone/issues/1292
    SUBROUTINE invoke_impose_min_flux_kernel_type(field, mass_flux, div, &
                                                    field_min, dt_step)
      USE impose_min_flux_kernel_mod, ONLY: impose_min_flux_code
      USE mesh_mod,                   ONLY: mesh_type
      USE operator_mod,               ONLY: operator_type, operator_proxy_type

      implicit none

      REAL(KIND=r_def), intent(in)    :: dt_step
      REAL(KIND=r_def), intent(in)    :: field_min
      TYPE(field_type), intent(in)    :: field, mass_flux
      TYPE(operator_type), intent(in) :: div
      INTEGER(KIND=i_def) cell
      INTEGER(KIND=i_def) nlayers
      TYPE(operator_proxy_type) div_proxy
      TYPE(field_proxy_type) field_proxy, mass_flux_proxy
      INTEGER(KIND=i_def), pointer :: map_w2(:,:) => null(), map_w3(:,:) => null()
      INTEGER(KIND=i_def) ndf_w3, undf_w3, ndf_w2, undf_w2
      TYPE(mesh_type), pointer :: mesh => null()
      !
      ! Initialise field and/or operator proxies
      !
      field_proxy = field%get_proxy()
      mass_flux_proxy = mass_flux%get_proxy()
      div_proxy = div%get_proxy()
      !
      ! Initialise number of layers
      !
      nlayers = field_proxy%vspace%get_nlayers()
      !
      ! Create a mesh object
      !
      mesh => field_proxy%vspace%get_mesh()
      !
      ! Look-up dofmaps for each function space
      !
      map_w3 => field_proxy%vspace%get_whole_dofmap()
      map_w2 => mass_flux_proxy%vspace%get_whole_dofmap()
      !
      ! Initialise number of DoFs for w3
      !
      ndf_w3 = field_proxy%vspace%get_ndf()
      undf_w3 = field_proxy%vspace%get_undf()
      !
      ! Initialise number of DoFs for w2
      !
      ndf_w2 = mass_flux_proxy%vspace%get_ndf()
      undf_w2 = mass_flux_proxy%vspace%get_undf()
      !
      ! Call kernels and communication routines
      !
      IF (field_proxy%is_dirty(depth=1)) THEN
        CALL field_proxy%halo_exchange(depth=1)
      END IF

      ! Extra Halo swap that is currently not added by PSyclone for a GH_INC
      ! field.  Issue #1292 will look at add a new type to cover this case.
      IF (mass_flux_proxy%is_dirty(depth=1)) THEN
        CALL mass_flux_proxy%halo_exchange(depth=1)
      END IF

      !
      DO cell=1,mesh%get_last_halo_cell(1)
        !
        CALL impose_min_flux_code(cell, nlayers, field_proxy%data, mass_flux_proxy%data, div_proxy%ncell_3d, &
&div_proxy%local_stencil, field_min, dt_step, ndf_w3, undf_w3, &
&map_w3(:,cell), ndf_w2, undf_w2, map_w2(:,cell))
      END DO
      !
      ! Set halos dirty/clean for fields modified in the above loop
      !
      CALL mass_flux_proxy%set_dirty()
      !
      !
    END SUBROUTINE invoke_impose_min_flux_kernel_type

  !-------------------------------------------------------------------------------
  !> This PSyKAl-lite code is required because, currently, PSYclone does not support
  !> the output of scalar variables from kernels.(See PSyclone issue #1818)
  !> This subroutine recovers a scalar value from a field. This is required
  !> as scalars can't currently be written to checkpoint files. The workaround is to
  !> copy the scalar to a field, which may then be checkpointed. On a restart the
  !> scalar value needs to be recovered from the checkpointed field.
    subroutine invoke_getvalue(field, val)
      implicit none
      real(r_def), intent(out)     :: val
      type(field_type), intent(in) :: field
      type(field_proxy_type)       :: field_proxy
      field_proxy = field%get_proxy()
      val = field_proxy%data(1)
    end subroutine invoke_getvalue

    ! Psyclone does not currently have native support for builtins with mixed
    ! precision, this will be addressed in https://github.com/stfc/PSyclone/issues/1786
    ! Perform innerproduct of a r_solver precision field in r_double precision
    subroutine invoke_rdouble_X_innerproduct_X(field_norm, field)

      use scalar_mod,         only: scalar_type
      use omp_lib,            only: omp_get_thread_num
      use omp_lib,            only: omp_get_max_threads
      use mesh_mod,           only: mesh_type
      use r_solver_field_mod, only: r_solver_field_type, r_solver_field_proxy_type

      implicit none

      real(kind=r_def), intent(out) :: field_norm
      type(r_solver_field_type), intent(in) :: field

      type(scalar_type)                           :: global_sum
      integer(kind=i_def)                         :: df
      real(kind=r_double), allocatable, dimension(:) :: l_field_norm
      integer(kind=i_def)                         :: th_idx
      integer(kind=i_def)                         :: loop0_start, loop0_stop
      integer(kind=i_def)                         :: nthreads
      type(r_solver_field_proxy_type)             :: field_proxy
      integer(kind=i_def)                         :: max_halo_depth_mesh
      type(mesh_type), pointer                    :: mesh => null()
      !
      ! Determine the number of OpenMP threads
      !
      nthreads = omp_get_max_threads()
      !
      ! Initialise field and/or operator proxies
      !
      field_proxy = field%get_proxy()
      !
      ! Create a mesh object
      !
      mesh => field_proxy%vspace%get_mesh()
      max_halo_depth_mesh = mesh%get_halo_depth()
      !
      ! Set-up all of the loop bounds
      !
      loop0_start = 1
      loop0_stop = field_proxy%vspace%get_last_dof_owned()
      !
      ! Call kernels and communication routines
      !
      !
      ! Zero summation variables
      !
      field_norm = 0.0_r_def
      ALLOCATE (l_field_norm(nthreads))
      l_field_norm = 0.0_r_double
      !
      !$omp parallel default(shared), private(df,th_idx)
      th_idx = omp_get_thread_num()+1
      !$omp do schedule(static)
      DO df=loop0_start,loop0_stop
        l_field_norm(th_idx) = l_field_norm(th_idx) + real(field_proxy%data(df),r_double)**2
      END DO
      !$omp end do
      !$omp end parallel
      !
      ! sum the partial results sequentially
      !
      DO th_idx=1,nthreads
        field_norm = field_norm+real(l_field_norm(th_idx),r_def)
      END DO
      DEALLOCATE (l_field_norm)
      global_sum%value = field_norm
      field_norm = global_sum%get_sum()
      !
    end subroutine invoke_rdouble_X_innerproduct_X

    ! Psyclone does not currently have native support for builtins with mixed
    ! precision, this will be addressed in https://github.com/stfc/PSyclone/issues/1786
    ! Perform innerproduct of a r_solver precision field in r_def precision
    subroutine invoke_rdouble_X_innerproduct_Y(field_norm, field1, field2)

      use scalar_mod,         only: scalar_type
      use omp_lib,            only: omp_get_thread_num
      use omp_lib,            only: omp_get_max_threads
      use mesh_mod,           only: mesh_type
      use r_solver_field_mod, only: r_solver_field_type, r_solver_field_proxy_type

      implicit none

      real(kind=r_def), intent(out) :: field_norm
      type(r_solver_field_type), intent(in) :: field1, field2

      type(scalar_type)                           :: global_sum
      integer(kind=i_def)                         :: df
      real(kind=r_double), allocatable, dimension(:) :: l_field_norm
      integer(kind=i_def)                         :: th_idx
      integer(kind=i_def)                         :: loop0_start, loop0_stop
      integer(kind=i_def)                         :: nthreads
      type(r_solver_field_proxy_type)             :: field1_proxy, field2_proxy
      integer(kind=i_def)                         :: max_halo_depth_mesh
      type(mesh_type), pointer                    :: mesh => null()
      !
      ! Determine the number of OpenMP threads
      !
      nthreads = omp_get_max_threads()
      !
      ! Initialise field and/or operator proxies
      !
      field1_proxy = field1%get_proxy()
      field2_proxy = field2%get_proxy()
      !
      ! Create a mesh object
      !
      mesh => field1_proxy%vspace%get_mesh()
      max_halo_depth_mesh = mesh%get_halo_depth()
      !
      ! Set-up all of the loop bounds
      !
      loop0_start = 1
      loop0_stop = field1_proxy%vspace%get_last_dof_owned()
      !
      ! Call kernels and communication routines
      !
      !
      ! Zero summation variables
      !
      field_norm = 0.0_r_def
      ALLOCATE (l_field_norm(nthreads))
      l_field_norm = 0.0_r_double
      !
      !$omp parallel default(shared), private(df,th_idx)
      th_idx = omp_get_thread_num()+1
      !$omp do schedule(static)
      DO df=loop0_start,loop0_stop
        l_field_norm(th_idx) = l_field_norm(th_idx) + real(field1_proxy%data(df),r_double)*real(field2_proxy%data(df),r_double)
      END DO
      !$omp end do
      !$omp end parallel
      !
      ! sum the partial results sequentially
      !
      DO th_idx=1,nthreads
        field_norm = field_norm+real(l_field_norm(th_idx),r_def)
      END DO
      DEALLOCATE (l_field_norm)
      global_sum%value = field_norm
      field_norm = global_sum%get_sum()
      !
    end subroutine invoke_rdouble_X_innerproduct_Y

    ! Psyclone does not currently have native support for builtins with mixed
    ! precision, this will be addressed in https://github.com/stfc/PSyclone/issues/1786
    ! Copy a field_type to a r_solver_field_type
    subroutine invoke_copy_to_rsolver(rsolver_field, field)

      use omp_lib,            only: omp_get_thread_num
      use omp_lib,            only: omp_get_max_threads
      use mesh_mod,           only: mesh_type
      use r_solver_field_mod, only: r_solver_field_type, r_solver_field_proxy_type
      use field_mod,          only: field_type, field_proxy_type

      implicit none

      type(r_solver_field_type), intent(inout) :: rsolver_field
      type(field_type),          intent(in)    :: field

      integer(kind=i_def)             :: df
      integer(kind=i_def)             :: loop0_start, loop0_stop
      type(r_solver_field_proxy_type) :: rsolver_field_proxy
      type(field_proxy_type)          :: field_proxy
      integer(kind=i_def)             :: max_halo_depth_mesh
      type(mesh_type), pointer        :: mesh => null()
      !
      ! Initialise field and/or operator proxies
      !
      rsolver_field_proxy = rsolver_field%get_proxy()
      field_proxy = field%get_proxy()
      !
      ! Create a mesh object
      !
      mesh => rsolver_field_proxy%vspace%get_mesh()
      max_halo_depth_mesh = mesh%get_halo_depth()
      !
      ! Set-up all of the loop bounds
      !
      loop0_start = 1
      loop0_stop = rsolver_field_proxy%vspace%get_last_dof_halo(1)
      !
      ! Call kernels and communication routines
      !
      IF (field_proxy%is_dirty(depth=1)) THEN
        CALL field_proxy%halo_exchange(depth=1)
      END IF
      !
      !$omp parallel default(shared), private(df)
      !$omp do schedule(static)
      DO df=loop0_start,loop0_stop
        rsolver_field_proxy%data(df) = real(field_proxy%data(df), r_solver)
      END DO
      !$omp end do
      !$omp end parallel
      !
      ! Set halos dirty/clean for fields modified in the above loop
      !
      CALL rsolver_field_proxy%set_dirty()
      CALL rsolver_field_proxy%set_clean(1)
      !
    end subroutine invoke_copy_to_rsolver

    ! Psyclone does not currently have native support for builtins with mixed
    ! precision, this will be addressed in https://github.com/stfc/PSyclone/issues/1786
    ! Copy a r_solver_field_type to a field_type
    subroutine invoke_copy_to_rdef(rdef_field, field)

      use omp_lib,            only: omp_get_thread_num
      use omp_lib,            only: omp_get_max_threads
      use mesh_mod,           only: mesh_type
      use r_solver_field_mod, only: r_solver_field_type, r_solver_field_proxy_type
      use field_mod,          only: field_type, field_proxy_type

      implicit none

      type(field_type),          intent(inout) :: rdef_field
      type(r_solver_field_type), intent(in)    :: field

      integer(kind=i_def)             :: df
      integer(kind=i_def)             :: loop0_start, loop0_stop
      type(r_solver_field_proxy_type) :: field_proxy
      type(field_proxy_type)          :: rdef_field_proxy
      integer(kind=i_def)             :: max_halo_depth_mesh
      type(mesh_type), pointer        :: mesh => null()
      !
      ! Initialise field and/or operator proxies
      !
      rdef_field_proxy = rdef_field%get_proxy()
      field_proxy = field%get_proxy()
      !
      ! Create a mesh object
      !
      mesh => rdef_field_proxy%vspace%get_mesh()
      max_halo_depth_mesh = mesh%get_halo_depth()
      !
      ! Set-up all of the loop bounds
      !
      loop0_start = 1
      loop0_stop = rdef_field_proxy%vspace%get_last_dof_halo(1)
      !
      ! Call kernels and communication routines
      !
      IF (field_proxy%is_dirty(depth=1)) THEN
        CALL field_proxy%halo_exchange(depth=1)
      END IF
      !
      !$omp parallel default(shared), private(df)
      !$omp do schedule(static)
      DO df=loop0_start,loop0_stop
        rdef_field_proxy%data(df) = real(field_proxy%data(df), r_def)
      END DO
      !$omp end do
      !$omp end parallel
      !
      ! Set halos dirty/clean for fields modified in the above loop
      !
      CALL rdef_field_proxy%set_dirty()
      CALL rdef_field_proxy%set_clean(1)
      !
    end subroutine invoke_copy_to_rdef

end module psykal_lite_mod
