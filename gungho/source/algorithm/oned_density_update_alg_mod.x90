!-----------------------------------------------------------------------------
! (C) Crown copyright 2017 Met Office. All rights reserved.
! The file LICENCE, distributed with this code, contains details of the terms
! under which the code may be used.
!-----------------------------------------------------------------------------
module oned_density_update_alg_mod

  use constants_mod,                     only: r_def, i_def
  use field_mod,                         only: field_type
  use function_space_mod,                only: function_space_type
  use log_mod,                           only: log_event,            &
                                               log_scratch_space,    &
                                               LOG_LEVEL_INFO,       &
                                               LOG_LEVEL_TRACE
  use subgrid_config_mod,                only: dep_pt_stencil_extent,          &
                                               rho_approximation_stencil_extent
  use runtime_constants_mod,             only: get_mass_matrix, w3_id

  use psykal_lite_mod,                   only: invoke_subgrid_coeffs,      &
                                               invoke_fv_mass_fluxes
  use transport_config_mod,              only: scheme, &
                                               transport_scheme_horz_cosmic
  use cusph_density_update_alg_mod,      only: cusph_density_update_alg
  use operator_mod,                      only: operator_type
  use dg_matrix_vector_kernel_mod,       only: dg_matrix_vector_kernel_type

  implicit none

  private
  public :: oned_density_update_alg

contains

!> @brief Algorithm which acts in one direction only and updates density using
!>        the advective form of transport equation
!> @details Updates the density in either the x or y direction depending on the
!>          direction variable. An advective update is performed but using the
!>          conservative flux operator, a multiplicative correction is made to
!>          ensure the advective update is performed.
!> @param[in]    direction        Direction in which to perform the 1D advective update
!> @param[in]    u                Wind values
!> @param[in]    dep_pts          Departure points for W2 points at lowest order
!> @param[in]    cell_orientation Orientation of cells, in particular halo cells
!> @param[in]    rho_in           Density at time n
!> @param[inout] rho_out          Density after advective update
  subroutine oned_density_update_alg(   direction,            &
                                        dep_pts,              &
                                        cell_orientation,     &
                                        detj_at_w2,           &
                                        rho_in,               &
                                        rho_out)

    implicit none

    integer(i_def), intent(in)          :: direction
    type(field_type),    intent(in)     :: dep_pts
    type(field_type),    intent(in)     :: cell_orientation
    type(field_type),    intent(in)     :: detj_at_w2
    type(field_type),    intent(in)     :: rho_in
    type(field_type),    intent(inout)  :: rho_out

    type( field_type ) :: a0, a1, a2
    type( field_type ) :: rho_adv, rho_hat_adv
    type( field_type ) :: rho_constant_1, rho_adv_np1, rho_constant_np1
    type( field_type ) :: mass_flux
    type( field_type ) :: mass

    type(function_space_type), pointer :: rho_fs   => null()
    type(function_space_type), pointer :: u_fs     => null()

    type(operator_type), pointer :: mm_w3 => null()

    rho_fs => rho_in%get_function_space()
    u_fs   => dep_pts%get_function_space() 

    a0 = field_type( vector_space = rho_fs )
    a1 = field_type( vector_space = rho_fs )
    a2 = field_type( vector_space = rho_fs )

    rho_adv           = field_type( vector_space = rho_fs )
    rho_hat_adv       = field_type( vector_space = rho_fs )
    rho_adv_np1       = field_type( vector_space = rho_fs )
    rho_constant_np1  = field_type( vector_space = rho_fs )
    rho_constant_1    = field_type( vector_space = rho_fs )
    mass_flux         = field_type( vector_space = u_fs )
    mass              = field_type( vector_space = rho_fs )

    ! Calculate the conservative fluxes and update rho
    call invoke( setval_c(mass_flux, 0.0_r_def), &
                 setval_c(a0,        0.0_r_def), &
                 setval_c(a1,        0.0_r_def), &
                 setval_c(a2,        0.0_r_def) )

    mm_w3 => get_mass_matrix(w3_id)
    call invoke( dg_matrix_vector_kernel_type(mass, rho_in, mm_w3) )
    call invoke_subgrid_coeffs(a0, a1, a2, mass, cell_orientation, direction, &
                               rho_approximation_stencil_extent, dep_pt_stencil_extent)
    call invoke_fv_mass_fluxes(mass, dep_pts, mass_flux, &
                               a0, a1, a2, direction, dep_pt_stencil_extent)

    call cusph_density_update_alg(rho_in, mass_flux, rho_adv_np1, &
                                  cell_orientation, direction)

    ! Calculate the fluxes for rho=1
    call invoke( setval_c(mass_flux,      0.0_r_def), &
                 setval_c(rho_constant_1, 1.0_r_def), &
                 setval_c(a0,             0.0_r_def), &
                 setval_c(a1,             0.0_r_def), &
                 setval_c(a2,             0.0_r_def) )

    mm_w3 => get_mass_matrix(w3_id)
    call invoke( dg_matrix_vector_kernel_type(mass, rho_constant_1, mm_w3) )
    call invoke_subgrid_coeffs(a0, a1, a2, mass, cell_orientation, direction, &
                               rho_approximation_stencil_extent, dep_pt_stencil_extent)

    call invoke_fv_mass_fluxes(mass, dep_pts, mass_flux, &
                               a0, a1, a2, direction, dep_pt_stencil_extent)

    call cusph_density_update_alg(rho_constant_1, mass_flux, rho_constant_np1, &
                                  cell_orientation, direction)

    ! Perform multiplicative correction to obtain the advective update of density
    call invoke( X_divideby_Y(rho_out, rho_adv_np1, rho_constant_np1) )

    nullify(mm_w3, rho_fs, u_fs)

  end subroutine oned_density_update_alg

end module oned_density_update_alg_mod
