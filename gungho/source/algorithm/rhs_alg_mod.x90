!-----------------------------------------------------------------------------
! Copyright (c) 2017,  Met Office, on behalf of HMSO and Queen's Printer
! For further details please refer to the file LICENCE.original which you
! should have received as part of this distribution.
!-----------------------------------------------------------------------------

!>@brief Wrapper algorithm to compute the rhs of the prognostic
!>       equations
module rhs_alg_mod

  use constants_mod,                       only: r_def, i_def
  use enforce_bc_kernel_mod,               only: enforce_bc_kernel_type
  use runtime_constants_mod,               only: get_geopotential, &
                                                 get_coordinates,  &
                                                 get_mass_matrix,  &
                                                 w2_id,            &
                                                 theta_space_id,   &
                                                 w3inv_id, dl_id,  &
                                                 get_coriolis
  use field_mod,                           only: field_type
  use finite_element_config_mod,           only: element_order, &
                                                 vorticity_in_w1
  use formulation_config_mod,              only: rotating
  use kinetic_energy_gradient_kernel_mod,  only: kinetic_energy_gradient_kernel_type
  use matrix_vector_kernel_mod,            only: matrix_vector_kernel_type
  use operator_mod,                        only: operator_type
  use hydrostatic_kernel_mod,              only: hydrostatic_kernel_type
  use psykal_lite_mod,                     only: invoke_ru_bd_kernel
  use quadrature_xyoz_mod,                 only: quadrature_xyoz_type
  use quadrature_face_mod,                 only: quadrature_face_type
  use quadrature_rule_gaussian_mod,        only: quadrature_rule_gaussian_type
  use derived_config_mod,                  only: bundle_size
  use field_indices_mod,                   only: igh_u, igh_t, igh_d, igh_p
  use io_config_mod,                       only: subroutine_timers 
  use timer_mod,                           only: timer
  use rhs_eos_kernel_mod,                  only: rhs_eos_kernel_type
  use moist_dyn_mod,                       only: num_moist_factors, gas_law
  use dg_matrix_vector_kernel_mod,         only: dg_matrix_vector_kernel_type
  use reference_element_mod,               only: reference_element_type
  use mesh_mod,                            only: mesh_type
  use vorticity_advection_kernel_mod,      only: vorticity_advection_kernel_type
  use compute_vorticity_alg_mod,           only: compute_vorticity_alg
  use fs_continuity_mod,                   only: W1, W2
  use function_space_collection_mod,       only: function_space_collection
  use w2_vorticity_advection_kernel_mod,   only: w2_vorticity_advection_kernel_type
  implicit none

  private
  public :: rhs_alg

contains

  !>@details A wrapper algorithm for computing the rhs of the prognostic
  !>         equations. If computing the lhs of the semi-implicit equations
  !>         a correction to the theta and rho advection terms is also computed 
  !>         (d_adv terms) namely:
  !>         \f[
  !>            \mathbf{R_u} = \mathbf{u} 
  !>                         + \alpha\Delta t\left[ 2\Omega \times \mathbf{u}
  !>                         + \nabla\left(\Phi + 1/2\mathbf{u}.\mathbf{u}\right)
  !>                         + Cp\theta\nabla\Pi \right]
  !>         \f]
  !>         \f[ R_\rho = \rho + d_{adv}\alpha\Delta t \nabla.\left(\rho\mathbf{u}\right) \f]
  !>         \f[ R_\theta = \theta + d_{adv}\alpha\Delta t\mathbf{u}.\nabla\theta \f]
  !>         This can be given either timelevel n or n+1 fields to compute
  !>         the appropriate part of the full right hand side
  !> @param[in,out] rhs The state vector to compute
  !> @param[in]     alpha_dt The offcentering parameter times the timestep
  !> @param[in]     base_state The model prognostic state to which increments are added
  !> @param[in]     state The current model prognostic state
  !> @param[in]     moist_dyn Factors relating to moist dynamics
  !> @param[in]     compute_eos Flag to control computation of the equation of
  !!                state
  !> @param[in]     dlayer_rhs Use damping layer in rhs yes/no
  subroutine rhs_alg(rhs, alpha_dt, base_state, state, moist_dyn, compute_eos, dlayer_rhs)
  
    implicit none

    real(r_def),                  intent(in)    :: alpha_dt
    logical,                      intent(in)    :: dlayer_rhs
  
    ! Form of state and rhs is [u,theta,rho,exner] 
    type(field_type), target,     intent(in)    :: base_state(bundle_size)
    type(field_type), target,     intent(in)    :: state(bundle_size)
    type(field_type),             intent(in)    :: moist_dyn(num_moist_factors)
    type(field_type),             intent(inout) :: rhs(bundle_size)
    logical,                      intent(in)    :: compute_eos

    type(operator_type), pointer :: mm_vel    => null(), &
                                    mm_wtheta => null(), &
                                    mm_w3_inv => null(), &
                                    coriolis  => null()
    type(field_type), pointer :: chi(:)       => null(), &
                                 geopotential => null(), &
                                 u            => null(), &
                                 theta        => null(), &
                                 rho          => null(), &
                                 u_base       => null(), &
                                 theta_base   => null(), &
                                 rho_base     => null(), &
                                 exner        => null()

    type(field_type)                       :: rhs_tmp, rhs_eos
    type(field_type)                       :: vorticity
    type(quadrature_xyoz_type)             :: qr, qr_exact
    type(quadrature_face_type)             :: qrf
    type(quadrature_rule_gaussian_type)    :: gaussian_quadrature
    class(reference_element_type), pointer :: reference_element => null()
    type (mesh_type),              pointer :: mesh => null()
    integer(i_def)                         :: mesh_id
    integer(i_def)                         :: vorticity_fs

    if ( subroutine_timers ) call timer('rhs_alg')

    geopotential => get_geopotential()
    mm_wtheta    => get_mass_matrix(theta_space_id)
    chi          => get_coordinates()
    mm_w3_inv    => get_mass_matrix(w3inv_id)
   
    ! Using modified velocity mass matrix if using damping layer
    if(dlayer_rhs) then
      mm_vel => get_mass_matrix(dl_id)
    else
      mm_vel => get_mass_matrix(w2_id)
    end if 


    u     => state(igh_u)
    theta => state(igh_t)
    rho   => state(igh_d)
    exner => state(igh_p)

    u_base     => base_state(igh_u)
    theta_base => base_state(igh_t)
    rho_base   => base_state(igh_d)
 
    rhs_tmp = field_type( vector_space=rhs(igh_u)%get_function_space() )
    rhs_eos = field_type( vector_space=rhs(igh_p)%get_function_space() )
    qr       = quadrature_xyoz_type(element_order+2, gaussian_quadrature)
    qr_exact = quadrature_xyoz_type(element_order+3, gaussian_quadrature)
    mesh              => theta%get_mesh()
    reference_element => mesh%get_reference_element()
    qrf = quadrature_face_type(element_order+2, .true., .false., &
                               reference_element, gaussian_quadrature)

    ! Initialise RHS u and theta fields
    call invoke( name = "Initialise RHS u and theta fields", &
                 setval_c( rhs_tmp,    0.0_r_def ),          & 
                 setval_c( rhs(igh_u), 0.0_r_def ),          &
                 setval_c( rhs(igh_t), 0.0_r_def ) )

    ! Compute rhs_u
    call invoke_ru_bd_kernel (rhs_tmp, exner, theta, moist_dyn, qrf)
    call invoke( name = "Compute rhs_u setup",                               &
                 hydrostatic_kernel_type( rhs_tmp, exner, theta, moist_dyn,  &
                                          geopotential, qr ),                &
                 kinetic_energy_gradient_kernel_type( rhs_tmp, u, chi, qr ), &
                 matrix_vector_kernel_type( rhs(igh_u), u_base, mm_vel ) )

     ! Compute the vorticity
    mesh_id = u%get_mesh_id()
    if ( vorticity_in_w1 ) then
      vorticity_fs = W1
    else
      vorticity_fs = W2
    end if
    vorticity = field_type( vector_space = &
                 function_space_collection%get_fs(mesh_id, element_order, &
                                                  vorticity_fs) )
    call compute_vorticity_alg(vorticity, u, .false.)
    if ( vorticity_in_w1 ) then
      call invoke( vorticity_advection_kernel_type(rhs_tmp, u, vorticity,    &
                                                   chi, qr_exact) )
    else
      call invoke( w2_vorticity_advection_kernel_type(rhs_tmp, u, vorticity, &
                                                      chi, qr_exact) )
    end if
  
    if ( rotating ) then
      coriolis => get_coriolis()
      call invoke( matrix_vector_kernel_type( rhs_tmp, u, coriolis ) )
    end if

    call invoke( name = "Compute rhs_u evaluation",                 &
                 inc_X_plus_bY( rhs(igh_u), alpha_dt, rhs_tmp ),    &
                 enforce_bc_kernel_type(rhs(igh_u)) )
 
    call invoke( name = "Compute rhs_theta and rhs_rho",                    &
    ! Compute rhs_theta
                 matrix_vector_kernel_type( rhs(igh_t), theta_base, mm_wtheta ), &
    ! Compute rhs_rho
                 setval_X( rhs(igh_d), rho_base ) )
    if ( compute_eos ) then
      qr = quadrature_xyoz_type(1, gaussian_quadrature)
      call invoke( name = "Compute rhs_eos",                                       &
                   rhs_eos_kernel_type(rhs_eos, exner, rho, theta,                 &
                                       moist_dyn(gas_law), chi, qr),               & 
                   dg_matrix_vector_kernel_type( rhs(igh_p), rhs_eos, mm_w3_inv ))
    else
      call invoke( setval_c(rhs(igh_p), 0.0_r_def) )
    end if

    nullify( mm_vel, mm_wtheta, mm_w3_inv, chi, &
             geopotential, u, theta, rho, exner, &
             mesh, reference_element )

    if ( subroutine_timers ) call timer('rhs_alg')

  end subroutine rhs_alg

end module rhs_alg_mod


