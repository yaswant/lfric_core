!-----------------------------------------------------------------------------
! (c) Crown copyright 2017 Met Office. All rights reserved.
! The file LICENCE, distributed with this code, contains details of the terms
! under which the code may be used.
!-----------------------------------------------------------------------------

!>@brief Calls to the fast physics schemes
module fast_physics_alg_mod

  use section_choice_config_mod, only: boundary_layer,    &
                                       boundary_layer_um, &
                                       convection,        &
                                       convection_um

  use constants_mod,        only: i_def,r_def
  use log_mod,              only: log_event,       &
                                  LOG_LEVEL_ERROR, &
                                  LOG_LEVEL_INFO

  use physics_config_mod,   only: heldsuarez_wind_placement,   &
                                  heldsuarez_thermo_placement, &
                                  blayer_placement,            &
                                  convection_placement,        &
                                  convection_placement_fast,   &
                                  blayer_placement_fast

  use convection_config_mod,only: cv_scheme,                   &
                                  cv_scheme_lambert_lewis
  ! Derived Types
  use field_mod,            only: field_type
  use field_collection_mod, only: field_collection_type

  ! Moisture species
  use mr_indices_mod,       only: nummr

  ! Wind mapping
  use map_fd_to_prognostics_alg_mod, only: set_wind

#ifdef UM_PHYSICS
  ! UM BL scheme
  use bl_alg_mod,  only: bl_alg_step
  ! UM conv scheme
  use conv_alg_mod, only: conv_alg_step
#endif

  implicit none

contains

  !> @details Collection of procedures for subgrid physics that act
  !>          within the outer loop of the solver
  !> @param[in,out] du                Increment to wind field
  !> @param[in,out] dtheta            Increment to theta field
  !> @param[in,out] mr                Mixing ratios
  !> @param[in]     outer             Outer loop counter
  !> @param[in]     theta             Theta field
  !> @param[in]     rho               Rho on w3 space
  !> @param[in]     exner             Exner pressure on w3 space
  !> @param[in]     mr_n              Mixing ratios at time level n
  !> @param[in]     shear             3D wind shear on wtheta points
  !> @param[in]     delta             Edge length on wtheta points
  !> @param[in]     max_diff_smag     Maximum diffusion coefficient allowed in this run
  !> @param[in,out] derived_fields    Group of derived fields
  !> @param[in,out] cloud_fields      Group of cloud fields
  !> @param[in,out] twod_fields       Group of 2D fields
  !> @param[in,out] visc_m_blend      Blended BL-Smag diffusion coefficient for momentum
  !> @param[in,out] visc_h_blend      Blended BL-Smag diffusion coefficient for scalars
  !> @param[in,out] physics_incs      Group of physics increments
  !> @param[in]     height_w3         Height in w3
  !> @param[in]     height_wth        Height in wth
  !> @param[in,out] jules_ancils      Ancillary fields for Jules
  !> @param[in,out] jules_prognostics Prognostic fields for Jules
  subroutine fast_physics(du, dtheta, mr, outer, theta, rho, exner, mr_n, &
                          shear, delta, max_diff_smag,                    &
                          derived_fields, cloud_fields, twod_fields,      &
                          visc_m_blend, visc_h_blend,                     &
                          physics_incs, height_w3, height_wth,            &
                          jules_ancils, jules_prognostics)

    implicit none

    ! Fields
    type( field_type ), intent( inout ) :: du, dtheta
    type( field_type ), intent( inout ) :: visc_m_blend, visc_h_blend
    type( field_type ), intent( in )    :: theta, rho, exner
    type( field_type ), intent( in )    :: shear, delta, max_diff_smag

    ! Field bundles
    type( field_type ), intent( inout )    :: mr(nummr)
    type( field_type ), intent( in )       :: mr_n(nummr)

    ! Field groups
    type( field_collection_type ), intent(inout) :: derived_fields
    type( field_collection_type ), intent(inout) :: cloud_fields
    type( field_collection_type ), intent(inout) :: twod_fields
    type( field_collection_type ), intent(inout) :: physics_incs

    ! Surface fields
    type( field_collection_type ), intent(inout) :: jules_ancils
    type( field_collection_type ), intent(inout) :: jules_prognostics

    ! Coordinate fields
    type( field_type ), intent( in )    :: height_w3, height_wth

    ! scalars
    integer(kind=i_def), intent(in)     :: outer

    ! Local fields (should be created at initialization, but done here for the time being)
    type( field_type ) :: dtheta_bl
    type( field_type ) :: du_bl
    type( field_type ) :: dv_bl
    type( field_type ) :: dw_bl

    logical :: boundary_layer_done ! Flag to indicate that boundary_layer has been calculated
    logical :: convection_done ! Flag to indicate that convection has been calculated

    boundary_layer_done=.false.
    convection_done=.false.

    if (heldsuarez_wind_placement > 0)then
      call log_event( &
         'fast_physics: Held-Suarez wind forcing is not currently available in fast physics.', &
         LOG_LEVEL_ERROR )
    end if

    if (heldsuarez_thermo_placement > 0)then
      call log_event( &
         'fast_physics: Held-Suarez thermal forcing is not currently available in fast physics.', &
         LOG_LEVEL_ERROR )
    end if

#ifdef UM_PHYSICS
    if (convection == convection_um .and.                       &
        convection_placement == convection_placement_fast .and. &
        cv_scheme == cv_scheme_lambert_lewis ) then
      ! Algorithm that calls the UM convection scheme
      ! Only suitable for Lambert Lewis convection scheme call
      call log_event( 'fast_physics: Running the convection scheme', LOG_LEVEL_INFO )
      call conv_alg_step(physics_incs, derived_fields, twod_fields, exner, mr)
      ! Flag that convection has been done in this timestep and increments
      ! need adding later in BL scheme
      convection_done=.true.
    end if

    if (boundary_layer == boundary_layer_um .and. &
        blayer_placement == blayer_placement_fast) then
      ! Algorithm that calls the UM Boundary Layer scheme
      call log_event( 'fast_physics: Running Boundary layer', LOG_LEVEL_INFO )
      call dtheta%copy_field_properties(dtheta_bl)
      call rho%copy_field_properties(du_bl)
      call rho%copy_field_properties(dv_bl)
      call theta%copy_field_properties(dw_bl)
      call invoke( setval_c(dtheta_bl, 0.0_r_def), &
                   setval_c(du_bl, 0.0_r_def),     &
                   setval_c(dv_bl, 0.0_r_def),     &
                   setval_c(dw_bl, 0.0_r_def) )
      call bl_alg_step(dtheta_bl, du_bl, dv_bl, mr, outer, theta, rho, mr_n,  &
                       exner, shear, delta, max_diff_smag,                    &
                       height_w3, height_wth, derived_fields, cloud_fields,   &
                       twod_fields, physics_incs, visc_m_blend, visc_h_blend, &
                       jules_ancils, jules_prognostics)
      boundary_layer_done=.true. ! We've done it, so need to collect this term later on
    end if
#endif

    !=====================================
    ! collect individual terms in parallel
    ! N.B. as currently coded, the convection increments are added in the
    ! boundary layer as part of the implicit solution
    !=====================================
    if (boundary_layer_done) then
      call set_wind(du, du_bl, dv_bl, dw_bl)
      call invoke(inc_X_plus_Y(dtheta, dtheta_bl))
    end if

    call dtheta%log_minmax(LOG_LEVEL_INFO, 'fast physics: dtheta' )

  end subroutine fast_physics

end module fast_physics_alg_mod
