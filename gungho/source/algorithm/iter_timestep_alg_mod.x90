!-----------------------------------------------------------------------------
! Copyright (c) 2017,  Met Office, on behalf of HMSO and Queen's Printer
! For further details please refer to the file LICENCE.original which you
! should have received as part of this distribution.
!-----------------------------------------------------------------------------

!>@brief A two time-level iterative time-discretisation of the nonlinear 
!>       3D equations
module iter_timestep_alg_mod

  use constants_mod,                     only: i_def,r_def
  use log_mod,                           only: log_event,         &
                                               log_scratch_space, &
                                               LOG_LEVEL_INFO,    &
                                               LOG_LEVEL_TRACE

  ! Configuration options
  use restart_control_mod,       only: restart_type
  use formulation_config_mod,    only: use_moisture, use_physics
  use output_config_mod,         only: diagnostic_frequency, subroutine_timers
  use solver_config_mod,         only: method, helmholtz_solve, use_operators
  use timestepping_config_mod,   only: dt, alpha, &
                                       outer_iterations, inner_iterations, &
                                       spinup_period, spinup_alpha, spinup_winds
  use idealised_config_mod,      only: test, idealised_test_held_suarez
  use derived_config_mod,        only: bundle_size
  use runtime_constants_mod,     only: get_qr, get_coordinates, get_height
  use fs_continuity_mod,         only: W3, Wtheta

  ! PsyKAl-lite kernels
  use psykal_lite_mod,           only: invoke_axpby, &
                                       invoke_viscosity, &
                                       invoke_X_innerproduct_X

  use field_bundle_mod,          only: clone_bundle, & 
                                       bundle_axpy, & 
                                       bundle_axpby, &
                                       add_bundle, &
                                       copy_bundle, &
                                       set_bundle_scalar, &
                                       bundle_ax 

  ! PsyKAl PSYClone kernels
  use held_suarez_kernel_mod,    only: held_suarez_kernel_type
  use enforce_bc_kernel_mod,     only: enforce_bc_kernel_type

  ! Derived Types
  use field_mod,                 only: field_type
  use quadrature_mod,            only: quadrature_type

  ! Algorithms
  use solver_mod,                only: solver_algorithm
  use rhs_alg_mod,               only: rhs_alg
  use advection_alg_mod,         only: advection_alg, advection_alg_init, advection_mr_alg
  use si_solver_alg_mod,         only: si_solver_alg, si_solver_init
  use si_operators_alg_mod,      only: create_si_operators, compute_si_operators
  use helmholtz_solver_alg_mod,  only: helmholtz_solver_init, &
                                       compute_inv_hb

  ! Field mappings
  use map_rho_to_theta_mod,      only: map_rho_to_theta
  use map_physics_fields_alg_mod,only: map_physics_fields_alg

  ! Moisture species
  use mr_indices_mod,            only: nummr

  ! Field indices
  use field_indices_mod,         only: igh_u, igh_t, igh_d, igh_p

  ! Mixing settings
  use mixing_config_mod,         only: viscosity

  use timer_mod,                 only: timer

  implicit none

  private

  ! 'State' items private to the algorithm that need to be 
  !  created once but used every step
  type( field_type ), allocatable :: state(:), state_n(:)
  type( field_type ), allocatable :: rhs_n(:), rhs_np1(:), rhs_adv(:)
  type( field_type ), allocatable :: rhs_heldsuarez(:), inc(:)
  type( field_type ) :: u_adv   ! Advective wind (centered in time)


  public :: iter_alg_init
  public :: iter_alg_step

contains
  !> @details Initialisation procedure for the timestepping algorithm
  !>         Initialises various internal fields
  !> @param[in]    mesh_id Mesh id of the mesh object on which the model runs
  !> @param[inout] u  3D wind field
  !> @param[inout] rho Density
  !> @param[inout] theta Potential temperature


  subroutine iter_alg_init( mesh_id, u, rho, theta)

    implicit none

    ! Mesh
    integer(i_def), intent(in) :: mesh_id 

    ! Prognostic fields    
    type( field_type ), intent( inout ) :: u, rho, theta

    !=== Allocate internal state field arrays ================================!

    allocate(state(bundle_size))
    allocate(state_n(bundle_size))
    allocate(rhs_n(bundle_size))
    allocate(rhs_np1(bundle_size))
    allocate(rhs_adv(bundle_size))
    allocate(rhs_heldsuarez(bundle_size))
    allocate(inc(bundle_size))


    !=== Initialise internal state field objects =============================!

    state(igh_u) = field_type( vector_space = u%get_function_space() )
    state(igh_t) = field_type( vector_space = theta%get_function_space() )
    state(igh_d) = field_type( vector_space = rho%get_function_space() )

    call clone_bundle(state, state_n,   bundle_size)
    call clone_bundle(state, rhs_n,     bundle_size)
    call clone_bundle(state, rhs_np1,   bundle_size)
    call clone_bundle(state, rhs_adv,   bundle_size)
    call clone_bundle(state, rhs_heldsuarez,   bundle_size)
    call clone_bundle(state, inc,       bundle_size)

    ! If held-suarez forcing is not used, then we want the following to be 
    ! initialized to zero
    call set_bundle_scalar(0.0_r_def, rhs_heldsuarez, bundle_size)

    ! Operators for si solves
    if ( helmholtz_solve ) call helmholtz_solver_init(state)
    if ( helmholtz_solve .or. use_operators ) call create_si_operators(mesh_id)

    !=== Copy prognostic field data to state arrays ===========================!
    call invoke( copy_field(u,     state(igh_u)), &
                 copy_field(theta, state(igh_t)), &
                 copy_field(rho,   state(igh_d)) )
    call si_solver_init(state)
    call advection_alg_init(mesh_id)

    u_adv = field_type( vector_space = u%get_function_space() ) 

    call log_event( "Dynamo: initialised timestepping algorithm", LOG_LEVEL_INFO )


  end subroutine iter_alg_init

  !> @details An algorithm for timestepping the 3D nonlinear equations
  !>         using a iterative process of the same form as endgame.
  !>         The algorithm splits all processes into one of three parts.
  !>         Old time level forcings computed once per timestep.
  !>         Advection terms computed in an outer loop using time-averaged
  !>         fields.
  !>         New time level forcings computed in an inner loop.
  !>         To match ENDGame 2 outer and 2 inner loops are used per timestep
  !>         by default. This means that there is one evaluation of old time
  !>         level terms, 2 evaluation of advective terms and 4 evaluations 
  !>         of new time level terms and increment updates per timestep
  !> @param[inout] u  3D wind field
  !> @param[inout] rho Density
  !> @param[inout] theta Potential temperature
  !> @param[inout] mr Mixing ratios
  !> @param[inout] xi Vorticity field
  !> @param[inout] u1_in_w3 component of wind on w3 space
  !> @param[inout] u2_in_w3 component of wind on w3 space
  !> @param[inout] u3_in_w3 component of wind on w3 space
  !> @param[inout] rho_in_wth rho on wth space
  !> @param[inout] theta_in_w3 theta on w3 space
  !> @param[inout] p_in_w3 pressure on w3 space
  !> @param[inout] p_in_wth pressure on wth space
  !> @param[in]    timestep model timestep number

  subroutine iter_alg_step(u, rho, theta, mr, xi,        &
                           u1_in_w3, u2_in_w3, u3_in_w3, &
                           rho_in_wth, theta_in_w3,      &
                           p_in_w3, p_in_wth, timestep)

    implicit none

    ! Prognostic fields    
    type( field_type ), intent( inout ) :: u, rho, theta, xi
    type( field_type ), intent( inout ) :: u1_in_w3, u2_in_w3, u3_in_w3
    type( field_type ), intent( inout ) :: rho_in_wth, theta_in_w3
    type( field_type ), intent( inout ) :: p_in_w3, p_in_wth
    type( field_type ), intent( inout ) :: mr(nummr)

    ! Timestep value
    integer, intent(in) :: timestep  

    ! quadrature object
    type(quadrature_type)       :: qr
    ! Coordinate fields
    type( field_type ), pointer :: chi(:) => null()

    type( field_type ), pointer :: height_w3  => null()
    type( field_type ), pointer :: height_wth => null()

    integer            :: outer, inner
    real(kind=r_def)   :: varalpha, varbeta ! alpha, beta weight to use
                                            ! these may differ from input values
                                            ! during the spinup period

    real(kind=r_def), dimension(3) :: si_err

    if ( subroutine_timers ) call timer('iter_timestep_alg')

    qr = get_qr()
    chi => get_coordinates()
    height_w3  => get_height(W3)
    height_wth => get_height(Wtheta)
    !Pass updated fields into state
    if (spinup_winds .and. timestep <= spinup_period)then
      call invoke( copy_field(u,     state(igh_u)), &
                   copy_field(theta, state(igh_t)), &
                   copy_field(rho,   state(igh_d)) )
    end if

    if (spinup_alpha .and. timestep <= spinup_period)then
      varalpha = alpha + (spinup_period - timestep + 1) * &
         (1.0 - alpha)/spinup_period
    else
      varalpha = alpha
    end if
    varbeta = 1.0 - varalpha

    !============================================================================
    ! Fields for physics
    !============================================================================
    if (use_physics)then
      call map_rho_to_theta(rho, theta, rho_in_wth) 
      call map_physics_fields_alg(u, rho, theta, rho_in_wth,                    &
                                  u1_in_w3, u2_in_w3, u3_in_w3, theta_in_w3,    &
                                  p_in_w3, p_in_wth)
      call p_in_wth%log_minmax(LOG_LEVEL_INFO, 'p_in_wth')
      call p_in_w3%log_minmax(LOG_LEVEL_INFO, 'p_in_w3')
      call log_event( "Dynamo: generated physics fields.", LOG_LEVEL_INFO )
    end if

    !=== Do a single timestep ==============================================!
    call copy_bundle(state, state_n, bundle_size)
    if ( helmholtz_solve .or. use_operators ) call compute_si_operators(state_n)
    if ( helmholtz_solve ) call compute_inv_hb()
    call rhs_alg(rhs_n, varbeta*dt, state_n)
    !=== Section 3.1:  Outer (advection loop) ==============================!
    do outer = 1,outer_iterations
      ! Some idealized (fast) physics terms
      if ( test == idealised_test_held_suarez) then
        call set_bundle_scalar(0.0_r_def, rhs_heldsuarez, bundle_size)
        call invoke(held_suarez_kernel_type(rhs_heldsuarez(igh_u), rhs_heldsuarez(igh_t), &
                    state_n(igh_u), state_n(igh_t), state_n(igh_d), chi, qr))
        call invoke( enforce_bc_kernel_type( rhs_heldsuarez(igh_u) ) )
      end if

      ! Set advecting velocity & terms
      call invoke_axpby(varalpha, state_n(igh_u), varbeta, state(igh_u), u_adv)
      call advection_alg(rhs_adv, state_n, xi, u_adv)
      if ( use_moisture ) call advection_mr_alg(mr, u_adv)
      call bundle_ax(dt, rhs_adv, rhs_adv, bundle_size) 
      !=== Section 3.2: Inner (nonlinear, coriolis loop) ===================!
      do inner = 1,inner_iterations
        call rhs_alg(rhs_np1, -varalpha*dt, state)

        ! === Section 3.3: Solve for increments ============================!
        ! Add on advective terms: rhs = rhs_n - rhs_np1 + rhs_adv
        ! (reuse rhs_np1 for rhs)

        call bundle_axpy(-1.0_r_def, rhs_np1, rhs_n, rhs_np1, bundle_size)
        call add_bundle(rhs_np1, rhs_adv, rhs_np1, bundle_size)
        call add_bundle(rhs_np1, rhs_heldsuarez, rhs_np1, bundle_size)
             
        write( log_scratch_space, '(A,2I3)' ) 'loop indices (o, i): ', &
                                                  , outer, inner
        call log_event( log_scratch_space, LOG_LEVEL_INFO )

        ! Write out si residuals
        call invoke_X_innerproduct_X(si_err(1), rhs_np1(1))
        call invoke_X_innerproduct_X(si_err(2), rhs_np1(2))
        call invoke_X_innerproduct_X(si_err(3), rhs_np1(3))
        write( log_scratch_space, '(A,E16.8)' ) &
          'Residual in momentum equation:  ',sqrt(si_err(1))
        call log_event( log_scratch_space, LOG_LEVEL_INFO )
        write( log_scratch_space, '(A,E16.8)' ) &
          'Residual in energy equation:    ',sqrt(si_err(2))
        call log_event( log_scratch_space, LOG_LEVEL_INFO )
        write( log_scratch_space, '(A,E16.8)' ) &
          'Residual in continuity equation:',sqrt(si_err(3))
        call log_event( log_scratch_space, LOG_LEVEL_INFO )

        call si_solver_alg(state, rhs_np1, state_n)
        !=== End Section 3.3 ===============================================!
      end do ! INNER
      !=== End Section 3.2 =================================================!
    end do ! OUTER
    !=== End Section 3.1 ===================================================!
    ! Apply viscosity
    if ( viscosity ) then
      call log_event( 'Applying Viscosity', LOG_LEVEL_INFO )      
      call invoke_viscosity(inc(igh_t), theta, inc(igh_u), u, chi)
      call invoke( set_field_scalar(0.0_r_def, inc(igh_d)) )
      call bundle_axpy(dt, inc, state, state, bundle_size)
    end if    
    
    ! Return fields in u, theta, rho arrays
    ! The if statement prevents this update if spinup_winds is true
    if (.not. (spinup_winds .and. timestep <= spinup_period))then
      call invoke( copy_field(state(igh_u), u) )
    end if

    call invoke( copy_field(state(igh_t), theta), &
                 copy_field(state(igh_d), rho) )

    if ( subroutine_timers ) call timer('iter_timestep_alg')

  end subroutine iter_alg_step

end module iter_timestep_alg_mod
