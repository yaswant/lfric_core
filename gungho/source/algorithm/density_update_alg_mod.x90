!-----------------------------------------------------------------------------
! Copyright (c) 2017,  Met Office, on behalf of HMSO and Queen's Printer
! For further details please refer to the file LICENCE.original which you
! should have received as part of this distribution.
!-----------------------------------------------------------------------------
!> @brief Algorithm which updates rho using rho_n - dt * div(mass_flux)
!> @details  Inputs are the the mass flux and rho at time level n. Output is rho at
!>           time level n+1. The divergence of the mass flux is calculated and
!>           multiplied by dt and added to the density rho at time n.
module density_update_alg_mod

  use constants_mod,                     only: i_def
  use field_mod,                         only: field_type
  use quadrature_mod,                    only: quadrature_type, GAUSSIAN
  use log_mod,                           only: log_event,            &
                                               log_scratch_space,    &
                                               LOG_LEVEL_INFO,       &
                                               LOG_LEVEL_TRACE

  use timestepping_config_mod,           only: dt
  use psykal_lite_mod,                   only: invoke_axpy, invoke_multiply_field_data
  use rrho_kernel_mod,                   only: rrho_kernel_type
  use runtime_constants_mod,             only: get_mass_matrix, w3inv_id
  use operator_mod,                      only: operator_type
  use dg_matrix_vector_kernel_mod,       only: dg_matrix_vector_kernel_type

  implicit none

  private
  public :: density_update_alg

contains

!> @brief Algorithm which updates rho using rho_n - dt * div(mass_flux)
!> @details  Inputs are the the mass flux and rho at time level n. Output is rho at
!>           time level n+1. The divergence of the mass flux is calculated and
!>           multiplied by dt and added to the density rho at time n.
!> @param[in] rho_n      density at time n
!> @param[in] mass_flux  mass flux
!> @param[inout] rho     density at time n+1
  subroutine density_update_alg(  rho_n,          &
                                  mass_flux,      &
                                  rho,            &
                                  detj_at_w2  )

    implicit none
 
    type(field_type),    intent(in)     :: rho_n
    type(field_type),    intent(inout)  :: mass_flux
    type(field_type),    intent(inout)  :: rho
    type(field_type),    intent(in)     :: detj_at_w2

    type( field_type )                  :: div, div_temp, mass_flux_mult
    type( quadrature_type )             :: qr
    type(operator_type),       pointer  :: m3_inv => null()

    qr = quadrature_type(3, GAUSSIAN)
    m3_inv => get_mass_matrix(w3inv_id)

    div            = field_type( vector_space = rho_n%get_function_space() )
    div_temp       = field_type( vector_space = rho_n%get_function_space() )
    mass_flux_mult = field_type( vector_space = detj_at_w2%get_function_space() )

    call invoke_multiply_field_data(mass_flux,detj_at_w2,mass_flux_mult)

    call invoke( rrho_kernel_type( div_temp, mass_flux_mult, qr ) )

    call invoke( dg_matrix_vector_kernel_type(div, div_temp, m3_inv) )

    call invoke_axpy(dt, div, rho_n, rho)

  end subroutine density_update_alg

end module density_update_alg_mod
