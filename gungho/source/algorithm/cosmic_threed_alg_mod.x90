!-----------------------------------------------------------------------------
! (C) Crown copyright 2018 Met Office. All rights reserved.
! For further details please refer to the file COPYRIGHT.txt
! which you should have received as part of this distribution.
!-----------------------------------------------------------------------------
!
!-------------------------------------------------------------------------------
!> @brief Algorithm for 3D COSMIC transport scheme.
!> @details The algorithm performs a horizontal update of density using Cosmic
!>          and then does a single Cosmic update in the vertical. The algorithm
!>          may be defined as rho_np1 = Z(rho_xy) with rho_xy the horizontal
!>          transport of rho. This algorithm outputs increment which is added
!>          to rho_n using rho_n - dt*increment.
module cosmic_threed_alg_mod

  use constants_mod,                     only: i_def
  use field_mod,                         only: field_type
  use function_space_mod,                only: function_space_type
  use function_space_collection_mod,     only: function_space_collection
  use finite_element_config_mod,         only: element_order
  use fs_continuity_mod,                 only: W2, W3
  use runtime_constants_mod,             only: get_coordinates,               &
                                               get_cell_orientation,          &
                                               get_detj_at_w2
  use cusph_split_transport_alg_mod,     only: cusph_split_transport_alg
  use cusph_fv_density_update_alg_mod,   only: cusph_fv_density_update_alg
  use flux_direction_mod,                only: x_direction, y_direction,      &
                                               z_direction
  use vert_conservative_cosmic_alg_mod,  only: vert_conservative_cosmic_alg
  use cosmic_divergence_alg_mod,         only: cosmic_divergence_alg
  use cusph_cosmic_transport_alg_mod,    only: cusph_cosmic_transport_step
  use output_config_mod,                 only: subroutine_timers
  use timer_mod,                         only: timer
  use timestepping_config_mod,           only: dt

  implicit none

  private

  public :: cosmic_threed_transport_step

contains

  !> @brief Algorithm for 3D COSMIC transport scheme.
  !> @details The algorithm performs a horizontal update of density using Cosmic
  !>          and then does a single Cosmic update in the vertical. The algorithm
  !>          may be defined as rho_np1 = Z(rho_xy) with rho_xy the horizontal
  !>          transport of rho.
  !> @param[in,out] increment    Density increment
  !> @param[in]     rho          Density field
  !> @param[in]     dep_pts_x    Departure points in x direction
  !> @param[in]     dep_pts_y    Departure points in x direction
  !> @param[in]     dep_pts_z    Departure points in x direction
  subroutine cosmic_threed_transport_step( increment,     &
                                           rho,           &
                                           dep_pts_x,     &
                                           dep_pts_y,     &
                                           dep_pts_z )

    implicit none

    type(field_type), intent(inout) :: increment
    type(field_type), intent(inout) :: rho
    type(field_type), intent(in)    :: dep_pts_x
    type(field_type), intent(in)    :: dep_pts_y
    type(field_type), intent(in)    :: dep_pts_z

    type(field_type), pointer :: cell_orientation => null()
    type(field_type), pointer :: detj_at_w2 => null()

    type(field_type) :: rho_xy
    type(field_type) :: increment_xy
    type(field_type) :: increment_z
    type(field_type) :: mass_flux_z

    type(function_space_type), pointer :: w2_fs => null()
    type(function_space_type), pointer :: w3_fs => null()

    cell_orientation => get_cell_orientation()
    detj_at_w2       => get_detj_at_w2()

    w2_fs => dep_pts_x%get_function_space()
    w3_fs => rho%get_function_space()

    rho_xy       = field_type( vector_space = w3_fs )
    increment_xy = field_type( vector_space = w3_fs )
    increment_z  = field_type( vector_space = w3_fs )
    mass_flux_z  = field_type( vector_space = w2_fs )

    if ( subroutine_timers ) call timer('3D Cosmic update')

    ! Calculate increment in horizontal (x-y) direction.
    call cusph_cosmic_transport_step( increment_xy, rho, dep_pts_x, dep_pts_y )

    call invoke( X_minus_bY( rho_xy, rho, dt, increment_xy ) )

    ! Update density in the vertical given the horizontally transported density.
    call vert_conservative_cosmic_alg( mass_flux_z, rho_xy, dep_pts_z, dt )

    call cosmic_divergence_alg( increment_z, mass_flux_z, detj_at_w2,       &
                                cell_orientation, z_direction )

    call invoke( X_plus_Y( increment, increment_xy, increment_z ) )

    if ( subroutine_timers ) call timer('3D Cosmic update')

    nullify( cell_orientation, detj_at_w2, w2_fs, w3_fs )

  end subroutine cosmic_threed_transport_step

end module cosmic_threed_alg_mod
