!-----------------------------------------------------------------------------
! (C) Crown copyright 2017 Met Office. All rights reserved.
! The file LICENCE, distributed with this code, contains details of the terms
! under which the code may be used.
!-----------------------------------------------------------------------------
!> @brief    Module which updates rho using rho_n - dt * div(mass_flux) where
!>           mass flux is in either the x or y direction.
!> @details  The calculation of the divergence uses finite differences of the
!>           mass flux and the algorithm is designed for use with finite volume
!>           type fields.
module cusph_fv_density_update_alg_mod
  
  use constants_mod,                     only: i_def, r_def, LARGE_REAL_NEGATIVE
  use field_mod,                         only: field_type
  use timestepping_config_mod,           only: dt
  use psykal_lite_mod,                   only: invoke_set_field_scalar, &
                                               invoke_fv_divergence,    &
                                               invoke_multiply_field_data
  use dg_matrix_vector_kernel_mod,       only: dg_matrix_vector_kernel_type
  use operator_mod,                      only: operator_type
  use runtime_constants_mod,             only: get_mass_matrix, w3inv_id
  use flux_direction_mod,                only: x_direction, y_direction

  implicit none

  private
  public :: cusph_fv_density_update_alg

contains

  !> @brief    Algorithm which updates rho by adding the increment -dt*div(mass_flux).
  !> @details  This algorithm is used for the Cosmic update where mass flux in either
  !>           the x or y direction is provided.
  !>           To ensure that the correct divergence is calculated the mass flux
  !>           is multiplied by the detJ values calculated at the W2 dofs.
  !>           The calculated div term is then divided by the detJ values
  !>           calculated at the W3 dofs, where density (rho) is located.
  !>           The divergence term is calculated by a simple difference of the
  !>           flux on opposite faces.
  !> @param[in,out]   increment           Density increment
  !> @param[in]       mass_flux_x         Mass flux in y direction
  !> @param[in]       mass_flux_y         Mass flux in x direction
  !> @param[in]       cell_orientation    Orientation of cells
  !> @param[in]       direction           Direction in which divergence update is performed
  !> @param[in]       detj_at_w2          DetJ values located at W2 dofs (lowest order)
  subroutine cusph_fv_density_update_alg( increment,                &
                                          mass_flux_x,              &
                                          mass_flux_y,              &
                                          cell_orientation,         &
                                          direction,                &
                                          detj_at_w2 )

    implicit none
 
    type(field_type),    intent(inout)  :: increment
    type(field_type),    intent(in)     :: mass_flux_x
    type(field_type),    intent(in)     :: mass_flux_y
    type(field_type),    intent(in)     :: detj_at_w2
    type(field_type),    intent(in)     :: cell_orientation
    integer(i_def),      intent(in)     :: direction

    type( field_type )                  :: div_temp
    type( field_type )                  :: mass_flux_mult_x, mass_flux_mult_y
    type(operator_type), pointer        :: m3_inv => null()

    m3_inv            => get_mass_matrix(w3inv_id)
    div_temp          = field_type( vector_space = increment%get_function_space() )
    mass_flux_mult_x  = field_type( vector_space = detj_at_w2%get_function_space() )
    mass_flux_mult_y  = field_type( vector_space = detj_at_w2%get_function_space() )

    call invoke( setval_c(div_temp,            LARGE_REAL_NEGATIVE),  &
                 setval_c(mass_flux_mult_x,    LARGE_REAL_NEGATIVE),  &
                 setval_c(mass_flux_mult_y,    LARGE_REAL_NEGATIVE)   &
                 )

    ! Multiply mass flux by detJ at W2 dofs
    call invoke_multiply_field_data(mass_flux_x,detj_at_w2,mass_flux_mult_x)
    call invoke_multiply_field_data(mass_flux_y,detj_at_w2,mass_flux_mult_y)

    ! Calculate divergence in either x or y direction using finite-volume divergence
    ! Pass in both x and y direction fluxes. These variables are kept together
    ! in order that halo exchange which takes into account panel orientation can
    ! be performed in the future.
    call invoke_fv_divergence(div_temp,mass_flux_mult_x,mass_flux_mult_y,     &
                                                    cell_orientation, direction)

    ! Divide the divergence by detJ values at W3 dofs
    call invoke( dg_matrix_vector_kernel_type(increment, div_temp, m3_inv) )

    nullify( m3_inv )

  end subroutine cusph_fv_density_update_alg

end module cusph_fv_density_update_alg_mod
