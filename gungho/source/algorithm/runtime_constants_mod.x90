!-----------------------------------------------------------------------------
! Copyright (c) 2017,  Met Office, on behalf of HMSO and Queen's Printer
! For further details please refer to the file LICENCE.original which you
! should have received as part of this distribution.
!-----------------------------------------------------------------------------
!
!> @brief A module providing various run time constants
!>
!> @details A module holding various objects that are created at
!>         setup and are not changed thereafter but are needed
!>         throughout the algorithm layers
module runtime_constants_mod

  use constants_mod,                     only: i_def, r_def
  use log_mod,                           only: log_event, &
                                               LOG_LEVEL_INFO, &
                                               LOG_LEVEL_ERROR, &
                                               log_scratch_space
  ! Configuration
  use base_mesh_config_mod,              only: geometry,           &
                                               geometry_spherical, &
                                               f_lat
  use finite_element_config_mod,         only: element_order, &
                                               nqp_exact
  use formulation_config_mod,            only: dlayer_on, &
                                               rotating
  use transport_config_mod,              only: scheme,               &
                                               scheme_horz_cosmic,   &
                                               scheme_yz_bip_cosmic, &
                                               scheme_cosmic_3D
  use mixing_config_mod,                 only: smagorinsky
  use timestepping_config_mod,           only: dt

  use multigrid_config_mod,              only: multigrid_chain_nitems, l_multigrid
  use finite_element_config_mod,         only: coordinate_order  
  
  use function_space_chain_mod,          only: multigrid_function_space_chain, &
                                               w2_multigrid_function_space_chain, &
                                               wtheta_multigrid_function_space_chain
  use assign_coordinate_field_mod,       only: assign_coordinate_field
  use assign_orography_field_mod,        only: assign_orography_field
  
  ! Derived Types
  use field_mod,                         only: field_type
  use function_space_mod,                only: function_space_type
  use function_space_collection_mod,     only: function_space_collection
  use fs_continuity_mod,                 only: W0, W1, W2, W2broken, W2H, W2V, W3, Wtheta, Wchi
  use mesh_mod,                          only: mesh_type
  use operator_mod,                      only: operator_type
  use quadrature_xyoz_mod,               only: quadrature_xyoz_type
  use quadrature_rule_gaussian_mod,      only: quadrature_rule_gaussian_type

  ! PsyKAl-lite kernels
  use psykal_lite_mod,                   only: invoke_mpi_calc_cell_orientation

  use multiplicity_kernel_mod,           only: multiplicity_kernel_type

  ! PsyKAl PSYClone kernels
  use compute_latlon_kernel_mod,             only: compute_latlon_kernel_type
  use compute_derahm_matrices_kernel_mod,    only: compute_derahm_matrices_kernel_type
  use mg_derahm_mat_kernel_mod,              only: mg_derahm_mat_kernel_type
  use compute_mass_matrix_kernel_w2_mod,     only: compute_mass_matrix_kernel_w2_type
  use compute_div_operator_kernel_mod,       only: compute_div_operator_kernel_type
  use compute_dl_matrix_kernel_mod,          only: compute_dl_matrix_kernel_type
  use compute_geopotential_kernel_mod,       only: compute_geopotential_kernel_type
  use mm_diagonal_kernel_mod,                only: mm_diagonal_kernel_type
  use invert_local_operator_kernel_mod,      only: invert_local_operator_kernel_type
  use matrix_vector_kernel_mod,              only: matrix_vector_kernel_type
  use get_height_kernel_mod,                 only: get_height_kernel_type
  use compute_geopotential_kernel_mod,       only: compute_geopotential_kernel_type
  use calc_detj_at_w2_kernel_mod,            only: calc_detj_at_w2_kernel_type
  use calc_dA_at_w2_kernel_mod,              only: calc_dA_at_w2_kernel_type
  use calc_delta_at_wtheta_kernel_mod,       only: calc_delta_at_wtheta_kernel_type
  use w2_normalisation_kernel_mod,           only: w2_normalisation_kernel_type
  use compute_coriolis_matrix_kernel_mod,    only: compute_coriolis_matrix_kernel_type
  use compute_mass_matrix_kernel_w2_mod,     only: compute_mass_matrix_kernel_w2_type
  use io_config_mod,                         only: subroutine_timers
  use timer_mod,                             only: timer

  implicit none

  private

  ! Variables private to this module that can only be accessed by public
  ! functions returning pointers to them

  type(field_type), target                               :: mm0_diag
  type(field_type), target                               :: mm1_diag
  type(field_type), target                               :: mm2_diag
  type(field_type), target                               :: mm3_diag
  type(field_type), target                               :: eye
  type(field_type), target                               :: mmtheta_diag
  type(operator_type), target                            :: mm_w0
  type(operator_type), target                            :: mm_w1
  type(operator_type), allocatable, dimension(:), target :: mm_w2
  type(operator_type), target                            :: mm_w2b
  type(operator_type), target                            :: mm_w2v
  type(operator_type), target                            :: mm_w2h
  type(operator_type), allocatable, dimension(:), target :: mm_w3
  type(operator_type), allocatable, dimension(:), target :: mm_w3_inv
  type(operator_type), allocatable, dimension(:), target :: mm_wtheta
  type(operator_type), allocatable, dimension(:), target :: mm_dl
  type(field_type), target                               :: geopotential
  type(field_type), target                               :: temp_w2_field
  type(field_type), target                               :: cell_orientation
  type(field_type), target                               :: detj_at_w2
  type(field_type), target                               :: detj_at_w2_shifted
  type(field_type), target                               :: dA_at_w2
  type(field_type), target                               :: delta_at_wtheta
  type(field_type), target                               :: max_diff_wtheta
  type(operator_type), allocatable, dimension(:), target :: div
  type(operator_type), target                            :: grad, curl,  &
                                                            div_v, div_h,    &
                                                            broken_div
  type( quadrature_xyoz_type ),target                    :: qr
  type( quadrature_rule_gaussian_type )                  :: quadrature_rule
  type(field_type), target                               :: m0_lumped_inv
  type(field_type), target                               :: m1_lumped_inv 
  type(field_type), allocatable, dimension(:), target    :: mt_lumped_inv
  type(field_type), target                               :: height_wth, height_w3
  type(field_type), target                               :: nodal_rmultiplicity_w1
  type(field_type), allocatable, dimension(:), target    :: nodal_rmultiplicity_w2
  type(field_type), target                               :: nodal_rmultiplicity_w2h
  type(field_type), target                               :: nodal_rmultiplicity_w0
  type(field_type), allocatable, dimension(:), target    :: w2_normalisation
  type(operator_type), target                            :: coriolis
  type(field_type), allocatable, dimension(:,:), target  :: chi_mg
  ! 2D fields
  type(field_type), target              :: latitude_W3, longitude_W3

  ! Enumerated types
  integer(i_def), parameter, public :: w0_id          = 0_i_def
  integer(i_def), parameter, public :: w1_id          = 1_i_def
  integer(i_def), parameter, public :: w2_id          = 2_i_def
  integer(i_def), parameter, public :: w3_id          = 3_i_def
  integer(i_def), parameter, public :: wt_id          = 4_i_def
  integer(i_def), parameter, public :: w3inv_id       = 5_i_def
  integer(i_def), parameter, public :: eye_id         = 6_i_def
  integer(i_def), parameter, public :: theta_space_id = 7_i_def
  integer(i_def), parameter, public :: dl_id          = 8_i_def
  integer(i_def), parameter, public :: w2v_id         = 9_i_def
  integer(i_def), parameter, public :: w2h_id         = 10_i_def
  integer(i_def), parameter, public :: w2b_id         = 11_i_def


  ! Public functions to create and access the module contents

  public :: create_runtime_constants
  public :: final_runtime_constants
  public :: get_coordinates
  public :: get_geopotential
  public :: get_cell_orientation
  public :: get_detj_at_w2
  public :: get_detj_at_w2_shifted
  public :: get_dA_at_w2
  public :: get_max_diff
  public :: get_delta_at_wtheta
  public :: get_mass_matrix
  public :: get_mass_matrix_diagonal
  public :: get_grad
  public :: get_curl
  public :: get_div
  public :: get_broken_div
  public :: get_div_v
  public :: get_div_h
  public :: get_qr
  public :: get_rmultiplicity
  public :: get_inverse_lumped_mass_matrix
  public :: get_height
  public :: get_normalisation
  public :: get_coriolis
  public :: get_latitude
  public :: get_longitude


  
  ! overload the interfaces for MG versions
  interface get_coordinates
     module procedure get_coordinates_fine, get_coordinates_mg
  end interface get_coordinates

  interface get_div
     module procedure get_div_fine, get_div_mg
  end interface get_div

  interface get_mass_matrix
     module procedure get_mass_matrix_fine, get_mass_matrix_mg
  end interface get_mass_matrix

  interface get_normalisation
     module procedure get_normalisation_fine, get_normalisation_mg
  end interface get_normalisation

  interface get_inverse_lumped_mass_matrix
     module procedure get_inverse_lumped_mass_matrix_fine, get_inverse_lumped_mass_matrix_mg
  end interface get_inverse_lumped_mass_matrix
  
contains
  !>@brief Subroutine to create the runtime constants
  !> @param[in] mesh_id         Mesh_id
  !> @param[in] twod_mesh_id    Mesh_id for 2D domain
  !> @param[in] chi             chi field
  !> @param[in] shifted_mesh_id Mesh_id for vertically shifted field
  !> @param[in] shifted_chi     chi field for vertically shifted field
  subroutine create_runtime_constants(mesh_id,twod_mesh_id, chi,   &
                                      shifted_mesh_id, shifted_chi)
    implicit none

    integer(i_def),             intent(in) :: mesh_id, twod_mesh_id
    type(field_type), target,   intent(in) :: chi(:)
    integer(i_def), optional,   intent(in) :: shifted_mesh_id
    type(field_type), optional, intent(in) :: shifted_chi(:)

    type(function_space_type), pointer :: w0_fs     => null()
    type(function_space_type), pointer :: w1_fs     => null()
    type(function_space_type), pointer :: w2_fs     => null()
    type(function_space_type), pointer :: w2b_fs    => null()
    type(function_space_type), pointer :: w2h_fs    => null()
    type(function_space_type), pointer :: w2v_fs    => null()
    type(function_space_type), pointer :: w3_fs     => null()
    type(function_space_type), pointer :: wtheta_fs => null()
    type(function_space_type), pointer :: wth_k0_fs => null() ! lowest order wth fs
    type(function_space_type), pointer :: w3_k0_fs  => null() ! lowest order w3 fs
    type(function_space_type), pointer :: w3_k0_2d_fs => null() ! lowest order w3 fs
    type(field_type)                   :: ones
    type(field_type)                   :: dx_at_w2

    ! Temporary fields
    type(field_type) :: nodal_multiplicity_w1
    type(field_type) :: nodal_multiplicity_w2
    type(field_type) :: nodal_multiplicity_w2_shifted
    type(field_type) :: nodal_multiplicity_w2h
    type(field_type) :: nodal_multiplicity_w0

    ! Auxiliary parameters for PSyclone builtins
    integer(i_def), parameter :: i_minus_one = -1_i_def
    integer(i_def) :: iconst
    real(r_def) :: const
    ! temporaries needed for Multigrid
    integer(kind=i_def)       :: i, level, chi_space, multigrid_levels
    integer(kind=i_def)       :: mg_mesh_id
    
    if ( subroutine_timers ) call timer('runtime_constants_alg')
    call log_event( "Gungho: creating runtime_constants", LOG_LEVEL_INFO )

    if(l_multigrid) then
       multigrid_levels=multigrid_chain_nitems
    else
       multigrid_levels=1
    end if
    
    ! make the multigrid chi field.
    allocate(chi_mg(3,multigrid_levels))
    do i = 1, 3
       chi_mg(i,1) = chi(i)
    end do
     ! Compute coordinates
    if ( coordinate_order == 0 ) then
       chi_space = W0
       call log_event( "runtime_constants: Computing W0 coordinate fields for MG", LOG_LEVEL_INFO )
    else
       chi_space = Wchi
       call log_event( "runtime_constants: Computing Wchi coordinate fields for MG", LOG_LEVEL_INFO )
    end if

    if(l_multigrid) then
       ! get the mesh_id from the other function space
       ! need to get the mesh_id to create the chi fs, so get it from the W3 chain
       w3_fs     => function_space_collection%get_fs( mesh_id, element_order, W3 )
       call multigrid_function_space_chain%set_current(w3_fs%get_id())
       
       ! finest level is already done, so loop through levels 2, multigrid_levels
       do level = 2, multigrid_levels
          w3_fs=>multigrid_function_space_chain%get_next()
          mg_mesh_id=w3_fs%get_mesh_id()
          do i = 1, 3
             chi_mg(i,level) = field_type(vector_space = &
                  function_space_collection%get_fs( &
                  mg_mesh_id, coordinate_order, chi_space) )
          end do
          call assign_coordinate_field(chi_mg(:,level),mg_mesh_id)
          call assign_orography_field(chi_mg(:,level),mg_mesh_id)
       end do
    end if
    ! set level = 1 for the finest level MG/ or single level only
    level = 1   
    
    !=========== Create function spaces required for setup ===================!

    w0_fs     => function_space_collection%get_fs( mesh_id, element_order, W0 )
    w1_fs     => function_space_collection%get_fs( mesh_id, element_order, W1 )
    w2_fs     => function_space_collection%get_fs( mesh_id, element_order, W2 )
    w2b_fs    => function_space_collection%get_fs( mesh_id, element_order, W2broken )
    w2v_fs    => function_space_collection%get_fs( mesh_id, element_order, W2V )
    w2h_fs    => function_space_collection%get_fs( mesh_id, element_order, W2H )
    w3_fs     => function_space_collection%get_fs( mesh_id, element_order, W3 )
    wtheta_fs => function_space_collection%get_fs( mesh_id, element_order, Wtheta )
    w3_k0_fs  => function_space_collection%get_fs( mesh_id,             0, W3 )
    wth_k0_fs => function_space_collection%get_fs( mesh_id,             0, Wtheta )
    w3_k0_2d_fs => function_space_collection%get_fs( twod_mesh_id,      0, W3 )

    !================= Create quadrature object =====================!
    qr = quadrature_xyoz_type(nqp_exact, quadrature_rule)
    !=================== Create geopotential field ============================!

    geopotential = field_type( vector_space = w0_fs )
    call invoke( compute_geopotential_kernel_type( geopotential, chi ) )
    !=================== Create cell orientation field ========================!

    if (scheme == scheme_yz_bip_cosmic .or. &
        scheme == scheme_horz_cosmic   .or. &
        scheme == scheme_cosmic_3D   ) then

      temp_w2_field = field_type( vector_space = w2_fs )
      cell_orientation = field_type( vector_space = w3_fs )
      call invoke( setval_c(temp_w2_field,    9999.0_r_def), &
                   setval_c(cell_orientation, 9999.0_r_def) )
      call invoke_mpi_calc_cell_orientation( temp_w2_field, cell_orientation )

   end if
!=================== Create DeRahm cochain operators =========================!

   ! multigrid ops
    allocate(mm_w2(multigrid_levels), &
             mm_dl(multigrid_levels), &
             mm_w3(multigrid_levels), &
             mm_w3_inv(multigrid_levels), &
             div(multigrid_levels),   &
             mm_wtheta(multigrid_levels) )
    allocate(w2_normalisation(multigrid_levels))
    ! These operators are MG valued (which could be 1 if then single level)
    mm_w2(level)     = operator_type( w2_fs, w2_fs )  
    mm_dl(level)     = operator_type( w2_fs, w2_fs )
    mm_wtheta(level) = operator_type( wtheta_fs, wtheta_fs )
    mm_w3(level)     = operator_type( w3_fs, w3_fs )
    mm_w3_inv(level) = operator_type( w3_fs, w3_fs ) 
    div(level)       = operator_type( w3_fs, w2_fs )   
    mm_w0 = operator_type( w0_fs, w0_fs )
    mm_w1 = operator_type( w1_fs, w1_fs )
    mm_w2b = operator_type( w2b_fs, w2b_fs )
    mm_w2v = operator_type( w2v_fs, w2v_fs )
    mm_w2h = operator_type( w2h_fs, w2h_fs )
    grad = operator_type( w1_fs, w0_fs )
    curl = operator_type( w2_fs, w1_fs )
    broken_div = operator_type( w3_fs, w2b_fs )
    div_v  = operator_type( w3_fs, w2v_fs )
    div_h  = operator_type( w3_fs, w2h_fs )
    call invoke( name = "Create mass matrix operators",                              &
                 compute_derahm_matrices_kernel_type(mm_w0, mm_w1,                   &
                                                     mm_w2(level), mm_w2b,           &
                                                     mm_w3(level), mm_wtheta(level), &
                                                     grad, curl,                     &
                                                     div(level), broken_div,         &
                                                     chi_mg(:,level), qr),           &
                 invert_local_operator_kernel_type(mm_w3_inv(level), mm_w3(level)) )
    ! Construct vertical- and horizontal-only velocity mass matrices
    call invoke( compute_mass_matrix_kernel_w2_type( mm_w2v, chi_mg(:,level), qr ) )
    call invoke( compute_mass_matrix_kernel_w2_type( mm_w2h, chi_mg(:,level), qr ) )
    ! Construct vertical- and horizontal-only weak divergence operator    
    call invoke( compute_div_operator_kernel_type( div_v, chi_mg(:,level), qr ) )
    call invoke( compute_div_operator_kernel_type( div_h, chi_mg(:,level), qr ) )
    ! Compute W2 mass matrix with damping layer
    ! Eventually this may be absorbed into the DeRham complex computations
    ! but until the final form is settled upon it is left separate
    if (dlayer_on) call invoke( compute_dl_matrix_kernel_type( &
                                    mm_dl(level), chi_mg(:,level), qr) )

    !================= Create mass matrix diagonal fields =====================!

    mm0_diag       = field_type( vector_space = w0_fs )
    mm1_diag       = field_type( vector_space = w1_fs )
    mm2_diag       = field_type( vector_space = w2_fs )
    mm3_diag       = field_type( vector_space = w3_fs )
    mmtheta_diag   = field_type( vector_space = wtheta_fs )
    eye            = field_type( vector_space = w3_fs )
    ! Initialise fields
    call invoke( name = "Create mass matrix diagonal fields",      &
                 setval_c(mm0_diag,     0.0_r_def),                &
                 setval_c(mm1_diag,     0.0_r_def),                &
                 setval_c(mm2_diag,     0.0_r_def),                &
                 setval_c(mm3_diag,     0.0_r_def),                &
                 setval_c(mmtheta_diag, 0.0_r_def),                &
    ! Compute mass matrices
                 mm_diagonal_kernel_type(mm0_diag, mm_w0),                &
                 mm_diagonal_kernel_type(mm1_diag, mm_w1),                &
                 mm_diagonal_kernel_type(mm2_diag, mm_w2(level)),         &
                 mm_diagonal_kernel_type(mm3_diag, mm_w3(level)),         &
                 mm_diagonal_kernel_type(mmtheta_diag, mm_wtheta(level)), &
    ! W3 equations are solved pointwise and so the mass matrix is inverted
    ! exactly, therefore the diagonal, where used should be 1
                 setval_c(eye, 1.0_r_def) )

    !================= Create inverse lumped mass matrices =====================!

    ones           = field_type( vector_space = w1_fs )
    m1_lumped_inv  = field_type( vector_space = w1_fs )
    call invoke( name = "Create m1_lumped_inv",                         &
                 setval_c(ones,          1.0_r_def),                    &
                 setval_c(m1_lumped_inv, 0.0_r_def),                    &
                 matrix_vector_kernel_type(m1_lumped_inv, ones, mm_w1), &
                 inc_X_powint_n(m1_lumped_inv, i_minus_one) )

    ones           = field_type( vector_space = w0_fs )
    m0_lumped_inv  = field_type( vector_space = w0_fs )
    call invoke( name = "Create m0_lumped_inv",                         &
                 setval_c(ones,          1.0_r_def),                    &
                 setval_c(m0_lumped_inv, 0.0_r_def),                    &
                 matrix_vector_kernel_type(m0_lumped_inv, ones, mm_w0), &
                 inc_X_powint_n(m0_lumped_inv, i_minus_one) )

    ones           = field_type( vector_space = wtheta_fs )
    allocate(mt_lumped_inv(multigrid_levels))
    mt_lumped_inv(level)  = field_type( vector_space = wtheta_fs )
    call invoke( name = "Create mt_lumped_inv",                                           &
                 setval_c(ones,          1.0_r_def),                                      &
                 setval_c(mt_lumped_inv(level), 0.0_r_def),                               &
                 matrix_vector_kernel_type(mt_lumped_inv(level), ones, mm_wtheta(level)), &
                 inc_X_powint_n(mt_lumped_inv(level), i_minus_one) )

    !================= Create a height field =====================!
    ! Note that these fields represent the height relative to
    ! the surface as represented by a sphere.  With the current
    ! representation of chi, this will result in the lowest
    ! levels being -ve. This should be reviewed with ticket #562
    height_w3  = field_type( vector_space = w3_k0_fs )
    height_wth = field_type( vector_space = wth_k0_fs )
    call invoke( get_height_kernel_type(height_w3, chi) )
    call invoke( get_height_kernel_type(height_wth, chi) )

    !=== Create multiplicity for horizontally continuous fields ===!
    ! NB most of the time we want 1/multiplicity

    nodal_multiplicity_w1  = field_type( vector_space = w1_fs )
    nodal_multiplicity_w2  = field_type( vector_space = w2_fs )
    if ( present(shifted_mesh_id) ) then
      nodal_multiplicity_w2_shifted = field_type( vector_space = &
                            function_space_collection%get_fs( shifted_mesh_id, element_order, W2 ) )
      call invoke( name = "create_shifted_multiplicity",             &
                   multiplicity_kernel_type(nodal_multiplicity_w2_shifted) )
    end if

    nodal_multiplicity_w2h = field_type( vector_space = w2h_fs )
    nodal_multiplicity_w0  = field_type( vector_space = w0_fs )
    call invoke( name = "Create multiplicity",                     &
                 setval_c(nodal_multiplicity_w1, 0.0_r_def),       &
                 multiplicity_kernel_type(nodal_multiplicity_w1),  &
                 setval_c(nodal_multiplicity_w2, 0.0_r_def),       &
                 multiplicity_kernel_type(nodal_multiplicity_w2),  &
                 setval_c(nodal_multiplicity_w2h, 0.0_r_def),      &
                 multiplicity_kernel_type(nodal_multiplicity_w2h), &
                 setval_c(nodal_multiplicity_w0, 0.0_r_def),       &
                 multiplicity_kernel_type(nodal_multiplicity_w0) )

    ! Calculate the reciprocals
    nodal_rmultiplicity_w1  = field_type( vector_space = w1_fs )
    allocate(nodal_rmultiplicity_w2(multigrid_levels))
    nodal_rmultiplicity_w2(level)  = field_type( vector_space = w2_fs )
    nodal_rmultiplicity_w2h = field_type( vector_space = w2h_fs )
    nodal_rmultiplicity_w0  = field_type( vector_space = w0_fs )

    ones = field_type( vector_space = w1_fs )
    call invoke( setval_c(ones, 1.0_r_def), &
                 X_divideby_Y(nodal_rmultiplicity_w1, ones, nodal_multiplicity_w1) )

    ones = field_type( vector_space = w2_fs )
    call invoke( setval_c(ones, 1.0_r_def), &
                 X_divideby_Y(nodal_rmultiplicity_w2(level), ones, nodal_multiplicity_w2) )

    ones = field_type( vector_space = w2h_fs )
    call invoke( setval_c( ones, 1.0_r_def ), &
                 X_divideby_Y(nodal_rmultiplicity_w2h, ones, nodal_multiplicity_w2h) )

    ones = field_type( vector_space = w0_fs )
    call invoke( setval_c(ones, 1.0_r_def), &
                 X_divideby_Y(nodal_rmultiplicity_w0, ones, nodal_multiplicity_w0) )

    w2_normalisation(level) = field_type( vector_space = w2_fs )
    call invoke( setval_c(w2_normalisation(level), 0.0_r_def),                     &
                 w2_normalisation_kernel_type(w2_normalisation(level), chi),       &
                 inc_X_divideby_Y(w2_normalisation(level), nodal_multiplicity_w2), &
                 inc_X_powint_n(w2_normalisation(level), i_minus_one) )

    ! Compute Coriolis operator
    if ( rotating ) then
      coriolis = operator_type( w2_fs, w2_fs )
      call invoke( compute_coriolis_matrix_kernel_type(coriolis, chi, qr) )
    end if

    ! Compute latitude and longitude maps
    latitude_w3 = field_type( vector_space = w3_k0_2d_fs )
    longitude_w3 = field_type( vector_space = w3_k0_2d_fs )
    if ( geometry == geometry_spherical ) then
      call invoke( compute_latlon_kernel_type( latitude_w3, longitude_w3, chi) )
    else
      call invoke( setval_c(latitude_w3, f_lat),  &
                   setval_c(longitude_w3, 0_r_def) )
    end if


    !================ Create detj values at W2 dof locations ==================!

    detj_at_w2 = field_type( vector_space = w2_fs )
    call invoke(setval_c(detj_at_w2,0.0_r_def),               &
                calc_detj_at_w2_kernel_type(detj_at_w2,chi),  &
                inc_X_divideby_Y(detj_at_w2, nodal_multiplicity_w2) )


    !================ Create detj values at vertically shifted W2 dofs ========!

    if ( present(shifted_mesh_id) ) then
      detj_at_w2_shifted = field_type( vector_space = &
                            function_space_collection%get_fs( shifted_mesh_id, &
                                                          element_order, W2 ) )
      call invoke(setval_c(detj_at_w2_shifted,0.0_r_def),               &
                  calc_detj_at_w2_kernel_type(detj_at_w2_shifted,shifted_chi),  &
                  inc_X_divideby_Y(detj_at_w2_shifted, nodal_multiplicity_w2_shifted) )
    end if

    !================ Create dA values at W2 dof locations ==================!

    dA_at_w2 = field_type( vector_space = w2_fs )
    call invoke( setval_c(dA_at_w2, 0.0_r_def),&
                 calc_dA_at_w2_kernel_type(dA_at_w2, chi) )

    !================ Create edge lengths and delta values ==================!

    ! Need to define field properties outside IF test as the fields are passed
    ! to the BL scheme, even if Smagorinsky is false
    dx_at_w2 = field_type( vector_space = w2_fs )
    delta_at_wtheta = field_type( vector_space = wtheta_fs )
    max_diff_wtheta = field_type( vector_space = wtheta_fs )

    if (smagorinsky) then
      call invoke( X_divideby_Y(dx_at_w2, detj_at_w2, dA_at_w2),&
                   calc_delta_at_wtheta_kernel_type(delta_at_wtheta, dx_at_w2)  )

      ! calculate maximum diffusion coefficient allowed in this run for stability
      ! based on UMDP 28 equation 32: max_diff = dx^2/(8*dt)
      const = 0.125_r_def/dt
      iconst = 2_i_def
      max_diff_wtheta = delta_at_wtheta
      call invoke( inc_X_powint_n( max_diff_wtheta, iconst ),  & 
                   inc_a_times_X( const, max_diff_wtheta )     &
                 )

   end if

   
   if(l_multigrid) then
      ! compute on the courser multigrid levels
      call multigrid_function_space_chain%set_current(w3_fs%get_id())
      call w2_multigrid_function_space_chain%set_current(w2_fs%get_id())
      call wtheta_multigrid_function_space_chain%set_current(wtheta_fs%get_id())    
      
      do level = 2, multigrid_levels
         
         write(log_scratch_space,'(A,I0,A)') "runtime_constants[",level,"]:creating ops"
         call log_event(log_scratch_space,LOG_LEVEL_INFO)
         
         w3_fs=>multigrid_function_space_chain%get_next()
         w2_fs=>w2_multigrid_function_space_chain%get_next()
         wtheta_fs=>wtheta_multigrid_function_space_chain%get_next()
         
         mm_w2(level)     = operator_type( w2_fs, w2_fs )  
         mm_dl(level)     = operator_type( w2_fs, w2_fs )
         mm_wtheta(level) = operator_type( wtheta_fs, wtheta_fs )
         mm_w3(level)     = operator_type( w3_fs, w3_fs )
         mm_w3_inv(level) = operator_type( w3_fs, w3_fs ) 
         div(level)       = operator_type( w3_fs, w2_fs )
         
         call invoke(mg_derahm_mat_kernel_type( mm_w2(level), &
              mm_w3(level), mm_wtheta(level), div(level), chi_mg(:,level), qr),&
              invert_local_operator_kernel_type(mm_w3_inv(level), mm_w3(level)) )        
         
         ! Compute W2 mass matrix with damping layer
         ! Eventually this may be absorbed into the DeRham complex computations
         ! but until the final form is settled upon it is left separate
         if (dlayer_on) call invoke( compute_dl_matrix_kernel_type( &
              mm_dl(level), chi_mg(:,level), qr) )
         
         ones = field_type( vector_space = wtheta_fs )
         
         mt_lumped_inv(level)  = field_type( vector_space = wtheta_fs )
         ! Create mt_lumped_inv    
         call invoke(  &
              setval_c(ones,          1.0_r_def),                        &
              setval_c(mt_lumped_inv(level), 0.0_r_def),                        &
              matrix_vector_kernel_type(mt_lumped_inv(level), ones, mm_wtheta(level)), &
              inc_X_powint_n(mt_lumped_inv(level), i_minus_one) )
         
         w2_normalisation(level) = field_type( vector_space = w2_fs )
         nodal_multiplicity_w2 = field_type( vector_space = w2_fs )
         call invoke( setval_c(w2_normalisation(level), 0.0_r_def),                 &
              setval_c(nodal_multiplicity_w2, 2.0_r_def),                   &
              w2_normalisation_kernel_type(w2_normalisation(level), chi_mg(:,level) ),       &
              inc_X_divideby_Y(w2_normalisation(level), nodal_multiplicity_w2), &
              inc_X_powint_n(w2_normalisation(level), i_minus_one) )       

         
         
      end do
   end if
   
    nullify( w0_fs  )
    nullify( w1_fs  )
    nullify( w2_fs  )
    nullify( w2b_fs )
    nullify( w2h_fs )
    nullify( w2v_fs )
    nullify( w3_fs  )
    nullify( wtheta_fs )
    nullify( wth_k0_fs )
    nullify( w3_k0_fs  )
    nullify( w3_k0_2d_fs  )

    call log_event( "Gungho: created runtime_constants", LOG_LEVEL_INFO )
    if ( subroutine_timers ) call timer('runtime_constants_alg')

  end subroutine create_runtime_constants

  !> @brief Function to return a pointer to the coordinate array on the finest level
  !> @return The coordinate field array
  function get_coordinates_fine() result(coords_ptr)
    implicit none
    type(field_type), pointer :: coords_ptr(:)
    coords_ptr => chi_mg(:,1)

  end function get_coordinates_fine

  !> @brief Function to return a pointer to the coordinate array on the specified level
  !> @param level integer multigrid level
  !> @return The coordinate field array
  function get_coordinates_mg(level) result(coords_ptr)
    implicit none
    integer, intent(in) :: level
    type(field_type), pointer :: coords_ptr(:)
    
    coords_ptr => chi_mg(:,level)
  end function get_coordinates_mg
  !> @brief Function to return a pointer to the latitude map
  !> @return The latitude field array
  function get_latitude() result(lat_ptr)
    implicit none
    type(field_type), pointer :: lat_ptr
    lat_ptr => latitude_w3
  end function get_latitude

  !> @brief Function to return a pointer to the longitude map
  !> @return The longitude field array
  function get_longitude() result(long_ptr)
    implicit none
    type(field_type), pointer :: long_ptr
    long_ptr => longitude_w3
  end function get_longitude

  !> @brief Function to return a pointer to a height field
  !> @param[in] space the space of the desired height field
  !> @return A height field
  function get_height(space) result(height)
    implicit none
    integer(i_def), intent(in) :: space
    type(field_type), pointer :: height

    select case (space)
    case (W3)
      height => height_w3
    case (Wtheta)
      height => height_wth
    case default
      height => null()
      call log_event( "Height not available on requested space", LOG_LEVEL_ERROR)
    end select

  end function get_height

  !> @brief Function to return a pointer to the geopotential field
  !> @return The geopotential field
  function get_geopotential() result(phi)
    implicit none
    type(field_type), pointer :: phi
    phi => geopotential

  end function get_geopotential

  !> @brief Function to return a pointer to the cell_orientation field
  !> @return The cell_orientation field
  function get_cell_orientation() result(orientation)
    implicit none
    type(field_type), pointer :: orientation

    if (scheme == scheme_yz_bip_cosmic .or. &
        scheme == scheme_horz_cosmic   .or. &
        scheme == scheme_cosmic_3D   ) then
      orientation => cell_orientation
    else
      call log_event( "Cell orientation variable has not been created", LOG_LEVEL_ERROR )
    end if

  end function get_cell_orientation


  !> @brief Function to return the detj values at W2 dof locations
  !> @return The detj field
  function get_detj_at_w2() result(detj)
    implicit none
    type(field_type), pointer :: detj
    detj => detj_at_w2

  end function get_detj_at_w2


  !> @brief Function to return the detj values at W2 dof locations on vertically
  !>        shifted mesh
  !> @return The detj field for vertically shifted mesh
  function get_detj_at_w2_shifted() result(detj)
    implicit none
    type(field_type), pointer :: detj
    detj => detj_at_w2_shifted

  end function get_detj_at_w2_shifted


  !> @brief Function to return the dA values at W2 dof locations
  !> @return The dA field
  function get_dA_at_w2() result(dA)
    implicit none
    type(field_type), pointer :: dA
    dA => dA_at_w2

  end function get_dA_at_w2

  !> @brief Function to return the delta_at_wtheta values at Wtheta dof locations
  !> @return The delta_at_wtheta field
  function get_delta_at_wtheta() result(delta)
    implicit none
    type(field_type), pointer :: delta
    delta => delta_at_wtheta

  end function get_delta_at_wtheta

  !> @brief Function to return the max Smagorinsky diffusion allowed in this run at wtheta
  !> @return The max_diff_wtheta field
  function get_max_diff() result(max_diff_smag)
    implicit none
    type(field_type), pointer :: max_diff_smag
    max_diff_smag => max_diff_wtheta

  end function get_max_diff

  !> @brief Function to return a pointer to a mass matrix
  !! If the operator is evaluated on several multigrid layers, then the finest is returned
  !> @param[in] i the index of the desired mass matrix field
  !> @return The mass matrix operator
  function get_mass_matrix_fine(i) result(mm)
    implicit none
    integer(i_def), intent(in) :: i
    type(operator_type), pointer :: mm

    select case (i)
      case (w0_id)
        mm => mm_w0
      case (w1_id)
        mm => mm_w1
      case (w2_id)
        mm => mm_w2(1)
      case (w2b_id)
        mm => mm_w2b
      case (w2v_id)
        mm => mm_w2v
      case (w2h_id)
        mm => mm_w2h
      case (w3_id)
        mm => mm_w3(1)
      case (w3inv_id)
        mm => mm_w3_inv(1)
      case (wt_id)
        mm => mm_wtheta(1)
      case (theta_space_id)
        mm => mm_wtheta(1)
      case (dl_id)
        mm => mm_dl(1)
      case default
        mm => null()
        call log_event( "Mass matrix operator does not exist", LOG_LEVEL_ERROR)
        stop
    end select

  end function get_mass_matrix_fine

  !> @brief Function to return a pointer to a mass matrix
  !! Only for operator evaluated on several multigrid layers
  !> @param[in] i the index of the desired mass matrix field
  !> @param[in] level integer multigrid level  
  !> @return The mass matrix operator  
  function get_mass_matrix_mg(i, level) result(mm)
    implicit none
    integer(i_def), intent(in) :: i
    integer(kind=i_def), intent(in) :: level
    type(operator_type), pointer :: mm 

    select case (i)
      case (w2_id)
        mm => mm_w2(level)
      case (w3_id)
        mm => mm_w3(level)
      case (w3inv_id)
        mm => mm_w3_inv(level)
      case (dl_id)
        mm => mm_dl(level)
      case default
        mm => null()
        call log_event( "Mass matrix operator does not exist", LOG_LEVEL_ERROR)
        stop
    end select

  end function get_mass_matrix_mg

  !> @brief Function to return a pointer to a mass matrix diagonal
  !> @param[in] i Index of the desired mass matrix diagonal field
  !> @return The mass matrix diagonal field
  function get_mass_matrix_diagonal(i) result(mmd)
    implicit none
    integer(i_def), intent(in) :: i
    type(field_type), pointer :: mmd

    select case (i)
      case (w0_id)
        mmd => mm0_diag
      case (w1_id)
        mmd => mm1_diag
      case (w2_id)
        mmd => mm2_diag
      case (eye_id)
        mmd => eye
      case (wt_id)
        mmd => mmtheta_diag
      case (w3_id)
        mmd => mm3_diag
      case (theta_space_id)
        mmd => mmtheta_diag
      case default
        mmd => null()
        call log_event( "Mass matrix diagonal does not exist", LOG_LEVEL_ERROR)
        stop
    end select

  end function get_mass_matrix_diagonal

  !> @brief Function to return a pointer to the grad operator
  !> @return The grad operator
  function get_grad() result(grad_op)
    implicit none
    type(operator_type), pointer :: grad_op
    grad_op => grad

  end function get_grad

  !> @brief Function to return a pointer to the curl operator
  !> @return The curl operator
  function get_curl() result(curl_op)
    implicit none
    type(operator_type), pointer :: curl_op
    curl_op => curl

  end function get_curl

  !> @brief Function to return a pointer to the div operator, on the finest multigrid_level
  !> @return The grad operator
  function get_div_fine() result(div_op)
    implicit none
    type(operator_type), pointer :: div_op
    div_op => div(1)

  end function get_div_fine

  !> @brief Function to return a pointer to the div operator
  !! on the specified multigrid_level
  !> @param[in] level integer multigrid level    
  !> @return The grad operator  
  function get_div_mg(level) result(div_op)
    implicit none
    integer, intent(in) :: level
    type(operator_type), pointer :: div_op
    div_op => div(level)
  end function get_div_mg

  !> @brief Function to return a pointer to the broken div operator
  !> @return The broken div operator
  function get_broken_div() result(broken_div_op)
    implicit none
    type(operator_type), pointer :: broken_div_op
    broken_div_op => broken_div

  end function get_broken_div

  !> @brief Function to return a pointer to the vertical div operator
  !> @return The div operator
  function get_div_v() result(div_v_op)
    implicit none
    type(operator_type), pointer :: div_v_op
    div_v_op => div_v

  end function get_div_v

  !> @brief Function to return a pointer to the horizontal div operator
  !> @return The div operator
  function get_div_h() result(div_h_op)
    implicit none
    type(operator_type), pointer :: div_h_op
    div_h_op => div_h

  end function get_div_h

  !> @brief Function to return a pointer to the quadrature object
  !> @return The quadrature object
  function get_qr() result(qr_obj)
    implicit none
    type(quadrature_xyoz_type), pointer :: qr_obj
    qr_obj => qr

  end function get_qr

  !> @brief Function to return a pointer to a inverse lumped mass matrix
  !! on the finest multigrid level
  !> @param[in] i Index of the desired lumped mass matrix inverse field
  !> @return The lumped inverse mass matrix 
  function get_inverse_lumped_mass_matrix_fine(i) result(mm)
    implicit none
    integer(i_def), intent(in) :: i
    type(field_type), pointer :: mm

    select case (i)
      case (w0_id)
        mm => m0_lumped_inv
      case (w1_id)
        mm => m1_lumped_inv
      case (wt_id)
        mm => mt_lumped_inv(1)
      case (theta_space_id)
        mm => mt_lumped_inv(1)
      case default
        mm => null()
        call log_event( "Lumped inverse mass matrix does not exist", LOG_LEVEL_ERROR)
        stop
    end select

  end function get_inverse_lumped_mass_matrix_fine

  !> @brief Function to return a pointer to a inverse lumped mass matrix
  !! on the specified multigrid level
  !> @param[in] i Index of the desired lumped mass matrix inverse field
  !> @param[in] level Integer the multigrid level
  !> @return The lumped inverse mass matrix   
    function get_inverse_lumped_mass_matrix_mg(i,level) result(mm)
    implicit none
    integer(kind=i_def), intent(in) :: i
    integer(kind=i_def), intent(in) :: level
    type(field_type), pointer :: mm

    select case (i)
      case (wt_id)
        mm => mt_lumped_inv(level)
      case (theta_space_id)
        mm => mt_lumped_inv(level)
      case default
        mm => null()
        call log_event( "Lumped inverse mass matrix does not exist", LOG_LEVEL_ERROR)
        stop
    end select

  end function get_inverse_lumped_mass_matrix_mg

  !> @brief Function to return a pointer to the reciprocal of the multiplicity field
  !> @return The nodal multiplicity field
  !> @param[in] space enumeration of the function space
  function get_rmultiplicity(space) result(rmultiplicity)
    implicit none
    integer(i_def), intent(in) :: space
    type(field_type), pointer :: rmultiplicity

    select case (space)
      case (W0)
        rmultiplicity => nodal_rmultiplicity_w0
      case (W1)
        rmultiplicity => nodal_rmultiplicity_w1
      case (W2)
        rmultiplicity => nodal_rmultiplicity_w2(1)
      case (W2H)
        rmultiplicity => nodal_rmultiplicity_w2h
      case default
        rmultiplicity => null()
        call log_event( "RMultiplicity does not exist for given space", &
           LOG_LEVEL_ERROR)
    end select

  end function get_rmultiplicity

  !> @brief Function to return a pointer to the normalisation of prognostic fields
  !! on the finest multigrid level
  !> @return The normalisation field
  !> @param[in] space enumeration of the function space
  function get_normalisation_fine(space) result(normalisation)
    implicit none
    integer(i_def), intent(in) :: space
    type(field_type), pointer :: normalisation

    select case (space)
      case (w2_id)
        normalisation => w2_normalisation(1)
      case (wt_id)
        normalisation => mt_lumped_inv(1)
      case (theta_space_id)
        normalisation => mt_lumped_inv(1)
      case default
        normalisation => null()
        call log_event( " Normalisation does not exist for given space", &
           LOG_LEVEL_ERROR)
    end select

  end function get_normalisation_fine

  !> @brief Function to return a pointer to the normalisation of prognostic fields
  !! on the specified multigrid level
  !> @return The normalisation field
  !> @param[in] space enumeration of the function space
  !> @param[in] level integer the multigrid level
  function get_normalisation_mg(space, level) result(normalisation)
    implicit none
    integer(kind=i_def), intent(in) :: space
    integer(kind=i_def), intent(in) :: level
    type(field_type), pointer :: normalisation

    select case (space)
      case (w2_id)
        normalisation => w2_normalisation(level)       
      case (wt_id)
        normalisation => mt_lumped_inv(level)
      case (theta_space_id)
        normalisation => mt_lumped_inv(level)
      case default
        normalisation => null()
        call log_event( " Normalisation does not exist for given space", &
           LOG_LEVEL_ERROR)
    end select

  end function get_normalisation_mg  
  

  !> @brief Function to return the Coriolis operator
  !> @return The Coriolis operator
  function get_coriolis() result(coriolis_op)
    implicit none
    type(operator_type), pointer :: coriolis_op
    if ( rotating ) then
      coriolis_op => coriolis
    else
      call log_event( "Coriolis operator not computed for nonrotating flow", &
                      LOG_LEVEL_ERROR)
    end if
  end function get_coriolis

  !> @brief Explicitly reclaim memory from module scope variables
  !
  subroutine final_runtime_constants()

    implicit none

    call mm0_diag%field_final()
    call mm1_diag%field_final()
    call mm2_diag%field_final()
    call mm3_diag%field_final()
    call eye%field_final()
    call mmtheta_diag%field_final()
    call mm_w0%operator_final()
    call mm_w1%operator_final()
    if(allocated(mm_w2)) then
       deallocate(mm_w2)
    end if
    call mm_w2b%operator_final()
    if(allocated(mm_w3)) then
       deallocate(mm_w3)
    end if
    if(allocated(mm_w3_inv)) then
       deallocate(mm_w3_inv)
    end if
    if(allocated(mm_wtheta)) then
       deallocate(mm_wtheta)
    end if
    if(allocated(mm_dl)) then
       deallocate(mm_dl)
    end if
    call geopotential%field_final()
    call temp_w2_field%field_final()
    call cell_orientation%field_final()
    call detj_at_w2%field_final()
    call detj_at_w2_shifted%field_final()
    call dA_at_w2%field_final()
    call grad%operator_final()
    call curl%operator_final()
    if(allocated(div)) then
       deallocate(div)
    end if
    call broken_div%operator_final()
    call qr%quadrature_final()
    call m0_lumped_inv%field_final()
    call m1_lumped_inv%field_final()
    if(allocated(mt_lumped_inv)) then
       deallocate(mt_lumped_inv)
    end if
    call height_wth%field_final()
    call height_w3%field_final()
    call nodal_rmultiplicity_w1%field_final()
    if(allocated(nodal_rmultiplicity_w2)) then
       deallocate(nodal_rmultiplicity_w2)
    end if
    call nodal_rmultiplicity_w2h%field_final()
    call nodal_rmultiplicity_w0%field_final()
    if(allocated(w2_normalisation)) then
       deallocate(w2_normalisation)
    end if    

  end subroutine final_runtime_constants

end module runtime_constants_mod
