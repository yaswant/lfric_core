!-----------------------------------------------------------------------------
! Copyright (c) 2017,  Met Office, on behalf of HMSO and Queen's Printer
! For further details please refer to the file LICENCE.original which you
! should have received as part of this distribution.
!-----------------------------------------------------------------------------

!>@brief Initialisation of prognostic fields
module init_prognostic_fields_alg_mod

  use log_mod,                         only: log_event,         &
                                             log_scratch_space, &
                                             LOG_LEVEL_INFO,    &
                                             LOG_LEVEL_TRACE
  use constants_mod,                   only: r_def, i_def, str_short

  use mass_matrix_solver_alg_mod,      only: mass_matrix_solver_alg
  use runtime_constants_mod,           only: get_coordinates, &
                                             get_mass_matrix, &
                                             w3inv_id,        &
                                             get_height

  ! Configuration and restart/checkpoint options
  use finite_element_config_mod,       only: element_order
  use formulation_config_mod,          only: use_moisture, set_hydrostatic_init
  use initial_density_config_mod,      only: sample_init_rho
  use initial_wind_config_mod,         only: smp_init_wind
  use restart_control_mod,             only: restart_type

  ! PsyKAl-lite kernels
  use psykal_lite_mod,                 only: invoke_initial_mr_kernel, &
                                             invoke_initial_rho_sample_kernel, &
                                             invoke_initial_exner_sample_kernel, &
                                             invoke_hydrostatic_exner_kernel
  use field_bundle_mod,                only: set_bundle_scalar

  ! PsyKAl PSYClone kernels
  use set_rho_kernel_mod,              only: set_rho_kernel_type
  use set_exner_kernel_mod,            only: set_exner_kernel_type
  use initial_u_kernel_mod,            only: initial_u_kernel_type
  use enforce_bc_kernel_mod,           only: enforce_bc_kernel_type
  use initial_theta_kernel_mod,        only: initial_theta_kernel_type
  use project_pressure_kernel_mod,     only: project_pressure_kernel_type
  use sample_eos_rho_kernel_mod,       only: sample_eos_rho_kernel_type

  ! Derived Types
  use field_mod,                       only: field_type
  use quadrature_xyoz_mod,             only: quadrature_xyoz_type
  use quadrature_rule_gaussian_mod,    only: quadrature_rule_gaussian_type
  use sample_initial_u_kernel_mod,     only: sample_initial_u_kernel_type 
  use operator_mod,                    only: operator_type

  ! Handles
  use fs_continuity_mod,               only: W3, Wtheta
  use mr_indices_mod,                  only: nummr, mr_names
  implicit none

  private
  public :: init_prognostic_fields_alg

contains

  !> @details An algorithm for initialising prognostic fields for 
  !>          all solvers.
  !> @param[inout] u 3D Wind field
  !> @param[inout] rho Density
  !> @param[inout] theta Potential temperature
  !> @param[inout] exner Exner pressure
  !> @param[inout] mr Field bundle containing the moisture mixring ratios
  !> @param[inout] xi The vorticity field 
  !> @param[in] restart Checkpoint/restart type with timestepping information
  subroutine init_prognostic_fields_alg(u, rho, theta, exner, mr, xi, restart)

    implicit none

    ! Prognostic fields
    type( field_type ), intent( inout ) :: u, rho, theta, exner, xi, mr(nummr)
    ! Control
    type( restart_type ), intent(in)      :: restart

    type( quadrature_xyoz_type )          :: qr
    type( quadrature_rule_gaussian_type ) :: quadrature_rule
    type( field_type )                    :: r_u
    ! Coordinate fields
    type( field_type ), pointer           :: chi(:) => null(), &
                                             height_wth => null(), &
                                             height_w3 => null()
    ! Operators
    type(operator_type), pointer        :: m3_inv => null()

    integer :: i
    character(str_short) :: name

    real(kind=r_def) :: initial_time

    qr = quadrature_xyoz_type(element_order+3, quadrature_rule)
    chi => get_coordinates()

    !=== Initialise global prognostic fields ==================================!

    if (.not.restart%read_checkpoint()) then           ! No check point to start from

      call log_event( "Gungho: Initialising prognostic fields", LOG_LEVEL_INFO )

      ! Initialise Xi
      call invoke( setval_c( xi, 0.0_r_def ) )
      ! Initialise theta
      call invoke( initial_theta_kernel_type( theta, chi ) )

      initial_time = 0.0_r_def

      if (smp_init_wind) then
        call invoke( name = "Initialise U",    &
                     setval_c( u, 0.0_r_def ), &
                     sample_initial_u_kernel_type( u, chi ) )
      else
        ! Initialise U and Rho according to formulation
        r_u = field_type( vector_space =  u%get_function_space() )
        call invoke( name = "Initialise U and Rho",   &
                     setval_c( u,   0.0_r_def ),      &
                     setval_c( r_u, 0.0_r_def ),      &
                     initial_u_kernel_type( r_u, chi, initial_time, qr ), &
                     enforce_bc_kernel_type( r_u ) )
        call mass_matrix_solver_alg(u, r_u)
      end if

      if (set_hydrostatic_init) then
        height_w3 => get_height(W3)
        height_wth => get_height(Wtheta)

        ! Initialize from theta distro using hydrostatic balance, then compute rho with equation of state
        call invoke_hydrostatic_exner_kernel( exner, theta, height_wth, &
                                                    height_w3, chi )
        call invoke( sample_eos_rho_kernel_type( rho, exner, theta ) )
      else
        if(sample_init_rho) then
          call invoke_initial_rho_sample_kernel  ( rho,   chi, initial_time )
          call invoke_initial_exner_sample_kernel( exner, chi, initial_time )
        else
          call invoke( set_rho_kernel_type   ( rho,   chi, initial_time, qr ), &
                       set_exner_kernel_type ( exner, chi, initial_time, qr ) )
          call log_event( "Gungho: Computing initial pressure as projection..", LOG_LEVEL_INFO )
          m3_inv => get_mass_matrix(w3inv_id)
          qr = quadrature_xyoz_type(1, quadrature_rule)

          ! Pressure has already been initialized
          call invoke( project_pressure_kernel_type( exner, rho, theta, &
                                                     chi, m3_inv, qr ) )

        end if
      end if

      if (use_moisture) then
        call invoke_initial_mr_kernel( theta, rho, mr(:), chi )
      else
        call set_bundle_scalar(0.0_r_def, mr, nummr)
      end if

      call log_event( "Gungho: Initialised prognostic fields", LOG_LEVEL_INFO )

    else                                   ! Recorded check point to start from

      call log_event("Reading checkpoint file to restart rho",LOG_LEVEL_INFO)
      call rho%read_restart("restart_rho", trim(restart%startfname("rho")))

      call log_event("Reading checkpoint file to restart u",LOG_LEVEL_INFO)
      call u%read_restart("restart_u", trim(restart%startfname("u")))

      call log_event("Reading checkpoint file to restart theta",LOG_LEVEL_INFO)
      call theta%read_restart("restart_theta", trim(restart%startfname("theta")))

      call log_event("Reading checkpoint file to restart exner",LOG_LEVEL_INFO)
      call exner%read_restart("restart_exner", trim(restart%startfname("exner")))

      call log_event("Reading checkpoint file to restart xi",LOG_LEVEL_INFO)
      call xi%read_restart("restart_xi", trim(restart%startfname("xi")))

      if (use_moisture)then
        do i=1,nummr
          write(name, '(A,A)') 'restart_',trim(mr_names(i))
          write(log_scratch_space,'(A,A)') "Reading checkpoint file to restart ", trim(name)
          call log_event(log_scratch_space,LOG_LEVEL_INFO)
          call mr(i)%read_restart(trim(name),trim(restart%startfname(mr_names(i))))
        end do
      end if

      call log_event( "Gungho: Read prognostic fields from files", LOG_LEVEL_INFO )
    end if

    !==========================================================================!
    nullify( m3_inv, chi, height_wth, height_w3 )

  end subroutine init_prognostic_fields_alg

end module init_prognostic_fields_alg_mod
