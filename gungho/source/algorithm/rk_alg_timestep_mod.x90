!-----------------------------------------------------------------------------
! Copyright (c) 2017,  Met Office, on behalf of HMSO and Queen's Printer
! For further details please refer to the file LICENCE.original which you
! should have received as part of this distribution.
!-----------------------------------------------------------------------------

!>@brief A Runge-Kutta time-discretisation of the non-linear 3D equations,
!>       currently using 3-stage SSP.
!>@details An algorithm for timestepping the 3D nonlinear linear
!>         equations using a multistage explicit Runge-Kutta algorithm.
module rk_alg_timestep_mod

  use constants_mod,                     only: i_def,r_def
  use log_mod,                           only: log_event,         &
                                               log_scratch_space, &
                                               LOG_LEVEL_INFO,    &
                                               LOG_LEVEL_TRACE

  ! Configuration options
  use finite_element_config_mod, only: element_order, wtheta_on
  use formulation_config_mod,    only: rotating
  use runge_kutta_init_mod,      only: num_rk_stage, ak
  use timestepping_config_mod,   only: dt, &
                                       outer_iterations, inner_iterations
  use idealised_config_mod,      only: test, idealised_test_held_suarez
  use derived_config_mod,        only: bundle_size
  use runtime_constants_mod,     only: get_geopotential,         &
                                       get_mass_matrix,          &
                                       get_mass_matrix_diagonal, &
                                       get_qr, &
                                       get_inverse_lumped_mass_matrix, &
                                       theta_space_id, &
                                       get_coordinates 
  use transport_config_mod,      only: operators, &
                                       transport_operators_fv
  use mixing_config_mod,         only: viscosity
  
  ! PsyKAl-lite kernels
  use psykal_lite_mod,                   only: invoke_ru_bd_kernel, &
                                               invoke_multiply_field_data, &
                                               invoke_viscosity

  use field_bundle_mod,                  only: clone_bundle, &
                                               bundle_axpy,  & 
                                               bundle_axpby, &
                                               add_bundle,   &
                                               copy_bundle,  &
                                               set_bundle_scalar
  
  ! PsyKAl PSYClone kernels
  use held_suarez_kernel_mod,              only: held_suarez_kernel_type
  use enforce_bc_kernel_mod,               only: enforce_bc_kernel_type
  use rotation_kernel_mod,                 only: rotation_kernel_type
  use pressure_gradient_kernel_mod,        only: pressure_gradient_kernel_type
  use kinetic_energy_gradient_kernel_mod,  only: kinetic_energy_gradient_kernel_type
  use geopotential_gradient_kernel_mod,    only: geopotential_gradient_kernel_type

  ! Derived Types
  use field_mod,                         only: field_type
  use quadrature_mod,                    only: quadrature_type
  use operator_mod,                      only: operator_type

  ! Algorithms
  use solver_mod,                        only: solver_algorithm
  use conservation_algorithm_mod,        only: conservation_algorithm
  use advection_alg_mod,                 only: advection_alg, advection_alg_init
  use si_solver_alg_mod,                 only: si_solver_alg, si_solver_init

  ! Field indices
  use field_indices_mod,                 only: igh_u, igh_t, igh_d

  use output_config_mod,                   only: subroutine_timers 
  use timer_mod,                           only: timer

  implicit none

  private

  ! 'State' items that need to be created once but used every step
  type( field_type ), allocatable :: state(:), state_n(:), rhs(:), inc(:)
  type( field_type ), allocatable :: rhs_prediction(:,:)

  public :: rk_alg_init
  public :: rk_alg_step

contains

  !> @details Initialisation procedure for the rk timestepping algorithm
  !>          Initialises various internal fields
  !> @param[in]    mesh_id Id of mesh object on which the model runs
  !> @param[inout] u 3D wind field
  !> @param[inout] rho Density
  !> @param[inout] theta Potential temperature

  subroutine rk_alg_init( mesh_id, u, rho, theta)
    implicit none

    ! Mesh
    integer(i_def), intent(in)          :: mesh_id 

    ! Prognostic fields
    type( field_type ), intent( inout ) :: u, rho, theta

    ! Timestepping variables
    integer :: stage

    !=== Allocate internal state field arrays ================================!

    allocate(state(bundle_size))   
    allocate(state_n(bundle_size))
    allocate(rhs(bundle_size))
    allocate(inc(bundle_size))

    allocate ( rhs_prediction(bundle_size,num_rk_stage) )

    !=== Initialise internal state field objects =============================!

    state(igh_u) = field_type( vector_space = u%get_function_space() )
    state(igh_t) = field_type( vector_space = theta%get_function_space() )
    state(igh_d) = field_type( vector_space = rho%get_function_space() )

    call clone_bundle(state, state_n, bundle_size)
    call clone_bundle(state, rhs, bundle_size)
    call clone_bundle(state, inc, bundle_size)

    do stage = 1,num_rk_stage
      call clone_bundle(state, rhs_prediction(:,stage), bundle_size) 
    end do


    !=== Copy prognostic field data to state arrays ===========================!
    call invoke( copy_field(u,     state(igh_u)), &
                 copy_field(theta, state(igh_t)), &
                 copy_field(rho,   state(igh_d)) )

    call advection_alg_init(mesh_id)

    call log_event( "Gungho: initialised timestepping algorithm", LOG_LEVEL_INFO )

  end subroutine rk_alg_init

  !> @brief A Runge-Kutta time-discretisation of the non-linear 3D equations,
  !>        currently using 3-stage SSP.
  !> @details An algorithm for timstepping the 3D nonlinear
  !>          equations using a Runge-Kutta algortihm. 
  !>          The type of Runge-Kutta scheme can be simply changed by
  !>          modifying the (currently hardwired) Butcher tableau (ak array)
  !>          The current set up uses the 3rd-order three stage
  !>          Strong-Stability-Preserving (SSP3) method
  !> @param[inout] u 3D wind field
  !> @param[inout] rho Density
  !> @param[inout] theta Potential temperature
  !> @param[inout] xi Vorticity field  

  subroutine rk_alg_step(u, rho, theta, xi)


    ! Prognostic fields    
    type( field_type ), intent( inout ) :: u, rho, theta, xi

    ! the quadrature object
    type( quadrature_type )    :: qr

    ! Temporary items
    type( field_type )    :: diagnostic_field

    ! temps for retrieving things from runtime_constants
    type(field_type),   pointer :: mm_lumped_inv_wtheta => null()
    type(field_type),   pointer :: geopotential => null()
    type( field_type ), pointer :: chi(:) => null()

    integer(i_def) :: st, stage
    if ( subroutine_timers ) call timer('rk_alg')
    qr = get_qr()

    ! Get geopotential field from runtime_constants
    geopotential => get_geopotential()

    ! Get mm operators from runtime_constants 
    mm_lumped_inv_wtheta => get_inverse_lumped_mass_matrix(theta_space_id)

    ! Get coordinates
    chi => get_coordinates()
    !=== Do a single timestep ==============================================!

    ! Runge-Kutta algorithm
    call copy_bundle(state, state_n, bundle_size)
    do stage = 1,num_rk_stage
      write( log_scratch_space, '(A,I0)' ) '  Start of stage ', stage
      call log_event( log_scratch_space, LOG_LEVEL_INFO )
      ! Compute advection terms
      call advection_alg( rhs_prediction(:,stage), &
                          state_n, xi, state(igh_u))
      ! Compute new rhs
      if ( rotating ) &
        call invoke( rotation_kernel_type( rhs_prediction(1,stage), state(igh_u), chi, qr ) )
        if( wtheta_on ) &
          call invoke_ru_bd_kernel ( rhs_prediction(1, stage), state(igh_d), state(igh_t), qr )
        call invoke( pressure_gradient_kernel_type       &
             ( rhs_prediction(1,stage), state(igh_d), state(igh_t), qr ) )
        call invoke( kinetic_energy_gradient_kernel_type &
             ( rhs_prediction(1,stage), state(igh_u), chi, qr ) )
        call invoke( geopotential_gradient_kernel_type   &
             ( rhs_prediction(1,stage), geopotential, qr ) )
        call invoke( enforce_bc_kernel_type( rhs_prediction(1,stage) ) ) 
        call set_bundle_scalar(0.0_r_def, rhs, bundle_size)

      do st = 1, stage
        call bundle_axpy(ak(stage,st), rhs_prediction(:,stage), rhs, rhs, bundle_size)
      end do

      ! Invert mass matrices
      if ( operators == transport_operators_fv) then
        ! Equation is in pointwise form already so dont need to do any mass
        ! matrix inversions   
        call invoke( copy_field(rhs(igh_t), inc(igh_t)) )
      else
        ! Use full mass matrix
        call solver_algorithm( inc(igh_t), rhs(igh_t) )
      end if
      call solver_algorithm( inc(igh_u), rhs(igh_u) )
      call invoke( copy_field(rhs(igh_d), inc(igh_d)) )

      ! Add increments
      call bundle_axpy(dt, inc, state_n, state, bundle_size)

      ! Diagnostics
      diagnostic_field = inc(igh_t)
      call diagnostic_field%log_minmax(LOG_LEVEL_TRACE, 'theta_inc');
      diagnostic_field = inc(igh_u)
      call diagnostic_field%log_minmax(LOG_LEVEL_TRACE, 'u_inc');
      diagnostic_field = inc(igh_d)
      call diagnostic_field%log_minmax(LOG_LEVEL_TRACE, 'rho_inc');

    end do

    ! Apply viscosity
    if ( viscosity ) then
      call log_event( 'Applying Viscosity', LOG_LEVEL_INFO )      
      call invoke_viscosity(inc(igh_t), theta, inc(igh_u), u, chi)
      call invoke( set_field_scalar(0.0_r_def, inc(igh_d)) )
      call bundle_axpy(dt, inc, state, state, bundle_size)
    end if

    ! Return fields in u, theta, rho arrays
    call invoke( copy_field(state(igh_u), u),     &
                 copy_field(state(igh_t), theta), &
                 copy_field(state(igh_d), rho) )
    if ( subroutine_timers ) call timer('rk_alg')

  end subroutine rk_alg_step

end module rk_alg_timestep_mod
