!-------------------------------------------------------------------------------
! Copyright (c) 2017,  Met Office, on behalf of HMSO and Queenâ€™s Printer
! For further details please refer to the file LICENCE.original which you
! should have received as part of this distribution.
!-------------------------------------------------------------------------------
!> @brief Algorithm which calculates the mass fluxes using the symmetric COSMIC
!>        method.
!>        The algorithm below outputs the mass fluxes at timestep n+1 (np1)
!>        given the wind fields at timestep n and n+1 and the density field at
!>        timestep n.
module cusph_split_transport_alg_mod

  use constants_mod,                     only: r_def, i_def
  use field_mod,                         only: field_type
  use function_space_mod,                only: function_space_type
  use flux_direction_mod,                only: x_direction, y_direction
  use biperiodic_deppt_config_mod,       only: method
  use subgrid_config_mod,                only: dep_pt_stencil_extent
  use psykal_lite_mod,                   only: invoke_calc_departure_wind,     &
                                               invoke_calc_deppts,             &
                                               invoke_axpby,                   &
                                               invoke_plus_field_data

  use oned_advective_density_update_alg_mod,  only: oned_advective_density_update_alg
  use oned_conservative_flux_alg_mod,         only: oned_conservative_flux_alg
  use cosmic_halo_exchange_x_kernel_mod,      only: cosmic_halo_exchange_x_kernel_type
  use cosmic_halo_exchange_y_kernel_mod,      only: cosmic_halo_exchange_y_kernel_type

  implicit none

  private
  public :: cusph_split_transport_alg

contains

!> @brief Algorithm which calculates the mass fluxes using the symmetric COSMIC
!>        method.
!>        The algorithm below outputs the mass fluxes at timestep n+1 (np1)
!>        given the wind fields at timestep n and n+1 and the density field at
!>        timestep n.
!> @param[in] u_n               Winds at time level n
!> @param[in] u_np1             Winds at time level n+1
!> @param[in] rho               Density at time level n
!> @param[in] chi               Coordinate field array
!> @param[in] cell_orientation  Orientation of cells held in W3 field
!> @param[in] mesh_id           mesh id
!> @param[out] mass_flux        Mass fluxes in 2D used to update density
  subroutine cusph_split_transport_alg( u_n,                &
                                        u_np1,              &
                                        rho,                &
                                        chi,                &
                                        cell_orientation,   &
                                        mesh_id,            &
                                        mass_flux,          &
                                        detj_at_w2 )

    implicit none

    type(field_type),    intent(in)     :: u_n
    type(field_type),    intent(in)     :: u_np1
    type(field_type),    intent(in)     :: chi(3)
    type(field_type),    intent(in)     :: rho
    type(field_type),    intent(in)     :: cell_orientation
    integer(i_def),      intent(in)     :: mesh_id
    type(field_type),    intent(out)    :: mass_flux
    type(field_type),    intent(in)     :: detj_at_w2

    type( field_type ) :: departure_wind_n, departure_wind_np1, dep_pts_x, dep_pts_y
    type( field_type ) :: mass_flux_x, mass_flux_y
    type( field_type ) :: rho_n, rho_adv_x, rho_adv_y, rho_hat_adv_x, rho_hat_adv_y
    type( field_type ) :: rho_x, rho_y

    type(function_space_type), pointer :: w2_fs     => null()
    type(function_space_type), pointer :: w3_fs     => null()

    w3_fs => rho%get_function_space()
    w2_fs => u_n%get_function_space()

    departure_wind_n   = field_type( vector_space = w2_fs )
    departure_wind_np1 = field_type( vector_space = w2_fs )
    dep_pts_x          = field_type( vector_space = w2_fs )
    dep_pts_y          = field_type( vector_space = w2_fs )
    mass_flux_x        = field_type( vector_space = w2_fs )
    mass_flux_y        = field_type( vector_space = w2_fs )

    rho_adv_x          = field_type( vector_space = w3_fs )
    rho_adv_y          = field_type( vector_space = w3_fs )
    rho_hat_adv_x      = field_type( vector_space = w3_fs )
    rho_hat_adv_y      = field_type( vector_space = w3_fs )
    rho_x              = field_type( vector_space = w3_fs )
    rho_y              = field_type( vector_space = w3_fs )

    ! Calculate the departure wind used for calculating departure points
    call invoke_calc_departure_wind(departure_wind_n,u_n,chi)
    call invoke_calc_departure_wind(departure_wind_np1,u_np1,chi)

    ! Calculate the departure points for W2 nodal points at lowest order
    call invoke( set_field_scalar(0.0_r_def,dep_pts_x), &
                 set_field_scalar(0.0_r_def,dep_pts_y) )
    call invoke_calc_deppts(departure_wind_n,departure_wind_np1,dep_pts_x,cell_orientation,x_direction,method,dep_pt_stencil_extent)
    call invoke_calc_deppts(departure_wind_n,departure_wind_np1,dep_pts_y,cell_orientation,y_direction,method,dep_pt_stencil_extent)

    ! Perform two 1D advective updates in the x and y directions (horizontal)
    call oned_advective_density_update_alg(x_direction,u_n, dep_pts_x, cell_orientation, detj_at_w2, rho, rho_adv_x)
    call oned_advective_density_update_alg(y_direction,u_n, dep_pts_y, cell_orientation, detj_at_w2, rho, rho_adv_y)

    ! Average the two advective density updates with the density at timestep level n
    call invoke_axpby(0.5_r_def,rho,0.5_r_def,rho_adv_x,rho_hat_adv_x)
    call invoke_axpby(0.5_r_def,rho,0.5_r_def,rho_adv_y,rho_hat_adv_y)

    ! Halo exchange density fields
    call invoke( cosmic_halo_exchange_x_kernel_type(rho_x,rho_hat_adv_x,rho_hat_adv_y,     &
                   cell_orientation),                                                      &
                 cosmic_halo_exchange_y_kernel_type(rho_y,rho_hat_adv_x,rho_hat_adv_y,     &
                   cell_orientation) )

    ! Calculate separately the conservative fluxes in the x and y directions (horizontal)
    call invoke( set_field_scalar(0.0_r_def,mass_flux_x), &
                 set_field_scalar(0.0_r_def,mass_flux_y), &
                 set_field_scalar(0.0_r_def,mass_flux) )

    call oned_conservative_flux_alg(x_direction,u_n, dep_pts_x, cell_orientation, rho_y, mass_flux_x)
    call oned_conservative_flux_alg(y_direction,u_n, dep_pts_y, cell_orientation, rho_x, mass_flux_y)

    ! Combine the mass fluxes in the x and y directions
    call invoke_plus_field_data(mass_flux_x,mass_flux_y,mass_flux)

  end subroutine cusph_split_transport_alg

end module cusph_split_transport_alg_mod
