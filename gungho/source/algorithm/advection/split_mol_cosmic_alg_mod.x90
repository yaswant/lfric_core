!-----------------------------------------------------------------------------
! (c) Crown copyright 2020 Met Office. All rights reserved.
! The file LICENCE, distributed with this code, contains details of the terms
! under which the code may be used.
!-----------------------------------------------------------------------------
!
!-------------------------------------------------------------------------------
!> @brief Algorithm for a split vertical (cosmic) + multi-steps MOL horizontal.
!> @details The algorithm performs a vertical cosmic followed by multi-steps
!>          horizontal MOL scheme for a density field.
!-------------------------------------------------------------------------------

module split_mol_cosmic_alg_mod

  use constants_mod,                     only: i_def, r_def, tiny_eps
  use field_mod,                         only: field_type
  use function_space_mod,                only: function_space_type
  use function_space_collection_mod,     only: function_space_collection
  use finite_element_config_mod,         only: element_order
  use fs_continuity_mod,                 only: W2, W3
  use runtime_constants_mod,             only: get_cell_orientation,          &
                                               get_detj_at_w2
  use flux_direction_mod,                only: z_direction
  use vert_conservative_cosmic_alg_mod,  only: vert_conservative_cosmic_alg
  use cosmic_divergence_alg_mod,         only: cosmic_divergence_alg
  use io_config_mod,                     only: subroutine_timers
  use timer_mod,                         only: timer
  use timestepping_config_mod,           only: dt
  use rk_transport_rho_mod,              only: rk_transport_rho_step
  use split_vector_field_kernel_mod,     only: split_vector_field_kernel_type
  use calc_vertical_departure_alg_mod,   only: calc_vertical_departure
  use transport_config_mod,              only: rho_splitting_vh,               &
                                               rho_splitting_hv,               &
                                               rho_splitting_strang,           &
                                               cfl_mol_2d_stab
  use log_mod,                           only: log_event, LOG_LEVEL_ERROR

  implicit none

  private
  integer(i_def) :: mol_substeps

  public :: split_mol_cosmic_transport_step
  public :: split_transport_rho_step

contains

!=========================================================================================
!> @brief Algorithm to compute the vertical displacement then
!>        call the split-transport-routine.
!> @param[in]     wind      The 3D advecting wind
!> @param[in,out] rho       Density field
!> @param[in] rho_splitting The (rho) splitting option
!=========================================================================================

subroutine split_transport_rho_step( wind, rho, rho_splitting )

  implicit none

  type(field_type), intent(inout)    :: rho
  type(field_type), intent(in)       :: wind
  integer(i_def),   intent(in)       :: rho_splitting

  type(field_type)          :: dep_z
  type(field_type), pointer :: detj_w2 => null()
  type(field_type), pointer :: cell_orient => null()

  call wind%copy_field_properties(dep_z)
  detj_w2 => get_detj_at_w2()
  cell_orient => get_cell_orientation()

  if ( subroutine_timers ) call timer('split_transport_rho_step')

  call calc_vertical_departure(wind, dep_z)

  call split_mol_cosmic_transport_step( rho, wind, dep_z,     &
                                        detj_w2, cell_orient, &
                                        rho_splitting         )

  if ( subroutine_timers ) call timer('split_transport_rho_step')

  nullify( detj_w2, cell_orient )

end subroutine split_transport_rho_step

!=========================================================================================
!> @brief Algorithm to do sub-step MOL horizontally and vertical COSMIC transport scheme.
!> @details The algorithm performs a vertical cosmic then multi-steps
!>          horizontal update using the MOL method for a density field.
!> @param[in,out] rho               Density field
!> @param[in]     wind              The wind field
!> @param[in,out] dep_pts_z         Departure points in z direction
!> @param[in]     detj_at_w2        Det(J) at W2 dofs
!> @param[in]     cell_orientation  Orientation of halo cells
!> @param[in]     rho_splitting     The (rho) splitting option
!=========================================================================================

subroutine split_mol_cosmic_transport_step(  rho,              &
                                             wind,             &
                                             dep_pts_z,        &
                                             detj_at_w2,       &
                                             cell_orientation, &
                                             rho_splitting     )
  implicit none

  type(field_type), intent(inout)    :: rho
  type(field_type), intent(in)       :: wind
  type(field_type), intent(inout)    :: dep_pts_z
  type(field_type), intent(in)       :: detj_at_w2
  type(field_type), intent(in)       :: cell_orientation
  integer(i_def),   intent(in)       :: rho_splitting

  type(field_type) :: rho_xy
  type(field_type) :: increment_xy
  type(field_type) :: increment_z
  type(field_type) :: mass_flux_z
  type(field_type) :: wind_horiz
  type(field_type) :: wind_horiz2
  type(field_type) :: wind_vert
  integer(i_def)   :: sub_step_count
  real(r_def)      :: dt_substep, half_dt
  real(r_def)      :: cfl_min, cfl_max

  type(function_space_type), pointer :: wind_fs  => null()
  type(function_space_type), pointer :: rho_fs   => null()

  wind_fs  => wind%get_function_space()
  rho_fs   => rho%get_function_space()

  call rho_xy%initialise( vector_space = rho_fs )
  call increment_xy%initialise( vector_space = rho_fs )
  call increment_z%initialise( vector_space = rho_fs )

  call mass_flux_z%initialise( vector_space = wind_fs )
  call wind_horiz%initialise( vector_space = wind_fs )
  call wind_horiz2%initialise( vector_space = wind_fs )
  call wind_vert%initialise( vector_space = wind_fs )

  if ( subroutine_timers ) call timer('split_mol_cosmic_transport_step')

    ! Split wind to wind_horiz=(u,v,0) and wind_vert=(0,0,w)

    call invoke( name = "split_wind_into_horizontal_and_vertical",            &
                 split_vector_field_kernel_type(wind_horiz, wind_vert, wind), &
                 X_divideby_Y(wind_horiz2,wind_horiz,detj_at_w2),             &
                 inc_a_times_X(dt, wind_horiz2)                               )

    call wind_horiz2%field_minmax(cfl_min, cfl_max)
    cfl_max = max(abs(cfl_max),abs(cfl_min))

    call invoke( setval_X(rho_xy, rho) )

    if ( rho_splitting /= rho_splitting_strang .and. &
         rho_splitting /= rho_splitting_vh     .and. &
         rho_splitting /= rho_splitting_hv           ) then
     call log_event( "split_mol_cosmic_alg_mod: Invalid rho splitting option", LOG_LEVEL_ERROR )
    end if

    half_dt  = 0.5_r_def*dt
    if (rho_splitting == rho_splitting_strang) then
       !
       ! Perform the first half-step vertical transport (Strang splitting)
       !
       call invoke(inc_a_times_X(0.5_r_def,dep_pts_z))
       call vert_conservative_cosmic_alg( mass_flux_z, rho_xy, dep_pts_z, half_dt)
       call cosmic_divergence_alg( increment_z, mass_flux_z, detj_at_w2,   &
                                  cell_orientation, z_direction )
       call invoke( inc_X_minus_bY( rho_xy, half_dt, increment_z ) )
    elseif (rho_splitting == rho_splitting_vh) then
       !
       ! Perform a simple vertical(dt)+horizontal(dt) split
       !
       call vert_conservative_cosmic_alg( mass_flux_z, rho_xy, dep_pts_z, dt)
       call cosmic_divergence_alg( increment_z, mass_flux_z, detj_at_w2,   &
                                  cell_orientation, z_direction )
       call invoke( inc_X_minus_bY( rho_xy, dt, increment_z ) )
  end if
  !
  ! Perform a full step horizontal transport using small sub-steps (if required)
  !

  mol_substeps = int(cfl_max/(cfl_mol_2d_stab+tiny_eps)) + 1_i_def
  dt_substep = dt/real(mol_substeps, r_def)

  do sub_step_count = 1, mol_substeps
     call rk_transport_rho_step( wind_horiz, rho_xy, dt_substep)
  end do

  if ( rho_splitting == rho_splitting_strang ) then
       !
       ! Perform the second half-step vertical transport (Strang splitting)
       !
       call vert_conservative_cosmic_alg( mass_flux_z, rho_xy, dep_pts_z, half_dt)
       call cosmic_divergence_alg( increment_z, mass_flux_z, detj_at_w2,   &
                                 cell_orientation, z_direction )
       call invoke( inc_X_minus_bY( rho_xy, half_dt, increment_z ) )

    elseif ( rho_splitting == rho_splitting_hv ) then
       !
       ! Perform a simple horizontal(dt) + vertical(dt) split
       !
       call vert_conservative_cosmic_alg( mass_flux_z, rho_xy, dep_pts_z, dt )
       call cosmic_divergence_alg( increment_z, mass_flux_z, detj_at_w2,   &
                                   cell_orientation, z_direction )
       call invoke( inc_X_minus_bY( rho_xy, dt, increment_z ) )
  end if

  ! Overwrite rho with the new rho
  call invoke( setval_X(rho, rho_xy) )

  if ( subroutine_timers ) call timer('split_mol_cosmic_transport_step')

  nullify( wind_fs, rho_fs )

end subroutine split_mol_cosmic_transport_step

end module split_mol_cosmic_alg_mod
