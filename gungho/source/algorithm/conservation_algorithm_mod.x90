!-----------------------------------------------------------------------------
! Copyright (c) 2017,  Met Office, on behalf of HMSO and Queen's Printer
! For further details please refer to the file LICENCE.original which you
! should have received as part of this distribution.
!-----------------------------------------------------------------------------

!>@brief Algorithm to compute various conserved quantities 
!>@details Computes various conserved quantities and checks how
!> accurately they have been conserved
module conservation_algorithm_mod

  use compute_total_aam_kernel_mod,    only: compute_total_aam_kernel_type
  use compute_total_energy_kernel_mod, only: compute_total_energy_kernel_type
  use compute_total_mass_kernel_mod,   only: compute_total_mass_kernel_type
  use compute_total_pv_kernel_mod,     only: compute_total_pv_kernel_type

  use constants_mod,                   only: r_def, i_def
  use function_space_collection_mod,   only: function_space_collection
  use field_mod,                       only: field_type
  use finite_element_config_mod,       only: element_order, &
                                             vorticity_in_w1, &
                                             nqp_exact
  use fs_continuity_mod,               only: W3
  use function_space_mod,              only: function_space_type
  use log_mod,                         only: log_event,         &
                                             log_scratch_space, &
                                             LOG_LEVEL_INFO
  use quadrature_xyoz_mod,             only: quadrature_xyoz_type
  use quadrature_rule_gaussian_mod,    only: quadrature_rule_gaussian_type
  use runtime_constants_mod,           only: get_geopotential, &
                                             get_coordinates
  use io_config_mod,                   only: subroutine_timers 
  use timer_mod,                       only: timer

  implicit none

  private
  public :: conservation_algorithm

contains

  !>@brief Algorithm to compute various conserved quantities
  !>@details Computes various conserved quantities and checks how
  !> accurately they have been conserved
  !> @param[in] tstep Current timestep
  !> @param[in] rho Density field
  !> @param[in] u Velocity field
  !> @param[in] theta Potential temperature field
  !> @param[in] exner Exner pressure field
  !> @param[in] xi Vorticity field
  subroutine conservation_algorithm( tstep,        &
                                     rho,          &
                                     u,            &
                                     theta,        &
                                     exner,        &
                                     xi)
  implicit none
    integer, intent(in) :: tstep


    ! Prognostic fields    
    type( field_type ), intent( in ) :: u, rho, theta, exner, xi

    ! Local variables
    type( field_type ), pointer :: geopotential => null()
    type( quadrature_xyoz_type ) :: qr
    type( quadrature_rule_gaussian_type ) :: quadrature_rule
    ! Coordinate fields
    type( field_type ), pointer :: chi(:) => null()

    type(function_space_type), pointer :: w3_fs => null()

    type( field_type ) :: mass, pv, aam, energy
    real(kind=r_def) :: total_mass, total_pv, total_aam, total_energy 
    integer(i_def)   :: mesh 

    if ( subroutine_timers ) call timer('conservation_alg')

    geopotential => get_geopotential()
    chi          => get_coordinates()

    ! Get a quadrature rule
    qr = quadrature_xyoz_type(nqp_exact, quadrature_rule)
    mesh = rho%get_mesh_id()
    w3_fs => function_space_collection%get_fs( mesh, element_order, W3)
   
    ! Initialise Mass, Energy and Axial Angular Momentum fields
    mass = field_type( vector_space = w3_fs )
    energy = field_type( vector_space = w3_fs )
    aam = field_type( vector_space = w3_fs )

    ! Compute conserved quantities
    call invoke( name = "Compute conserved quantities",                   &
    ! Compute Total Mass
                 compute_total_mass_kernel_type(mass, rho, chi, qr),      &
                 sum_X(total_mass, mass),                                 &
    ! Compute Total Energy
                 compute_total_energy_kernel_type(energy,                 &
                                                  u, rho, exner, theta,   &
                                                  geopotential, chi, qr), &
                 sum_X(total_energy, energy),                             &
    ! Compute Total Axial Angular Momentum
                 compute_total_aam_kernel_type(aam, u, rho, chi, qr),     &
                 sum_X(total_aam, aam) )

    ! Gradient of theta not defined for the Wtheta case, TBD
    if ( .false. .and. vorticity_in_w1 ) then
      ! Compute Ertel PV
      pv = field_type( vector_space = w3_fs )
      call invoke( name = "Compute total PV",                            &
                   compute_total_pv_kernel_type(pv, xi, theta, chi, qr), &
                   sum_X(total_pv, pv) )
      write( log_scratch_space, '(A,I4,4E32.24)')  &
             'Conservation  = ',&
              tstep, total_mass, total_energy, total_pv, total_aam
    else
      write( log_scratch_space, '(A,I4,4E32.24)')  &
             'Conservation  = ',&
              tstep, total_mass, total_energy, total_aam
    end if

    call log_event( log_scratch_space, LOG_LEVEL_INFO )

    nullify( geopotential )
    nullify( chi, w3_fs )

    if ( subroutine_timers ) call timer('conservation_alg')

  end subroutine conservation_algorithm

end module conservation_algorithm_mod
