!-----------------------------------------------------------------------------
! (c) Crown copyright 2017 Met Office. All rights reserved.
! The file LICENCE, distributed with this code, contains details of the terms
! under which the code may be used.
!-----------------------------------------------------------------------------

!>@brief Calls to the slow physics schemes
module slow_physics_alg_mod

  use constants_mod,             only: i_def,r_def
  use log_mod,                   only: log_event,         &
                                       LOG_LEVEL_WARNING, LOG_LEVEL_INFO
  use physics_config_mod,        only: heldsuarez_thermo_placement, &
                                       heldsuarez_wind_placement
  ! PsyKAl PSYClone kernels
  use held_suarez_fv_kernel_mod,      only: held_suarez_fv_kernel_type
  use held_suarez_fv_wind_kernel_mod, only: held_suarez_fv_wind_kernel_type
  
  ! Derived Types
  use field_mod,                 only: field_type

  use fs_continuity_mod,         only: W2

  use runtime_constants_mod,     only: get_rmultiplicity

  implicit none

contains

  !> @details Collection of procedures for subgrid physics that act
  !>          before the outer loop of the solver
  !
  !>@param[in] chi Coordinate array
  !>@param[inout] du increment to wind field 
  !>@param[in] u  wind field 
  !>@param[inout] dtheta increment to theta field 
  !>@param[in] theta theta field 
  !>@param[in] exner_in_wth Exner pressure on wth points
  subroutine slow_physics(chi, du, u, dtheta, theta, exner_in_wth)

    implicit none

    ! Prognostic fields    
    type( field_type ), intent( in )    :: chi(3)
    type( field_type ), intent( in )    :: u, theta, exner_in_wth
    type( field_type ), intent( inout ) :: dtheta
    type( field_type ), intent( inout ) :: du
    

    ! Local fields (should be created at initialization, but done here for the time being)
    type( field_type ) :: dtheta_hs
    type( field_type ) :: du_hs

    type(field_type),   pointer  :: w2_rmultiplicity => null() ! 1/multiplicity of w2
    
    logical :: held_suarez_thermo_done ! Flag to indicate that held_suarez has been calculated
    logical :: held_suarez_wind_done ! Flag to indicate that held_suarez has been calculated

    w2_rmultiplicity => get_rmultiplicity( W2 ) ! 1/multiplicity of w2

    held_suarez_thermo_done=.false.
    held_suarez_wind_done=.false.

    if (heldsuarez_thermo_placement < 0)then
      dtheta_hs = field_type( vector_space = theta%get_function_space() )
      call invoke( setval_c(dtheta_hs, 0.0_r_def) )
      call log_event( &
         'slow_physics: Running Held-Suarez theta forcing', &
         LOG_LEVEL_INFO )
      call invoke(held_suarez_fv_kernel_type(dtheta_hs, theta, exner_in_wth, chi))
      held_suarez_thermo_done=.true. ! We've done it, so need to collect this term later on
    end if

    if (heldsuarez_wind_placement < 0)then
      du_hs = field_type( vector_space = u%get_function_space() )
      call invoke( setval_c(du_hs, 0.0_r_def) )
      call log_event( &
         'slow_physics: Running Held-Suarez wind forcing', &
         LOG_LEVEL_INFO )
      call invoke(held_suarez_fv_wind_kernel_type(du_hs, u, w2_rmultiplicity, &
         exner_in_wth, chi))
      held_suarez_wind_done=.true. ! We've done it, so need to collect this term later on
    end if

    !=====================================
    ! collect individual terms in parallel
    !=====================================
    if (held_suarez_thermo_done)then
      call invoke(inc_X_plus_Y(dtheta, dtheta_hs))
    end if

    if (held_suarez_wind_done)then
      call invoke(inc_X_plus_Y(du, du_hs))
    end if

    call dtheta%log_minmax(LOG_LEVEL_INFO, 'slow physics: dtheta' )
    call du%log_minmax(LOG_LEVEL_INFO, 'slow physics: du' )

  end subroutine slow_physics



end module slow_physics_alg_mod
