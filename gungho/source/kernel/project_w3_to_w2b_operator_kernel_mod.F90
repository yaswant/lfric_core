!-----------------------------------------------------------------------------
! (c) Crown copyright 2022 Met Office. All rights reserved.
! The file LICENCE, distributed with this code, contains details of the terms
! under which the code may be used.
!-----------------------------------------------------------------------------
!
!-------------------------------------------------------------------------------

!> @brief Compute an operator to project a scalar field (treated as a component
!!        of a vector) from the scalar W3 space into a W2b field.
module project_w3_to_w2b_operator_kernel_mod

use kernel_mod,              only : kernel_type
use argument_mod,            only : arg_type, func_type,       &
                                    GH_OPERATOR,               &
                                    GH_FIELD, GH_SCALAR,       &
                                    GH_REAL, GH_INTEGER,       &
                                    GH_READ, GH_WRITE,         &
                                    ANY_DISCONTINUOUS_SPACE_3, &
                                    GH_BASIS, GH_DIFF_BASIS,   &
                                    CELL_COLUMN, GH_QUADRATURE_XYoZ
use constants_mod,           only : r_def, i_def
use fs_continuity_mod,       only : W2broken, W3, Wchi

implicit none

private
!-------------------------------------------------------------------------------
! Public types
!-------------------------------------------------------------------------------
!> The type declaration for the kernel. Contains the metadata needed by the PSy layer
type, public, extends(kernel_type) :: project_w3_to_w2b_operator_kernel_type
  private
  type(arg_type) :: meta_args(4) = (/                                           &
       arg_type(GH_OPERATOR, GH_REAL,    GH_WRITE,  W2broken, W3),              &
       arg_type(GH_FIELD*3,  GH_REAL,    GH_READ,   Wchi),                      &
       arg_type(GH_FIELD,    GH_REAL,    GH_READ,   ANY_DISCONTINUOUS_SPACE_3), &
       arg_type(GH_SCALAR,   GH_INTEGER, GH_READ)                               &
       /)
  type(func_type) :: meta_funcs(3) = (/                                         &
       func_type(W2broken,   GH_BASIS),                                         &
       func_type(W3,         GH_BASIS),                                         &
       func_type(Wchi,       GH_BASIS,   GH_DIFF_BASIS)                         &
       /)
  integer :: operates_on = CELL_COLUMN
  integer :: gh_shape = GH_QUADRATURE_XYoZ
contains
  procedure, nopass :: project_w3_to_w2b_operator_code
end type

!-------------------------------------------------------------------------------
! Contained functions/subroutines
!-------------------------------------------------------------------------------
public :: project_w3_to_w2b_operator_code

contains

!> @brief Compute an operator to project a W3 field into a W2b field.
!> @details Compute an operator to project a scalar field
!!          from W3 into a W2broken field.
!!          The component of the vector to be projected is given by the
!!          direction field.
!> @param[in]     cell                Horizontal cell index
!> @param[in]     nlayers             Number of layers
!> @param[in]     ncell               Total number of cells in the 3D mesh
!> @param[in,out] projection_operator Projection operator to compute
!> @param[in]     chi1                1st coordinate field in Wchi
!> @param[in]     chi2                2nd coordinate field in Wchi
!> @param[in]     chi3                3rd coordinate field in Wchi
!> @param[in]     panel_id            Field giving the ID for mesh panels.
!> @param[in]     direction           Index of the vector component (1,2 or 3) to project
!> @param[in]     ndf_w2b             Number of degrees of freedom per cell for vector space
!> @param[in]     basis_w2b           Basis functions for the vector space at quadrature points
!> @param[in]     ndf_w3              Number of degrees of freedom per cell for scalar space
!> @param[in]     basis_w3            Basis functions for the scalar space at quadrature points
!> @param[in]     ndf_wx              Number of degrees of freedom per cell for the coordinate space
!> @param[in]     undf_wx             Number of unique degrees of freedom for the coordinate space
!> @param[in]     map_wx              Dofmap for the cell at the base of the column for the coordinate space
!> @param[in]     basis_wx            Basis functions for the coordinate space at quadrature points
!> @param[in]     diff_basis_wx       Differential basis functions for the coordinate space at quadrature points
!> @param[in]     ndf_pid             Number of degrees of freedom per cell for panel_id
!> @param[in]     undf_pid            Number of unique degrees of freedom for panel_id
!> @param[in]     map_pid             Dofmap for the cell at the base of the column for panel_id
!> @param[in]     nqp_h               Number of horizontal quadrature points
!> @param[in]     nqp_v               Number of vertical quadrature points
!> @param[in]     wqp_h               Weights of horizontal quadrature points
!> @param[in]     wqp_v               Weights of vertical quadrature points
subroutine project_w3_to_w2b_operator_code( cell, nlayers,              &
                                            ncell,                      &
                                            projection_operator,        &
                                            chi1, chi2, chi3,           &
                                            panel_id,                   &
                                            direction,                  &
                                            ndf_w2b, basis_w2b,         &
                                            ndf_w3, basis_w3,           &
                                            ndf_wx, undf_wx, map_wx,    &
                                            basis_wx, diff_basis_wx,    &
                                            ndf_pid, undf_pid, map_pid, &
                                            nqp_h, nqp_v, wqp_h, wqp_v )

  use coordinate_jacobian_mod, only: pointwise_coordinate_jacobian

  implicit none

  ! Arguments
  integer(kind=i_def),                     intent(in) :: cell, ncell, nlayers, nqp_h, nqp_v
  integer(kind=i_def),                     intent(in) :: ndf_w2b, ndf_w3, ndf_wx, ndf_pid
  integer(kind=i_def),                     intent(in) :: undf_wx, undf_pid
  integer(kind=i_def), dimension(ndf_wx),  intent(in) :: map_wx
  integer(kind=i_def), dimension(ndf_pid), intent(in) :: map_pid

  real(kind=r_def), dimension(3,ndf_w2b,nqp_h,nqp_v), intent(in) :: basis_w2b
  real(kind=r_def), dimension(1,ndf_w3,nqp_h,nqp_v),  intent(in) :: basis_w3
  real(kind=r_def), dimension(1,ndf_wx,nqp_h,nqp_v),  intent(in) :: basis_wx
  real(kind=r_def), dimension(3,ndf_wx,nqp_h,nqp_v),  intent(in) :: diff_basis_wx

  real(kind=r_def), dimension(ndf_w2b, ndf_w3, ncell),  intent(inout) :: projection_operator
  real(kind=r_def), dimension(undf_wx),                 intent(in)    :: chi1, chi2, chi3
  real(kind=r_def), dimension(undf_pid),                intent(in)    :: panel_id

  integer(kind=i_def), intent(in) :: direction

  real(kind=r_def), intent(in) :: wqp_h(nqp_h)
  real(kind=r_def), intent(in) :: wqp_v(nqp_v)

  ! Internal variables
  integer(kind=i_def)                 :: df_wx, df_w3, df_w2b, ik, k, qp_h, qp_v
  real(kind=r_def), dimension(ndf_wx) :: chi1_e, chi2_e, chi3_e
  real(kind=r_def), dimension(3,3)    :: jac
  real(kind=r_def)                    :: detj
  real(kind=r_def), dimension(3)      :: a3d, v

  integer(kind=i_def) :: ipanel

  ipanel = int(panel_id(map_pid(1)), i_def)

  do k = 0, nlayers-1
    do df_wx = 1, ndf_wx
      chi1_e(df_wx) = chi1(map_wx(df_wx) + k)
      chi2_e(df_wx) = chi2(map_wx(df_wx) + k)
      chi3_e(df_wx) = chi3(map_wx(df_wx) + k)
    end do

    ik = 1 + k + (cell-1)*nlayers
    projection_operator(:,:,ik) = 0.0_r_def
    do qp_v = 1,nqp_v
      do qp_h = 1,nqp_h
        call pointwise_coordinate_jacobian(ndf_wx, chi1_e, chi2_e, chi3_e,  &
                                           ipanel, basis_wx(:,:,qp_h,qp_v), &
                                           diff_basis_wx(:,:,qp_h,qp_v),    &
                                           jac, detj)
        a3d = 0.0_r_def
        do df_w3 = 1,ndf_w3
          a3d(direction) = basis_w3(1,df_w3,qp_h,qp_v)

          do df_w2b = 1,ndf_w2b
            ! Projection from w3*3 to w2b = jac*v . a3d
            v = matmul(jac, basis_w2b(:,df_w2b,qp_h,qp_v))

            projection_operator(df_w2b, df_w3, ik) = projection_operator(df_w2b, df_w3, ik) &
                                                + wqp_h(qp_h)*wqp_v(qp_v)*dot_product(v,a3d)
          end do
        end do
      end do
    end do
  end do

end subroutine project_w3_to_w2b_operator_code

end module project_w3_to_w2b_operator_kernel_mod
